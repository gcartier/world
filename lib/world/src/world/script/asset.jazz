;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Asset Scripts
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.script.asset jazz


(import (jazz.io)
        (world)
        (world.asset)
        (world.autoload)
        (world.entity)
        (world.scripter)
        (world.scriptum))


(definition protected asset-entities
  (make-table test: asset=? hash: asset-hash))

;; quick hack to think through
(definition protected (reset-asset-entities)
  (set! asset-entities (make-table test: asset=? hash: asset-hash)))


(definition public (find-asset-entity asset)
  (table-ref asset-entities asset #f))


;; quick hack
(definition public (unregister-asset asset)
  (table-clear asset-entities asset))


(definition public (register-asset-script asset)
  (or (table-ref asset-entities asset #f)
      (let ((entity (new Asset-Entity asset: asset)))
        (new Script parent: entity text: (load-text asset))
        (table-set! asset-entities asset entity)
        entity)))


(definition public (run-asset-script asset (synchronous?: synchronous? #f))
  (let ((entity (register-asset-script asset)))
    (let ((script (get-script entity)))
      (set-kind script 'client)
      (run-task entity script synchronous?: synchronous?))))


(definition public (save-asset-script asset entity)
  (let ((file (retrieve-file asset)))
    (call-with-output-file (path-settings file)
      (lambda (output)
        (display (get-text (get-script entity)) output)))))


;;;
;;;; Asset-Entity
;;;


(class Asset-Entity extends Entity
  
  
  (property asset initialize #f accessors generate)
  
  
  (method override (task-name self)
    'asset)
  
  
  (method override (virtual? self)
    #t)
  
  
  (method override (fallthrough self)
    )))
