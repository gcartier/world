;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Work
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.work jazz


(import (jazz.debuggee)
        (jazz.evaluate)
        (jazz.system)
        (world)
        (world.autoload)
        (world.settings)
        (world.syntax (phase syntax)))


;; Not settings to be active as soon as possible in the boot

(definition worker?
  #t)

(definition worker-verbose?
  #f)

(definition worker-debug?
  #f)


;;;
;;;; Process
;;;


(definition world-repositories
  #f)

(definition protected (set-world-repositories repositories)
  (set! world-repositories repositories))


(definition (spawn-worker-process)
  (define (determine-worker-path)
    (let ((filename (add-extension (get-worker-filename) (executable-extension kernel-platform))))
      (let ((bundle-path (and kernel-bundle-install (string-append kernel-bundle-install filename))))
        (if (and bundle-path (file-exists? bundle-path))
            bundle-path
          (let ((install-path (string-append kernel-install filename)))
            (if (file-exists? install-path)
                install-path
              (error "Unable to find worker executable")))))))
  
  (let ((worker-path (determine-worker-path))
        (server-port (open-tcp-server 0)))
    (open-process
      (list
        path: worker-path
        arguments: `("-:daqQ-"
                     "-port" ,(number->string (socket-info-port-number (tcp-server-socket-info server-port)))
                     ,@(if world-repositories
                           (list "-repositories" (join world-repositories ";"))
                         '())
                     ;; use variable as this is not correct at the moment because of the evaluate always hack
                     ,@(let ((debug? worker-debug?)
                             (arg (command-argument "debugger")))
                         (if (and debug? arg)
                             (list "-debugger" arg)
                           '())))
        stdin-redirection: #f
        stdout-redirection: #f
        stderr-redirection: #f
        show-console: #f))
    (read server-port)))


(definition (kill-worker-process port)
  (write '() port)
  (force-output port)
  ;; we should do this to wait for the worker to exit else
  ;; the worker can get a broken pipe if we exit too soon
  ;; (process-status port)
  (close-port port))


;;;
;;;; Port
;;;


(definition worker-port
  #f)


(definition protected (get-worker-port)
  (or worker-port
      (let ((port (spawn-worker-process)))
        (set! worker-port port)
        port)))


(definition protected (close-worker-port)
  (when worker-port
    (kill-worker-process worker-port)
    (set! worker-port #f)))


(definition protected (worker-work command . arguments)
  (let ((port (get-worker-port)))
    (write (cons command arguments) port)
    (force-output port)))


;;;
;;;; Hooks
;;;


(definition (interpreted-hook unit-name)
  (when worker?
    (when worker-verbose?
      (debug 'expand-unit unit-name))
    (worker-work 'expand-unit unit-name)
    (eval-worker-result 'unit)
    #t))


(definition (script-hook path)
  (when worker?
    (when worker-verbose?
      (debug 'expand-script path))
    (worker-work 'expand-script path)
    (eval-worker-result 'script)
    #t))


(definition (evaluate-hook forms syntax str container line col evaluation-mode)
  (when worker?
    (let ((evaluation-mode (effective-evaluation-mode (or evaluation-mode (find-setting 'world.evaluation-mode 'interpret)))))
      (when worker-verbose?
        (debug 'expand-evaluate))
      (worker-work 'expand-evaluate syntax str container line col evaluation-mode)
      (when (eval-worker-result 'evaluate evaluation-mode: evaluation-mode)
        (hook-evaluate forms syntax str container line col)))
    #t))


(definition (console-hook unit-name expr evaluator)
  (if (not worker?)
      (values #f #f)
    (when worker-verbose?
      (debug 'expand-console expr))
    (worker-work 'expand-console unit-name expr)
    (values (eval-worker-result 'console evaluator: evaluator) #t)))


(definition (eval-worker-result kind (evaluation-mode: evaluation-mode #f) (evaluator: evaluator #f))
  (let ((info (read-binary-content (get-worker-port) unmarshall)))
    (unless (eof-object? info)
      (bind (what expr exception) info
        (if (eq? what 'result)
            (parameterize ((generate-symbol-for "%")
                           (generate-symbol-context (gensym))
                           (generate-symbol-counter 0))
              (cond ((eq? evaluation-mode 'compile)
                     (load-file expr))
                    (evaluator
                     (evaluator expr))
                    (else
                     (eval expr)
                     #t)))
          (display-walk-problems~ (current-world) expr)
          (if evaluator
              (unspecified)
            #f))))))


(add-exit-job! close-worker-port)


(set-load-interpreted-hook
  interpreted-hook)

(set-load-script-hook
  script-hook)

(set-evaluate-forms-hook
  evaluate-hook)

(set-console-evaluate-hook
  console-hook))
