;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Work
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.work jazz


(import (jazz.application)
        (jazz.component)
        (jazz.debuggee)
        (jazz.debugger.stub)
        (jazz.evaluate)
        (jazz.io)
        (jazz.presence)
        (world)
        (world.context)
        (world.master)
        (world.settings)
        (world.task)
        (world.working))


;;;
;;;; Process
;;;


(definition spawn-mutex
  (make-mutex 'spawn))

(definition (with-spawn-mutex thunk)
  (unwind-protect
      (begin
        (mutex-lock! spawn-mutex)
        (thunk))
    (mutex-unlock! spawn-mutex)))


(definition protected worker
  #f)

(definition public (require-worker)
  (assert (not (worker?))
    (with-spawn-mutex
      (lambda ()
        (or worker
            (let ((wrk (if master-process
                           (connect-worker (request-worker-port master-process))
                         (spawn-worker))))
              (set! worker wrk)
              wrk))))))

(definition public (require-worker-port)
  (let ((worker (require-worker)))
    (get-service (find-connection (get-presence worker) (get-uuid (get-ior worker))))))

(definition public (set-worker wrk)
  (set! worker wrk))


(definition world-repositories
  '())

(definition protected (get-world-repositories)
  world-repositories)

(definition protected (set-world-repositories repositories)
  (set! world-repositories repositories)
  ;; spawning worker will setup repositories
  (and worker
       (bind (what . data) (process-repositories worker repositories)
         (worker-result what data))))


(definition (worker-crashes)
  (parse {Directory Settings "work" "crashes"}))

(definition (worker-cache)
  (parse {Directory Settings "work" "caches" "code"}))

(definition (worker-optimize?)
  (world-setting 'world.worker-optimize? #f))

(definition (worker-debug?)
  (world-setting 'world.worker-debug? #f))

(definition (worker-manual?)
  (world-setting 'world.worker-manual? #f))


(definition protected (setup-worker-process)
  (load-unit 'jazz.presence)
  (load-unit 'jazz.presence.transmission)
  (load-unit 'jazz.presence.remotable.IOR))


(definition protected (spawn-worker)
  (let ((crashes (worker-crashes))
        (cache (worker-cache))
        (optimize? (worker-optimize?))
        (debug? (worker-debug?))
        (manual? (worker-manual?)))
    (spawn-slave "worker" (get-worker-filename)
      (lambda (master slave cookie)
        (process-setup slave crashes cache optimize? debug? world-repositories))
      (lambda (master slave cookie)
        slave)
      purpose: 'working
      arguments: (list "-window" (if (window?) "true" "false"))
      debug?: debug?
      manual?: manual?
      wait?: #t)))


(definition protected (connect-worker port)
  (start-remote-presence purpose: 'working)
  (let ((remote-register (connect-remote-register "localhost" port purpose: 'working)))
    (load-object remote-register 'world.worker.remote 'world-remote-worker)))


(definition protected (hide-worker-process)
  (and worker
       (bind (what . data) (process-hide worker)
         (worker-result what data))))


(definition protected (kill-worker-process)
  (when worker
    (unless master-process
      (process-quit worker))
    (set! worker #f)))


(definition protected (calling-worker thunk)
  (thunk))


(unless (worker?)
  (setup-worker-process))


;;;
;;;; Settings
;;;


(definition (worker-evaluation)
  (world-setting 'world.worker-evaluation 'interpret))


(set-debug-working? (world-setting 'world.debug-scripts? bot?))


;;;
;;;; Run
;;;


(definition public (worker-run instance script-code read-handler walk-handler error-handler run-handler result-handler)
  (declare (proper-tail-calls))
  (define (evaluate-code)
    (with-working
      (lambda ()
        (let ((syntax 'jazz)
              (str script-code)
              (container 'instance)
              (line -1)
              (col 0))
          (if (not use-worker?)
              (with-evaluating
                (lambda ()
                  (cons 'eval (evaluate-forms syntax str container line col 'interpret evaluate?: #f scripting?: #t))))
            (calling-worker
              (lambda ()
                (process-run (require-worker) 'interpret #f syntax str container line col))))))))
  
  (bind (what . data) (evaluate-code)
    (case what
      ((read)
       (read-handler data))
      ((walk)
       (walk-handler data))
      ((error)
       (error-handler data))
      ((eval)
       (let ()
         (define (eval-script)
           (run-handler)
           (when instance
             (startup instance))
           (result-handler
             (parameterize ((generate-symbol-for (format "%{a}&" (eval-count))))
               (eval data)))
           (when (and instance (not (synchronous-task?)))
             (fallthrough instance)))
         
         (if debug-working?
             (eval-script)
           (continuation-capture
             (lambda (exit)
               (with-exception-handler
                 (lambda (exc)
                   (continuation-capture
                     (lambda (cont)
                       (continuation-graft exit
                         (lambda ()
                           (error-handler (list (exception-reason exc) (exception-location exc cont) (continuation-stack cont))))))))
                 eval-script)))))))))


;;;
;;;; Hooks
;;;


(definition (unit-hook unit-name)
  (and use-worker?
       (begin
         (when (world-setting 'world.worker-verbose? #f)
           (debug 'unit unit-name))
         (let ((evaluation (worker-evaluation)))
           (with-feedback evaluation unit-name
             (lambda (feedback? time?)
               (bind (what . data) (calling-worker (lambda () (process-unit (require-worker) evaluation feedback? time? unit-name)))
                 (worker-result what data)))))
         #t)))


(definition (script-hook path)
  (and use-worker?
       (begin
         (when (world-setting 'world.worker-verbose? #f)
           (debug 'script path))
         (let ((evaluation 'interpret))
           (bind (what . data) (calling-worker (lambda () (process-script (require-worker) evaluation path)))
             (worker-result what data)))
         #t)))


(definition (evaluate-hook forms syntax str container line col evaluation-mode walk-debugger walk-cookie)
  (and use-worker?
       (begin
         (when (world-setting 'world.worker-verbose? #f)
           (debug 'evaluate))
         (let ((evaluation (worker-evaluation)))
           (with-feedback evaluation 'evaluation
             (lambda (feedback? time?)
               (bind (what . data) (calling-worker (lambda () (process-evaluate (require-worker) evaluation feedback? syntax str container line col)))
                 (when (worker-result what data walk-debugger: walk-debugger walk-cookie: walk-cookie)
                   (hook-evaluate forms syntax str container line col))))))
         #t)))


(definition (console-hook unit-name expr evaluator)
  (if (not use-worker?)
      (values #f #f)
    (when (world-setting 'world.worker-verbose? #f)
      (debug 'console))
    (let ((evaluation 'interpret))
      (bind (what . data) (calling-worker (lambda () (process-console (require-worker) unit-name expr)))
        (values (wrap-evaluate
                  (lambda ()
                    (worker-result what data evaluator: evaluator)))
                #t)))))


(definition (with-feedback evaluation name proc)
  (let ((world (current-world+)))
    (let ((visible? (and (current-toplevel) (get-visible? (current-toplevel)))))
      (let ((feedback? (world-setting 'world.worker-feedback? (if (client?) (not visible?) #f)))
            (time? (world-setting 'world.worker-time? #f)))
        (when (and feedback?
                   (eq? evaluation 'compile)
                   (neq? name 'evaluation)
                   name
                   world
                   visible?)
          (display-message world (format "Compiling {a}..." name)))
        (proc feedback? time?)))))


(definition (worker-result what data (evaluator: evaluator #f) (read-handler: read-handler #f) (walk-handler: walk-handler #f) (walk-debugger: walk-debugger #f) (walk-cookie: walk-cookie #f) (error-handler: error-handler #f))
  (when (world-setting 'world.worker-verbose-result? #f)
    (debug 'result what))
  (case what
    ((eval evaload)
     (parameterize ((generate-symbol-for "%")
                    (generate-symbol-context (gensym)))
       (let ((expansion (if (eq? what 'eval)
                            data
                          (call-with-input-file data
                            read-binary))))
         (if evaluator
             (evaluator expansion)
           (eval expansion)
           #t))))
    ((load)
     (load-file data #t))
    ((read)
     (bind (reason location) data
       (if read-handler
           (read-handler reason location)
         (error "{a}" reason))))
    ((walk)
     (bind (reason detail) data
       (if walk-handler
           (walk-handler reason detail)
         (let ((world (current-world+))
               (zone (current-zone+)))
           (cond (walk-debugger
                  (report-walk-problems walk-debugger (get-local-process) reason detail walk-cookie))
                 ((and world zone (get-ready? zone) (not (get-controller-debugger)))
                  (display-walk-problems world detail))
                 (else
                  (error "{a}" reason))))
         (if evaluator
             (unspecified)
           #f))))
    ((error)
     (bind (reason location stack) data
       (if error-handler
           (error-handler reason location stack)
         (error "{a}" reason))))))


(unless (worker?)
  (add-exit-job! kill-worker-process))


(unless (worker?)
  (set-load-interpreted-hook
    unit-hook)
  
  (set-load-script-hook
    script-hook)
  
  (set-evaluate-forms-hook
    evaluate-hook)
  
  (set-console-evaluate-hook
    console-hook)))
