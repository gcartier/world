;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Assets
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.assets jazz


(import (jazz.io))


(definition public (gather-assets where dir)
  (let ((queue (new-queue)))
    (define (gather dir spine)
      (when (and dir (exists? dir))
        (when (exists? (new-file dir ".assets"))
          (enqueue queue (new Assets where (reverse spine) dir)))
        (iterate-directory dir
          (lambda (dir)
            (gather dir (cons (get-name dir) spine)))
          files?: #f
          directories?: #t
          recursive?: #f)))
    
    (gather dir '())
    (queue-list queue)))


;;;
;;;; Assets
;;;


(class Assets extends Object
  
  
  (slot where     getter generate)
  (slot spine     getter generate)
  (slot directory getter generate)
  
  
  (method override (initialize self where spine directory)
    (set! self.where where)
    (set! self.spine spine)
    (set! self.directory directory))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}{?: {l}~}" where (not-null? spine) spine))))
  
  
  (method public (get-unique-spine self)
    (cons (symbol->string where) spine))
  
  
  (method public (make-directory self path) <Directory>
    (new-directory directory (listify-path self path)))
  
  
  (method public (make-file self path) <File>
    (new-file directory (listify-path self path)))
  
  
  (method (listify-path self path)
    (if (string? path)
        (tokenise-filename path)
      path))))
