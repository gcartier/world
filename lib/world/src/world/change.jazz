;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Changes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Alain Marcotte


(module world.change jazz


(import (world)
        (world.autoload)
        (world.syntax (phase syntax)))


(class Change extends Object
  
  
  (slot name                getter generate)
  (slot id             <fx> getter generate)
  (slot forward-count       getter generate)
  (slot forward-store       getter generate)
  (slot forward-apply       getter generate)
  (slot backward-count      getter generate)
  (slot backward-store      getter generate)
  (slot backward-apply      getter generate)
  
  
  (method override (initialize name id forward-count forward-store forward-apply backward-count backward-store backward-apply)
    (set! name~self name)
    (set! id~self id)
    (set! forward-count~self forward-count)
    (set! forward-store~self forward-store)
    (set! forward-apply~self forward-apply)
    (set! backward-count~self backward-count) 
    (set! backward-store~self backward-store)
    (set! backward-apply~self backward-apply))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" name)))))


(definition protected changes <table>
  (make-table test: eq?))

(definition protected changeids <vector>
  (make-vector 256 #f))


(definition next-change-id <fx>
  0)

(definition (next-change-id!)
  (prog1 next-change-id
    (increase! next-change-id)))


(definition protected (register-change name (forward-count: forward-count #f) (forward-store: forward-store #f) (forward-apply: forward-apply #f) (backward-count: backward-count #f) (backward-store: backward-store #f) (backward-apply: backward-apply #f))
  (let ((id (next-change-id!)))
    (let ((change (new Change name id forward-count forward-store forward-apply backward-count backward-store backward-apply)))
      (table-set! changes name change)
      (vector-set! changeids id change)
      (unspecified))))


(definition public inline (name->change name) <Change>
  (table-ref changes name))

(definition public inline (id->change id <fx>) <Change>
  (vector-ref changeids id))


(definition public (bidirectional-change name time forward backward (metadata: metadata #f))
  (let ((history (current-history)))
    (let ((change (name->change name)))
      (let ((id (get-id~ change))
            (forward-store (get-forward-store~ change))
            (backward-store (get-backward-store~ change)))
        (add-change~ history id
          (lambda (push)
            (push time)
            (forward forward-store push))
          (lambda (push)
            (push time)
            (backward backward-store push))
          metadata)))))


(definition public (bidirectional-size change)
  (let ((time 1)
        (forward (get-forward-count~ change))
        (backward (get-backward-count~ change)))
    (+ time forward time backward)))


(definition protected (max-bidirectional-size)
  (let ((max -1))
    (iterate-table changes
      (lambda (name change)
        (let ((size (bidirectional-size change)))
          (when (> size max)
            (set! max size)))))
    max))


(definition public (forward-count id)
  (get-forward-count~ (id->change id)))
  

(definition public (forward-change id pop)
  (let ((change (id->change id)))
    (let ((forward (get-forward-apply~ change))
          (time (pop)))
      (forward pop time))))


(definition public (backward-change id pop)
  (let ((change (id->change id)))
    (let ((backward (get-backward-apply~ change))
          (time (pop)))
      (backward pop time))))


(definition protected (forward-changes changes)
  (let ((len (f64vector-length changes))
        (offset 0))
    (let (iter)
      (when (< offset len)
        (let ((clnt-id (flonum->fixnum (f64vector-ref changes offset)))
              (change-id (flonum->fixnum (f64vector-ref changes (+ offset 1)))))
          (increase! offset 2)
          ;; ignore our own changes
          (if (and client-no (= client-no clnt-id))
              ;; also skip over the time
              (increase! offset (+ 1 (forward-count change-id)))
            (forward-change change-id
              (lambda ()
                (prog1 (f64vector-ref changes offset)
                  (increase! offset))))))
        (iter))))))
