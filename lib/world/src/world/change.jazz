;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Changes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Alain Marcotte


(module world.change jazz


(import (world)
        (world.autoload)
        (world.syntax (phase syntax)))


(class Change extends Object
  
  
  (slot name      getter generate)
  (slot id   <fx> getter generate)
  (slot forward   getter generate)
  (slot backward  getter generate)
  
  
  (method override (initialize name id forward backward)
    (set! name~self name)
    (set! id~self id)
    (set! forward~self forward)
    (set! backward~self backward))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" name)))))


(definition protected changes <table>
  (make-table test: eq?))

(definition protected changeids <vector>
  (make-vector 256 #f))


(definition next-change-id <fx>
  0)

(definition (next-change-id!)
  (prog1 next-change-id
    (increase! next-change-id)))


(definition protected (register-change name (forward: forward #f) (backward: backward #f))
  (let ((id (next-change-id!)))
    (let ((change (new Change name id forward backward)))
      (table-set! changes name change)
      (vector-set! changeids id change)
      (unspecified))))


(definition public inline (name->change name) <Change>
  (table-ref changes name))

(definition public inline (id->change id <fx>) <Change>
  (vector-ref changeids id))


(definition public (change name forward backward (metadata: metadata #f))
  (let ((history (current-history)))
    (let ((change (name->change name)))
      (add-change~ history (get-id~ change) forward backward metadata))))
  

(definition public (forward-change id next)
  (let ((change (id->change id)))
    (let ((forward (get-forward~ change)))
      (forward next))))


(definition public (backward-change id next)
  (let ((change (id->change id)))
    (let ((backward (get-backward~ change)))
      (backward next)))))
