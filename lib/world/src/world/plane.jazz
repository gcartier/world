;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Planes
;;;


(module world.plane jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.geometry))


;;;
;;;; Plane
;;;


(class Plane extends Object
  
  
  (slot origin <f32vector> initialize #f getter generate)
  (slot normal <f32vector> initialize #f getter generate)
  
  
  (method override (initialize origin normal)
    (nextmethod)
    (set! origin~self origin)
    (set! normal~self normal))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a} {a}" origin normal))))
  
  
  (method public (set-origin orig)
    (set! origin orig))
  
  
  (method public (set-vector vec)
    (set! normal (vertex-normalize vec)))
  
  
  (method public (distance-to-point point) <fl>
    (receive (intersect? t) (intersect (new Movement point (vertex-negate normal)))
      t))
  
  
  (method public (intersect r)
    (let ((denom (dot-product normal (get-normal~ r))))
      (if (= denom 0.)
          (values #f 0.)
        (let ((numer (dot-product normal (get-origin~ r)))
              (distance (- (dot-product origin normal))))
          (values #t (- (/ (+ numer distance) denom)))))))
  
  
  (method public (closest point) <f32vector>
    (let ((t (distance-to-point point)))
      (vertex- point (vertex-scalar* normal t))))))
