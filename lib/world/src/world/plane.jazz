;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Planes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Alain Marcotte


(module world.plane jazz


(import (world.geometry.base)
        (world.geometry.classes)
        (world.syntax (phase syntax)))


(proclaim (warn optimizations))

(declare (optimize-dead-local-variables))


;;;
;;;; Plane
;;;


(proclaim (not warn optimizations))


(class Plane-Class extends (:class Object)
  
  
  (method override (marshall-object self object)
    (serialize-object (class-of object)
                      (vector (serialize (get-origin object))
                              (serialize (get-normal object)))))
  
  
  (method override (unmarshall-object self content)
    (bind-vector (origin normal) content
      (allocate self
                (deserialize origin)
                (deserialize normal)))))


(proclaim (warn optimizations))


(class Plane metaclass Plane-Class extends Object
  
  
  (slot origin <vertex> initialize (make-vertex) getter generate)
  (slot normal <vertex> initialize (make-vertex) getter generate)
  
  
  (method override (initialize self origin normal)
    (nextmethod self)
    (vertex-copy! self.origin origin)
    (vertex-copy! self.normal normal))
  
  
  (method override (assemble self origin normal)
    (vertex-copy! self.origin origin)
    (vertex-copy! self.normal normal))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a} {a}" (present-vertex origin) (present-vertex normal)))))
  
  
  (method public (set-origin self orig)
    (vertex-copy! origin orig))
  
  
  (method public (set-normal self nrm)
    (vertex-copy! normal nrm))
  
  
  (method public (set-vector self vec)
    (vertex-normalize! normal vec)))


(definition public (allocate-plane)
  (allocate Plane
            (make-vertex)
            (make-vertex)))


;;;
;;;; Pool
;;;


(constant planes-cardinality <fx>
  4096)

(definition protected planes-pool <vector>
  (make-vector planes-cardinality #f))

(definition protected planes-allocated <fx>
  0)

(definition protected planes-available <fx>
  0)


(definition protected (alloc-plane$) <Plane>
  (if (= planes-available 0)
      (assert (< planes-allocated planes-cardinality)
        (increase! planes-allocated)
        (allocate-plane))
    (decrease! planes-available)
    (let ((plane (vector-ref planes-pool planes-available)))
      (vector-set! planes-pool planes-available #f)
      plane)))


(definition protected (make-plane$ origin normal) <Plane>
  (let ((plane (alloc-plane$)))
    (assemble plane origin normal)
    plane))


(definition protected (free-plane$ plane <Plane>) <void>
  (assert (< planes-available planes-cardinality))
  (vector-set! planes-pool planes-available plane)
  (increase! planes-available)))
