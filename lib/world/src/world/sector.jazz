;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Sectors
;;;


(module world.sector jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (time)
        (world)
        (world.autoload)
        (world.actor)
        (world.array)
        (world.block)
        (world.client.window)
        (world.foreign)
        (world.geometry)
        (world.history)
        (world.object)
        (world.sprite)
        (world.texture)
        (world.window))


;;;
;;;; Sector
;;;


(class Sector extends Object
  
  
  (slot index         <list>    getter generate)
  (slot blocks        <list>    accessors generate)
  (slot vao           <object>  getter generate)
  (slot vao-uptodate? <bool>    accessors generate)
  
  
  (method override (initialize index)
    (set! index~self index)
    (set! blocks~self '())
    (set! vao~self (new VAO))
    (set! vao-uptodate?~self #f))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (let ((blocks (length blocks)))
          (format output "{l} with {a} block{a}" index blocks (format-plural blocks))))))
  
  
  ;;;
  ;;;; VAO
  ;;;
  
  
  (method (update-vao)
    (when (not vao-uptodate?)
      (let ((vbo-vertices
              (let ((all-faces (map get-faces~ blocks)))
                (let ((triangles-count 0)
                      (triangle-floats (* 3 3 3)))
                  (for-each (lambda (faces)
                              (for-each (lambda (face)
                                          (increase! triangles-count (length (get-triangles~ face))))
                                        faces))
                            all-faces)
                  (let ((f32 (make-f32vector (* triangles-count triangle-floats))))
                    (let ((n 0))
                      (define (add-vertex vert)
                        (f32vector-set! f32 n (vertex-x vert))
                        (f32vector-set! f32 (+ n 1) (vertex-y vert))
                        (f32vector-set! f32 (+ n 2) (vertex-z vert))
                        (set! n (+ n 3)))
                      
                      (for-each (lambda (faces)
                                  (for-each (lambda (face)
                                              (for-each (lambda (triangle)
                                                          (let ((normal (get-normal~ triangle)))
                                                            (add-vertex (get-v1~ triangle)) (add-vertex (get-t1~ triangle)) (add-vertex normal)
                                                            (add-vertex (get-v2~ triangle)) (add-vertex (get-t2~ triangle)) (add-vertex normal)
                                                            (add-vertex (get-v3~ triangle)) (add-vertex (get-t3~ triangle)) (add-vertex normal)))
                                                        (get-triangles~ face)))
                                            faces))
                                all-faces))
                    f32)))))
        (generate-buffer~ vao)
        (set-vbo~ vao vbo-vertices)
        (set! vao-uptodate? #t))))
  
  
  (method (free-vao)
    (free-resources~ vao))))
