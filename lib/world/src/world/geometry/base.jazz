;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Geometry Base
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.geometry.base jazz


(import (world.geometry.classes)
        (world.geometry.syntax (phase syntax))
        (world.homogeneous))


(proclaim (warn optimizations))

(declare (optimize-dead-local-variables))


;;;
;;;; Vertex
;;;


(definition public (vertex+ v1 <vertex> v2 <vertex>) <vertex>
  (vertex
    (+ (vertex-x v1) (vertex-x v2))
    (+ (vertex-y v1) (vertex-y v2))
    (+ (vertex-z v1) (vertex-z v2))))


(definition public (vertex+! res <vertex> v1 <vertex> v2 <vertex>) <vertex>
  (vertex-x-set! res (+ (vertex-x v1) (vertex-x v2)))
  (vertex-y-set! res (+ (vertex-y v1) (vertex-y v2)))
  (vertex-z-set! res (+ (vertex-z v1) (vertex-z v2)))
  res)


(definition public (vertex-increase! res <vertex> v <vertex>) <void>
  (vertex-x-set! res (+ (vertex-x res) (vertex-x v)))
  (vertex-y-set! res (+ (vertex-y res) (vertex-y v)))
  (vertex-z-set! res (+ (vertex-z res) (vertex-z v))))


(definition public (vertex+xyz v <vertex> x <fl> y <fl> z <fl>) <vertex>
  (vertex
    (+ (vertex-x v) x)
    (+ (vertex-y v) y)
    (+ (vertex-z v) z)))


(definition public (vertex+xyz! res <vertex> v <vertex> x <fl> y <fl> z <fl>) <vertex>
  (vertex! res
           (+ (vertex-x v) x)
           (+ (vertex-y v) y)
           (+ (vertex-z v) z)))


(definition public (vertex- v1 <vertex> v2 <vertex>) <vertex>
  (vertex
    (- (vertex-x v1) (vertex-x v2))
    (- (vertex-y v1) (vertex-y v2))
    (- (vertex-z v1) (vertex-z v2))))


(definition public (vertex-! res <vertex> v1 <vertex> v2 <vertex>) <vertex>
  (vertex-x-set! res (- (vertex-x v1) (vertex-x v2)))
  (vertex-y-set! res (- (vertex-y v1) (vertex-y v2)))
  (vertex-z-set! res (- (vertex-z v1) (vertex-z v2)))
  res)


(definition public (vertex-decrease! res <vertex> v <vertex>) <void>
  (vertex-x-set! res (- (vertex-x res) (vertex-x v)))
  (vertex-y-set! res (- (vertex-y res) (vertex-y v)))
  (vertex-z-set! res (- (vertex-z res) (vertex-z v))))


(definition public (vertex-negate v <vertex>) <vertex>
  (vertex
    (- (vertex-x v))
    (- (vertex-y v))
    (- (vertex-z v))))


(definition public (vertex-negate! res <vertex> v <vertex>) <vertex>
  (vertex-x-set! res (- (vertex-x v)))
  (vertex-y-set! res (- (vertex-y v)))
  (vertex-z-set! res (- (vertex-z v)))
  res)


(definition public (vertex* v1 <vertex> v2 <vertex>) <vertex>
  (vertex
    (* (vertex-x v1) (vertex-x v2))
    (* (vertex-y v1) (vertex-y v2))
    (* (vertex-z v1) (vertex-z v2))))


(definition public (vertex*! res <vertex> v1 <vertex> v2 <vertex>) <vertex>
  (vertex-x-set! res (* (vertex-x v1) (vertex-x v2)))
  (vertex-y-set! res (* (vertex-y v1) (vertex-y v2)))
  (vertex-z-set! res (* (vertex-z v1) (vertex-z v2)))
  res)


(definition public (vertex/ v1 <vertex> v2 <vertex>) <vertex>
  (vertex
    (/ (vertex-x v1) (vertex-x v2))
    (/ (vertex-y v1) (vertex-y v2))
    (/ (vertex-z v1) (vertex-z v2))))


(definition public (vertex/! res <vertex> v1 <vertex> v2 <vertex>) <vertex>
  (vertex-x-set! res (/ (vertex-x v1) (vertex-x v2)))
  (vertex-y-set! res (/ (vertex-y v1) (vertex-y v2)))
  (vertex-z-set! res (/ (vertex-z v1) (vertex-z v2)))
  res)


(definition public (vertex-inverse v <vertex>) <vertex>
  (vertex
    (/ (vertex-x v))
    (/ (vertex-y v))
    (/ (vertex-z v))))


(definition public (vertex-inverse! res <vertex> v <vertex>) <vertex>
  (vertex-x-set! res (/ (vertex-x v)))
  (vertex-y-set! res (/ (vertex-y v)))
  (vertex-z-set! res (/ (vertex-z v)))
  res)


(definition public (vertex-scalar* v <vertex> r <fv>) <vertex>
  (vertex
    (* r (vertex-x v))
    (* r (vertex-y v))
    (* r (vertex-z v))))


(definition public (vertex-scalar*! res <vertex> v <vertex> r <fv>) <vertex>
  (vertex-x-set! res (* r (vertex-x v)))
  (vertex-y-set! res (* r (vertex-y v)))
  (vertex-z-set! res (* r (vertex-z v)))
  res)


(definition public (vertex-scalar/ v <vertex> r <fv>) <vertex>
  (vertex
    (/ (vertex-x v) r)
    (/ (vertex-y v) r)
    (/ (vertex-z v) r)))


(definition public (vertex-scalar/! res <vertex> v <vertex> r <fv>) <vertex>
  (vertex-x-set! res (/ (vertex-x v) r))
  (vertex-y-set! res (/ (vertex-y v) r))
  (vertex-z-set! res (/ (vertex-z v) r))
  res)


(definition public (vertex-norm v <vertex>) <fl>
  (let ((x (vertex-x v))
        (y (vertex-y v))
        (z (vertex-z v)))
    (sqrt (+ (* x x)
             (* y y)
             (* z z)))))


(definition public (vertex-norm! res <f64vector> v <vertex>) <f64vector>
  (f64-set! res (let ((x (vertex-x v))
                      (y (vertex-y v))
                      (z (vertex-z v)))
                  (sqrt (+ (* x x)
                           (* y y)
                           (* z z)))))
  res)


(definition public (vertex-abs v <vertex>) <vertex>
  (vertex (abs (vertex-x v))
          (abs (vertex-y v))
          (abs (vertex-z v))))


(definition public (vertex-abs! res <vertex> v <vertex>) <vertex>
  (vertex! res
           (abs (vertex-x v))
           (abs (vertex-y v))
           (abs (vertex-z v)))
  res)


(definition public (vertex-normalize v <vertex>) <vertex>
  (vertex-scalar* v (f64& (/ (vertex-norm& v)))))


(definition public (vertex-normalize! res <vertex> v <vertex>) <vertex>
  (vertex-scalar*! res v (f64& (/ (vertex-norm& v)))))


(definition public (vertex-normalize-safe v <vertex>) <vertex>
  (let ((norm (vertex-norm& v)))
    (if (= norm 0.)
        v
      (vertex-scalar* v (/ norm)))))


(definition public (vertex-normalize-safe! res <vertex> v <vertex>) <vertex>
  (let ((norm (vertex-norm& v)))
    (if (= norm 0.)
        (vertex-copy! res v)
      (vertex-scalar*! res v (f64& (/ norm)))))
  res)


(definition public (vertex-max v <vertex>) <fl>
  (max (vertex-x v)
       (vertex-y v)
       (vertex-z v)))


(definition public (vertex-lerp p <vertex> q <vertex> t <fv>) <vertex>
  (vertex-lerp! (make-vertex) p q t))


(definition public (vertex-lerp! res <vertex> p <vertex> q <vertex> t <fv>) <vertex>
  (vertex+! res (vertex-scalar*& p (f64& (- 1. t))) (vertex-scalar*& q t))
  res))
