;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Geometry Base
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.geometry.base jazz


(import (world.geometry.classes)
        (world.homogeneous))


(proclaim (warn optimizations))

(declare (optimize-dead-local-variables))


;;;
;;;; Vertex
;;;


(definition public (vertex+ v1 <f64vector> v2 <f64vector>) <f64vector>
  (vertex
    (+ (vertex-x v1) (vertex-x v2))
    (+ (vertex-y v1) (vertex-y v2))
    (+ (vertex-z v1) (vertex-z v2))))


(definition public (vertex+xyz v <f64vector> x <fl> y <fl> z <fl>) <f64vector>
  (vertex
    (+ (vertex-x v) x)
    (+ (vertex-y v) y)
    (+ (vertex-z v) z)))


(definition public (vertex+! res <f64vector> v1 <f64vector> v2 <f64vector>) <f64vector>
  (vertex-x-set! res (+ (vertex-x v1) (vertex-x v2)))
  (vertex-y-set! res (+ (vertex-y v1) (vertex-y v2)))
  (vertex-z-set! res (+ (vertex-z v1) (vertex-z v2)))
  res)


(definition public (vertex-increase! res <f64vector> v <f64vector>) <void>
  (vertex-x-set! res (+ (vertex-x res) (vertex-x v)))
  (vertex-y-set! res (+ (vertex-y res) (vertex-y v)))
  (vertex-z-set! res (+ (vertex-z res) (vertex-z v))))


(definition public (vertex- v1 <f64vector> v2 <f64vector>) <f64vector>
  (vertex
    (- (vertex-x v1) (vertex-x v2))
    (- (vertex-y v1) (vertex-y v2))
    (- (vertex-z v1) (vertex-z v2))))


(definition public (vertex-! res <f64vector> v1 <f64vector> v2 <f64vector>) <f64vector>
  (vertex-x-set! res (- (vertex-x v1) (vertex-x v2)))
  (vertex-y-set! res (- (vertex-y v1) (vertex-y v2)))
  (vertex-z-set! res (- (vertex-z v1) (vertex-z v2)))
  res)


(definition public (vertex-decrease! res <f64vector> v <f64vector>) <void>
  (vertex-x-set! res (- (vertex-x res) (vertex-x v)))
  (vertex-y-set! res (- (vertex-y res) (vertex-y v)))
  (vertex-z-set! res (- (vertex-z res) (vertex-z v))))


(definition public (vertex-negate v <f64vector>) <f64vector>
  (vertex
    (- (vertex-x v))
    (- (vertex-y v))
    (- (vertex-z v))))


(definition public (vertex-negate! res <f64vector> v <f64vector>) <f64vector>
  (vertex-x-set! res (- (vertex-x v)))
  (vertex-y-set! res (- (vertex-y v)))
  (vertex-z-set! res (- (vertex-z v)))
  res)


(definition public (vertex* v1 <f64vector> v2 <f64vector>) <f64vector>
  (vertex
    (* (vertex-x v1) (vertex-x v2))
    (* (vertex-y v1) (vertex-y v2))
    (* (vertex-z v1) (vertex-z v2))))


(definition public (vertex*! res <f64vector> v1 <f64vector> v2 <f64vector>) <f64vector>
  (vertex-x-set! res (* (vertex-x v1) (vertex-x v2)))
  (vertex-y-set! res (* (vertex-y v1) (vertex-y v2)))
  (vertex-z-set! res (* (vertex-z v1) (vertex-z v2)))
  res)


(definition public (vertex/ v1 <f64vector> v2 <f64vector>) <f64vector>
  (vertex
    (/ (vertex-x v1) (vertex-x v2))
    (/ (vertex-y v1) (vertex-y v2))
    (/ (vertex-z v1) (vertex-z v2))))


(definition public (vertex/! res <f64vector> v1 <f64vector> v2 <f64vector>) <f64vector>
  (vertex-x-set! res (/ (vertex-x v1) (vertex-x v2)))
  (vertex-y-set! res (/ (vertex-y v1) (vertex-y v2)))
  (vertex-z-set! res (/ (vertex-z v1) (vertex-z v2)))
  res)


(definition public (vertex-inverse v <f64vector>) <f64vector>
  (vertex
    (/ (vertex-x v))
    (/ (vertex-y v))
    (/ (vertex-z v))))


(definition public (vertex-inverse! res <f64vector> v <f64vector>) <f64vector>
  (vertex-x-set! res (/ (vertex-x v)))
  (vertex-y-set! res (/ (vertex-y v)))
  (vertex-z-set! res (/ (vertex-z v)))
  res)


(definition public (vertex-scalar* v <f64vector> r <fl>) <f64vector>
  (vertex
    (* r (vertex-x v))
    (* r (vertex-y v))
    (* r (vertex-z v))))


(definition public (vertex-scalar*! res <f64vector> v <f64vector> r <fl>) <f64vector>
  (vertex-x-set! res (* r (vertex-x v)))
  (vertex-y-set! res (* r (vertex-y v)))
  (vertex-z-set! res (* r (vertex-z v)))
  res)


(definition public (vertex-scalar/ v <f64vector> r <fl>) <f64vector>
  (vertex
    (/ (vertex-x v) r)
    (/ (vertex-y v) r)
    (/ (vertex-z v) r)))


(definition public (vertex-scalar/! res <f64vector> v <f64vector> r <fl>) <f64vector>
  (vertex-x-set! res (/ (vertex-x v) r))
  (vertex-y-set! res (/ (vertex-y v) r))
  (vertex-z-set! res (/ (vertex-z v) r))
  res)


(definition public (vertex-norm v <f64vector>) <fl>
  (let ((x (vertex-x v))
        (y (vertex-y v))
        (z (vertex-z v)))
    (sqrt (+ (* x x)
             (* y y)
             (* z z)))))


(definition public (vertex-norm! res <f64vector> v <f64vector>) <f64vector>
  (f64-set! res (let ((x (vertex-x v))
                      (y (vertex-y v))
                      (z (vertex-z v)))
                  (sqrt (+ (* x x)
                           (* y y)
                           (* z z)))))
  res)


(definition public (vertex-abs v <f64vector>) <f64vector>
  (vertex (abs (vertex-x v))
          (abs (vertex-y v))
          (abs (vertex-z v))))


(definition public (vertex-abs! res <f64vector> v <f64vector>) <f64vector>
  (vertex! res
           (abs (vertex-x v))
           (abs (vertex-y v))
           (abs (vertex-z v)))
  res)


(definition public (vertex-normalize v <f64vector>) <f64vector>
  (vertex-scalar* v (/ (vertex-norm v))))


(definition public (vertex-normalize! res <f64vector> v <f64vector>) <f64vector>
  (vertex-scalar*! res v (/ (vertex-norm v))))


(definition public (vertex-normalize-safe v <f64vector>) <f64vector>
  (let ((norm (vertex-norm v)))
    (if (= norm 0.)
        v
      (vertex-scalar* v (/ norm)))))


(definition public (vertex-normalize-safe! res <f64vector> v <f64vector>) <f64vector>
  (let ((norm (vertex-norm v)))
    (if (= norm 0.)
        (vertex-copy! res v)
      (vertex-scalar*! res v (/ norm))))
  res)


(definition public (vertex-max v <f64vector>) <fl>
  (max (vertex-x v)
       (vertex-y v)
       (vertex-z v)))


(definition public (vertex-lerp p <f64vector> q <f64vector> t <fl>) <f64vector>
  (vertex-lerp! (make-vertex) p q t))


(definition public (vertex-lerp! res <f64vector> p <f64vector> q <f64vector> t <fl>) <f64vector>
  (vertex+! res (vertex-scalar* p (- 1. t)) (vertex-scalar* q t))
  res))
