;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Biome Layers
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


;; TODO
;; - straiten the for i = 0; i < areaHeight


(module world.layer jazz


(import (world.biome)
        (world.foreign))


(proclaim (warn optimizations))


;;;
;;;; Layer
;;;


(class Layer extends Object
  
  
  (slot parent    <Layer> getter generate)
  (slot chunkSeed getter generate)
  
  
  (method override (initialize self parent)
    (set! self.parent parent))
  
  
  (method public virtual (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    ))


;;;
;;;; Ocean
;;;


(class Ocean-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((layer (make-s32vector (* width height))))
      (loop (for i from 0 below height)
            (loop (for j from 0 below width)
                  (initChunkSeed (+ j x) (+ i y))
                  (s32vector-set! layer (+ j (* i width)) (if (= (nextInt 10) 0) 1 0))))
      layer)))


(class Remove-Too-Much-Ocean-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (let ((k1 (s32vector-ref pint (+ j1 1 (* (- (+ i1 1) 1) (+ width 2)))))
                          (l1 (s32vector-ref pint (+ j1 1 1 (* (+ i1 1) (+ width 2)))))
                          (i2 (s32vector-ref pint (+ (- (+ j1 1) 1) (* (+ i1 1) (+ width 2)))))
                          (j2 (s32vector-ref pint (+ j1 1 (* (+ i1 1 1) (+ width 2)))))
                          (k2 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                      (s32vector-set! aint (+ j1 (* i1 width)) k2)
                      (initChunkSeed (+ j1 x) (+ i1 y))
                      (when (and (= k2 0)
                                 (= k1 0)
                                 (= l1 0)
                                 (= i2 0)
                                 (= j2 0)
                                 (= (nextInt 2) 0))
                        (s32vector-set! aint (+ j1 (* i1 width)) 1)))))
        aint))))


(class Deep-Ocean-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate parent i j k l))
            (aint (make-s32vector (* width height))))
        (deepOceanLayer x y width height pint aint)
        aint))))


;;;
;;;; Islands
;;;


(class Islands-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (let ((k1 (s32vector-ref pint (+ j1 0 (* (+ i1 0) k))))
                          (l1 (s32vector-ref pint (+ j1 2 (* (+ i1 0) k))))
                          (i2 (s32vector-ref pint (+ j1 0 (* (+ i1 2) k))))
                          (j2 (s32vector-ref pint (+ j1 2 (* (+ i1 2) k))))
                          (k2 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                      (initChunkSeed (+ j1 x) (+ i1 y))
                      (if (or (/= k2 0)
                              (and (= k1 0)
                                   (= l1 0)
                                   (= i2 0)
                                   (= j2 0)))
                          (if (and (> k2 0)
                                   (or (= k1 0)
                                       (= l1 0)
                                       (= i2 0)
                                       (= j2 0)))
                              (if (= (nextInt 5) 0)
                                  (if (= k2 4)
                                      (s32vector-set! aint (+ j1 (* i1 width)) 4)
                                    (s32vector-set! aint (+ j1 (* i1 width)) 0))
                                (s32vector-set! aint (+ j1 (* i1 width)) k2))
                            (s32vector-set! aint (+ j1 (* i1 width)) k2))
                        (let ((l2 1)
                              (i3 1))
                          (when (and (/= k1 0)
                                     (let ((val l2))
                                       (increase! l2)
                                       (= (nextInt val) 0)))
                            (set! i3 k1))
                          (when (and (/= l1 0)
                                     (let ((val l2))
                                       (increase! l2)
                                       (= (nextInt val) 0)))
                            (set! i3 l1))
                          (when (and (/= i2 0)
                                     (let ((val l2))
                                       (increase! l2)
                                       (= (nextInt val) 0)))
                            (set! i3 i2))
                          (when (and (/= j2 0)
                                     (let ((val l2))
                                       (increase! l2)
                                       (= (nextInt val) 0)))
                            (set! i3 j2))
                          (if (= (nextInt 3) 0)
                              (s32vector-set! aint (+ j1 (* i1 width)) i3)
                            (if (= i3 4)
                                (s32vector-set! aint (+ j1 (* i1 width)) 4)
                              (s32vector-set! aint (+ j1 (* i1 width)) 0))))))))
        aint))))


(class Mushroom-Islands-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate parent i j k l))
            (aint (make-s32vector (* width height))))
        (mushroomIslandsLayer x y width height pint aint)
        aint))))


;;;
;;;; Regions
;;;


(class Regions-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate parent i j k l)))
        (let ((aint (make-s32vector (* width height))))
          (loop (for i1 from 0 below height)
                (loop (for j1 from 0 below width)
                      (let ((k1 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                        (initChunkSeed (+ j1 x) (+ i1 y))
                        (if (= k1 0)
                            (s32vector-set! aint (+ j1 (* i1 width)) 0)
                          ;; hack around (nextInt 6) returning almost always 0 2 4
                          (let ((l1 (modulo (nextInt 299999) 6)))
                            (if (= l1 0)
                                (set! l1 4)
                              (if (<= l1 1)
                                  (set! l1 3)
                                (set! l1 1)))
                            (s32vector-set! aint (+ j1 (* i1 width)) l1))))))
          aint)))))


;;;
;;;; CoolWarm
;;;


(class CoolWarm-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ 1 width 1))
          (l (+ 1 height 1)))
      (let ((pint (generate parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (initChunkSeed (+ j1 x) (+ i1 y))
                    (let ((k1 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                      (when (= k1 1)
                        (let ((l1 (s32vector-ref pint (+ j1 1 (* (- (+ i1 1) 1) k))))
                              (i2 (s32vector-ref pint (+ j1 1 1 (* (+ i1 1) k))))
                              (j2 (s32vector-ref pint (+ (- (+ j1 1) 1) (* (+ i1 1) k))))
                              (k2 (s32vector-ref pint (+ j1 1 (* (+ i1 1 1) k)))))
                          (let ((flag
                                  (or (= l1 3)
                                      (= i2 3)
                                      (= j2 3)
                                      (= k2 3))))
                            (let ((flag1
                                    (or (= l1 4)
                                        (= i2 4)
                                        (= j2 4)
                                        (= k2 4))))
                              (when (or flag flag1)
                                (set! k1 2))))))
                      (s32vector-set!
                        aint
                        (+ j1 (* i1 width))
                        k1))))
        aint))))


;;;
;;;; HeatIce
;;;


(class HeatIce-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ 1 width 1))
          (l (+ 1 height 1)))
      (let ((pint (generate parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (let ((k1 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                      (when (= k1 4)
                        (let ((l1 (s32vector-ref pint (+ j1 1 (* (- (+ i1 1) 1) k))))
                              (i2 (s32vector-ref pint (+ j1 1 1 (* (+ i1 1) k))))
                              (j2 (s32vector-ref pint (+ (- (+ j1 1) 1) (* (+ i1 1) k))))
                              (k2 (s32vector-ref pint (+ j1 1 (* (+ i1 1 1) k)))))
                          (let ((flag
                                  (or (= l1 2)
                                      (= i2 2)
                                      (= j2 2)
                                      (= k2 2))))
                            (let ((flag1
                                    (or (= l1 1)
                                        (= i2 1)
                                        (= j2 1)
                                        (= k2 1))))
                              (when (or flag1 flag)
                                (set! k1 3))))))
                      (s32vector-set!
                        aint
                        (+ j1 (* i1 width))
                        k1))))
        aint))))


;;;
;;;; Special
;;;


(class Special-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((pint (generate parent x y width height))
          (aint (make-s32vector (* width height))))
      (loop (for i from 0 below height)
            (loop (for j from 0 below width)
                  (initChunkSeed (+ j x) (+ i y))
                  (let ((k (s32vector-ref pint (+ j (* i width)))))
                    (when (and (/= k 0) (= (nextInt 13) 0))
                      (bitwise-ior!
                        k
                        (bitwise-and
                          (arithmetic-shift (+ 1 (nextInt 15)) 8)
                          3840)))
                    (s32vector-set! aint (+ j (* i width)) k))))
      aint)))


;;;
;;;; Zoom
;;;


(class Zoom-Layer extends Layer
  
  
  (method protected virtual (fuzzy? self)
    #f)
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (arithmetic-shift x -1))
          (j (arithmetic-shift y -1))
          (k (+ (arithmetic-shift width -1) 2))
          (l (+ (arithmetic-shift height -1) 2)))
      (let ((pint (generate parent i j k l))
            (i1 (arithmetic-shift (- k 1) 1))
            (j1 (arithmetic-shift (- l 1) 1)))
        (let ((aint (make-s32vector (* i1 j1)))
              (aint2 (make-s32vector (* width height))))
          (zoomLayer x y width height pint aint aint2 (fuzzy? self))
          aint2))))
  
  
  @jazz
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (declare (proper-tail-calls))
    (declare (optimize-dead-local-variables))
    (declare (inline))
    (declare (inlining-limit 1000))
    (let ()
      (define (selectRandom a <fx> b <fx>) <fx>
        (if (= (nextInt 2) 0)
            a
          b))
      
      (let ((i (arithmetic-shift x -1))
            (j (arithmetic-shift y -1))
            (k (+ (arithmetic-shift width -1) 2))
            (l (+ (arithmetic-shift height -1) 2)))
        (let ((pint (generate parent i j k l))
              (i1 (arithmetic-shift (- k 1) 1))
              (j1 (arithmetic-shift (- l 1) 1)))
          (let ((aint (make-s32vector (* i1 j1))))
            (loop (for k1 from 0 below (- l 1))
                  (let ((l1 (* (arithmetic-shift k1 1) i1))
                        (i2 0))
                    (let ((j2 (s32vector-ref pint (+ i2 0 (* (+ k1 0) k))))
                          (k2 (s32vector-ref pint (+ i2 0 (* (+ k1 1) k)))))
                      (loop (for i2 from 0 below (- k 1))
                            (initChunkSeed
                              (arithmetic-shift (+ i2 i) 1)
                              (arithmetic-shift (+ k1 j) 1))
                            (let ((l2 (s32vector-ref pint (+ i2 1 (* (+ k1 0) k))))
                                  (m2 (s32vector-ref pint (+ i2 1 (* (+ k1 1) k)))))
                              (s32vector-set! aint l1 j2)
                              (s32vector-set! aint (+ l1 i1) (selectRandom j2 k2))
                              (increase! l1)
                              (s32vector-set! aint l1 (selectRandom j2 l2))
                              (s32vector-set! aint (+ l1 i1) (selectModeOrRandom j2 l2 k2 m2))
                              (increase! l1)
                              (set! j2 l2)
                              (set! k2 m2))))))
            (let ((aint2 (make-s32vector (* width height))))
              (loop (for j3 from 0 below height)
                    (let ((src-start (+ (* (+ j3 (bitwise-and y 1)) i1) (bitwise-and x 1))))
                      (subs32vector-move! aint src-start (+ src-start width) aint2 (* j3 width))))
              aint2))))))
  
  
  @jazz
  (method protected virtual (selectModeOrRandom a <fx> b <fx> c <fx> d <fx>) <fx>
    (cond ((and (= b c) (= c d)) b)
          ((and (= a b) (= a c)) a)
          ((and (= a b) (= a d)) a)
          ((and (= a c) (= a d)) a)
          ((and (= a b) (/= c d)) a)
          ((and (= a c) (/= b d)) a)
          ((and (= a d) (/= b c)) a)
          ((and (= b c) (/= a d)) b)
          ((and (= b d) (/= a c)) b)
          ((and (= c d) (/= a b)) c)
          (else (s32vector-ref (s32vector a b c d) (nextInt 4))))))


(class Fuzzy-Zoom-Layer extends Zoom-Layer
  
  
  (method override (fuzzy? self)
    #t)
  
  
  @jazz
  (method override (selectModeOrRandom a <fx> b <fx> c <fx> d <fx>) <fx>
    (s32vector-ref (s32vector a b c d) (nextInt 4))))


(definition public (magnify layer count)
  (loop (repeat count)
        (set! layer (new Zoom-Layer layer)))
  layer)


(class Voronoi-Zoom-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((x (- x 2))
          (y (- y 2)))
      (let ((i (arithmetic-shift x -2))
            (j (arithmetic-shift y -2))
            (k (+ (arithmetic-shift width -2) 2))
            (l (+ (arithmetic-shift height -2) 2)))
        (let ((pint (generate parent i j k l))
              (i1 (arithmetic-shift (- k 1) 2))
              (j1 (arithmetic-shift (- l 1) 2)))
          (let ((aint (make-s32vector (* i1 j1)))
                (aint2 (make-s32vector (* width height))))
            (voronoiLayer x y width height pint aint aint2)
            aint2))))))


;;;
;;;; Smooth
;;;


(class Smooth-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate parent i j k l))
            (aint (make-s32vector (* width height))))
        (smoothLayer x y width height pint aint)
        aint)))
  
  
  @jazz
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (let ((k1 (s32vector-ref pint (+ j1 0 (* (+ i1 1) k))))
                          (l1 (s32vector-ref pint (+ j1 2 (* (+ i1 1) k))))
                          (i2 (s32vector-ref pint (+ j1 1 (* (+ i1 0) k))))
                          (j2 (s32vector-ref pint (+ j1 1 (* (+ i1 2) k))))
                          (k2 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                      (cond ((and (= k1 l1) (= i2 j2))
                             (initChunkSeed (+ j1 x) (+ i1 y))
                             (if (= (nextInt 2) 0)
                                 (set! k2 k1)
                               (set! k2 i2)))
                            (else
                             (when (= k1 l1)
                               (set! k2 k1))
                             (when (= i2 j2)
                               (set! k2 i2))))
                      (s32vector-set! aint (+ j1 (* i1 width)) k2))))
          aint))))


;;;
;;;; River
;;;


(class Init-River-Layer extends Layer


  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((pint (generate parent x y width height))
          (aint (make-s32vector (* width height))))
      (loop (for i from 0 below height)
            (loop (for j from 0 below width)
                  (initChunkSeed (+ j x) (+ i y))
                  (s32vector-set! aint (+ j (* i width))
                    (if (> (s32vector-ref pint (+ j (* i width))) 0)
                        (+ (nextInt 299999) 2)
                      0))))
      aint)))


(class River-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (define (filter val <fx>) <fx>
      (if (>= val 2)
          (+ 2 (bitwise-and val 1))
        val))
    
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (let ((k1 (filter (s32vector-ref pint (+ j1 0 (* (+ i1 1) k)))))
                          (l1 (filter (s32vector-ref pint (+ j1 2 (* (+ i1 1) k)))))
                          (i2 (filter (s32vector-ref pint (+ j1 1 (* (+ i1 0) k)))))
                          (j2 (filter (s32vector-ref pint (+ j1 1 (* (+ i1 2) k)))))
                          (k2 (filter (s32vector-ref pint (+ j1 1 (* (+ i1 1) k))))))
                      (s32vector-set!
                        aint
                        (+ j1 (* i1 width))
                        (if (and (= k2 k1)
                                 (= k2 i2)
                                 (= k2 l1)
                                 (= k2 j2))
                            -1 ;; todo -1
                          River)))))
        aint))))


(class Mix-River-Layer extends Layer
  
  
  (slot biome-layer <Layer>)
  (slot river-layer <Layer>)
  
  
  (method override (initialize self biome-layer river-layer)
    (set! self.biome-layer biome-layer)
    (set! self.river-layer river-layer))
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((biome (generate biome-layer x y width height))
          (river (generate river-layer x y width height))
          (aint (make-s32vector (* width height))))
      (loop (for i from 0 below (* width height))
            (do (if (and (/= (s32vector-ref biome i) Ocean)
                         (/= (s32vector-ref biome i) DeepOcean))
                    (if (= (s32vector-ref river i) River)
                        (if (= (s32vector-ref biome i) IcePlains)
                            (s32vector-set! aint i FrozenRiver)
                          (if (and (/= (s32vector-ref biome i) MushroomIsland)
                                   (/= (s32vector-ref biome i) MushroomIslandShore))
                              (s32vector-set! aint i (bitwise-and (s32vector-ref river i) 255))
                            (s32vector-set! aint i MushroomIslandShore)))
                      (s32vector-set! aint i (s32vector-ref biome i)))
                  (s32vector-set! aint i (s32vector-ref biome i)))))
      aint)))


;;;
;;;; Biomes
;;;


(class Biomes-Layer extends Layer
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((layer (make-s32vector (* width height))))
      (let ((pint (generate parent x y width height))
            (aint (make-s32vector (* width height))))
        (loop (for i from 0 below height)
              (loop (for j from 0 below width)
                    (initChunkSeed (+ j x) (+ i y))
                    (let ((k (s32vector-ref pint (+ j (* i width)))))
                      (let ((l (arithmetic-shift (bitwise-and k 3840) -8)))
                        (set! k (bitwise-and k -3841))
                        (cond @wait ((and settings
                                    (>= (fixedBiome settings) 0))
                               (s32vector-set! aint (+ j (* i width)) (fixedBiome settings)))
                              ((oceanic? k)
                               (s32vector-set! aint (+ j (* i width)) k))
                              ((= k (get-id MUSHROOM_ISLAND))
                               (s32vector-set! aint (+ j (* i width)) k))
                              ((= k 1)
                               (if (> l 0)
                                   (if (= (nextInt 3) 0)
                                       (s32vector-set! aint (+ j (* i width)) (get-id MESA_PLATEAU))
                                     (s32vector-set! aint (+ j (* i width)) (get-id MESA_PLATEAU_F)))
                                 (s32vector-set! aint (+ j (* i width)) (vector-ref Warm-Biomes (nextInt (vector-length Warm-Biomes))))))
                              ((= k 2)
                               (if (> l 0)
                                   (s32vector-set! aint (+ j (* i width)) (get-id JUNGLE))
                                 (s32vector-set! aint (+ j (* i width)) (vector-ref Medium-Biomes (nextInt (vector-length Medium-Biomes))))))
                              ((= k 3)
                               (if (> l 0)
                                   (s32vector-set! aint (+ j (* i width)) (get-id MEGA_TAIGA))
                                 (s32vector-set! aint (+ j (* i width)) (vector-ref Cold-Biomes (nextInt (vector-length Cold-Biomes))))))
                              ((= k 4)
                               (s32vector-set! aint (+ j (* i width)) (vector-ref Ice-Biomes (nextInt (vector-length Ice-Biomes)))))
                              (else
                               (s32vector-set! aint (+ j (* i width)) (get-id MUSHROOM_ISLAND))))))))
        aint))))


(definition (oceanic? id <fx>)
  (or (= id Ocean)
      (= id DeepOcean)
      (= id FrozenOcean)))


;;;
;;;; Hills
;;;


(class Hills-Layer extends Layer
  
  
  (slot river-layer <Layer>)
  
  
  (method override (initialize self parent river-layer)
    (nextmethod self parent)
    (set! self.river-layer river-layer))
  
  
  (method override (generate self x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((pint (generate parent (- x 1) (- y 1) (+ width 2) (+ height 2)))
          (rint (generate river-layer (- x 1) (- y 1) (+ width 2) (+ height 2)))
          (aint (make-s32vector (* width height))))
      (loop (for i from 0 below height)
            (loop (for j from 0 below width)
                  (initChunkSeed (+ j x) (+ i y))
                  (let ((k (s32vector-ref pint (+ j 1 (* (+ i 1) (+ width 2)))))
                        (l (s32vector-ref rint (+ j 1 (* (+ i 1) (+ width 2))))))
                    (let ((flag (= (modulo (- l 2) 29) 0)))
                      (when (> k 255) (break))
                      (let ((biome (id->biome k)))
                        (let ((flag1 (and biome (mutation? biome))))
                          (if (and (/= k 0)
                                   (>= l 2)
                                   (= (modulo (- l 2) 29) 1)
                                   (not flag1))
                              (let ((biome3 (mutate biome)))
                                (s32vector-set!
                                  aint
                                  (+ j (* i width))
                                  (if (not biome3)
                                      k
                                    (get-id biome3))))
                            (if (and (/= (nextInt 3) 0) (not flag))
                                (s32vector-set! aint (+ j (* i width)) k)
                              (let ((biome1 biome))
                                (cond ((eq? biome DESERT)
                                       (set! biome1 DESERT_HILLS))
                                      ((eq? biome FOREST)
                                       (set! biome1 FOREST_HILLS))
                                      ((eq? biome BIRCH_FOREST)
                                       (set! biome1 BIRCH_FOREST_HILLS))
                                      ((eq? biome ROOFED_FOREST)
                                       (set! biome1 PLAINS))
                                      ((eq? biome TAIGA)
                                       (set! biome1 TAIGA_HILLS))
                                      ((eq? biome MEGA_TAIGA)
                                       (set! biome1 MEGA_TAIGA_HILLS))
                                      ((eq? biome COLD_TAIGA)
                                       (set! biome1 COLD_TAIGA_HILLS))
                                      ((eq? biome PLAINS)
                                       (if (= (nextInt 3) 0)
                                           (set! biome1 FOREST_HILLS)
                                         (set! biome1 FOREST)))
                                      ((eq? biome ICE_PLAINS)
                                       (set! biome1 ICE_MOUNTAINS))
                                      ((eq? biome JUNGLE)
                                       (set! biome1 JUNGLE_HILLS))
                                      ((eq? biome OCEAN)
                                       (set! biome1 DEEP_OCEAN))
                                      ((eq? biome EXTREME_HILLS)
                                       (set! biome1 EXTREME_HILLS_PLUS))
                                      ((eq? biome SAVANNA)
                                       (set! biome1 SAVANNA_PLATEAU))
                                      ((biomesEqualOrMesaPlateau self k (get-id MESA_PLATEAU_F))
                                       (set! biome1 MESA))
                                      (else
                                       (when (and (eq? biome DEEP_OCEAN)
                                                  (= (nextInt 3) 0))
                                         (let ((i1 (nextInt 2)))
                                           (if (= i1 0)
                                               (set! biome1 PLAINS)
                                             (set! biome1 FOREST))))))
                                (let ((j2 (get-id biome1)))
                                  (when (and flag (/= j2 k))
                                    (let ((biome2 (mutate biome1)))
                                      (set! j2
                                            (if (not biome2)
                                                k
                                              (get-id biome2)))))
                                  (if (= j2 k)
                                      (s32vector-set! aint (+ j (* i width)) k)
                                    (let ((k2 (s32vector-ref pint (+ j 1 (* (+ i 0) (+ width 2)))))
                                          (j1 (s32vector-ref pint (+ j 2 (* (+ i 1) (+ width 2)))))
                                          (k1 (s32vector-ref pint (+ j 0 (* (+ i 1) (+ width 2)))))
                                          (l1 (s32vector-ref pint (+ j 1 (* (+ i 2) (+ width 2)))))
                                          (i2 0))
                                      (when (biomesEqualOrMesaPlateau self k2 k) (increase! i2))
                                      (when (biomesEqualOrMesaPlateau self j1 k) (increase! i2))
                                      (when (biomesEqualOrMesaPlateau self k1 k) (increase! i2))
                                      (when (biomesEqualOrMesaPlateau self l1 k) (increase! i2))
                                      (if (>= i2 3)
                                          (s32vector-set! aint (+ j (* i width)) j2)
                                        (s32vector-set! aint (+ j (* i width)) k))))))))))))))
      aint))
  
  
  (method (biomesEqualOrMesaPlateau self biomeIDA <fx> biomeIDB <fx>)
    (if (= biomeIDA biomeIDB)
        #t
      (let ((biome (id->biome biomeIDA))
            (biome1 (id->biome biomeIDB)))
        (if (and biome biome1)
            (if (and (neq? biome MESA_PLATEAU_F) (neq? biome MESA_PLATEAU))
                (or (eq? biome biome1)
                    (eq? (class-of biome) (class-of biome1)))
              (or (eq? biome1 MESA_PLATEAU_F) (eq? biome1 MESA_PLATEAU)))
          #f)))))


;;;
;;;; World
;;;

;; TODO
;; - GenLayerBiomeEdge
;; - GenLayerShore
;; - the zoom 4x has some islands and shore in it


(definition river-size <fx>
  4)


;;                 ocean
;;                   |
;;              fuzzy zoom
;;                   |
;;                islands
;;                   |
;;                  zoom
;;                   |
;;               islands 3x
;;                   |
;;         remove too much ocean
;;                   |
;;                regions
;;                   |
;;                islands
;;                   |
;;          CoolWarm HeatIce Special
;;                   |
;;                zoom 2x
;;                   |
;;                islands
;;                   |
;;            mushroom islands
;;                   |
;;               deep ocean
;;              /          \
;;         init river    biomes
;;            /              \
;;         zoom 2x ------  zoom 2x
;;          /            \   /
;;   zoom river size     hills
;;        /                \
;;      river            zoom 4x
;;      /                    \
;;   smooth                smooth
;;      \                    /
;;       ----- mix river ----
;;                |
;;           *variations*
;;                |
;;            voronoi (2x)
;;                |
;;             *world*

(definition public world-layer <:Layer>
  (let ((variations #f)
        (world #f))
    (lambda ((variations? #f))
      (or (if variations? variations world)
          (let ((ocean (new Ocean-Layer #f)))
            (let ((fuzzy-zoom-ocean (new Fuzzy-Zoom-Layer ocean)))
              (let ((islands (new Islands-Layer fuzzy-zoom-ocean)))
                (let ((zoom-ocean (new Zoom-Layer islands)))
                  (let ((islands (new Islands-Layer (new Islands-Layer (new Islands-Layer zoom-ocean)))))
                    (let ((remove-too-much-ocean (new Remove-Too-Much-Ocean-Layer islands)))
                      (let ((regions (new Regions-Layer remove-too-much-ocean)))
                        (let ((islands (new Islands-Layer regions)))
                          (let ((special (new Special-Layer (new HeatIce-Layer (new CoolWarm-Layer islands @gives-a-super-cool-mostly-ocean-with-big-islands-world zoom-ocean)))))
                            (let ((zoom-special-2x (new Zoom-Layer (new Zoom-Layer special))))
                              (let ((islands (new Islands-Layer zoom-special-2x)))
                                (let ((mushroom-islands (new Mushroom-Islands-Layer islands)))
                                  (let ((deep-ocean (new Deep-Ocean-Layer mushroom-islands)))
                                    (let ((init-river (new Init-River-Layer deep-ocean)))
                                      (let ((magnify-init-river-2x (magnify init-river 2)))
                                        (let ((magnify-init-river (magnify magnify-init-river-2x river-size)))
                                          (let ((river (new River-Layer magnify-init-river)))
                                            (let ((smooth-river (new Smooth-Layer river))
                                                  (biomes (new Biomes-Layer deep-ocean)))
                                              (let ((magnify-biomes-2x (magnify biomes 2)))
                                                (let ((hills (new Hills-Layer magnify-biomes-2x magnify-init-river-2x)))
                                                  (let ((zoom-hills-4x (new Zoom-Layer (new Zoom-Layer (new Zoom-Layer (new Zoom-Layer hills))))))
                                                    (let ((smooth-hills (new Smooth-Layer zoom-hills-4x)))
                                                      (let ((mix-river (new Mix-River-Layer smooth-hills smooth-river)))
                                                        (let ((voronoi (new Voronoi-Zoom-Layer mix-river)))
                                                          (set! variations mix-river)
                                                          (set! world voronoi)
                                                          (if variations?
                                                              variations
                                                            world)))))))))))))))))))))))))))))


(definition public (generate-variations-biomes region-x <fx> region-z <fx> chunk-x <fx> chunk-z <fx>) <s32vector>
  (let ((world (world-layer #t)))
    (generate world
      (- (+ (* region-x 128) (* chunk-x 4)) 2)
      (- (+ (* region-z 128) (* chunk-z 4)) 2)
      10 10)))


(definition public (generate-biomes region-x <fx> region-z <fx> chunk-x <fx> chunk-z <fx>) <s32vector>
  (let ((world (world-layer)))
    (generate world
      (+ (* region-x 512) (* chunk-x 16))
      (+ (* region-z 512) (* chunk-z 16))
      16 16)))


(definition public (generate-u8biomes region-x <fx> region-z <fx> chunk-x <fx> chunk-z <fx>) <u8vector>
  (let ((biomes (generate-biomes region-x region-z chunk-x chunk-z)))
    (let ((len (s32vector-length biomes)))
      (let ((vec (make-u8vector len)))
        (loop (for i from 0 below len)
              (u8vector-set! vec i (s32vector-ref biomes i)))
        vec)))))
