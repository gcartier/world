;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Biome Layers
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


;; TODO
;; - straiten the for i = 0; i < areaHeight


(module world.layer jazz


(import (jazz.geometry)
        (world)
        (world.autoload)
        (world.biome)
        (world.foreign)
        (world.geometry)
        (world.syntax (phase syntax)))


(proclaim (warn optimizations))


;;;
;;;; Layer
;;;


(class Layer extends Object
  
  
  (slot parent    <Layer> getter generate)
  (slot chunkSeed getter generate)
  
  
  (method override (initialize parent)
    (set! parent~self parent))
  
  
  (method public virtual (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    ))


;;;
;;;; Ocean
;;;


(class Ocean-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((layer (make-s32vector (* width height))))
      (loop (for i from 0 below height)
            (loop (for j from 0 below width)
                  (initChunkSeed (+ j x) (+ i y))
                  (s32vector-set! layer (+ j (* i width)) (if (= (nextInt 10) 0) 1 0))))
      layer)))


(class Remove-Too-Much-Ocean-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate~ parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (let ((k1 (s32vector-ref pint (+ j1 1 (* (- (+ i1 1) 1) (+ width 2)))))
                          (l1 (s32vector-ref pint (+ j1 1 1 (* (+ i1 1) (+ width 2)))))
                          (i2 (s32vector-ref pint (+ (- (+ j1 1) 1) (* (+ i1 1) (+ width 2)))))
                          (j2 (s32vector-ref pint (+ j1 1 (* (+ i1 1 1) (+ width 2)))))
                          (k2 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                      (s32vector-set! aint (+ j1 (* i1 width)) k2)
                      (initChunkSeed (+ j1 x) (+ i1 y))
                      (when (and (= k2 0)
                                 (= k1 0)
                                 (= l1 0)
                                 (= i2 0)
                                 (= j2 0)
                                 (= (nextInt 2) 0))
                        (s32vector-set! aint (+ j1 (* i1 width)) 1)))))
        aint))))


;;;
;;;; Regions
;;;


(class Regions-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate~ parent i j k l)))
        (let ((aint (make-s32vector (* width height))))
          (loop (for i1 from 0 below height)
                (loop (for j1 from 0 below width)
                      (let ((k1 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                        (initChunkSeed (+ j1 x) (+ i1 y))
                        (if (= k1 0)
                            (s32vector-set! aint (+ j1 (* i1 width)) 0)
                          ;; hack around (nextInt 6) returning almost always 0 2 4
                          (let ((l1 (modulo (nextInt 299999) 6)))
                            (if (= l1 0)
                                (set! l1 4)
                              (if (<= l1 1)
                                  (set! l1 3)
                                (set! l1 1)))
                            (s32vector-set! aint (+ j1 (* i1 width)) l1))))))
          aint)))))


;;;
;;;; CoolWarm
;;;


(class CoolWarm-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ 1 width 1))
          (l (+ 1 height 1)))
      (let ((pint (generate~ parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (initChunkSeed (+ j1 x) (+ i1 y))
                    (let ((k1 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                      (when (= k1 1)
                        (let ((l1 (s32vector-ref pint (+ j1 1 (* (- (+ i1 1) 1) k))))
                              (i2 (s32vector-ref pint (+ j1 1 1 (* (+ i1 1) k))))
                              (j2 (s32vector-ref pint (+ (- (+ j1 1) 1) (* (+ i1 1) k))))
                              (k2 (s32vector-ref pint (+ j1 1 (* (+ i1 1 1) k)))))
                          (let ((flag
                                  (or (= l1 3)
                                      (= i2 3)
                                      (= j2 3)
                                      (= k2 3))))
                            (let ((flag1
                                    (or (= l1 4)
                                        (= i2 4)
                                        (= j2 4)
                                        (= k2 4))))
                              (when (or flag flag1)
                                (set! k1 2))))))
                      (s32vector-set!
                        aint
                        (+ j1 (* i1 width))
                        k1))))
        aint))))


;;;
;;;; HeatIce
;;;


(class HeatIce-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ 1 width 1))
          (l (+ 1 height 1)))
      (let ((pint (generate~ parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (let ((k1 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                      (when (= k1 4)
                        (let ((l1 (s32vector-ref pint (+ j1 1 (* (- (+ i1 1) 1) k))))
                              (i2 (s32vector-ref pint (+ j1 1 1 (* (+ i1 1) k))))
                              (j2 (s32vector-ref pint (+ (- (+ j1 1) 1) (* (+ i1 1) k))))
                              (k2 (s32vector-ref pint (+ j1 1 (* (+ i1 1 1) k)))))
                          (let ((flag
                                  (or (= l1 2)
                                      (= i2 2)
                                      (= j2 2)
                                      (= k2 2))))
                            (let ((flag1
                                    (or (= l1 1)
                                        (= i2 1)
                                        (= j2 1)
                                        (= k2 1))))
                              (when (or flag1 flag)
                                (set! k1 3))))))
                      (s32vector-set!
                        aint
                        (+ j1 (* i1 width))
                        k1))))
        aint))))


;;;
;;;; Special
;;;


(class Special-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((pint (generate~ parent x y width height))
          (aint (make-s32vector (* width height))))
      (loop (for i from 0 below height)
            (loop (for j from 0 below width)
                  (initChunkSeed (+ j x) (+ i y))
                  (let ((k (s32vector-ref pint (+ j (* i width)))))
                    (when (and (/= k 0) (= (nextInt 13) 0))
                      (bitwise-ior!
                        k
                        (bitwise-and
                          (arithmetic-shift (+ 1 (nextInt 15)) 8)
                          3840)))
                    (s32vector-set! aint (+ j (* i width)) k))))
      aint)))


;;;
;;;; Zoom
;;;


(class Zoom-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (declare (proper-tail-calls))
    (declare (optimize-dead-local-variables))
    (declare (inline))
    (declare (inlining-limit 1000))
    (let ()
      (define (selectRandom a <fx> b <fx>) <fx>
        (if (= (nextInt 2) 0)
            a
          b))
      
      (let ((i (arithmetic-shift x -1))
            (j (arithmetic-shift y -1))
            (k (+ (arithmetic-shift width -1) 2))
            (l (+ (arithmetic-shift height -1) 2)))
        (let ((pint (generate~ parent i j k l))
              (i1 (arithmetic-shift (- k 1) 1))
              (j1 (arithmetic-shift (- l 1) 1)))
          (let ((aint (make-s32vector (* i1 j1))))
            (loop (for k1 from 0 below (- l 1))
                  (let ((l1 (* (arithmetic-shift k1 1) i1))
                        (i2 0))
                    (let ((j2 (s32vector-ref pint (+ i2 0 (* (+ k1 0) k))))
                          (k2 (s32vector-ref pint (+ i2 0 (* (+ k1 1) k)))))
                      (loop (for i2 from 0 below (- k 1))
                            (initChunkSeed
                              (arithmetic-shift (+ i2 i) 1)
                              (arithmetic-shift (+ k1 j) 1))
                            (let ((l2 (s32vector-ref pint (+ i2 1 (* (+ k1 0) k))))
                                  (m2 (s32vector-ref pint (+ i2 1 (* (+ k1 1) k)))))
                              (s32vector-set! aint l1 j2)
                              (s32vector-set! aint (+ l1 i1) (selectRandom j2 k2))
                              (increase! l1)
                              (s32vector-set! aint l1 (selectRandom j2 l2))
                              (s32vector-set! aint (+ l1 i1) (selectModeOrRandom j2 l2 k2 m2))
                              (increase! l1)
                              (set! j2 l2)
                              (set! k2 m2))))))
            (let ((aint2 (make-s32vector (* width height))))
              (loop (for j3 from 0 below height)
                    (let ((src-start (+ (* (+ j3 (bitwise-and y 1)) i1) (bitwise-and x 1))))
                      (subs32vector-move! aint src-start (+ src-start width) aint2 (* j3 width))))
              aint2))))))
  
  
  (method protected virtual (selectModeOrRandom a <fx> b <fx> c <fx> d <fx>) <fx>
    (cond ((and (= b c) (= c d)) b)
          ((and (= a b) (= a c)) a)
          ((and (= a b) (= a d)) a)
          ((and (= a c) (= a d)) a)
          ((and (= a b) (/= c d)) a)
          ((and (= a c) (/= b d)) a)
          ((and (= a d) (/= b c)) a)
          ((and (= b c) (/= a d)) b)
          ((and (= b d) (/= a c)) b)
          ((and (= c d) (/= a b)) c)
          (else (s32vector-ref (s32vector a b c d) (nextInt 4))))))


(class Fuzzy-Zoom-Layer extends Zoom-Layer
  
  
  (method override (selectModeOrRandom a <fx> b <fx> c <fx> d <fx>) <fx>
    (s32vector-ref (s32vector a b c d) (nextInt 4))))


(definition public (magnify layer count)
  (loop (repeat count)
        (set! layer (new Zoom-Layer layer)))
  layer)


;;;
;;;; Zmooth
;;;


(class Zmooth-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate~ parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (let ((k1 (s32vector-ref pint (+ j1 0 (* (+ i1 1) k))))
                          (l1 (s32vector-ref pint (+ j1 2 (* (+ i1 1) k))))
                          (i2 (s32vector-ref pint (+ j1 1 (* (+ i1 0) k))))
                          (j2 (s32vector-ref pint (+ j1 1 (* (+ i1 2) k))))
                          (k2 (s32vector-ref pint (+ j1 1 (* (+ i1 1) k)))))
                      (cond ((and (= k1 l1) (= i2 j2))
                             (initChunkSeed (+ j1 x) (+ i1 y))
                             (if (= (nextInt 2) 0)
                                 (set! k2 k1)
                               (set! k2 i2)))
                            (else
                             (when (= k1 l1)
                               (set! k2 k1))
                             (when (= i2 j2)
                               (set! k2 i2))))
                      (s32vector-set! aint (+ j1 (* i1 width)) k2))))
          aint))))


;;;
;;;; River
;;;


(class Init-River-Layer extends Layer


  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((pint (generate~ parent x y width height))
          (aint (make-s32vector (* width height))))
      (loop (for i from 0 below height)
            (loop (for j from 0 below width)
                  (initChunkSeed (+ j x) (+ i y))
                  (s32vector-set! aint (+ j (* i width))
                    (if (> (s32vector-ref pint (+ j (* i width))) 0)
                        (+ (nextInt 299999) 2)
                      0))))
      aint)))


(class River-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (define (filter val <fx>) <fx>
      (if (>= val 2)
          (+ 2 (bitwise-and val 1))
        val))
    
    (let ((i (- x 1))
          (j (- y 1))
          (k (+ width 2))
          (l (+ height 2)))
      (let ((pint (generate~ parent i j k l))
            (aint (make-s32vector (* width height))))
        (loop (for i1 from 0 below height)
              (loop (for j1 from 0 below width)
                    (let ((k1 (filter (s32vector-ref pint (+ j1 0 (* (+ i1 1) k)))))
                          (l1 (filter (s32vector-ref pint (+ j1 2 (* (+ i1 1) k)))))
                          (i2 (filter (s32vector-ref pint (+ j1 1 (* (+ i1 0) k)))))
                          (j2 (filter (s32vector-ref pint (+ j1 1 (* (+ i1 2) k)))))
                          (k2 (filter (s32vector-ref pint (+ j1 1 (* (+ i1 1) k))))))
                      (s32vector-set!
                        aint
                        (+ j1 (* i1 width))
                        (if (and (= k2 k1)
                                 (= k2 i2)
                                 (= k2 l1)
                                 (= k2 j2))
                            -1 ;; todo -1
                          River)))))
        aint))))


(class Mix-River-Layer extends Layer
  
  
  (slot biome-layer <Layer>)
  (slot river-layer <Layer>)
  
  
  (method override (initialize biome-layer river-layer)
    (set! biome-layer~self biome-layer)
    (set! river-layer~self river-layer))
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((biome (generate~ biome-layer x y width height))
          (river (generate~ river-layer x y width height))
          (aint (make-s32vector (* width height))))
      (loop (for i from 0 below (* width height))
            (do (if (and (/= (s32vector-ref biome i) Ocean)
                         (/= (s32vector-ref biome i) DeepOcean))
                    (if (= (s32vector-ref river i) River)
                        (if (= (s32vector-ref biome i) IcePlains)
                            (s32vector-set! aint i FrozenRiver)
                          (if (and (/= (s32vector-ref biome i) MushroomIsland)
                                   (/= (s32vector-ref biome i) MushroomIslandShore))
                              (s32vector-set! aint i (bitwise-and (s32vector-ref river i) 255))
                            (s32vector-set! aint i MushroomIslandShore)))
                      (s32vector-set! aint i (s32vector-ref biome i)))
                  (s32vector-set! aint i (s32vector-ref biome i)))))
      aint)))


;;;
;;;; Biomes
;;;


(class Biomes-Layer extends Layer
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((layer (make-s32vector (* width height))))
      (let ((pint (generate~ parent x y width height))
            (aint (make-s32vector (* width height))))
        (loop (for i from 0 below height)
              (loop (for j from 0 below width)
                    (initChunkSeed (+ j x) (+ i y))
                    (let ((k (s32vector-ref pint (+ j (* i width)))))
                      (let ((l (arithmetic-shift (bitwise-and k 3840) -8)))
                        (set! k (bitwise-and k -3841))
                        (cond @wait ((and settings
                                    (>= (fixedBiome~ settings) 0))
                               (s32vector-set! aint (+ j (* i width)) (fixedBiome~ settings)))
                              ((oceanic? k)
                               (s32vector-set! aint (+ j (* i width)) k))
                              ((= k (get-id~ MUSHROOM_ISLAND))
                               (s32vector-set! aint (+ j (* i width)) k))
                              ((= k 1)
                               (terminal (s32vector-ref pint (+ j (* i width))) l)
                               (if (> l 0)
                                   (if (= (nextInt 3) 0)
                                       (s32vector-set! aint (+ j (* i width)) (get-id~ MESA_PLATEAU))
                                     (s32vector-set! aint (+ j (* i width)) (get-id~ MESA_PLATEAU_F)))
                                 (s32vector-set! aint (+ j (* i width)) (vector-ref Warm-Biomes (nextInt (vector-length Warm-Biomes))))))
                              ((= k 2)
                               (if (> l 0)
                                   (s32vector-set! aint (+ j (* i width)) (get-id~ JUNGLE))
                                 (s32vector-set! aint (+ j (* i width)) (vector-ref Medium-Biomes (nextInt (vector-length Medium-Biomes))))))
                              ((= k 3)
                               (if (> l 0)
                                   (s32vector-set! aint (+ j (* i width)) (get-id~ MEGA_TAIGA))
                                 (s32vector-set! aint (+ j (* i width)) (vector-ref Cold-Biomes (nextInt (vector-length Cold-Biomes))))))
                              ((= k 4)
                               (s32vector-set! aint (+ j (* i width)) (vector-ref Ice-Biomes (nextInt (vector-length Ice-Biomes)))))
                              (else
                               (s32vector-set! aint (+ j (* i width)) (get-id~ MUSHROOM_ISLAND))))))))
        aint))))


(definition (oceanic? id <fx>)
  (or (= id Ocean)
      (= id DeepOcean)
      (= id FrozenOcean)))


;;;
;;;; Hills
;;;


(class Hills-Layer extends Layer
  
  
  (slot river-layer <Layer>)
  
  
  (method override (initialize parent river-layer)
    (nextmethod parent)
    (set! river-layer~self river-layer))
  
  
  (method override (generate x <fx> y <fx> width <fx> height <fx>) <s32vector>
    (let ((pint (generate~ parent (- x 1) (- y 1) (+ width 2) (+ height 2)))
          (rint (generate~ river-layer (- x 1) (- y 1) (+ width 2) (+ height 2)))
          (aint (make-s32vector (* width height))))
      (loop (for i from 0 below height)
            (loop (for j from 0 below width)
                  (initChunkSeed (+ j x) (+ i y))
                  (let ((k (s32vector-ref pint (+ j 1 (* (+ i 1) (+ width 2)))))
                        (l (s32vector-ref rint (+ j 1 (* (+ i 1) (+ width 2))))))
                    (let ((flag (= (modulo (- l 2) 29) 0)))
                      (when (> k 255) (break))
                      (let ((biome (id->biome k)))
                        (let ((flag1 (and biome (mutation?~ biome))))
                          (if (and (/= k 0)
                                   (>= l 2)
                                   (= (modulo (- l 2) 29) 1)
                                   (not flag1))
                              (let ((biome3 (mutate~ biome)))
                                (s32vector-set!
                                  aint
                                  (+ j (* i width))
                                  (if (not biome3)
                                      k
                                    (get-id~ biome3))))
                            (if (and (/= (nextInt 3) 0) (not flag))
                                (s32vector-set! aint (+ j (* i width)) k)
                              (let ((biome1 biome))
                                (cond ((eq? biome DESERT)
                                       (set! biome1 DESERT_HILLS))
                                      ((eq? biome FOREST)
                                       (set! biome1 FOREST_HILLS))
                                      ((eq? biome BIRCH_FOREST)
                                       (set! biome1 BIRCH_FOREST_HILLS))
                                      ((eq? biome ROOFED_FOREST)
                                       (set! biome1 PLAINS))
                                      ((eq? biome TAIGA)
                                       (set! biome1 TAIGA_HILLS))
                                      ((eq? biome MEGA_TAIGA)
                                       (set! biome1 MEGA_TAIGA_HILLS))
                                      ((eq? biome COLD_TAIGA)
                                       (set! biome1 COLD_TAIGA_HILLS))
                                      ((eq? biome PLAINS)
                                       (if (= (nextInt 3) 0)
                                           (set! biome1 FOREST_HILLS)
                                         (set! biome1 FOREST)))
                                      ((eq? biome ICE_PLAINS)
                                       (set! biome1 ICE_MOUNTAINS))
                                      ((eq? biome JUNGLE)
                                       (set! biome1 JUNGLE_HILLS))
                                      ((eq? biome OCEAN)
                                       (set! biome1 DEEP_OCEAN))
                                      ((eq? biome EXTREME_HILLS)
                                       (set! biome1 EXTREME_HILLS_PLUS))
                                      ((eq? biome SAVANNA)
                                       (set! biome1 SAVANNA_PLATEAU))
                                      ((biomesEqualOrMesaPlateau k (get-id~ MESA_PLATEAU_F))
                                       (set! biome1 MESA))
                                      (else
                                       (when (and (eq? biome DEEP_OCEAN)
                                                  (= (nextInt 3) 0))
                                         (let ((i1 (nextInt 2)))
                                           (if (= i1 0)
                                               (set! biome1 PLAINS)
                                             (set! biome1 FOREST))))))
                                (let ((j2 (get-id~ biome1)))
                                  (when (and flag (/= j2 k))
                                    (let ((biome2 (mutate~ biome1)))
                                      (set! j2
                                            (if (not biome2)
                                                k
                                              (get-id~ biome2)))))
                                  (if (= j2 k)
                                      (s32vector-set! aint (+ j (* i width)) k)
                                    (let ((k2 (s32vector-ref pint (+ j 1 (* (+ i 0) (+ width 2)))))
                                          (j1 (s32vector-ref pint (+ j 2 (* (+ i 1) (+ width 2)))))
                                          (k1 (s32vector-ref pint (+ j 0 (* (+ i 1) (+ width 2)))))
                                          (l1 (s32vector-ref pint (+ j 1 (* (+ i 2) (+ width 2)))))
                                          (i2 0))
                                      (when (biomesEqualOrMesaPlateau k2 k) (increase! i2))
                                      (when (biomesEqualOrMesaPlateau j1 k) (increase! i2))
                                      (when (biomesEqualOrMesaPlateau k1 k) (increase! i2))
                                      (when (biomesEqualOrMesaPlateau l1 k) (increase! i2))
                                      (if (>= i2 3)
                                          (s32vector-set! aint (+ j (* i width)) j2)
                                        (s32vector-set! aint (+ j (* i width)) k))))))))))))))
      aint))
  
  
  (method (biomesEqualOrMesaPlateau biomeIDA <fx> biomeIDB <fx>)
    (if (= biomeIDA biomeIDB)
        #t
      (let ((biome (id->biome biomeIDA))
            (biome1 (id->biome biomeIDB)))
        (if (and biome biome1)
            (if (and (neq? biome MESA_PLATEAU_F) (neq? biome MESA_PLATEAU))
                (or (eq? biome biome1)
                    (eq? (class-of biome) (class-of biome1)))
              (or (eq? biome1 MESA_PLATEAU_F) (eq? biome1 MESA_PLATEAU)))
          #f)))))


;;;
;;;; World
;;;


(definition river-size <fx>
  4)


(definition public world-layer <:Layer>
  (let ((heightmap #f)
        (world #f))
    (lambda ((heightmap? #f))
      (or (if heightmap? heightmap world) ;; ugly to fix
          (let ((ocean (new Ocean-Layer #f)))
            (let ((fuzzy-zoom-ocean (new Fuzzy-Zoom-Layer ocean)))
              (let ((zoom-ocean (new Zoom-Layer fuzzy-zoom-ocean)))
                (let ((remove-too-much-ocean (new Remove-Too-Much-Ocean-Layer zoom-ocean)))
                  (let ((regions (new Regions-Layer remove-too-much-ocean)))
                    (let ((special (new Special-Layer (new HeatIce-Layer (new CoolWarm-Layer regions)))))
                      (let ((init-river (new Init-River-Layer special)))
                        (let ((magnify-init-river-2x (magnify init-river 2)))
                          (let ((magnify-init-river (magnify magnify-init-river-2x river-size)))
                            (let ((river (new River-Layer magnify-init-river)))
                              (let ((smooth-river (new Zmooth-Layer river))
                                    (biomes (new Biomes-Layer special)))
                                (let ((magnify-biomes-2x (magnify biomes 2)))
                                  (let ((hills (new Hills-Layer magnify-biomes-2x magnify-init-river-2x)))
                                    (let ((zoom-hills-4x (new Zoom-Layer (new Zoom-Layer (new Zoom-Layer (new Zoom-Layer hills))))))
                                      (let ((smooth-hills (new Zmooth-Layer zoom-hills-4x)))
                                        (let ((mix-river (new Mix-River-Layer smooth-hills smooth-river)))
                                          (set! heightmap mix-river)
                                          (set! world (new Zoom-Layer (new Zoom-Layer mix-river)))
                                          (if heightmap?
                                              heightmap
                                            world)))))))))))))))))))))


(definition public (generate-heightmap-biomes region-x <fx> region-z <fx> chunk-x <fx> chunk-z <fx>) <s32vector>
  (let ((world (world-layer #t)))
    (generate~ world
      (+ (* region-x 128) (* chunk-x 4))
      (+ (* region-z 128) (* chunk-z 4))
      10 10)))


(definition public (generate-biomes region-x <fx> region-z <fx> chunk-x <fx> chunk-z <fx>) <s32vector>
  (let ((world (world-layer)))
    (generate~ world
      (+ (* region-x 512) (* chunk-x 16))
      (+ (* region-z 512) (* chunk-z 16))
      16 16)))


(definition public (generate-u8biomes region-x <fx> region-z <fx> chunk-x <fx> chunk-z <fx>) <u8vector>
  (let ((biomes (generate-biomes region-x region-z chunk-x chunk-z)))
    (let ((len (s32vector-length biomes)))
      (let ((vec (make-u8vector len)))
        (loop (for i from 0 below len)
              (u8vector-set! vec i (s32vector-ref biomes i)))
        vec)))))
