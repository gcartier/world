;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Retain
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.retain jazz


(definition protected debug-retain?
  #f)


(definition protected refcounts
  (make-table test: eq?))

(definition protected retainers
  (make-table test: eq?))


(definition protected (find-refcount obj)
  (table-ref refcounts obj 0))


(definition protected (increase-refcount obj)
  (table-set! refcounts obj (+ (table-ref refcounts obj 0) 1)))


(definition protected (decrease-refcount obj)
  (table-set! refcounts obj (- (table-ref refcounts obj 0) 1)))


(definition protected (validate-retainers obj refcount)
  (let ((retainers (table-ref retainers obj '())))
    (let ((retainers-count (length retainers)))
      (assert (= retainers-count refcount)))))


(definition protected (add-retainer obj retainer)
  (assert retainer)
  (table-add retainers obj retainer))


(definition protected (remove-retainer obj retainer)
  (let ((retainers (table-ref retainers obj '())))
    (assert (memq? retainer retainers)))
  (table-subtract! retainers obj retainer))


(definition protected (transfer-retainer obj old-retainer new-retainer)
  (remove-retainer obj old-retainer)
  (add-retainer obj new-retainer)))
