;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Dyes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.dye jazz


(import (jazz.graphic)
        (world.syntax (phase syntax)))


(proclaim (warn optimizations))

(declare (optimize-dead-local-variables))


;;;
;;;; Dye
;;;


(definition public inline (dye red <fl> green <fl> blue <fl> alpha <fl>) <dye>
  (f32vector red green blue alpha))


(definition public inline (make-dye) <dye>
  (f32vector 0. 0. 0. 0.))


(definition public inline (dye? obj <object>) <bool>
  (f32vector? obj))


(definition public (copy-dye dye <dye>) <dye>
  (let ((copy (make-dye)))
    (dye-copy! copy dye)
    copy))


(definition public inline (dye-red dye <dye>) <fl>
  (f32vector-ref dye 0))

(definition public inline (dye-green dye <dye>) <fl>
  (f32vector-ref dye 1))

(definition public inline (dye-blue dye <dye>) <fl>
  (f32vector-ref dye 2))

(definition public inline (dye-alpha dye <dye>) <fl>
  (f32vector-ref dye 3))


(definition public inline (dye-red-set! dye <dye> red <fl>) <void>
  (f32vector-set! dye 0 red))

(definition public inline (dye-green-set! dye <dye> green <fl>) <void>
  (f32vector-set! dye 1 green))

(definition public inline (dye-blue-set! dye <dye> blue <fl>) <void>
  (f32vector-set! dye 2 blue))

(definition public inline (dye-alpha-set! dye <dye> alpha <fl>) <void>
  (f32vector-set! dye 3 alpha))


(definition public (dye=? c1 <dye> c2 <dye>) <bool>
  (and (= (dye-red c1) (dye-red c2))
       (= (dye-green c1) (dye-green c2))
       (= (dye-blue c1) (dye-blue c2))
       (= (dye-alpha c1) (dye-alpha c2))))


(definition public (dye! dye <dye> red <fl> green <fl> blue <fl> alpha <fl>) <void>
  (dye-red-set! dye red)
  (dye-green-set! dye green)
  (dye-blue-set! dye blue)
  (dye-alpha-set! dye alpha))


(definition public (dye-copy! dst <dye> src <dye>) <void>
  (dye-red-set! dst (dye-red src))
  (dye-green-set! dst (dye-green src))
  (dye-blue-set! dst (dye-blue src))
  (dye-alpha-set! dst (dye-alpha src)))


(definition public (color->dye color <Color>) <dye>
  (dye (get-red color)
       (get-green color)
       (get-blue color)
       (get-alpha color)))


(definition public (dye->color dye <dye>) <Color>
  (new Color
    red: (dye-red dye)
    green: (dye-green dye)
    blue: (dye-blue dye)
    alpha: (dye-alpha dye)))


(definition public (present-dye dye <dye>) <string>
  (format "~{Dye {r precision: 3} {r precision: 3} {r precision: 3} {r precision: 3}}"
          (dye-red dye)
          (dye-green dye)
          (dye-blue dye)
          (dye-alpha dye))))
