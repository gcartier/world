;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Networking
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.network jazz


(import (jazz.geometry)
        (jazz.syntax (phase syntax))
        (world)
        (world.autoload)
        (world.configure)
        (world.syntax (phase syntax))
        (world.task))


;;;
;;;; Task
;;;


(definition public (network-task task <Task>)
  (declare (proper-tail-calls))
  (with-task task
    (lambda (exit)
      (let (iter (previous (current-seconds)))
        (let ((time (current-seconds)))
          (let ((elapse (- time previous)))
            (with-task-mutex
              (lambda ()
                (unless (stopping?~ task)
                  (site (network on?: #t)
                    (case zone-kind
                      ((client)
                       (send-outgoing))
                      ((server)
                       (broadcast-outgoing)))))))
            (let ((duration (- (current-seconds) time)))
              (task-sleep task network-period duration exit))
            (iter time)))))))


(definition public (alive-task task <Task>)
  (declare (proper-tail-calls))
  (with-task task
    (lambda (exit)
      (let (iter (previous (current-seconds)))
        (let ((time (current-seconds)))
          (let ((elapse (- time previous)))
            (with-task-mutex
              (lambda ()
                (unless (stopping?~ task)
                  (site (alive on?: #t)
                    (case zone-kind
                      ((client)
                       (send-alive))
                      ((server)
                       (check-liveliness)))))))
            (let ((duration (- (current-seconds) time)))
              (task-sleep task alive-period duration exit))
            (iter time)))))))


;;;
;;;; Outgoing
;;;


(definition protected outgoing-max
  1024)

(definition protected outgoing-changes
  (make-f64vector outgoing-max))

(definition protected outgoing-size
  0)


(definition (maybe-grow-outgoing new-size)
  (define (grow vect <f64vector> old-reserved <fx> new-reserved <fx>) <f64vector>
    (let ((new-vect (make-f64vector new-reserved)))
      (subf64vector-move! vect 0 old-reserved new-vect 0)
      new-vect))
  
  (when (> new-size outgoing-max)
    (let ((new-reserved (fxround (* (cast <fl> new-size) GOLDEN_GROWTH))))
      (set! outgoing-changes (grow outgoing-changes outgoing-size new-reserved))
      (set! outgoing-max new-reserved))))


;;;
;;;; Client
;;;


(definition protected (outgoing-push val <fl>)
  (let ((new-size (+ outgoing-size 1)))
    (maybe-grow-outgoing new-size)
    (f64vector-set! outgoing-changes outgoing-size val)
    (set! outgoing-size new-size)))


(definition protected (send-outgoing)
  (when (> outgoing-size 0)
    ;; quick first draft to optimize
    (let ((changes (make-f64vector outgoing-size)))
      (subf64vector-move! outgoing-changes 0 outgoing-size changes 0)
      (send-client-update~ (client-tier) changes)
      (set! outgoing-size 0))))


(definition protected (send-alive)
  (send-client-alive~ (client-tier)))


;;;
;;;; Server
;;;


(definition protected (outgoing-push-changes changes <f64vector>)
  (let ((len (f64vector-length changes)))
    (let ((new-size (+ outgoing-size len)))
      (maybe-grow-outgoing new-size)
      (subf64vector-move! changes 0 len outgoing-changes outgoing-size)
      (set! outgoing-size new-size))))


(definition protected (broadcast-outgoing)
  (when (> outgoing-size 0)
    ;; quick first draft to optimize
    (let ((changes (make-f64vector outgoing-size)))
      (subf64vector-move! outgoing-changes 0 outgoing-size changes 0)
      (broadcast-processor-update~ (current-processor) changes)
      (set! outgoing-size 0))))


(definition protected (check-liveliness)
  (check-clients-liveliness~ (server-tier))))
