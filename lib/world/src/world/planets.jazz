;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Planets Zone
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.planets jazz


(import (jazz.opengl.render)
        (jazz.syntax (phase syntax))
        (gaea.geometry)
        (gaea.math)
        (world)
        (world.area)
        (world.atlas)
        (world.autoload)
        (world.biomes)
        (world.context)
        (world.face)
        (world.index)
        (world.material)
        (world.mesh)
        (world.planet)
        (world.render)
        (world.renderer)
        (world.sector)
        (world.settings)
        (world.skybox)
        (world.zone))


(class Planets-Zone extends Zone
  
  
  ;; for now an explicit list of planets
  (slot planets  initialize #f getter generate)
  (slot atlas    initialize #f getter generate)
  (slot material initialize #f getter generate)
  (slot renderer initialize #f getter generate)
  
  
  (method override (planets? self)
    #t)
  
  
  ;;;
  ;;;; Populate
  ;;;
  
  
  (method override (populate-zone self)
    (let ((space-radius (world-setting 'world.radius 150.))
          (space-density (world-setting 'world.density 5)))
      (define (place-planets)
        (set! planets '())
        (loop (repeat space-density)
              (let ((position (random-space))
                    (size (random-between (world-setting 'world.planet-min-size 5.) (world-setting 'world.planet-max-size 50.)))
                    (biome (random-biome)))
                (let ((image (get-earth biome)))
                  (let ((planet (new Planet parent: self position: position size: size image: image)))
                    (set! planets (cons planet planets))
                    (generate-planet planet material)
                    (add-element self planet)
                    (let ((trees (get-trees biome)))
                      (when trees
                        (populate-trees self planet trees))))))))
      
      (define (random-space)
        (vertex (random-between (- space-radius) space-radius)
                (random-between (- space-radius) space-radius)
                (random-between (- space-radius) space-radius)))
      
      (site populate
        (place-planets))
      @convert
      (place-player self)))
  
  
  (method (populate-trees self planet trees)
    (let ((world (current-world)))
      (let ((size (get-size planet))
            (count (world-setting 'world.planet-trees #f)))
        (when count
          (loop (repeat (random (fxround (* count size))))
                (let ((model (random-element trees)))
                  (receive (dir pos) (random-position self planet)
                    (let ((entity (place-model world model pos savable?: #f)))
                      (realign-lookat entity dir)
                      (when (client?)
                        (bake-entity-sun self entity))))))))))
  
  
  (method package (place-player self)
    (let ((me (current-me))
          (planets (collect-planets self)))
      (unless (null? planets)
        (let ((planet (random-element planets)))
          (receive (dir pos) (random-position self planet 25.)
            (set-position me pos)
            (realign-lookat me dir))))))
  
  
  (method (random-position self planet (surface-distance 0.))
    (let ((world (current-world)))
      (let ((position (get-position planet))
            (radius (get-radius planet))
            (dir (random-direction)))
        (let ((pos (vertex+ position (vertex-scalar*^ dir (+ radius surface-distance)))))
          (values dir pos)))))
  
  
  ;;;
  ;;;; Generate
  ;;;
  
  
  (method override (generate-visual self sector <Sector>)
    (let ((faces (section-faces self (get-index sector))))
      (when faces
        (bind-values (vertices neighbors) (prepare-vertices/neighbors sector faces)
          (let ((lightmap (prepare-lightmap sector faces)))
            (with-render-mutex
              (lambda ()
                (free-material-mesh sector)
                (setup-material-mesh sector material)
                (let ((mesh (get-material-mesh sector)))
                  (fill-vertices/neighbors sector mesh #f vertices neighbors)
                  (fill-lightmap sector mesh #f lightmap)
                  (setup-array mesh))))))))
    (set-visual-uptodate? sector #t)
    (set-visual-revision sector (get-revision generator))
    (set-visual-generating? sector #f)
    (decrease-sectors-generating self 1)
    (increase-sectors-generated self))
  
  
  ;; quick try
  (method override (generate-physical self area revision-update?)
    (let ((faces (section-faces self (get-index area))))
      (when faces
        (let ((areas (make-table test: equal?)))
          (for-each (lambda (face)
                      (let ((poly (get-polygon face)))
                        (let ((center (get-center poly)))
                          (let ((index (position-index area center)))
                            (table-add areas index poly)))))
                    faces)
          (table-iterate areas
            (lambda (index polygons)
              (add-polygons area #f index polygons #f)))))))
  
  
  ;; quick try
  (method (section-faces self index)
    ;; quick hack
    (if (not planets)
        #f
    (continuation-capture
      (lambda (return)
        (for-each (lambda (planet)
                    (table-iterate (generate-planet planet material)
                      (lambda (idx faces)
                        (when (index=? idx index)
                          (continuation-return return faces)))))
                  planets)
        #f))))
  
  
  ;;;
  ;;;; Blocks
  ;;;
  
  
  (method override (prepare-blocks self)
    (nextmethod self)
    (let ((world (current-world)))
      (let ((atlas (make-block-atlas self
                     (lambda (atlas)
                       (add-asset atlas (find-texture-asset world "world/turf"))
                       (add-asset atlas (find-texture-asset world "world/greek"))
                       (add-asset atlas (find-texture-asset world "world/ground"))
                       (add-asset atlas (find-texture-asset world "world/fire"))
                       (add-asset atlas (find-texture-asset world "world/funky"))
                       (add-asset atlas (find-texture-asset world "world/roman"))
                       @w
                       (add-file atlas (find-texture-asset world "earth"))))))
        (let ((material (new Material name: 'planets atlas: atlas texture: (extract-texture atlas))))
          (let ((renderer (new Material-Block-Renderer 'block material)))
            (set-renderer material renderer)
            (set! self.atlas atlas)
            (set! self.material material)
            (set! self.renderer renderer))))))
  
  
  ;;;
  ;;;; Render
  ;;;
  
  
  (method override (render-zone self)
    (render renderer)
    (reset renderer))
  
  
  ;;;
  ;;;; Skybox
  ;;;
  
  
  (method override (prepare-skybox self)
    (install-skybox self (find-skybox "world/nebula")))
  
  
  ;;;
  ;;;; Edition
  ;;;
  
  
  (method override (zone-editable? self)
    #f)))
