;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Working
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.working jazz


(definition protected working-debug?
  #f)

(definition protected (set-working-debug? debug?)
  (set! working-debug? debug?))


(definition protected (with-working thunk)
  (if working-debug?
      (thunk)
    (continuation-capture
      (lambda (exit)
        (with-exception-handler
          (lambda (exc)
            (continuation-capture
              (lambda (cont)
                (continuation-graft exit
                  (lambda ()
                    (list 'error (exception-reason exc) (exception-location exc cont) (continuation-stack cont)))))))
          thunk)))))


(definition protected (with-evaluating thunk)
  (if working-debug?
      (thunk)
    (continuation-capture
      (lambda (exit)
        (with-exception-handler
          (lambda (exc)
            (continuation-capture
              (lambda (cont)
                (continuation-graft exit
                  (lambda ()
                    (list 'read (exception-reason exc) (exception-location exc cont)))))))
          (lambda ()
            (catch (Walk-Problems exc
                     (list 'walk (exception-reason exc) (exception-detail exc)))
              (thunk)))))))))
