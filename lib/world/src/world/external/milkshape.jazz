;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; MilkShape 3D
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


;; This is a port of Mete Ciragan's msViewer
;;
;; email:          mciragan@gmx.net
;; web:            http://www.milkshape3d.com


(module world.external.milkshape jazz


(import (jazz.geometry)
        (jazz.io)
        (jazz.platform)
        (world)
        (world.animation)
        (world.atlas)
        (world.binary)
        (world.context)
        (world.dye)
        (world.foreign)
        (world.generate)
        (world.geometry)
        (world.material)
        (world.model)
        (world.renderers)
        (world.support)
        (world.texture))


(proclaim (warn optimizations))


;;;
;;;; Vertex
;;;


(class MS3D-Vertex extends Object
  
  
  (slot flags   <fx>                              accessors generate)
  (slot vertex  <vertex> initialize (make-vertex) accessors generate)
  (slot bone-id <fx>                              accessors generate)
  
  
  (method override (initialize self flags vertex bone-id)
    (set! self.flags flags)
    (set! self.vertex vertex)
    (set! self.bone-id bone-id)))


;;;
;;;; Triangle
;;;


(class MS3D-Triangle extends Object
  
  
  (slot flags           <fx+>       accessors generate)
  (slot vertex-indices  <list>      accessors generate)
  (slot vertex-normals  <list>      accessors generate)
  (slot s               <f64vector> accessors generate)
  (slot t               <f64vector> accessors generate)
  (slot d               <f64vector> accessors generate)
  (slot smoothing-group <fx+>       accessors generate)
  (slot group-index     <fx+>       accessors generate)
  
  
  (method override (initialize self flags vertex-indices vertex-normals s t d smoothing-group group-index)
    (set! self.flags flags)
    (set! self.vertex-indices vertex-indices)
    (set! self.vertex-normals vertex-normals)
    (set! self.s s)
    (set! self.t t)
    (set! self.d d)
    (set! self.smoothing-group smoothing-group)
    (set! self.group-index group-index)))


;;;
;;;; Group
;;;


(class MS3D-Group extends Object
  
  
  (slot flags          <fx+>     accessors generate)
  (slot name           <string+> accessors generate)
  (slot triangles      <vector>  accessors generate)
  (slot material-index <fx>      accessors generate)
  
  
  (method override (initialize self flags name triangles material-index)
    (set! self.flags flags)
    (set! self.name name)
    (set! self.triangles triangles)
    (set! self.material-index material-index)))


;;;
;;;; Model
;;;


(class MS3D-Model extends Object
  
  
  (slot vertices      <vector> accessors generate)
  (slot triangles     <vector> accessors generate)
  (slot groups        <vector> accessors generate)
  (slot materials     <vector> accessors generate)
  (slot animation-fps <fl>     accessors generate)
  (slot current-frame <fl>     accessors generate)
  (slot total-frames  <fx>     accessors generate)
  (slot bones         <object> accessors generate)
  
  
  (method package (extract-atlas self file <File>)
    (define (triangle-ref index <fx>) <MS3D-Triangle>
      (vector-ref triangles index))
    
    (define (material-ref index <fx>) <Material>
      (vector-ref materials index))
    
    (let ((atlas (new Atlas repeat?: #t pad?: #f))
          (textures (remove-duplicates (remove-empty (map get-texture-name (vector->list materials))) test: filename=?)))
      (for-each (lambda (texture)
                  (add-file atlas (new-brother file texture)))
                textures)
      (let ((texture (extract-texture atlas mipmap?: #f)))
        (complete atlas)
        (loop (for group <MS3D-Group> in-vector groups)
              (let ((material (material-ref (get-material-index group))))
                (let ((name (get-texture-name material)))
                  (let ((flipped? (extension=? (filename-extension name) "tga"))
                        (uv (image-rect atlas (filename-base name))))
                    (let ((left (uv-left uv))
                          (top (uv-top uv))
                          (texture-depth (uv-depth uv)))
                      (let ((width (- (uv-right uv) left))
                            (height (- (uv-bottom uv) top)))
                        ;; adjust texture coordinates
                        (loop (for triangle-index <fx> in-vector (get-triangles group))
                              (let ((triangle (triangle-ref triangle-index)))
                                (let ((s (get-s triangle))
                                      (t (get-t triangle))
                                      (d (get-d triangle)))
                                  (vertex-x-set! s (+ left (* (vertex-x s) width)))
                                  (vertex-y-set! s (+ left (* (vertex-y s) width)))
                                  (vertex-z-set! s (+ left (* (vertex-z s) width)))
                                  (cond (flipped?
                                         (vertex-x-set! t (+ top (* (- 1. (vertex-x t)) height)))
                                         (vertex-y-set! t (+ top (* (- 1. (vertex-y t)) height)))
                                         (vertex-z-set! t (+ top (* (- 1. (vertex-z t)) height))))
                                        (else
                                         (vertex-x-set! t (+ top (* (vertex-x t) height)))
                                         (vertex-y-set! t (+ top (* (vertex-y t) height)))
                                         (vertex-z-set! t (+ top (* (vertex-z t) height)))))
                                  (vertex-x-set! d texture-depth)
                                  (vertex-y-set! d texture-depth)
                                  (vertex-z-set! d texture-depth))))))))))
        (values atlas texture))))
  
  
  (method package (extract-geometry self groups material)
    (define (count-triangles) <fx>
      (let ((count 0))
        (loop (for group <MS3D-Group> in groups)
              (increase! count (vector-length (get-triangles group))))
        count))
    
    (let ((mesher (new Mesher (count-triangles) 0 material: material)))
      (define (vertex-ref index <fx>) <MS3D-Vertex>
        (vector-ref vertices index))
      
      (define (add-ms3d-triangle triangle <MS3D-Triangle>)
        (let ((indices (get-vertex-indices triangle))
              (normals (get-vertex-normals triangle))
              (s (get-s triangle))
              (t (get-t triangle))
              (d (get-d triangle)))
          (let ((v1 (vertex-ref (cast <fx> (first indices))))
                (v2 (vertex-ref (cast <fx> (second indices))))
                (v3 (vertex-ref (cast <fx> (third indices))))
                (uv (f32vector (vertex-x s) (vertex-x t) (vertex-y s) (vertex-y t) (vertex-z s) (vertex-z t)))
                (tdepth (vertex-x d)))
            (add-triangle mesher
              (get-vertex v1) (first  normals) 0 1 (get-bone-id v1)
              (get-vertex v2) (second normals) 2 3 (get-bone-id v2)
              (get-vertex v3) (third  normals) 4 5 (get-bone-id v3)
              uv
              tdepth))))
      
      (loop (for group <MS3D-Group> in groups)
            (loop (for triangle-index <fx> in-vector (get-triangles group))
                  (add-ms3d-triangle (cast <MS3D-Triangle> (vector-ref triangles triangle-index)))))
      (get-mesh mesher debuggable-mesh?: #t)))
  
  
  (method package (extract-bounds self)
    (define (vertex-ref index <fx>)
      (get-vertex (cast <MS3D-Vertex> (vector-ref vertices index))))
    
    (let ((bounds (let ((vert (vertex-ref 0)))
                    (let ((x (vertex-x vert))
                          (y (vertex-y vert))
                          (z (vertex-z vert)))
                      (cuboid x y z x y z)))))
      (loop (for n from 1 below (vector-length vertices))
            (let ((vertex (vertex-ref n)))
              (let ((x (vertex-x vertex))
                    (y (vertex-y vertex))
                    (z (vertex-z vertex)))
                (when (< x (cuboid-left bounds))
                  (cuboid-left-set! bounds x))
                (when (> x (cuboid-right bounds))
                  (cuboid-right-set! bounds x))
                (when (< y (cuboid-bottom bounds))
                  (cuboid-bottom-set! bounds y))
                (when (> y (cuboid-top bounds))
                  (cuboid-top-set! bounds y))
                (when (< z (cuboid-back bounds))
                  (cuboid-back-set! bounds z))
                (when (> z (cuboid-front bounds))
                  (cuboid-front-set! bounds z)))))
      ;; a try to standardize bounds bottom at 0
      ;; this may not be 100% correct as vertices are not
      ;; standardized but it seems to give very good results
      (let ((bottom (cuboid-bottom bounds)))
        (cuboid-bottom-set! bounds 0.)
        (cuboid-top-set! bounds (- (cuboid-top bounds) bottom)))
      bounds))
  
  
  (method package (extract-animation self name frames)
    (let ((animation (new Animation name animation-fps (or frames total-frames) bones)))
      (when (window?)
        (setup-bones animation))
      animation)))


;;;
;;;; Import
;;;


(definition public (load-ms3d file <File> (metadata: metadata #f)) <Model>
  (let ((name (get-base file))
        (render (or (model-metadata-render metadata) '(transparent)))
        (mask (model-metadata-mask metadata))
        (frames (model-metadata-frames metadata)))
    (define (add-animations model <Model>)
      (let ((dir <Directory> (get-parent file)))
        (iterate-directory dir
          (lambda (file <File>)
            (when (extension=? (get-extension file) "ms3d")
              (let ((base (get-base file)))
                (unless (filename=? base name)
                  (assert (starts-with? base (concatenate name "_")))
                  (let ((name (subseq base (+ (cardinality name) 1) (cardinality base)))
                        (ms3d (load-model file)))
                    (add-animation model name (extract-animation ms3d name frames)))))))
          files?: #t
          directories?: #f)))
    
    ;; necessary as animations come from distinct files
    (define (validate-animations model <Model>)
      (unless (apply = (map (lambda (anim <Animation>)
                              (vector-length (get-bones anim)))
                            (all-animations model)))
        (error "Inconsistant bones in model: {a}" name)))
    
    (let ((model (new Model name: name rotation: (vertex 0. PI/2 0.))))
      (install-metadata model metadata)
      (let ((ms3d (load-model file)))
        (when debug-ms3d?
          (set-native model ms3d))
        (if (not (window?))
            (begin
              (set-anchor model 'origin)
              (set-bounds model (extract-bounds ms3d))
              (set-animation model (extract-animation ms3d "Idle" frames))
              (add-animations model))
          ;; order is important as extract-atlas adjusts texture coordinates
          (bind-values (atlas texture) (extract-atlas ms3d file)
            (define (extract-mesh groups renderer)
              (let ((material (new Material atlas: atlas texture: texture renderer: renderer)))
                (extract-geometry ms3d groups material)))
            
            (define (iterate-groups proc)
              (let ((renderer (require-renderer (car render)))
                    (render? (if (not mask) #t (car mask)))
                    (renderscan (cdr render))
                    (maskscan (and mask (cdr mask))))
                (loop (for group in-vector (get-groups ms3d))
                      (do (proc group renderer render?)
                          (unless (null? renderscan)
                            (set! renderer (require-renderer (car renderscan)))
                            (set! renderscan (cdr renderscan)))
                        (unless (or (not maskscan) (null? maskscan))
                          (set! render? (car maskscan))
                          (set! maskscan (cdr maskscan)))))))
            
            (when debug-meshes?
              (set-atlas model atlas))
            (if (not (window?))
                (set-meshes model #f)
              (if debug-meshes?
                  (let ((queue (new-queue)))
                    (iterate-groups
                      (lambda (group renderer render?)
                        (when render?
                          (enqueue queue (extract-mesh (list group) renderer)))))
                    (set-meshes model (list->vector (queue-list queue))))
                (let ((render-groups (make-table test: eq?)))
                  (iterate-groups
                    (lambda (group renderer render?)
                      (when render?
                        (table-add render-groups renderer group))))
                  (let ((queue (new-queue)))
                    (iterate-table render-groups
                      (lambda (renderer groups)
                        (enqueue queue (extract-mesh groups renderer))))
                    (set-meshes model (list->vector (queue-list queue))))))))
          (set-anchor model 'origin)
          (set-bounds model (extract-bounds ms3d))
          (set-animation model (extract-animation ms3d "Idle" frames))
          (add-animations model)
          (validate-animations model)))
      model)))


(definition (load-model file <File>) <MS3D-Model>
  (call-with-input-file (path-settings file)
    (lambda (port)
      (let ((MAX_TEXTURE_FILENAME_SIZE 128))
        (let ((content (load-binary file))
              (pos <fx> 0))
          (define (<-s8) <fx>
            (prog1 (scan-s8 content pos)
              (increase! pos 1)))
          
          (define (<-u8) <fx>
            (prog1 (scan-u8 content pos)
              (increase! pos 1)))
          
          (define (<-s16) <fx>
            (prog1 (scan-s16 content pos)
              (increase! pos 2)))
          
          (define (<-u16) <fx>
            (prog1 (scan-u16 content pos)
              (increase! pos 2)))
          
          (define (<-s32) <int>
            (prog1 (scan-s32 content pos)
              (increase! pos 4)))
          
          (define (<-float) <fl>
            (prog1 (scan-float content pos)
              (increase! pos float-size)))
          
          (define (<-floats32! vec <f32vector>) <f32vector>
            (let ((len (f32vector-length vec)))
              (scan-floats32! content pos vec len)
              (increase! pos (* len float-size))
              vec))
          
          (define (<-floats64! vec <f64vector>) <f64vector>
            (let ((len (f64vector-length vec)))
              (scan-floats64! content pos vec len)
              (increase! pos (* len float-size))
              vec))
          
          (define (<-string size <fx>)
            (prog1 (scan-string content pos size)
              (increase! pos size)))
          
          (define (<-vertex) <f64vector>
            (<-floats64! (make-vertex)))
          
          (define (<-normal) <f64vector>
            (<-vertex))
          
          (define (<-tex) <f64vector>
            (<-floats64! (make-vertex)))
          
          (define (<-color) <f32vector>
            (<-floats32! (make-dye)))
          
          (define (<-indices) <list>
            (list
              (<-u16)
              (<-u16)
              (<-u16)))
          
          (define (<-normals) <list>
            (list
              (<-normal)
              (<-normal)
              (<-normal)))
          
          ;; vertices
          (define (<-vertices) <vector>
            (let ((num-vertices (<-u16)))
              (let ((vertices (make-vector num-vertices)))
                (loop (for i from 0 below num-vertices)
                      (let ((flags (<-u8))
                            (vertex (<-vertex))
                            (bone-id (<-s8))
                            (reference-count (<-u8)))
                        (vector-set! vertices i (new MS3D-Vertex flags vertex bone-id))))
                vertices)))
          
          ;; triangles
          (define (<-triangles) <vector>
            (let ((num-triangles (<-u16)))
              (let ((triangles (make-vector num-triangles)))
                (loop (for i from 0 below num-triangles)
                      (let ((flags (<-u16))
                            (vertex-indices (<-indices))
                            (vertex-normals (<-normals))
                            (s (<-tex))
                            (t (<-tex))
                            (d (vertex 0. 0. 0.))
                            (smoothing-group (<-u8))
                            (group-index (<-u8)))
                        (let ((triangle
                                (new MS3D-Triangle
                                  flags
                                  vertex-indices
                                  vertex-normals
                                  s
                                  t
                                  d
                                  smoothing-group
                                  group-index)))
                          (vector-set! triangles i triangle))))
                triangles)))
          
          ;; groups
          (define (<-groups) <vector>
            (let ((num-groups (<-u16)))
              (let ((groups (make-vector num-groups)))
                (loop (for i from 0 below num-groups)
                      (let ((flags (<-u8))
                            (name (<-string 32))
                            (triangles
                              (let ((num-group-triangles (<-u16)))
                                (let ((group-triangles (make-vector num-group-triangles)))
                                  (loop (for j from 0 below num-group-triangles)
                                        (vector-set! group-triangles j (<-u16)))
                                  group-triangles)))
                            (material-index (<-s8)))
                        (let ((group
                                (new MS3D-Group
                                  flags
                                  name
                                  triangles
                                  material-index)))
                          (vector-set! groups i group))))
                groups)))
          
          ;; materials
          (define (<-materials) <vector>
            (let ((num-materials (<-u16)))
              (let ((materials (make-vector num-materials)))
                (loop (for i from 0 below num-materials)
                      (let ((name (<-string 32))
                            (ambient (<-color))
                            (diffuse (<-color))
                            (specular (<-color))
                            (emissive (<-color))
                            (shininess (<-float))
                            (transparency (<-float))
                            (mode (<-u8))
                            (texture (<-string MAX_TEXTURE_FILENAME_SIZE))
                            (alphamap (<-string MAX_TEXTURE_FILENAME_SIZE)))
                        ;; set alpha
                        (dye-alpha-set! ambient transparency)
                        (dye-alpha-set! diffuse transparency)
                        (dye-alpha-set! specular transparency)
                        (dye-alpha-set! emissive transparency)
                        (let ((material
                                (new Material
                                  name: name
                                  ambient: ambient
                                  diffuse: diffuse
                                  specular: specular
                                  emissive: emissive
                                  shininess: shininess
                                  transparency: transparency
                                  mode: mode
                                  texture-name: texture
                                  alphamap: alphamap)))
                          (vector-set! materials i material))))
                materials)))
          
          ;; bones
          (define (<-bones animation-fps <fl>) <vector+>
            (let ((num-bones (<-u16)))
              (let ((bones (and (window?) (make-vector num-bones))))
                (loop (for i from 0 below num-bones)
                      (let ((flags (<-u8))
                            (name (<-string 32))
                            (parent-name (<-string 32))
                            (rot (euler-quaternion (<-vertex)))
                            (pos (<-vertex))
                            (num-key-frames-rot (<-u16))
                            (num-key-frames-pos (<-u16)))
                        (let ((rotation-keys (make-vector num-key-frames-rot))
                              (position-keys (make-vector num-key-frames-pos)))
                          ;; the frame time is in seconds, so multiply it by the animation fps
                          ;; to get the frames rotation channel
                          (loop (for j from 0 below num-key-frames-rot)
                                (let ((time (<-float))
                                      (key (euler-quaternion (<-vertex))))
                                  (let ((time (* time animation-fps)))
                                    (vector-set! rotation-keys j (new KeyFrame time key)))))
                          ;; translation channel
                          (loop (for j from 0 below num-key-frames-pos)
                                (let ((time (<-float))
                                      (key (<-vertex)))
                                  (let ((time (* time animation-fps)))
                                    (vector-set! position-keys j (new KeyFrame time key)))))
                          (when (window?)
                            (let ((bone (new Bone flags name i parent-name rot pos rotation-keys position-keys)))
                              (vector-set! bones i bone))))))
                bones)))
          
          ;; header
          (assert (equal? (<-string 10) "MS3D000000"))
          (assert (= (<-s32) 4))
          
          ;; content
          (let ((model (new MS3D-Model))
                (vertices (<-vertices))
                (triangles (<-triangles))
                (groups (<-groups))
                (materials (<-materials)))
            ;; vertices
            (set-vertices model vertices)
            ;; triangles
            (set-triangles model triangles)
            ;; groups
            (set-groups model groups)
            ;; materials
            (set-materials model materials)
            ;; animation
            (let ((animation-fps (max 1. (<-float)))
                  (current-frame (<-float))
                  (total-frames (<-s32)))
              (set-animation-fps model animation-fps)
              (set-current-frame model current-frame)
              (set-total-frames model total-frames))
            ;; bones
            (set-bones model (<-bones (get-animation-fps model)))
            ;; comments
            ;; vertex extra
            ;; bone extra
            ;; model extra
            model)))))))
