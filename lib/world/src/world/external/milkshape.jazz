;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; MilkShape 3D
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


;; This is a port of Mete Ciragan's msViewer
;;
;; email:          mciragan@gmx.net
;; web:            http://www.milkshape3d.com


(module world.external.milkshape jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.system)
        (world)
        (world.animation)
        (world.atlas)
        (world.binary)
        (world.dye)
        (world.external)
        (world.foreign)
        (world.geometry)
        (world.homogeneous)
        (world.material)
        (world.mesh)
        (world.model)
        (world.renderers)
        (world.support)
        (world.texture)
        (world.triangle))


(proclaim (warn optimizations))


;;;
;;;; Vertex
;;;


(class MS3D-Vertex extends Object
  
  
  (slot flags   <fx>                                 accessors generate)
  (slot vertex  <f64vector> initialize (make-vertex) accessors generate)
  (slot bone-id <fx>                                 accessors generate)
  
  
  (method override (initialize flags vertex bone-id)
    (set! flags~self flags)
    (set! vertex~self vertex)
    (set! bone-id~self bone-id)))


;;;
;;;; Triangle
;;;


(class MS3D-Triangle extends Object
  
  
  (slot flags           <fx+>       accessors generate)
  (slot vertex-indices  <list>      accessors generate)
  (slot vertex-normals  <list>      accessors generate)
  (slot s               <f64vector> accessors generate)
  (slot t               <f64vector> accessors generate)
  (slot d               <f64vector> accessors generate)
  (slot smoothing-group <fx+>       accessors generate)
  (slot group-index     <fx+>       accessors generate)
  
  
  (method override (initialize flags vertex-indices vertex-normals s t d smoothing-group group-index)
    (set! flags~self flags)
    (set! vertex-indices~self vertex-indices)
    (set! vertex-normals~self vertex-normals)
    (set! s~self s)
    (set! t~self t)
    (set! d~self d)
    (set! smoothing-group~self smoothing-group)
    (set! group-index~self group-index)))


;;;
;;;; Group
;;;


(class MS3D-Group extends Object
  
  
  (slot flags          <fx+>     accessors generate)
  (slot name           <string+> accessors generate)
  (slot triangles      <vector>  accessors generate)
  (slot material-index <fx+>     accessors generate)
  
  
  (method override (initialize flags name triangles material-index)
    (set! flags~self flags)
    (set! name~self name)
    (set! triangles~self triangles)
    (set! material-index~self material-index)))


;;;
;;;; Model
;;;


(class MS3D-Model extends Object
  
  
  (slot vertices      <vector> accessors generate)
  (slot triangles     <vector> accessors generate)
  (slot groups        <vector> accessors generate)
  (slot materials     <vector> accessors generate)
  (slot animation-fps <fl>     accessors generate)
  (slot current-time  <fl>     accessors generate)
  (slot total-frames  <fx>     accessors generate)
  (slot bones         <object> accessors generate)
  
  
  (method (extract-atlas file <File>) <Atlas>
    (define (triangle-ref index <fx>) <MS3D-Triangle>
      (vector-ref triangles index))
    
    (define (material-ref index <fx>) <Material>
      (vector-ref materials index))
    
    (let ((atlas (new Atlas mipmap?: #f repeat?: #t pad?: #f normalize?: #f))
          (textures (remove-duplicates (remove-empty (map get-texture-name~ (vector->list materials))) test: filename=?)))
      (for-each (lambda (texture)
                  (add-file~ atlas (new-brother~ file texture)))
                textures)
      (complete~ atlas)
      (loop (for group <MS3D-Group> in-vector groups)
            (let ((material (material-ref (get-material-index~ group))))
              (let ((uv (image-rect~ atlas (extract-base (get-texture-name~ material)))))
                (let ((left (uv-left uv))
                      (top (uv-top uv))
                      (texture-depth (uv-depth uv)))
                  (let ((width (- (uv-right uv) left))
                        (height (- (uv-bottom uv) top)))
                    ;; adjust texture coordinates
                    (loop (for triangle-index in-vector (get-triangles~ group))
                          (let ((triangle (triangle-ref triangle-index)))
                            (let ((s (get-s~ triangle))
                                  (t (get-t~ triangle))
                                  (d (get-d~ triangle)))
                              (vertex-x-set! s (+ left (* (vertex-x s) width)))
                              (vertex-y-set! s (+ left (* (vertex-y s) width)))
                              (vertex-z-set! s (+ left (* (vertex-z s) width)))
                              (vertex-x-set! t (+ top (* (- 1. (vertex-x t)) height)))
                              (vertex-y-set! t (+ top (* (- 1. (vertex-y t)) height)))
                              (vertex-z-set! t (+ top (* (- 1. (vertex-z t)) height)))
                              (vertex-x-set! d texture-depth)
                              (vertex-y-set! d texture-depth)
                              (vertex-z-set! d texture-depth)))))))))
      atlas))
  
  
  @w
  (method (extract-geometry groups)
    (generate-mesh
      (lambda (setup-proc triangle-proc quad-proc)
        (define (add-triangle triangle)
          (let ((indices (get-vertex-indices~ triangle))
                (normals (get-vertex-normals~ triangle))
                (s (get-s~ triangle))
                (t (get-t~ triangle))
                (d (get-d~ triangle)))
            (let ((v1 (vertex-ref (first indices)))
                  (v2 (vertex-ref (second indices)))
                  (v3 (vertex-ref (third indices))))
              (triangle-proc (get-vertex~ v1) (get-vertex~ v2) (get-vertex~ v3)
                (get-vertex~ v1) (first  normals) (vertex (vertex-x s) (vertex-x t) (vertex-x d)) (get-bone-id~ v1)
                (get-vertex~ v2) (second normals) (vertex (vertex-y s) (vertex-y t) (vertex-y d)) (get-bone-id~ v2)
                (get-vertex~ v3) (third  normals) (vertex (vertex-z s) (vertex-z t) (vertex-z d)) (get-bone-id~ v3)))))
        
        (setup-proc (vector-length triangles) 0)
        (loop (for triangle in-vector triangles)
              (add triangle)))))
  
  
  (method (extract-geometry groups)
    (define (count-triangles) <fx>
      (let ((count 0))
        (loop (for group <MS3D-Group> in groups)
              (increase! count (vector-length (get-triangles~ group))))
        count))
    
    (let ((vertex-floats 10))
      (let ((triangle-floats (* 3 vertex-floats))
            (triangle-neighbors-floats (* 3 7))
            (triangle-lightmap-floats (* 3 3))
            (triangles-count (count-triangles)))
        (let ((content (make-f32vector (* triangles-count triangle-floats)))
              (neighbors (make-f32vector (* triangles-count triangle-neighbors-floats)))
              (lightmap (make-f32vector (* triangles-count triangle-lightmap-floats) 0.))
              (n 0)
              (m 0))
          (define (vertex-ref index <fx>) <MS3D-Vertex>
            (vector-ref vertices index))
          
          (define (add-triangle triangle <MS3D-Triangle>)
            (let ((indices (get-vertex-indices~ triangle))
                  (normals (get-vertex-normals~ triangle))
                  (s (get-s~ triangle))
                  (t (get-t~ triangle))
                  (d (get-d~ triangle)))
              (let ((v1 (vertex-ref (first indices)))
                    (v2 (vertex-ref (second indices)))
                    (v3 (vertex-ref (third indices))))
                (add
                  (get-vertex~ v1) (first  normals) (vertex (vertex-x s) (vertex-x t) (vertex-x d)) (get-bone-id~ v1)
                  (get-vertex~ v2) (second normals) (vertex (vertex-y s) (vertex-y t) (vertex-y d)) (get-bone-id~ v2)
                  (get-vertex~ v3) (third  normals) (vertex (vertex-z s) (vertex-z t) (vertex-z d)) (get-bone-id~ v3)))))
          
          (define (add v1 n1 t1 b1 v2 n2 t2 b2 v3 n3 t3 b3)
            (add-vertex v1) (add-normal n1) (add-texture t1) (add-bone b1) (add-neighbors 0. v2 v3)
            (add-vertex v2) (add-normal n2) (add-texture t2) (add-bone b2) (add-neighbors 1. v1 v3)
            (add-vertex v3) (add-normal n3) (add-texture t3) (add-bone b3) (add-neighbors 2. v1 v2))
          
          (define (add-vertex vert)
            (f32vector-set! content n (vertex-x vert))
            (f32vector-set! content (+ n 1) (vertex-y vert))
            (f32vector-set! content (+ n 2) (vertex-z vert))
            (set! n (+ n 3)))
          
          (define (add-normal normal)
            (f32vector-set! content n (vertex-x normal))
            (f32vector-set! content (+ n 1) (vertex-y normal))
            (f32vector-set! content (+ n 2) (vertex-z normal))
            (set! n (+ n 3)))
          
          (define (add-texture tex)
            (f32vector-set! content n (vertex-x tex))
            (f32vector-set! content (+ n 1) (vertex-y tex))
            (f32vector-set! content (+ n 2) (vertex-z tex))
            (set! n (+ n 3)))
          
          (define (add-bone bone-id <fx>)
            (f32vector-set! content n (cast <fl> bone-id))
            (set! n (+ n 1)))
          
          (define (add-neighbors rank n1 n2)
            (f32vector-set! neighbors m rank)
            (set! m (+ m 1))
            (f32vector-set! neighbors m (vertex-x n1))
            (f32vector-set! neighbors (+ m 1) (vertex-y n1))
            (f32vector-set! neighbors (+ m 2) (vertex-z n1))
            (set! m (+ m 3))
            (f32vector-set! neighbors m (vertex-x n2))
            (f32vector-set! neighbors (+ m 1) (vertex-y n2))
            (f32vector-set! neighbors (+ m 2) (vertex-z n2))
            (set! m (+ m 3)))
          
          (loop (for group <MS3D-Group> in groups)
                (loop (for triangle-index <fx> in-vector (get-triangles~ group))
                      (add-triangle (vector-ref triangles triangle-index))))
          (values content neighbors lightmap)))))
  
  
  (method (extract-bounds)
    (define (vertex-ref index <fx>)
      (get-vertex~ (cast <MS3D-Vertex> (vector-ref vertices index))))
    
    (let ((bounds (let ((vert (vertex-ref 0)))
                    (let ((x (vertex-x vert))
                          (y (vertex-y vert))
                          (z (vertex-z vert)))
                      (cuboid x y z x y z)))))
      (loop (for n from 1 below (vector-length vertices))
            (let ((vertex (vertex-ref n)))
              (let ((x (vertex-x vertex))
                    (y (vertex-y vertex))
                    (z (vertex-z vertex)))
                (when (< x (cuboid-left bounds))
                  (cuboid-left-set! bounds x))
                (when (> x (cuboid-right bounds))
                  (cuboid-right-set! bounds x))
                (when (< y (cuboid-bottom bounds))
                  (cuboid-bottom-set! bounds y))
                (when (> y (cuboid-top bounds))
                  (cuboid-top-set! bounds y))
                (when (< z (cuboid-back bounds))
                  (cuboid-back-set! bounds z))
                (when (> z (cuboid-front bounds))
                  (cuboid-front-set! bounds z)))))
      bounds))
  
  
  (method (extract-animation name)
    (let ((animation (new Animation name animation-fps total-frames bones)))
      (setup-bones~ animation)
      animation)))


;;;
;;;; Import
;;;


(definition public (load-ms3d file <File> (metadata: metadata #f)) <Model>
  (let ((name (get-base~ file))
        (render (or (model-metadata-render metadata) '(transparent)))
        (mask (model-metadata-mask metadata)))
    (define (add-animations model <Model>)
      (let ((dir <Directory> (get-parent~ file)))
        (iterate-directory~ dir
          (lambda (file <File>)
            (when (extension=? (get-extension~ file) "ms3d")
              (let ((base (get-base~ file)))
                (unless (filename=? base name)
                  (assert (starts-with? base (concatenate name "_")))
                  (let ((name (subseq base (+ (cardinality name) 1) (cardinality base)))
                        (ms3d (load-model file)))
                    (add-animation~ model name (extract-animation~ ms3d name)))))))
          files?: #t
          directories?: #f)))
    
    ;; necessary as animations come from distinct files
    (define (validate-animations model <Model>)
      (unless (apply = (map (lambda (anim <Animation>)
                              (vector-length (get-bones~ anim)))
                            (all-animations~ model)))
        (error "Inconsistant bones in model: {a}" name)))
    
    (let ((model (new Model name: name rotation: (vertex 0. PI/2 0.))))
      (set-metadata~ model metadata)
      (let ((ms3d (load-model file)))
        (when (get-debug-ms3d?)
          (set-native~ model ms3d))
        ;; order is important as extract-atlas adjusts texture coordinates
        (let ((atlas (extract-atlas~ ms3d file)))
          (when (get-debug-meshes?)
            (set-atlas~ model atlas))
          (let ((texture (get-texture~ atlas)))
            (define (extract-mesh groups renderer)
              (let ((mesh (new Mesh material: (new Material atlas: atlas texture: texture renderer: renderer))))
                (receive (data neighbors lightmap) (extract-geometry~ ms3d groups)
                  (allocate-vertices~ mesh)
                  (set-data~ (get-vertices-buffer~ mesh) data 10)
                  (when (get-debug-meshes?)
                    (set-vertices-data~ mesh data))
                  (allocate-neighbors~ mesh)
                  (set-data~ (get-neighbors-buffer~ mesh) neighbors 6)
                  (allocate-lightmap~ mesh)
                  (set-data~ (get-lightmap-buffer~ mesh) lightmap 3))
                mesh))
            
            (define (iterate-groups proc)
              (let ((renderer (require-renderer (car render)))
                    (render? (if (not mask) #t (car mask)))
                    (renderscan (cdr render))
                    (maskscan (and mask (cdr mask))))
                (loop (for group in-vector (get-groups~ ms3d))
                      (do (proc group renderer render?)
                          (unless (null? renderscan)
                            (set! renderer (require-renderer (car renderscan)))
                            (set! renderscan (cdr renderscan)))
                          (unless (or (not maskscan) (null? maskscan))
                            (set! render? (car maskscan))
                            (set! maskscan (cdr maskscan)))))))
            
            (if (get-debug-meshes?)
                (let ((queue (new-queue)))
                  (iterate-groups
                    (lambda (group renderer render?)
                      (when render?
                        (enqueue queue (extract-mesh (list group) renderer)))))
                  (set-meshes~ model (list->vector (queue-list queue))))
              (let ((render-groups (make-table test: eq?)))
                (iterate-groups
                  (lambda (group renderer render?)
                    (when render?
                      (table-add render-groups renderer group))))
                (let ((queue (new-queue)))
                  (iterate-table render-groups
                    (lambda (renderer groups)
                      (enqueue queue (extract-mesh groups renderer))))
                  (set-meshes~ model (list->vector (queue-list queue))))))))
        (set-bounds~ model (extract-bounds~ ms3d))
        (set-anchor~ model 'origin)
        (set-animation~ model (extract-animation~ ms3d #f))
        (add-animations model)
        (validate-animations model))
      model)))


(definition (load-model file <File>) <MS3D-Model>
  (call-with-input-file (path-settings file)
    (lambda (port)
      (let ((MAX_TEXTURE_FILENAME_SIZE 128))
        (let ((content (load-file-content file))
              (pos <fx> 0))
          (define (<-s8) <fx>
            (prog1 (scan-s8 content pos)
              (increase! pos 1)))
          
          (define (<-u8) <fx>
            (prog1 (scan-u8 content pos)
              (increase! pos 1)))
          
          (define (<-s16) <fx>
            (prog1 (scan-s16 content pos)
              (increase! pos 2)))
          
          (define (<-u16) <fx>
            (prog1 (scan-u16 content pos)
              (increase! pos 2)))
          
          (define (<-s32) <int>
            (prog1 (scan-s32 content pos)
              (increase! pos 4)))
          
          (define (<-float) <fl>
            (prog1 (scan-float content pos)
              (increase! pos float-size)))
          
          (define (<-floats32! vec <f32vector>) <f32vector>
            (let ((len (f32vector-length vec)))
              (scan-floats32! content pos vec len)
              (increase! pos (* len float-size))
              vec))
          
          (define (<-floats64! vec <f64vector>) <f64vector>
            (let ((len (f64vector-length vec)))
              (scan-floats64! content pos vec len)
              (increase! pos (* len float-size))
              vec))
          
          (define (<-string size <fx>)
            (prog1 (scan-string content pos size)
              (increase! pos size)))
          
          (define (<-vertex) <f64vector>
            (<-floats64! (make-vertex)))
          
          (define (<-normal) <f64vector>
            (<-vertex))
          
          (define (<-tex) <f64vector>
            (<-floats64! (make-vertex)))
          
          (define (<-color) <f32vector>
            (<-floats32! (make-dye)))
          
          (define (<-indices) <list>
            (list
              (<-u16)
              (<-u16)
              (<-u16)))
          
          (define (<-normals) <list>
            (list
              (<-normal)
              (<-normal)
              (<-normal)))
          
          ;; vertices
          (define (<-vertices) <vector>
            (let ((num-vertices (<-u16)))
              (let ((vertices (make-vector num-vertices)))
                (loop (for i from 0 below num-vertices)
                      (let ((flags (<-u8))
                            (vertex (<-vertex))
                            (bone-id (<-s8))
                            (reference-count (<-u8)))
                        (vector-set! vertices i (new MS3D-Vertex flags vertex bone-id))))
                vertices)))
          
          ;; triangles
          (define (<-triangles) <vector>
            (let ((num-triangles (<-u16)))
              (let ((triangles (make-vector num-triangles)))
                (loop (for i from 0 below num-triangles)
                      (let ((flags (<-u16))
                            (vertex-indices (<-indices))
                            (vertex-normals (<-normals))
                            (s (<-tex))
                            (t (<-tex))
                            (d (vertex 0. 0. 0.))
                            (smoothing-group (<-u8))
                            (group-index (<-u8)))
                        (let ((triangle
                                (new MS3D-Triangle
                                  flags
                                  vertex-indices
                                  vertex-normals
                                  s
                                  t
                                  d
                                  smoothing-group
                                  group-index)))
                          (vector-set! triangles i triangle))))
                triangles)))
          
          ;; groups
          (define (<-groups) <vector>
            (let ((num-groups (<-u16)))
              (let ((groups (make-vector num-groups)))
                (loop (for i from 0 below num-groups)
                      (let ((flags (<-u8))
                            (name (<-string 32))
                            (triangles
                              (let ((num-group-triangles (<-u16)))
                                (let ((group-triangles (make-vector num-group-triangles)))
                                  (loop (for j from 0 below num-group-triangles)
                                        (vector-set! group-triangles j (<-u16)))
                                  group-triangles)))
                            (material-index (<-s8)))
                        (let ((group
                                (new MS3D-Group
                                  flags
                                  name
                                  triangles
                                  material-index)))
                          (vector-set! groups i group))))
                groups)))
          
          ;; materials
          (define (<-materials) <vector>
            (let ((num-materials (<-u16)))
              (let ((materials (make-vector num-materials)))
                (loop (for i from 0 below num-materials)
                      (let ((name (<-string 32))
                            (ambient (<-color))
                            (diffuse (<-color))
                            (specular (<-color))
                            (emissive (<-color))
                            (shininess (<-float))
                            (transparency (<-float))
                            (mode (<-u8))
                            (texture (<-string MAX_TEXTURE_FILENAME_SIZE))
                            (alphamap (<-string MAX_TEXTURE_FILENAME_SIZE)))
                        ;; set alpha
                        (dye-alpha-set! ambient transparency)
                        (dye-alpha-set! diffuse transparency)
                        (dye-alpha-set! specular transparency)
                        (dye-alpha-set! emissive transparency)
                        (let ((material
                                (new Material
                                  name: name
                                  ambient: ambient
                                  diffuse: diffuse
                                  specular: specular
                                  emissive: emissive
                                  shininess: shininess
                                  transparency: transparency
                                  mode: mode
                                  texture-name: texture
                                  alphamap: alphamap)))
                          (vector-set! materials i material))))
                materials)))
          
          ;; bones
          (define (<-bones animation-fps <fl>) <vector>
            (let ((num-bones (<-u16)))
              (let ((bones (make-vector num-bones)))
                (loop (for i from 0 below num-bones)
                      (let ((flags (<-u8))
                            (name (<-string 32))
                            (parent-name (<-string 32))
                            (rot (euler-quaternion (<-vertex)))
                            (pos (<-vertex))
                            (num-key-frames-rot (<-u16))
                            (num-key-frames-pos (<-u16)))
                        (let ((rotation-keys (make-vector num-key-frames-rot))
                              (position-keys (make-vector num-key-frames-pos)))
                          ;; the frame time is in seconds, so multiply it by the animation fps
                          ;; to get the frames rotation channel
                          (loop (for j from 0 below num-key-frames-rot)
                                (let ((time (<-float))
                                      (key (euler-quaternion (<-vertex))))
                                  (let ((time (* time animation-fps)))
                                    (vector-set! rotation-keys j (new KeyFrame time key)))))
                          ;; translation channel
                          (loop (for j from 0 below num-key-frames-pos)
                                (let ((time (<-float))
                                      (key (<-vertex)))
                                  (let ((time (* time animation-fps)))
                                    (vector-set! position-keys j (new KeyFrame time key)))))
                          (let ((bone (new Bone flags name i parent-name rot pos rotation-keys position-keys)))
                            (vector-set! bones i bone)))))
                bones)))
          
          ;; header
          (assert (equal? (<-string 10) "MS3D000000"))
          (assert (= (<-s32) 4))
          
          ;; content
          (let ((model (new MS3D-Model))
                (vertices (<-vertices))
                (triangles (<-triangles))
                (groups (<-groups))
                (materials (<-materials)))
            ;; vertices
            (set-vertices~ model vertices)
            ;; triangles
            (set-triangles~ model triangles)
            ;; groups
            (set-groups~ model groups)
            ;; materials
            (set-materials~ model materials)
            ;; animation
            (let ((animation-fps (max 1. (<-float)))
                  (current-time (<-float))
                  (total-frames (<-s32)))
              (set-animation-fps~ model animation-fps)
              (set-current-time~ model current-time)
              (set-total-frames~ model total-frames))
            ;; bones
            (set-bones~ model (<-bones (get-animation-fps~ model)))
            ;; comments
            ;; vertex extra
            ;; bone extra
            ;; model extra
            model)))))))
