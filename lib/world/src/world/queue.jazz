;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Circular Queues
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.queue jazz


(import (jazz.math)
        (jazz.syntax (phase syntax))
        (world)
        (world.autoload))


(proclaim (not check bounds zero)
          (warn optimizations))


;;;
;;;; Circular
;;;


(class Circular-Queue extends Object
  
  
  (slot priority <fx>     getter generate)
  (slot size     <fx>)
  (slot content  <vector>)
  (slot head     <fx>)
  (slot tail     <fx>)
  (slot count    <fx>     getter generate)
  
  
  (method override (initialize self prio sz)
    (set! priority prio)
    (set! size sz)
    (set! content (make-vector sz))
    (set! head 0)
    (set! tail 0)
    (set! count 0))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" count))))
  
  
  (method public (empty? self)
    (= count 0))
  
  
  (method public (reset self proc)
    (loop (for n from 0 below count)
          (let ((offset (modulo (+ head n) size)))
            (when proc
              (let ((obj (vector-ref content offset)))
                ;; quick hack we need to understand how sometimes
                ;; in a bailout-client we can end up getting a #f
                (when obj
                  (proc obj priority))))
            (vector-set! content offset #f)))
    (set! head 0)
    (set! tail 0)
    (set! count 0))
  
  
  (method public (push self obj)
    (let ((new-count (+ count 1)))
      (when (> new-count size)
        (let ((new-size (if (= size 0) 8 (fxround (* size GOLDEN-RATIO)))))
          (let ((new-content (make-vector new-size)))
            (loop (for n from 0 below count)
                  (vector-set! new-content n (vector-ref content (modulo (+ head n) size))))
            (set! size new-size)
            (set! content new-content)
            (set! head 0)
            (set! tail count))))
      (vector-set! content tail obj)
      (set! tail (modulo (+ tail 1) size))
      (set! count new-count)))
  
  
  (method public (pop self)
    (if (<= count 0)
        (error "Queue is empty")
      (prog1 (vector-ref content head)
        (vector-set! content head #f)
        (set! head (modulo (+ head 1) size))
        (decrease! count)))))


;;;
;;;; Priorities
;;;


(class Priorities-Queue extends Object
  
  
  (slot count     <fx>)
  (slot queues    <vector>)
  (slot mutex              initialize (make-mutex 'queue))
  (slot condition-variable initialize (make-condition-variable 'queue))
  
  
  (method override (initialize self cnt <fx> (reserved 0))
    (nextmethod self)
    (assert (> cnt 0))
    (set! count cnt)
    (set! queues (list->vector (map (lambda (priority) (new Circular-Queue priority reserved))
                                    (naturals 0 count)))))
  
  
  (method inline (get-queue self priority <fx>) <Circular-Queue>
    (element queues priority))
  
  
  (method (top-queue self) <Circular-Queue+>
    (declare (proper-tail-calls))
    (let (iter (priority 0))
      (if (< priority count)
          (let ((queue (get-queue self priority)))
            (if (not (empty? queue))
                queue
              (iter (+ priority 1))))
        #f)))
  
  
  (method public (size self)
    (mutex-lock! mutex)
    (prog1 (let ((size 0))
             (loop (for queue <Circular-Queue> in-vector queues)
                   (increase! size (get-count queue)))
             size)
      (mutex-unlock! mutex)))
  
  
  (method public (sizes self)
    (loop (for p from 0 below count)
          (collect (get-count (get-queue self p)))))
  
  
  (method public (empty? self)
    (mutex-lock! mutex)
    (prog1 (not (top-queue self))
      (mutex-unlock! mutex)))
  
  
  (method public (has? self priority)
    (mutex-lock! mutex)
    (prog1 (not (empty? (get-queue self priority)))
      (mutex-unlock! mutex)))
  
  
  (method public (reset self proc)
    (mutex-lock! mutex)
    (loop (for queue <Circular-Queue> in-vector queues)
          (reset queue proc))
    (mutex-unlock! mutex))
  
  
  (method public (trim self priority proc)
    (mutex-lock! mutex)
    (let ((trimmed 0))
      (loop (for p from priority below count)
            (let ((queue (get-queue self p)))
              (increase! trimmed (get-count queue))
              (reset queue proc)))
      (prog1 trimmed
        (mutex-unlock! mutex))))
  
  
  (method public (trim-priority self priority proc)
    (mutex-lock! mutex)
    (let ((queue (get-queue self priority)))
      (let ((trimmed (get-count queue)))
        (reset queue proc)
        (prog1 trimmed
          (mutex-unlock! mutex)))))
  
  
  (method public (push self obj priority <fx>)
    (mutex-lock! mutex)
    (push (get-queue self priority) obj)
    (mutex-unlock! mutex)
    (condition-variable-signal! condition-variable))
  
  
  (method public (pop self timeout)
    (declare (proper-tail-calls))
    (let (iter)
      (mutex-lock! mutex)
      (let ((queue (top-queue self)))
        (cond (queue
               (prog1 (pop queue)
                 (mutex-unlock! mutex)))
              ((mutex-unlock! mutex condition-variable timeout)
               (iter))
              (else
               (eof-object))))))
  
  
  (method public (pop! self timeout info)
    (declare (proper-tail-calls))
    (let (iter)
      (mutex-lock! mutex)
      (let ((queue (top-queue self)))
        (cond (queue
               (let ((value (pop queue))
                     (priority (get-priority queue)))
                 (mutex-unlock! mutex)
                 (vector-set! info 0 value)
                 (vector-set! info 1 priority)))
              ((mutex-unlock! mutex condition-variable timeout)
               (iter))
              (else
               (vector-set! info 0 (eof-object))
               (vector-set! info 1 0)))))))


;;;
;;;; Pool
;;;


(define-pool queue$
  new-queue Queue)


(definition protected (make-queue$) <Queue>
  (let ((queue (need-queue$)))
    (reset-queue queue)
    queue)))
