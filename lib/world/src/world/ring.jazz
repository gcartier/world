;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Ring
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.ring jazz


(proclaim (warn optimizations))


(class Ring extends Object
  
  
  (slot size    <fx>     getter generate)
  (slot content <vector> getter generate)
  (slot now     <fx>     accessors generate)
  (slot head    <fx>     accessors generate)
  (slot count   <fx>     accessors generate)
  
  
  (method override (initialize self size <fx>)
    (nextmethod self)
    (set! self.size size)
    (set! content (make-vector size))
    (set! now 0)
    (set! head 0)
    (set! count 0))

  
  (method package (start-position self) <fx>
    0)
  
  
  (method (end-position self) <fx>
    count)
  
  
  (method package (first-position self) <fx>
    0)
  
  
  (method package (last-position self) <fx>
    (- (end-position self) 1))
  
  
  (method (index self n <fx>) <fx>
    (modulo (+ head n) size))
  
  
  (method package (add self obj)
    (vector-set! content (index self now) obj)
    (if (< count size)
        (begin
          (increase! count)
          (increase! now))
      (set! head (modulo (+ head 1) size))))))
