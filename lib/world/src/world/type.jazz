;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Change Types
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.type jazz


(import (world)
        (world.autoload)
        (world.commands)
        (world.entity)
        (world.geometry)
        (world.history)
        (world.index)
        (world.log)
        (world.syntax (phase syntax)))


;;;
;;;; Float
;;;


(definition protected (push-float push val)
  (push val))


(definition protected (pop-float pop)
  (pop))


;;;
;;;; Integer
;;;


(definition protected (push-integer push val)
  (push (cast <fl> val)))


(definition protected (pop-integer pop)
  (let ((fl (pop)))
    (flonum->fixnum fl)))


;;;
;;;; Vertex
;;;


(definition protected (push-vertex push vert)
  (push (vertex-x vert))
  (push (vertex-y vert))
  (push (vertex-z vert)))


(definition protected (pop-vertex! vert pop)
  (let* ((x (pop))
         (y (pop))
         (z (pop)))
    (vertex! vert x y z)))


;;;
;;;; LookAt
;;;


(definition protected (push-lookat push lookat)
  (let ((sight (get-sight~ lookat))
        (up (get-up~ lookat))
        (right (get-right~ lookat)))
    (push (vertex-x sight))
    (push (vertex-y sight))
    (push (vertex-z sight))
    (push (vertex-x up))
    (push (vertex-y up))
    (push (vertex-z up))
    (push (vertex-x right))
    (push (vertex-y right))
    (push (vertex-z right))))


(definition protected (pop-lookat! lookat pop)
  (let* ((sx (pop))
         (sy (pop))
         (sz (pop))
         (ux (pop))
         (uy (pop))
         (uz (pop))
         (rx (pop))
         (ry (pop))
         (rz (pop)))
    (lookat! lookat
             (vertex& sx sy sz)
             (vertex& ux uy uz)
             (vertex& rx ry rz))))


;;;
;;;; Literal
;;;


(definition protected (push-literal push literal)
  (push (cast <fl> (literal->id literal))))


(definition protected (pop-literal pop)
  (let ((literal-id (pop)))
    (id->literal (flonum->fixnum literal-id))))


;;;
;;;; Id
;;;


(definition protected (push-id push id)
  (push-literal push id))


(definition protected (pop-id pop)
  (pop-literal pop))


;;;
;;;; Class
;;;


(definition protected (push-class push class)
  (push-literal push (category-name class)))


(definition protected (pop-class pop)
  (let ((world (current-world)))
    (let ((class-name (pop-literal pop)))
      (class-name->class~ world class-name))))


;;;
;;;; Entity
;;;


(definition protected (push-entity push entity)
  (push (cast <fl> (literal->id (get-id~ entity)))))


(definition protected (pop-entity pop)
  (let ((id (pop)))
    (let ((entity (find-entity (id->literal (flonum->fixnum id)))))
      (when (not entity)
        (log-server (format "Unable to find entity {s}" (id->literal (flonum->fixnum id)))))
      entity)))


(definition protected (push-chunk-entity push entity)
  (let ((chunk (get-designer-chunk~ entity)))
    ;; devel assertion until fully debugged
    (assert chunk
      (let ((section-x (section-x~ chunk))
            (section-z (section-z~ chunk)))
        (push-integer push section-x)
        (push-integer push section-z)
        (push-id push (get-id~ entity))))))


(definition protected (pop-chunk-entity pop)
  (let ((zone (current-zone)))
    (let* ((section-x (pop-integer pop))
           (section-z (pop-integer pop))
           (id (pop-id pop)))
      ;; collabo quick test
      (let ((section-y 0))
        (index-chunk~ zone (indexed section-x section-y section-z))
        (find-entity id)))))


;;;
;;;; Commands
;;;


(definition protected (push-commands push commands)
  (push ;; quick hack for track-ground
        (if commands
            (cast <fl> (encode~ commands))
          -1.)))


(definition protected (pop-commands pop)
  (let ((commands-encoded (pop)))
    ;; quick hack for track-ground
    (if (= commands-encoded -1.)
        #f
      (let ((commands (new Commands)))
        (decode~ commands (flonum->fixnum commands-encoded))
        commands)))))
