;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Binary Read
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.binary jazz


(import (jazz.foreign)
        (jazz.io)
        (jazz.zlib))


;;;
;;;; Binary-Reader
;;;


(proclaim (warn optimizations))


(class Binary-Reader extends Object
  
  
  (slot content  <u8vector> getter generate)
  (slot size     <fx>       getter generate)
  (slot position <fx>       getter generate)
  
  
  (method override (initialize self source (compressed?: compressed? #f) (gzip?: gzip? #f))
    (cond ((u8vector? source)
           (set! content source))
          (else
           (let ((u8vec (load-binary source)))
             (set! content (if compressed? (zlib-inflate u8vec gzip?: gzip?) u8vec)))))
    (let ((len (u8vector-length content)))
      (assert (> len 0)
        (set! size len)
        (set! position 0))))
  
  
  (method public (more? self bytes <fx>)
    (<= (+ position bytes) size))
  
  
  (method public (<-s8 self) <fx>
    (prog1 (scan-s8 content position)
      (increase! position 1)))
  
  
  (method public (<-u8 self) <fx>
    (prog1 (scan-u8 content position)
      (increase! position 1)))
  
  
  (method public (<-u8vector self size <fx>)
    (prog1 (subu8vector content position (+ position size))
      (increase! position size)))
  
  
  (method public (<-s16 self) <fx>
    (prog1 (scan-s16 content position)
      (increase! position 2)))
  
  
  (method public (<-s16-big-endian self) <fx>
    (prog1 (scan-s16-big-endian content position)
      (increase! position 2)))
  
  
  (method public (<-u16 self) <fx>
    (prog1 (scan-u16 content position)
      (increase! position 2)))
  
  
  (method public (<-s32 self) <int>
    (prog1 (scan-s32 content position)
      (increase! position 4)))
  
  
  (method public (<-s32-big-endian self) <int>
    (prog1 (scan-s32-big-endian content position)
      (increase! position 4)))
  
  
  (method public (<-s64 self) <int>
    (prog1 (scan-s64 content position)
      (increase! position 8)))
  
  
  (method public (<-s64-big-endian self) <int>
    (prog1 (scan-s64-big-endian content position)
      (increase! position 8)))
  
  
  (method public (<-float self) <fl>
    (prog1 (scan-float content position)
      (increase! position float-size)))
  
  
  (method public (<-float-big-endian self) <fl>
    (prog1 (scan-float-big-endian content position)
      (increase! position float-size)))
  
  
  (method public (<-floats32! self vec <f32vector>) <f32vector>
    (let ((len (f32vector-length vec)))
      (scan-floats32! content position vec len)
      (increase! position (* len float-size))
      vec))
  
  
  (method public (<-floats64! self vec <f64vector>) <f64vector>
    (let ((len (f64vector-length vec)))
      (scan-floats64! content position vec len)
      (increase! position (* len float-size))
      vec))
  
  
  (method public (<-double self) <fl>
    (prog1 (scan-double content position)
      (increase! position double-size)))
  
  
  (method public (<-double-big-endian self) <fl>
    (prog1 (scan-double-big-endian content position)
      (increase! position double-size)))
  
  
  (method public (<-c-string self size <fx>)
    (prog1 (scan-c-string content position size)
      (increase! position size)))
  
  
  (method public (<-size-string self size <fx>)
    (prog1 (scan-size-string content position size)
      (increase! position size)))
  
  
  (method public (<-utf-8-string self size <fx>)
    (prog1 (scan-utf-8-string content position size)
      (increase! position size))))


;;;
;;;; Binary-Writer
;;;


(class Binary-Writer extends Object
  
  
  (slot file        <File+>    getter generate)
  (slot compressed? <bool>     getter generate)
  (slot gzip?       <bool>     getter generate)
  (slot port        <port>     getter generate)
  (slot buffer      <u8vector> getter generate)
  
  
  (method override (initialize self file (maximum: maximum #f) (compressed?: compressed? #f) (gzip?: gzip? #f))
    (set! self.file file)
    (set! self.compressed? compressed?)
    (set! self.gzip? gzip?)
    (set! port (open-u8vector))
    (set! buffer (make-u8vector (or maximum 8))))
  
  
  (method override (destroy self)
    (when file
      (let ((content (get-content self)))
        (call-with-output-file (path-settings file)
          (lambda (port)
            (write-subu8vector content 0 (u8vector-length content) port)))))
    (nextmethod self))
  
  
  (method public (get-content self) <u8vector>
    (let ((content (get-output-u8vector port)))
      (close-port port)
      (if compressed?
          (zlib-deflate content level: 4 gzip?: gzip?)
        content)))
  
  
  (method public (->s8 self val)
    (put-s8 buffer 0 val)
    (write-subu8vector buffer 0 1 port))
  
  
  (method public (->u8 self val)
    (put-u8 buffer 0 val)
    (write-subu8vector buffer 0 1 port))
  
  
  (method public (->s16 self val)
    (put-s16 buffer 0 val)
    (write-subu8vector buffer 0 2 port))
  
  
  (method public (->s16-big-endian self val)
    (put-s16-big-endian buffer 0 val)
    (write-subu8vector buffer 0 2 port))
  
  
  (method public (->u16 self val)
    (put-u16 buffer 0 val)
    (write-subu8vector buffer 0 2 port))
  
  
  (method public (->s32 self val)
    (put-s32 buffer 0 val)
    (write-subu8vector buffer 0 4 port))
  
  
  (method public (->s32-big-endian self val)
    (put-s32-big-endian buffer 0 val)
    (write-subu8vector buffer 0 4 port))
  
  
  (method public (->s64 self val)
    (put-s64 buffer 0 val)
    (write-subu8vector buffer 0 8 port))
  
  
  (method public (->s64-big-endian self val)
    (put-s64-big-endian buffer 0 val)
    (write-subu8vector buffer 0 8 port))
  
  
  (method public (->float self val)
    (put-float buffer 0 val)
    (write-subu8vector buffer 0 4 port))
  
  
  (method public (->floats32 self vec <f32vector>)
    (let ((len (f32vector-length vec)))
      (loop (for i from 0 below len)
            (->float self (f32vector-ref vec i)))))
  
  
  (method public (->floats64 self vec <f64vector>)
    (let ((len (f64vector-length vec)))
      (loop (for i from 0 below len)
            (->float self (f64vector-ref vec i)))))
  
  
  (method public (->float-big-endian self val)
    (put-float-big-endian buffer 0 val)
    (write-subu8vector buffer 0 4 port))
  
  
  (method public (->double self val)
    (put-double buffer 0 val)
    (write-subu8vector buffer 0 8 port))
  
  
  (method public (->double-big-endian self val)
    (put-double-big-endian buffer 0 val)
    (write-subu8vector buffer 0 8 port))
  
  
  (method public (->c-string self str size <fx>)
    (put-c-string buffer 0 str (string-length str) size)
    (write-subu8vector buffer 0 size port))
  
  
  (method public (->size-string self str size <fx>)
    (put-size-string buffer 0 str size)
    (write-subu8vector buffer 0 size port))
  
  
  (method public (->utf-8-string self val)
    (write-string val port))
  
  
  (method public (->u8vector self val <u8vector>)
    (write-subu8vector val 0 (u8vector-length val) port))))
