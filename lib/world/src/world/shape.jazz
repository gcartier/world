;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Shapes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.shape jazz


(import (jazz.geometry)
        (gaea.geometry)
        (gaea.uv)
        (world)
        (world.context)
        (world.face)
        (world.generate)
        (world.quad))


(proclaim (warn optimizations))

(declare (optimize-dead-local-variables))


;;;
;;;; Triangle
;;;


;; for tests
(definition public (triangle-generator (matrix: matrix #f))
  (let ((world (current-world)))
    (let ((uv (image-rect (get-block-atlas world) "stone")))
      (lambda (setup-proc triangle-proc quad-proc)
        (setup-proc 1 0)
        (let ((v1 (vertex -1. 0. 0.))
              (v2 (vertex  1. 0. 0.))
              (v3 (vertex  0. 1. 0.))
              (tl uv/left)
              (tt uv/top)
              (tr uv/right)
              (tb uv/bottom)
              (tdepth (uv-depth uv)))
          (triangle-proc
            v1 #f tl tb 0
            v2 #f tr tb 0
            v3 #f tr tt 0
            uv
            tdepth))))))


;;;
;;;; Quad
;;;


(definition public (generate-quad-mesh (neighbors?: neighbors? #f))
  (let ((world (current-world)))
    (let ((left -.5)
          (back -.5)
          (right .5)
          (front .5)
          (opaque-material (get-block-opaque world))
          (texture-coordinates (f32vector 0. 0. 0. 0. 0. 0. 0. 0.))
          (texture-depth 0.))
      (let ((faces
              (list
                (make-face opaque-material
                           texture-coordinates
                           texture-depth
                           (make-quad #f
                                      (vertex left 0. front) (vertex left 0. back) (vertex right 0. back) (vertex right 0. front))))))
        (generate-mesh (faces-generator faces) neighbors?: neighbors?)))))


;;;
;;;; Cube
;;;


(definition public (generate-cube-mesh (neighbors?: neighbors? #f) (instance?: instance? #f))
  (let ((world (current-world)))
    (let ((left -1.)
          (bottom -1.)
          (back -1.)
          (right 1.)
          (top 1.)
          (front 1.)
          (opaque-material (get-block-opaque world))
          (texture-coordinates (f32vector 0. 0. 0. 0. 0. 0. 0. 0.))
          (texture-depth 0.))
      (let ((faces
              (list
                ;; front
                (make-face opaque-material
                           texture-coordinates
                           texture-depth
                           (make-quad #f
                                      (vertex left top front) (vertex left bottom front) (vertex right bottom front) (vertex right top front)))
                ;; back
                (make-face opaque-material
                           texture-coordinates
                           texture-depth
                           (make-quad #f
                                      (vertex right top back) (vertex right bottom back) (vertex left bottom back) (vertex left top back)))
                ;; left
                (make-face opaque-material
                           texture-coordinates
                           texture-depth
                           (make-quad #f
                                      (vertex left top back) (vertex left bottom back) (vertex left bottom front) (vertex left top front)))
                ;; right
                (make-face opaque-material
                           texture-coordinates
                           texture-depth
                           (make-quad #f
                                      (vertex right top front) (vertex right bottom front) (vertex right bottom back) (vertex right top back)))
                ;; top
                (make-face opaque-material
                           texture-coordinates
                           texture-depth
                           (make-quad #f
                                      (vertex left top back) (vertex left top front) (vertex right top front) (vertex right top back)))
                ;; bottom
                (make-face opaque-material
                           texture-coordinates
                           texture-depth
                           (make-quad #f
                                      (vertex left bottom front) (vertex left bottom back) (vertex right bottom back) (vertex right bottom front))))))
        (generate-mesh (faces-generator faces) neighbors?: neighbors? instance?: instance?)))))


;;;
;;;; Sphere
;;;


(definition public (sphere-generator slices <fx> stacks <fx> uv <f32vector>)
  (lambda (setup-proc triangle-proc quad-proc)
    (let ((dphi (/ 360. (cast <fl> slices)))
          (dtheta (/ 180. (cast <fl> stacks))))
      (define (count-sphere)
        (let ((triangles (* slices 2))
              (quads (* slices (- stacks 2))))
          (setup-proc triangles quads)))
      
      (let ((DTOR (/ PI*2 360.))
            (tl uv/left)
            (tt uv/top)
            (tr uv/right)
            (tb uv/bottom)
            (texture-depth (uv-depth uv)))
        (define (add-triangle v1 v2 v3)
          (triangle-proc
            v1 #f tl tb 0
            v2 #f tr tb 0
            v3 #f tr tt 0
            uv
            texture-depth))
        
        (define (add-quad v1 v2 v3 v4)
          (quad-proc
            v1 #f tl tb 0
            v2 #f tr tb 0
            v3 #f tr tt 0
            v4 #f tl tt 0
            uv
            texture-depth))
        
        (count-sphere)
        (loop (for i from 0 below stacks)
              (let ((theta (- (* dtheta (cast <fl> i)) 90.)))
                (loop (for j from 0 below slices)
                      (let ((phi (* dphi (cast <fl> j))))
                        (let ((v1 (vertex (* (cos (* theta DTOR)) (cos (* phi DTOR)))
                                          (* (cos (* theta DTOR)) (sin (* phi DTOR)))
                                          (sin (* theta DTOR))))
                              (v2 (vertex (* (cos (* (+ theta dtheta) DTOR)) (cos (* phi DTOR)))
                                          (* (cos (* (+ theta dtheta) DTOR)) (sin (* phi DTOR)))
                                          (sin (* (+ theta dtheta) DTOR))))
                              (v3 (and (/= i (- stacks 1))
                                       (vertex (* (cos (* (+ theta dtheta) DTOR)) (cos (* (+ phi dphi) DTOR)))
                                               (* (cos (* (+ theta dtheta) DTOR)) (sin (* (+ phi dphi) DTOR)))
                                               (sin (* (+ theta dtheta) DTOR)))))
                              (v4 (and (/= i 0)
                                       (vertex (* (cos (* theta DTOR)) (cos (* (+ phi dphi) DTOR)))
                                               (* (cos (* theta DTOR)) (sin (* (+ phi dphi) DTOR)))
                                               (sin (* theta DTOR))))))
                          (if (and v3 v4)
                              (add-quad v4 v3 v2 v1)
                            (add-triangle (or v3 v4) v2 v1)))))))))))


(definition public (generate-sphere-mesh slices stacks uv (matrix: matrix #f) (neighbors?: neighbors? #f) (smooth-normals?: smooth-normals? #f) (material: material #f))
  (let ((generator (sphere-generator slices stacks uv)))
    (generate-mesh generator matrix: matrix neighbors?: neighbors? smooth-normals?: smooth-normals? material: material)))


;;;
;;;; Cylinder
;;;


(definition public (cylinder-generator slices <fx> stacks <fx> start-radius <fl> end-radius <fl> uv <f32vector>)
  (lambda (setup-proc triangle-proc quad-proc)
    (let ((dphi (/ 360. (cast <fl> slices)))
          (dside (/ 2. (cast <fl> stacks))))
      (define (count-cylinder)
        (let ((triangles 0)
              (quads (* slices stacks)))
          (setup-proc triangles quads)))
      
      (define add-triangle (wire-triangle triangle-proc))
      (define add-quad (wire-quad quad-proc))
      
      (let ((DTOR (/ PI*2 360.)))
        (count-cylinder)
        (loop (for i from 0 below stacks)
              (let ((side (- (* (cast <fl> i) dside) 1.))
                    (radius-from (/ (+ (* (cast <fl> i) start-radius)
                                       (* (cast <fl> (- stacks i)) end-radius))
                                    (cast <fl> stacks)))
                    (radius-to (/ (+ (* (cast <fl> (+ i 1)) start-radius)
                                     (* (cast <fl> (- stacks (+ i 1))) end-radius))
                                  (cast <fl> stacks))))
                (loop (for j from 0 below slices)
                      (let ((phi (* dphi (cast <fl> j))))
                        (let ((v1 (vertex (* radius-from (cos (* phi DTOR)))
                                          (* radius-from (sin (* phi DTOR)))
                                          side))
                              (v2 (vertex (* radius-to (cos (* phi DTOR)))
                                          (* radius-to (sin (* phi DTOR)))
                                          (+ side dside)))
                              (v3 (vertex (* radius-from (cos (* (+ phi dphi) DTOR)))
                                          (* radius-from (sin (* (+ phi dphi) DTOR)))
                                          side))
                              (v4 (vertex (* radius-to (cos (* (+ phi dphi) DTOR)))
                                          (* radius-to (sin (* (+ phi dphi) DTOR)))
                                          (+ side dside))))
                          (add-quad v1 v3 v4 v2))))))))))


(definition public (generate-cylinder-mesh slices stacks start-radius end-radius uv (matrix: matrix #f) (neighbors?: neighbors? #f) (smooth-normals?: smooth-normals? #f) (material: material #f))
  (let ((generator (cylinder-generator slices stacks start-radius end-radius uv)))
    (generate-mesh generator matrix: matrix neighbors?: neighbors? smooth-normals?: smooth-normals? material: material)))


;;;
;;;; Torus
;;;


(definition public (torus-generator slices <fx> stacks <fx> radius <fl> uv <f32vector>)
  (lambda (setup-proc triangle-proc quad-proc)
    (let ((dphi (/ 360. (cast <fl> slices)))
          (dtheta (/ PI*2 (cast <fl> stacks)))
          (dside (/ 2. (cast <fl> stacks))))
      (define (count-cylinder)
        (let ((triangles 0)
              (quads (* slices stacks)))
          (setup-proc triangles quads)))
      
      (define add-triangle (wire-triangle triangle-proc))
      (define add-quad (wire-quad quad-proc))
      
      (let ((up (vertex 0. 1. 0.))
            (DTOR (/ PI*2 360.)))
        (count-cylinder)
        (loop (for i from 0 below stacks)
              (let ((theta (* dtheta (cast <fl> i)))
                    (thetanext (* dtheta (cast <fl> (+ i 1))))
                    (side (- (* (cast <fl> i) dside) 1.)))
                (loop (for j from 0 below slices)
                      (let ((phi (* dphi (cast <fl> j))))
                        (let ((v1 (rotate-vertex-upon& theta
                                                       up
                                                       (vertex (- (* radius (cos (* phi DTOR))) 1.)
                                                               (* radius (sin (* phi DTOR)))
                                                               0.)))
                              (v2 (rotate-vertex-upon& thetanext
                                                       up
                                                       (vertex (- (* radius (cos (* phi DTOR))) 1.)
                                                               (* radius (sin (* phi DTOR)))
                                                               0.)))
                              (v3 (rotate-vertex-upon& theta
                                                       up
                                                       (vertex (- (* radius (cos (* (+ phi dphi) DTOR))) 1.)
                                                               (* radius (sin (* (+ phi dphi) DTOR)))
                                                               0.)))
                              (v4 (rotate-vertex-upon& thetanext
                                                       up
                                                       (vertex (- (* radius (cos (* (+ phi dphi) DTOR))) 1.)
                                                               (* radius (sin (* (+ phi dphi) DTOR)))
                                                               0.))))
                          (add-quad v1 v3 v4 v2))))))))))


(definition public (generate-torus-mesh slices stacks radius uv (matrix: matrix #f) (neighbors?: neighbors? #f) (smooth-normals?: smooth-normals? #f) (material: material #f))
  (let ((generator (torus-generator slices stacks radius uv)))
    (generate-mesh generator matrix: matrix neighbors?: neighbors? smooth-normals?: smooth-normals? material: material))))
