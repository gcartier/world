;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Creatures
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.creature jazz


(import (jazz.geometry)
        (jazz.syntax (phase syntax))
        (world)
        (world.actor)
        (world.autoload)
        (world.change)
        (world.commands)
        (world.configure)
        (world.context)
        (world.dye)
        (world.dyes)
        (world.generation.block)
        (world.geometry)
        (world.history)
        (world.homogeneous)
        (world.scriptum)
        (world.settings)
        (world.sound)
        (world.syntax (phase syntax))
        (world.task)
        (world.travel))


;;;
;;;; Creature
;;;


(definition public default-creature-behavior <symbol+>
  (world-setting 'world.creature-behavior #f))

(definition public non-user-aggroes? <bool>
  (world-setting 'world.non-user-aggroes? #f))


(definition protected attack-distance <fl>
  .1)


(definition public creature-models? <bool>
  (world-setting 'world.creature-models? #f))

(definition public (set-creature-models? flag <bool>)
  (set! creature-models? flag))


;; not correct should get info dynamically
(definition protected creature-ticks <fx>
  (fxround creature-rate))

;; aggro 5 times per second
(definition protected aggro-ticks <fx>
  (fxround/ creature-ticks 5))


(class Creature extends Actor
  
  
  (slot tick?            <bool>       initialize #t accessors generate)
  (slot tick-count       <fx>         initialize -1 accessors generate)
  (slot path-duration    <fl+>        initialize #f accessors generate)
  (slot behavior         <symbol+>    initialize #f accessors generate)
  (slot attack?          <bool>       initialize #f accessors generate)
  (slot attack-delay     <fl+>        initialize #f accessors generate)
  (slot aggro-radius     <fl+>        initialize #f accessors generate)
  (slot aggro-target     <Player+>    initialize #f accessors generate)
  (slot spawner          <object>     initialize #f accessors generate)
  
  
  (method override (prepare self rest)
    (nextmethod self rest)
    (prepare-pathing self))
  
  
  (method override (call-model-update self model)
    (nextmethod self model)
    (set! aggro-radius 25.)
    @until-horizontal-debugged
    (set! orientation (or (get-orientation model) 'horizontal)))
  
  
  (method package virtual (update-model self (anchor?: anchor? #f))
    )
  
  
  (method override (setup-area-cube self)
    (unless (client?)
      (nextmethod self)))
  
  
  (method override (update-area-cube self)
    (assert area-cube
      (nextmethod self)))
  
  
  (method override (run-task self script)
    (task-start self script))
  
  
  (method override (task-name self)
    (if (string? model)
        (let ((pos (string-find model #\/)))
          (if pos
              (string->symbol (substring model (+ pos 1) (string-length model)))
            (string->symbol model)))
      'creature))
  
  
  (method override (task-priority self)
    (if spawner
        creature-priority
      entity-priority))
  
  
  (method override (task-run self task script)
    (declare (proper-tail-calls))
    (if script
        (nextmethod self task script)
      (with-script-task task
        (lambda (exit)
          (let (iter (previous (current-seconds)))
            (handle-event self)
            ;; not 100% correct if preempted before task-mutex
            (let ((time (current-seconds)))
              (unless (stopping? task)
                (let ((elapse (- time previous))
                      (max-elapse 10.))
                  ;; ignore ridiculous elapses like
                  ;; coming back from computer sleep
                  (unless (> elapse max-elapse)
                    (let ((history (current-history)))
                      (unless (get-paused? history)
                        (tick-creature self time elapse))))))
              (let ((duration (- (current-seconds) time)))
                (task-sleep task creature-period duration exit))
              (iter time)))))))
  
  
  (method override (fallthrough self)
    (%%stay))
  
  
  (method (tick-creature self time elapse)
    (let ((world (current-world))
          (history (current-history)))
      (site (creature on?: #t)
        (tick-sliced world self #f time elapse)
        (with-task-mutex
          (lambda ()
            (unless (get-paused? history)
              (animate self elapse)))))))
  
  
  (method override (tick self commands time elapse)
    (let ((history (current-history)))
      (set! tick-count (modulo (+ tick-count 1) creature-ticks))
      (let ((script (get-script self)))
        (let ((tick (and script (get-tick script))))
          (if tick
              (running-handler script
                (lambda ()
                  (parameterize ((%%current-event (new Tick-Event self time elapse)))
                    (tick self))))
            (with-task-mutex
              (lambda ()
                (unless (get-paused? history)
                  (case (or behavior default-creature-behavior)
                    ((stay) (tick-actor self stay-commands time elapse #t 'slide))
                    ((move) (tick-actor self move-commands time elapse #t 'slide))
                    ((path) (tick-path self time elapse))
                    ((hostile) (tick-hostile self time elapse)))))))))))
  
  
  (method (tick-path self time elapse)
    (let ((me (current-me+)))
      (when (and tick? (or (not me) (neq? self (get-controlled me))))
        (when (and path-duration (> path-duration 0.))
          (decrease! path-duration elapse))
        (move-creature self time elapse)
        (when (and (= (modulo tick-count aggro-ticks) 0)
                   (<= path-duration 0.))
          (path-creature self)))))
  
  
  (method package (tick-hostile self time elapse)
    (let ((me (current-me+)))
      (when (record-change? self)
        (when (and tick? (or (not me) (neq? self (get-controlled me))))
          (when (and (not aggro-target) path-duration (> path-duration 0.))
            (decrease! path-duration elapse))
          (when aggro-target
            (when (and attack-delay (> attack-delay 0.))
              (decrease! attack-delay elapse))
            (lookat-target self)
            (set! motion 'walk))
          (set! attack? (and aggro-target (< (vertex-distance position (get-position aggro-target)) (+ (get-radius self) (get-radius aggro-target) attack-distance))))
          (if attack?
              (attack-player self aggro-target)
            (move-creature self time elapse))
          (when (and (not aggro-target) (= (modulo tick-count aggro-ticks) 0))
            (or (players-aggro self)
                (when (<= path-duration 0.)
                  (path-creature self))))))))
  
  
  (method (move-creature self time elapse (gravity?: gravity? #t) (response: response 'slide) (ignore-entities?: ignore-entities? #t) (ignore-players?: ignore-players? #f) (ignore-me?: ignore-me? #f))
    (let ((idle? (and (eq? motion 'idle) (not aggro-target))))
      (if (and jump? (not idle?))
          (begin
            (set! jump? #f)
            (tick-actor self move/jump-commands time elapse gravity? response ignore-entities?: ignore-entities? ignore-players?: ignore-players? ignore-me?: ignore-me? proportional-slide?: #f))
        (let ((commands (if idle? stay-commands move-commands)))
          (let ((info (tick-actor self commands time elapse gravity? response ignore-entities?: ignore-entities? ignore-players?: ignore-players? ignore-me?: ignore-me? proportional-slide?: #f)))
            (when (and info (or (not jump-time) (> time (+ jump-time (if aggro-target 1. 2.)))))
              (bind-values (center slide-collisions fall-collisions) info
                (when (or (not-null? slide-collisions) (random-jump? self))
                  (set! jump? #t)))))))))
  
  
  (method (random-jump? self)
    (let ((number (random-jump self)))
      (and number (< (random-real) number))))
  
  
  (method protected virtual (random-jump self)
    #f)
  
  
  (method override (adjust-fast-slide! self center new-center)
    (when aggro-target
      (let ((target (get-position aggro-target)))
        (let ((direct (vertex-& target center))
              (actual (vertex-& new-center center)))
          (let ((dist (vertex-norm& actual)))
            (when (and (not (vertex-zero? direct)) (> dist 0.))
              ;; only consider reasonable angles with the aggro target
              (when (< (abs (dot-product& (vertex-normalize& direct) (vertex-normalize& actual))) .1)
                (let ((proj (/ (dot-product& direct actual) dist)))
                  (when (< proj dist)
                    (vertex+! new-center center (vertex-scalar*& (vertex-normalize& actual) proj)))))))))))
  
  
  (method public virtual (aggro-sound self)
    #f)
  
  
  (method public virtual (attack-sound self)
    #f)
  
  
  (method (players-aggro self)
    (when (and aggro-radius (not aggro-target))
      (let ((zone (current-zone)))
        (let (iter (scan (get-players zone)))
          (if (null? scan)
              (begin
                (set! aggro-target #f)
                #f)
            (let ((player (car scan)))
              (if (and (or (user?) non-user-aggroes?) (can-aggro? self player) (aggro-sight? self player))
                  (begin
                    (aggro-player self player)
                    #t)
                (let (iter1 (creatures (get-aggroed-creatures player)))
                  (if (null? creatures)
                      (iter (cdr scan))
                    (let ((creature (car creatures)))
                      (if (aggro-sight? self creature)
                          (begin
                            (aggro-player self (get-aggro-target creature))
                            #t)
                        (iter1 (cdr creatures)))))))))))))
  
  
  (method (can-aggro? self player)
    (and (is? player Player)
         (not (admin-me? player))))
  
  
  (method (aggro-sight? self obj)
    (define (polygon-filter poly)
      (let ((element (get-element poly)))
        (and (not (eq? element self))
             (or (eq? element obj) (is-not? element Creature))
             (not (get-liquid-cube? (id->block (decode-id (get-data1 poly))))))))
    
    (let ((position (lens-center self))
          (pos (lens-center obj)))
      (let ((dist (vertex-distance position pos)))
        (and (<= dist aggro-radius)
             (< (vector-angle (get-sight lookat) (vertex-& pos position)) PI/2)
             (let ((dir (vertex-& pos position)))
               (and (not (vertex-zero? dir))
                    (let ((closest (ray-closest-polygon position (vertex-normalize dir) (+ aggro-radius (get-radius self) (get-radius obj)) filter: polygon-filter ignore-entities?: #f)))
                      @debug
                      (when closest
                        (let ((poly (first closest)))
                          (debug (or (get-element poly) (list (decode-id (get-data1 poly))
                                                               (flonum->fixnum (get-data2 poly))
                                                               (flonum->fixnum (get-data3 poly))
                                                               (flonum->fixnum (get-data4 poly)))))))
                      (and closest (equal? (get-element (first closest)) obj)))))))))
  
  
  (method (aggro-player self player)
    (when (neq? player aggro-target)
      (set! aggro-target player)
      (set-aggroed-creatures player (cons self (get-aggroed-creatures player)))
      (play-3d-sound-if (aggro-sound self) position))
    (lookat-target self))
  
  
  (method (lookat-target self)
    (let ((pos (get-position aggro-target)))
      (let ((dir (vertex-& (vertex& (vertex-x pos) (vertex-y position) (vertex-z pos)) position)))
        (unless (vertex-near-zero? dir)
          (world.changes:lookat-horizon-change self dir)))))
  
  
  (method override (attacking? self)
    attack?)
  
  
  (method (attack-player self player)
    (when (or (not attack-delay) (<= attack-delay 0))
      (play-3d-sound-if (attack-sound self) position)
      (when (find-animation (cache-model self) "AttackUnarmed")
        (transition-morphing self "AttackUnarmed")
        (set! animation-active "AttackUnarmed"))
      (wound player self 1.)
      (set! attack-delay 1.5)))
  
  
  (method (prepare-pathing self)
    (set! path-duration (random-between 1. 3.)))
  
  
  (method protected virtual (path-creature self)
    (cond ((< (random-real) .333)
           (set! motion 'idle))
          (else
           (set! motion 'walk)
           (horizon-sight! self (random-horizon))))
    (prepare-pathing self))
  
  
  (method override (wound self inflictor (amount #f))
    (nextmethod self inflictor amount)
    (let ((script (get-script self)))
      (if (and script (get-hit script))
          (let ((hit (get-hit script)))
            (running-handler script
              (lambda ()
                (parameterize ((%%current-event (new Script-Event self (current-seconds))))
                  (hit self)))))
        (when (and alive? (can-aggro? self inflictor))
          (aggro-player self inflictor)))))
  
  
  (method override (die self)
    (let ((zone (current-zone)))
      (set! alive? #f)
      (when aggro-target
        (set-aggroed-creatures aggro-target (remove! self (get-aggroed-creatures aggro-target))))
      (nextmethod self)))
  
  
  (method override (resurrect self)
    (when aggro-target
      (set-aggroed-creatures aggro-target (cons self (get-aggroed-creatures aggro-target))))
    (nextmethod self)))


;;;
;;;; Spawn
;;;


(class Spawn extends Creature
  
  
  (method override (simulated? self)
    #t))


;;;
;;;; Wolf
;;;


(class Wolf extends Spawn
  
  
  (method override (prepare self rest)
    (nextmethod self rest)
    (set! aggro-radius 40.)
    (update-model self))
  
  
  (method override (element-radiuses self)
    (vertex .4 .4 .4))
  
  
  (method override (element-radius self)
    .4)
  
  
  (method override (target-radius self)
    (* (average-scaling self) 1.8))
  
  
  (method override (update-model self (anchor?: anchor? #f))
    (cond ((and (local?) creature-models?)
           (set-scaling self .5)
           (change-model self "creature/direwolf" anchor?))
          (else
           (set-scaling self .4)
           (set-color self (dye .157 .157 .157 1.))
           (change-model self 'Wolf anchor?)
           (free-morphing self))))
  
  
  (method override (task-name self)
    'wolf)
  
  
  (method override (walk-speed self)
    (if aggro-target
        4.5
      3.))
  
  (method override (run-speed self)
    6.5)
  
  
  (method override (aggro-sound self)
    "sound/creature/wolf/aggro")
  
  (method override (attack-sound self)
    "sound/creature/wolf/attack")
  
  (method override (wound-sound self)
    "sound/creature/wolf/wound")
  
  (method override (wound-critical-sound self)
    "sound/creature/wolf/woundCritical")
  
  (method override (death-sound self)
    "sound/creature/wolf/death"))


;;;
;;;; Spider
;;;


(class Spider extends Spawn
  
  
  (method override (prepare self rest)
    (nextmethod self rest)
    (set! aggro-radius 25.)
    (update-model self))
  
  
  (method override (element-radiuses self)
    (vertex .4 .4 .4))
  
  
  (method override (element-radius self)
    .4)
  
  
  (method override (target-radius self)
    (* (average-scaling self) 1.8))
  
  
  (method override (update-model self (anchor?: anchor? #f))
    (cond ((and (local?) creature-models?)
           (set-scaling self .5)
           (change-model self "creature/scorpion" anchor?))
          (else
           (set-scaling self .4)
           (set-color self (dye 0. .235 0. 1.))
           (change-model self 'Spider anchor?)
           (free-morphing self))))
  
  
  (method override (task-name self)
    'spider)
  
  
  (method override (walk-speed self)
    (if aggro-target
        2.
      1.5))
  
  (method override (run-speed self)
    3.25)
  
  
  (method override (aggro-sound self)
    "sound/creature/spider/aggro")
  
  (method override (attack-sound self)
    "sound/creature/spider/attack")
  
  (method override (wound-sound self)
    "sound/creature/spider/wound")
  
  (method override (wound-critical-sound self)
    "sound/creature/spider/woundCritical")
  
  (method override (death-sound self)
    "sound/creature/spider/death"))


;;;
;;;; Jumper
;;;


(class Jumper extends Spawn
  
  
  (method override (prepare self rest)
    (nextmethod self rest)
    (set! aggro-radius 40.)
    (update-model self))
  
  
  (method override (element-radiuses self)
    (vertex .4 .4 .4))
  
  
  (method override (element-radius self)
    .4)
  
  
  (method override (target-radius self)
    (* (average-scaling self) 1.8))
  
  
  (method override (update-model self (anchor?: anchor? #f))
    (cond ((and (local?) creature-models?)
           (set-scaling self .5)
           (change-model self "creature/sporebat" anchor?))
          (else
           (set-scaling self .4)
           (set-color self (dye .4 0. .5 .3))
           (change-model self 'Jumper anchor?)
           (free-morphing self))))
  
  
  (method override (task-name self)
    'jumper)
  
  
  (method override (walk-speed self)
    (if aggro-target
        3.
      2.))
  
  (method override (run-speed self)
    5.)
  
  
  (method override (aggro-sound self)
    "sound/creature/jumper/aggro")
  
  (method override (attack-sound self)
    "sound/creature/jumper/attack")
  
  (method override (wound-sound self)
    "sound/creature/jumper/wound")
  
  (method override (wound-critical-sound self)
    "sound/creature/jumper/woundCritical")
  
  (method override (death-sound self)
    "sound/creature/jumper/death")
  
  
  (method override (random-jump self)
    .005))


;;;
;;;; Turtle
;;;


@wait-missing-turf-tile
(class Turtle extends Spawn
  
  
  (slot stomp-time <fl> initialize 0. accessors generate)
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-color green-dye)
    (set-model 'Turtle))
  
  
  (method override (element-radiuses)
    (vertex .2 .2 .2))
  
  
  (method override (element-radius)
    .2)
  
  
  (method override (target-radius)
    .3)
  
  
  (method override (path-creature)
    (let ((zone (current-zone)))
      (if (getf (get-properties zone) victor?:)
          (vertex-x-set! velocity turtle-speed)
        (nextmethod))))
  
  
  (method override (tick commands time elapse)
    (let ((zone (current-zone)))
      (when tick?
        (if stomp-time
            (when (>= time stomp-time)
              (set! stomp-time #f))
          (move-creature time elapse)
          (when (and (getf (get-properties zone) victor?:)
                     (or (< (get-x) -35.)
                         (> (get-x) -27.)))
            (rebound-x))))))
  
  
  (method override (stomp)
    (set! stomp-time (+ (current-seconds) 3.))
    #t)))
