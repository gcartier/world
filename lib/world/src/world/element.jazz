;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Elements
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.element jazz


(import (jazz.component)
        (jazz.geometry)
        (world)
        (world.autoload)
        (world.chunk)
        (world.draw)
        (world.dye)
        (world.dyes)
        (world.face)
        (world.generation.block)
        (world.geometry)
        (world.lookat)
        (world.quad)
        (world.syntax (phase syntax))
        (world.travel))


;;;
;;;; Element
;;;


(definition public (element? obj)
  (is? obj Element))


(definition protected (orb? name)
  (memq? name '(Orb OrbLong OrbWide OrbTall)))


(class Element extends Component
  
  
  (property position     <vertex>     initialize (make-zero-vertex)     getter generate setter explicit)
  (property lookat       <LookAt>     initialize (make-standard-lookat) getter generate setter explicit)
  (property scale        <vertex>     initialize (vertex 1. 1. 1.)      getter generate setter explicit)
  (property color        <dye>        initialize (make-dye)             getter generate setter explicit)
  (property user-data    <object>     initialize #f                     accessors generate)
  
  
  (slot matrix   <matrix>  initialize (make-identity-matrix) getter generate)
  (slot radiuses <vertex+> initialize #f                     getter explicit)
  (slot radius   <fl+>     initialize #f                     getter explicit)
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" (present-vertex position)))))
  
  
  (method public inline (get-x) <fl>
    (vertex-x position))
  
  (method public inline (get-y) <fl>
    (vertex-y position))
  
  (method public inline (get-z) <fl>
    (vertex-z position))
  
  
  (method public (set-position vert <vertex>) <void>
    (unless (vertex=? vert position)
      (let ((old-position (vertex-copy& position)))
        (vertex-copy! position vert)
        (update-matrix)
        (update-element old-position))))
  
  
  (method public (set-position-noupdate vert <vertex>) <void>
    (unless (vertex=? vert position)
      (vertex-copy! position vert)
      (update-matrix)))
  
  
  (method protected virtual (update-element old-position)
    )

  
  (method public (set-matrix mat <matrix>) <void>
    (matrix-copy! matrix mat))
  
  
  (method inline (get-sight) <vertex>
    (get-sight~ lookat))
  
  (method inline (get-up) <vertex>
    (get-up~ lookat))
  
  (method inline (get-right) <vertex>
    (get-right~ lookat))
  
  
  (method public (set-lookat l <LookAt>) <void>
    (unless (lookat=? l lookat)
      (lookat-copy! lookat l)
      (update-matrix)))
  
  
  (method public (init-lookat sight <vertex> up <vertex> (right #f)) <void>
    (lookat! lookat
             sight
             up
             (or right (cross-normal& sight up)))
    (update-matrix))
  
  
  (method public (horizon-sight! dir)
    (let ((world (current-world)))
      (unless (vertex-near-zero? dir)
        (init-lookat (vertex-normalize& dir)
                     (get-world-up~ world)))))
  
  
  (method public (realign-lookat new-up <vertex>) <void>
    (realign-lookat! lookat lookat new-up)
    (update-matrix))
  
  
  (method public (remove-up-roll dir <vertex> lookat <LookAt>) <void>
    (remove-up-roll! lookat dir lookat)
    (update-matrix))
  
  
  (method public (without-roll thunk)
    (let ((roll (vertical-angle (get-right~ lookat) (roll-free-up (get-sight~ lookat)))))
      ;; remove roll
      (set-lookat (rotate-lookat& lookat (- roll) (get-sight~ lookat)))
      (thunk)
      ;; restore roll
      (set-lookat (rotate-lookat& lookat roll (get-sight~ lookat)))))
  
  
  (method public (rotate angle <fl>) <void>
    (set-lookat (rotate-lookat& lookat angle (get-up~ lookat))))
  
  
  (method public (rotate-upon angle <fl> axis <vertex>) <void>
    (set-lookat (rotate-lookat& lookat angle axis)))
  
  
  (method public (set-scale s <vertex>) <void>
    (vertex-copy! scale s)
    (reset-radiuses)
    (update-matrix))
  
  (method public (set-scaling s <fl>) <void>
    (vertex! scale s s s)
    (reset-radiuses)
    (update-matrix))
  
  
  (method public inline (get-scale-x) <fl>
    (vertex-x (get-scale)))
  
  (method public inline (get-scale-y) <fl>
    (vertex-y (get-scale)))
  
  (method public inline (get-scale-z) <fl>
    (vertex-z (get-scale)))
  
  
  (method public (average-scaling) <fl>
    (let ((scale (get-scale)))
      (/ (+ (vertex-x scale)
            (vertex-y scale)
            (vertex-z scale))
         3.)))
  
  
  (method public (get-radiuses) <vertex>
    (or radiuses
        (begin
          (update-radiuses)
          radiuses)))
  
  (method public (set-radiuses r <vertex>) <void>
    (set! radiuses r)
    (set! radius (element-radius)))
  
  
  (method public inline (get-radius-x) <fl>
    (vertex-x (get-radiuses)))
  
  (method public inline (get-radius-y) <fl>
    (vertex-y (get-radiuses)))
  
  (method public inline (get-radius-z) <fl>
    (vertex-z (get-radiuses)))
  
  
  (method public inline (get-width) <fl>
    (* (get-radius-x) 2.))
  
  (method public inline (get-height) <fl>
    (* (get-radius-y) 2.))
  
  (method public inline (get-depth) <fl>
    (* (get-radius-z) 2.))
  
  
  (method public (get-radius) <fl>
    (or radius
        (begin
          (update-radiuses)
          radius)))
  
  (method public (set-radius r <fl>) <void>
    (set! radius r))
    
  
  (method public inline (get-red) <fl>
    (dye-red color))
  
  (method public inline (get-green) <fl>
    (dye-green color))
  
  (method public inline (get-blue) <fl>
    (dye-blue color))
  
  (method public inline (get-alpha) <fl>
    (dye-alpha color))

  
  (method protected virtual (gadget?)
    #f)
  
  
  (method protected virtual (inside? r vert)
    #f)
  
  
  (method protected virtual (element-faces)
    (cube-faces))
  
  
  (method protected virtual (element-polygons)
    (map get-polygon~ (element-faces)))
  
  
  (method public (cube-faces)
    (let ((world (current-world))
          (x (get-x))
          (y (get-y))
          (z (get-z))
          (bounds (make-cuboid&)))
      (get-bounds! bounds)
      (let ((material (get-block-opaque~ world))
            (left (cuboid-left bounds))
            (right (cuboid-right bounds))
            (bottom (cuboid-bottom bounds))
            (top (cuboid-top bounds))
            (back (cuboid-back bounds))
            (front (cuboid-front bounds)))
        (list
          ;; front face
          (make-face material
                     (f32vector 0. 1. 0. 0. 1. 0. 1. 1.)
                     0.
                     (make-quad self
                                (vertex left top front) (vertex left bottom front) (vertex right bottom front) (vertex right top front)))
          ;; back face
          (make-face material
                     (f32vector 1. 1. 1. 0. 0. 0. 0. 1.)
                     0.
                     (make-quad self
                                (vertex right top back) (vertex right bottom back) (vertex left bottom back) (vertex left top back)))
          ;; left face
          (make-face material
                     (f32vector 1. 0. 0. 0. 0. 1. 1. 1.)
                     0.
                     (make-quad self
                                (vertex left top back) (vertex left bottom back) (vertex left bottom front) (vertex left top front)))
          ;; right face
          (make-face material
                     (f32vector 1. 1. 0. 1. 0. 0. 1. 0.)
                     0.
                     (make-quad self
                                (vertex right top front) (vertex right bottom front) (vertex right bottom back) (vertex right top back)))
          ;; top face
          (make-face material
                     (f32vector 0. 0. 0. 1. 1. 1. 1. 0.)
                     0.
                     (make-quad self
                                (vertex left top back) (vertex left top front) (vertex right top front) (vertex right top back)))
          ;; bottom face
          (make-face material
                     (f32vector 0. 1. 0. 0. 1. 0. 1. 1.)
                     0.
                     (make-quad self
                                (vertex left bottom front) (vertex left bottom back) (vertex right bottom back) (vertex right bottom front)))))))
  
  
  (method protected virtual (element-radiuses)
    (error "Unable to get element radiuses"))
  
  
  (method protected virtual (element-radius)
    (vertex-norm (get-radiuses)))
  
  
  (method (reset-radiuses)
    (set! radiuses #f)
    (set! radius #f))
  
  
  (method (update-radiuses)
    (set-radiuses (element-radiuses)))
  
  
  (method protected virtual (target-radius)
    (get-radius))
  
  
  (method protected virtual (effective-matrix&)
    matrix)
  
  ;; aec quicky
  (method protected virtual (effective-matrix2&)
    matrix)
  
  
  (method protected virtual (update-matrix)
    (matrix-multiply! matrix (make-translation-matrix& position)
                             (make-lookat-matrix& lookat)
                             (make-scaling-matrix& scale)))
  
  
  (method public (get-bounds)
    (let ((bounds (make-cuboid)))
      (get-bounds! bounds)
      bounds))
  
  
  (method public (get-bounds! bounds <cuboid>) <cuboid>
    (let ((x (vertex-x position))
          (y (vertex-y position))
          (z (vertex-z position))
          (radius-x (get-radius-x))
          (radius-y (get-radius-y))
          (radius-z (get-radius-z)))
      (cuboid! bounds
               (- x radius-x)
               (- y radius-y)
               (- z radius-z)
               (+ x radius-x)
               (+ y radius-y)
               (+ z radius-z)))
    bounds)
  
  
  (method public virtual (transformed-bounds)
    (get-bounds))
  
  
  (method public virtual (transformed-parallelepiped)
    (let ((bounds (get-bounds)))
      (let ((left (cuboid-left bounds))
            (right (cuboid-right bounds))
            (bottom (cuboid-bottom bounds))
            (top (cuboid-top bounds))
            (back (cuboid-back bounds))
            (front (cuboid-front bounds)))
        (parallelepiped (vertex left bottom back)
                        (vertex left bottom front)
                        (vertex left top back)
                        (vertex left top front)
                        (vertex right bottom back)
                        (vertex right bottom front)
                        (vertex right top back)
                        (vertex right top front)))))
  
  
  (method public virtual (selection-bounds)
    (get-bounds))
  
  
  (method public virtual (transformed-selection-bounds)
    (transformed-parallelepiped))
  
  
  (method public virtual (transformed-center)
    (get-position))
  
  
  (method public virtual (transformed-radiuses)
    (get-radiuses))
  
  
  (method public virtual (lens-center)
    (transformed-center))
  
  
  (method public (can-see? elem view-radius accept?)
    (define (polygon-filter poly)
      (not (or (eq? (get-element~ poly) self)
               (get-liquid-cube?~ (id->block (decode-id (get-data1~ poly)))))))
    
    (let ((position (lens-center))
          (pos (lens-center~ elem)))
      (let ((dist (vertex-distance position pos)))
        (and (<= dist view-radius)
             (< (vector-angle (get-sight~ lookat) (vertex-& pos position)) PI/2)
             (let ((closest (ray-closest-polygon position (vertex-normalize (vertex-& pos position)) (+ view-radius (get-radius) (get-radius~ elem)) filter: polygon-filter ignore-non-physical?: #f ignore-entities?: #t)))
               (and closest (accept? (first closest))))))))
  
  
  (method public (set-color col)
    (dye-copy! color col))
  
  
  (method protected virtual (element-visible?)
    (get-visible?))
  
  
  (method protected virtual (virtual?)
    #f)
  
  
  (method protected virtual (drawable?)
    #f)
  
  
  (method protected virtual (draw)
    )
  
  
  (method protected virtual (draw-target color)
    (let ((bounds (selection-bounds)))
      (let ((matrix (matrix-multiply (effective-matrix&)
                                     (make-scaling-matrix (cuboid-radiuses bounds)))))
        (render-cube matrix wire-color: color))))
  
  
  (method protected virtual (draw-target-circle)
    (render-circle (vertex (get-x) (- (get-y) (* (get-scale-y) (get-radius-y))) (get-z)) (target-radius) purple-dye))
  
  
  (method protected virtual (target-moveable?)
    #t)
  
  
  ;;;
  ;;;; Tick
  ;;;
  
  
  (method protected virtual (tick . rest)
    )
  
  
  ;;;
  ;;;; Target
  ;;;
  
  
  (method protected virtual (activate-target)
    )
  
  
  (method protected virtual (deactivate-target)
    )
  
  
  (method protected virtual (describe-target add-header add-section add-info add-title add-target add-model)
    (add-title self)
    (add-target self)
    (add-model self))
  
  
  ;;;
  ;;;; History
  ;;;
  
  
  (method protected virtual (history-draw-now?)
    #f)
  
  
  (method protected virtual (history-draw-trail?)
    #f)
  
  
  ;;;
  ;;;; Script
  ;;;
  
  
  (method protected virtual (scriptable?)
    #f)
  
  
  ;;;
  ;;;; Designer
  ;;;
  
  
  (method public (set-property property value)
    (let ((zone (current-zone)))
      (let ((designer (get-designer~ zone)))
        (set-property~ designer self property value))))))
