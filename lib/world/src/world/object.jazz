;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Object
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.object jazz


(import (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.literals)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.window)
        (time)
        (world)
        (world.atlas)
        (world.autoload)
        (world.block)
        (world.draw)
        (world.element)
        (world.entity)
        (world.foreign)
        (world.generate)
        (world.geometry)
        (world.material)
        (world.model)
        (world.opengl)
        (world.shape)
        (world.syntax (phase syntax))
        (world.texture))


;;;
;;;; Atlas
;;;


(definition public (atlas-texture original name)
  (let ((atlas (new Atlas))
        (file (get-file~ original)))
    (add-file~ atlas file)
    (complete~ atlas)
    (let ((texture (get-texture~ atlas))
          (uv (image-rect~ atlas name)))
      (set-file~ texture file)
      (values texture uv))))


;;;
;;;; Faces
;;;


(definition public (make-faces-model faces (class: class #f) (orientation: orientation #f))
  (define (determine-bounds)
    (let ((left +inf.0)
          (bottom +inf.0)
          (back +inf.0)
          (right -inf.0)
          (top -inf.0)
          (front -inf.0))
      (for-each (lambda (face)
                  (let ((vertices (get-vertices~ (get-polygon~ face))))
                    (loop (for n from 0 below (vector-length vertices))
                          (let ((vert (vector-ref vertices n)))
                            (let ((x (vertex-x vert))
                                  (y (vertex-y vert))
                                  (z (vertex-z vert)))
                              (when (< x left) (set! left x))
                              (when (< y bottom) (set! bottom y))
                              (when (< z back) (set! back z))
                              (when (> x right) (set! right x))
                              (when (> y top) (set! top y))
                              (when (> z front) (set! front z)))))))
                faces)
      (cuboid left bottom back right top front)))
  
  (let ((world (current-world)))
    (let ((material (get-block-material~ world)))
      (let ((model (new (or class Block-Model) faces: faces orientation: orientation material: material)))
        (set-bounds~ model (determine-bounds))
        model))))


;;;
;;;; Sphere
;;;


(definition sphere-model
  #f)

(definition public (cache-sphere-model)
  (or sphere-model
      (let ((world (current-world)))
        (receive (texture uv) (atlas-texture (find-texture~ world "dirty.png") "dirty")
          (let ((material (new Material texture: texture)))
            (let ((model (new Model name: 'sphere mesh: (generate-sphere-mesh 48 24 uv neighbors?: #t smooth-normals?: #t material: material))))
              (set-bounds~ model (cuboid -1. -1. -1. 1. 1. 1.))
              (set! sphere-model model)
              model))))))


(class Sphere extends Entity
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-scale .5))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (set-model (cache-sphere-model)))
  
  
  (method override (element-radius)
    .5)
  
  
  (method override (inside? r vert)
    (let ((distance (vertex-norm (vertex-& vert position))))
      (<= distance (+ (get-radius) r))))))
