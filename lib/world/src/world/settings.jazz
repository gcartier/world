;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Settings
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.settings jazz


(import (jazz.component)
        (jazz.crash)
        (jazz.debuggee)
        (jazz.debuggee.configure)
        (jazz.io)
        (jazz.network)
        (jazz.platform)
        (jazz.presence)
        (jazz.settings)
        (jazz.snapshot)
        (jazz.template)
        (world)
        (world.capabilities)
        (world.master)
        (world.server)
        (world.stub))


;;;
;;;; Settings
;;;


(definition protected settings-updates
  (list->updates
    '((version: 100000 targets: settings))))


(definition (convert-settings dir old)
  (define (convert-initial)
    100000)
  
  (case old
    ((#f) (convert-initial))
    (else #f)))


(definition (copy-settings-if-not-exists settings-root (alias #f))
  (unless (exists? settings-root)
    (let ((templates (world-templates-directory alias)))
      (copy-template (new-directory templates "settings")
                     {Directory Home}
                     '()))))


(definition (determine-settings)
  (or (command-argument "settings")
      (let ((settings-dir {Directory Build "settings"}))
        (and (exists? settings-dir)
             (versioned-directory (parse settings-dir) 'settings settings-updates convert-settings feedback?: #f)))
      (let ((settings-root (get-settings-root))
            (settings-alias (get-settings-alias)))
        (versioned-directory (parse settings-root) 'settings settings-updates convert-settings feedback?: #f))))


(definition public (world-setting name default)
  (user-setting name default 'default))


;;;
;;;; Pathnames
;;;


(definition public (world-alias)
  (if (is-alias? 'World)
      'World
    'Build))


(definition public (world-file path (alias #f))
  (new-file (new Directory (list (or alias (world-alias))))
    path))


(definition public (world-directory path (alias #f))
  (new-directory (new Directory (list (or alias (world-alias))))
    path))


;;;
;;;; Templates
;;;


(definition public (world-templates-directory (alias #f))
  (world-directory '("devel" "templates") alias))


;;;
;;;; Scripting
;;;


(definition public (inhibit-scripts?)
  (world-setting 'world.inhibit-scripts? #f))


(definition public (scripting-available?)
  (let ((scripting? (world-setting 'world.scripting? #t)))
    ;; until scripting phase
    (if (serving?)
        #f
      scripting?)))


;;;
;;;; Setup
;;;


(definition traits-argument
  (let ((traits (command-argument "traits")))
    (if (not traits)
        '()
      (map string->symbol (split traits "/")))))

(definition traits-features
  (make-table test: eq?))


(definition protected name-setting
  #f)


(definition (register-name-setting)
  (let ((file {File Settings ".name"}))
    (when (exists? file)
      (set! name-setting (call-with-input-file (path-settings file)
                           read))
      (snapshot-user name-setting))))


(definition protected (update-name-setting name)
  (let ((file {File Settings ".name"}))
    (call-with-output-file (path-settings file)
      (lambda (output)
        (write name output))))
  (set! name-setting name)
  (snapshot-user name-setting))


(definition protected (client-machine-file name)
  (assert (not (serving?))
    (let ((dir {Directory Settings "machine"}))
      (new-file dir name))))


(definition public caps-settings
  #f)


(definition (register-caps-settings)
  (let ((file (client-machine-file "caps")))
    (let ((computer-speed (estimate-computer-speed)))
      (when (not (exists? file))
        (create-capabilities file computer-speed: computer-speed))
      (set! caps-settings (new File-Settings file: file))
      ;; recreate when version changed or speed improved
      (when (or (not (eqv? (get-setting caps-settings 'caps.version #f) caps-version))
                (> computer-speed (get-setting caps-settings 'caps.computer-speed)))
        (create-capabilities file computer-speed: computer-speed)
        (set! caps-settings (new File-Settings file: file))))
    (register-settings caps-settings)
    (add-snapshot-settings caps-settings)))


(definition protected (determine-machine-caps)
  (->list caps-settings))


(definition public options-settings
  #f)


(definition (register-options-settings)
  (let ((file {File Settings ".options"}))
    (when (not (exists? file))
      (create file))
    (set! options-settings (new File-Settings))
    (let ((setup (get-options-setup)))
      (when setup
        (setup options-settings)))
    (set-file options-settings file)
    (register-settings options-settings)
    (add-snapshot-settings options-settings)))


(definition public user-settings
  #f)


(definition (register-user-settings)
  (set! user-settings (new File-Settings))
  (let ((setup (get-settings-setup)))
    (when setup
      (setup user-settings)))
  (let ((file {File Settings ".settings"}))
    (when (exists? file)
      (set-file user-settings file traits: traits-argument features: traits-features)))
  (register-settings user-settings)
  (add-snapshot-settings user-settings))


(definition protected machine-identity
  #f)

(definition protected (set-machine-identity identity)
  (set! machine-identity identity))


(definition (load-machine-identity)
  (let ((file (client-machine-file "identity")))
    (set! machine-identity (and (exists? file)
                                (call-with-input-file (path-settings file)
                                  read)))))


(definition (update-machine-identity identity)
  (when (not machine-identity)
    (let ((file (client-machine-file "identity")))
      (call-with-output-file (path-settings file)
        (lambda (output)
          (write identity output))))
    (set! machine-identity identity)))


(definition protected (update-machine identity settings)
  (update-machine-identity identity)
  (update-machine-settings settings))


(definition protected (determine-machine-features)
  (list
    (cons 'kernel.system kernel-system)
    (cons 'kernel.platform kernel-platform)
    (cons 'kernel.compiler kernel-compiler)
    (cons 'kernel.processor kernel-processor)
    (cons 'kernel.windowing kernel-windowing)
    (cons 'kernel.safety kernel-safety)
    (cons 'kernel.features kernel-features)
    (cons 'kernel.properties kernel-properties)))


(definition public machine-settings
  #f)


(definition (require-machine-settings)
  (when (not machine-settings)
    (let ((file (client-machine-file "settings")))
      (when (not (exists? file))
        (create file))
      (set! machine-settings (new File-Settings))
      (set-file machine-settings file)
      (register-settings machine-settings)
      (add-snapshot-settings machine-settings))))


(definition (register-machine-settings)
  (let ((file (client-machine-file "settings")))
    (when (exists? file)
      (set! machine-settings (new File-Settings))
      (set-file machine-settings file)
      (register-settings machine-settings)
      (add-snapshot-settings machine-settings))))


(definition (unregister-machine-settings)
  (when machine-settings
    (unregister-settings machine-settings)
    (set! machine-settings #f)))


(definition (update-machine-settings settings)
  (unregister-machine-settings)
  (let ((file (client-machine-file "settings")))
    (if (not settings)
        (when (exists? file)
          (delete file))
      (save-settings settings file)))
  (register-machine-settings))


(definition protected live-settings
  (make-table test: eq?))

(definition protected (register-live-setting setting update)
  (table-set! live-settings setting update))

(definition protected (registered-live-setting setting)
  (table-ref live-settings setting #f))


(definition protected (set-machine-setting setting value)
  (require-machine-settings)
  (set-setting machine-settings setting value)
  (let ((update (registered-live-setting setting)))
    (when update
      (update value)))
  (save-content machine-settings))


(definition protected (unset-machine-setting setting)
  (require-machine-settings)
  (clear-setting machine-settings setting)
  (let ((update (registered-live-setting setting)))
    (when update
      (update (unspecified))))
  (save-content machine-settings))


(initialize-aliases)

(when aliases-setup
  (aliases-setup))

(register-alias 'Settings (tokenise-filename (determine-settings)) override?: #t)

(register-alias 'Documents (get-documents-root))

(register-alias 'Universe (get-universe-root))

(parameterize ((walk-for #f))
  (when (client?)
    (register-name-setting))
  (when (client?)
    (register-caps-settings))
  (register-options-settings)
  (register-user-settings)
  (when (client?)
    (load-machine-identity)
    (register-machine-settings)))

(let ((override (get-settings-override)))
  (when override
    (override user-settings)))


(let ((unknown-traits (collect-if (lambda (trait)
                                    (not (table-ref traits-features trait #f)))
                                  traits-argument)))
  (when (not-null? unknown-traits)
    (error "Unknown traits: {l}" unknown-traits)))


;;;
;;;; Bot
;;;


(definition public bot?
  (boolean-argument "bot" #f))

(definition protected (set-bot? flag)
  (set! bot? flag))


;;;
;;;; Role
;;;


(definition public default-role
  'user)


(definition public initial-role
  default-role)

(definition public (set-initial-role role)
  (set! initial-role role))


(definition public current-role
  default-role)

(definition public (set-current-role role)
  (set! current-role role))


(definition public inline (user?)
  (eq? current-role 'user))

(definition public inline (explorer?)
  (eq? current-role 'explorer))

(definition public inline (builder?)
  (eq? current-role 'builder))

(definition public inline (developer?)
  (eq? current-role 'developer))


(definition public inline (circler?)
  (user?))


(definition public inline (can-explore?)
  (or (explorer?)
      (builder?)
      (developer?)))

(definition public inline (can-build?)
  (or (builder?)
      (developer?)))


(definition public inline (2d?)
  (user?))


;; quick solution to server giving role too late
(definition public local-developer?
  (world-setting 'world.local-developer? #f))


;;;
;;;; Admin
;;;


(definition public admin-rights?
  #f)

(definition public (set-admin-rights? rights?)
  (set! admin-rights? rights?))


(definition public (admin?)
  admin-rights?)


;;;
;;;; Verified
;;;


(definition public user-verified?
  #f)

(definition public (set-user-verified? verified?)
  (set! user-verified? verified?))


(definition public (verified?)
  user-verified?)

(definition public (unverified?)
  (not user-verified?))


;;;
;;;; Mode
;;;


(definition public current-mode
  'visitor)

(definition public (set-current-mode mode)
  (set! current-mode mode))


(definition public inline (visitor?)
  (eq? current-mode 'visitor))

(definition public inline (observer?)
  (eq? current-mode 'observer))

(definition public inline (participant?)
  (eq? current-mode 'participant))


;;;
;;;; Random
;;;


(random-seed)


;;;
;;;; Presence
;;;


(when (world-setting 'jazz.debug-remote? #f)
  (set-debug-remote? #t))

(when (world-setting 'jazz.debug-blocking? #f)
  (set-debug-remote-blocking? #t))


;;;
;;;; Debuggee
;;;


(let ((title (world-setting 'world.debuggee-title #f)))
  (when title
    (current-process-title-set! title)))


;; loading debuggee modules up front improves debugging
;; robustness and correctly sets up current process title
(load-debuggee)


;;;
;;;; Snapshots
;;;


(definition public (setup-snapshots)
  (snapshots-directory {Directory Settings "work" "crashes"})
  (snapshot-max-depth (if bot? 1 3))
  (snapshot-max-width 4096)
  (snapshot-max-content 512))


(setup-snapshots)


;;;
;;;; Crashes
;;;


(setup-crash-handler #f)


;;;
;;;; Exceptions
;;;


;; need to think this through about letting gambit handle debugging
;; when there is a controlling terminal versus generating a snapshot
(unless (or (using-debugger?) (controlling-terminal?))
  (set-exception-debugger (application-exception-debugger))
  (set-exception-hook exception-debugger-hook))


;;;
;;;; Load
;;;


(define profile-load?
  (world-setting 'world.profile-load? #f))

(define profile-what
  (world-setting 'world.profile-what 'time))


(define profile-load-base
  #f)

(define profile-load-previous
  #f)

(define profile-load-done?
  #f)


(definition protected (load-reset)
  (set! profile-load-done? #f)
  (load-base))


(definition public (load-base)
  (when (and profile-load? (not profile-load-done?))
    (let ((time (current-seconds)))
      (set! profile-load-base kernel-boot)
      (set! profile-load-previous kernel-boot)
      (load-report 'base (- time profile-load-base) (- time profile-load-previous))
      (set! profile-load-previous time))))


(definition public (load-point name)
  (when (and profile-load? (not profile-load-done?))
    (let ((time (current-seconds)))
      (load-report name (- time profile-load-base) (- time profile-load-previous))
      (set! profile-load-previous time))))


(definition public (load-total name)
  (when (and profile-load? (not profile-load-done?))
    (let ((time (current-seconds)))
      (load-report name (- time profile-load-base) (- time profile-load-previous)))
    (set! profile-load-done? #t)))


(definition (load-report name total delta)
  (define (meg bytes)
    (fxround (/ (cast <fl> bytes) 1024. 1024.)))
  
  (case profile-what
    ((time)
     (format :terminal "{a} {a} {r precision: 3} {r precision: 3}{%}"
       (get-presence-title)
       name
       total
       delta))
    (else
     (gc)
     (bind-values (last-gc-heap-size last-gc-alloc last-gc-live last-gc-movable last-gc-nonmovable) (process-memory)
       (format :terminal "{a} {a} {a} {a} {a}{%}"
         (get-presence-title)
         name
         (meg last-gc-live)
         (meg last-gc-movable)
         (meg last-gc-nonmovable))))))


(load-base)


;;;
;;;; Slave
;;;


(definition protected slave?
  (boolean (command-argument "master")))


(definition protected master-process
  #f)

(definition protected master-cookie
  #f)


;;;
;;;; Client
;;;


(definition client-configuration
  #f)

(definition public (get-client-configuration)
  client-configuration)

(definition public (set-client-configuration configuration)
  (set! client-configuration configuration))


(definition public (user-client-directory)
  (let ((user (string-argument "user" #f)))
    (if user
        (new-directory {Directory Settings} (list "clients" user))
      (let ((user (find-kernel-file "user")))
        (if user
            (new Directory (tokenise-filename user))
          {Directory Settings})))))

(definition protected (user-client-file)
  (new-file (user-client-directory) ".client"))


;;;
;;;; Server
;;;


(definition server-file
  #f)

(definition public (get-server-file)
  server-file)


(definition server-configuration
  #f)

(definition public (get-server-configuration)
  server-configuration)


(definition (start-server-listener)
  (let ((name (or (command-argument "server") (world-setting 'world.server #f) default-server)))
    (let ((file (new-file (new-directory {Directory Settings "servers"} (tokenise-filename name)) ".server")))
      (if (exists? file)
          (let ((configuration (instantiate (read-form file))))
            (let ((host (effective-host configuration))
                  (service (effective-service configuration)))
              (assert (not (get-remote-listener))
                (start-remote-presence host: host service: service))
              (set! server-file file)
              (set! server-configuration configuration)))
        (error "Unable to find server: {a}" name)))))


(when (server?)
  (start-tier-listener-set! start-server-listener))


;;;
;;;; Processor
;;;


(definition (start-processor-listener)
  (let ((host (string-argument "host"))
        (service (integer-argument "port")))
    ;; worker might end up taking our service
    (let ((alternate-service (+ service 1)))
      (assert (not (get-remote-listener))
        (start-remote-presence host: host service: service alternate-service: alternate-service)))))


(when (processor?)
  (start-tier-listener-set! start-processor-listener))


;;;
;;;; Master
;;;


(definition protected use-worker?
  (if (worker?)
      #f
    ;; having the worker in environments without source needs
    ;; more work as the .o1 are now downloaded on-demand and
    ;; so the worker will have to do something similar
    (world-setting 'world.worker? #f @still-some-bugs (cond-expand
                                    (devel #t)
                                    (stable #t)
                                    (else #f)))))


;; YOWNU not sure
(when #t ;; (and (not (worker?)) use-worker?)
  (register-proxy 'world-remote-master
                  World-Remote-Master-Local-Proxy
                  (new World-Remote-Master))
  (register-proxy 'world-remote-master
                  World-Remote-Master-Local-Proxy
                  (new World-Remote-Master)
                  'working))


;;;
;;;; Slave
;;;


(definition (connect-to-master)
  (let ((master (string-argument "master"))
        (cookie (integer-argument "master-cookie")))
    (receive (host port alternate-port) (parse-host/service master)
      (set! master-process (connect-remote-reference host port (list 'world.master 'world-remote-master)))
      (set! master-cookie cookie))))


(when (and slave? (not (worker?)))
  (connect-slave-to-master-set! connect-to-master)))
