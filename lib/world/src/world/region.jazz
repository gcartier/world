;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Regions
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.region jazz


(import (jazz.io)
        (jazz.syntax (phase syntax))
        (jazz.zlib)
        (world)
        (world.autoload)
        (world.binary)
        (world.chunk)
        (world.section)
        (world.syntax (phase syntax)))


(proclaim (warn optimizations))


(constant inline region-size <fx>
  32)

(constant inline region-header-size <fx>
  8192
  @wait-constant-folding
  (* 8 1024))

(constant inline chunk-padding <fx>
  4096
  @wait-constant-folding
  (* 4 1024))

(constant inline chunk-header-size <fx>
  5)

(constant inline zlib-header-size <fx>
  2)

(constant inline zlib-footer-size <fx>
  4)


(class World-Region extends Object
  
  
  (slot refcount     <fx>       initialize 0                         getter generate)
  (slot x            <fx>                                            getter generate)
  (slot z            <fx>                                            getter generate)
  (slot file         <File>                                          getter generate)
  (slot port         <port>                                          getter generate)
  (slot header       <u8vector>                                      getter generate)
  (slot chunks-mutex <object>   initialize (make-mutex 'chunks)      getter generate)
  (slot chunks       <table>    initialize (make-table test: equal?) getter generate)
  (slot modified?    <bool>     initialize #f                        accessors generate)
  (slot compact?     <bool>     initialize #f                        accessors generate)
  
  
  (method override (initialize x z file)
    (set! x~self x)
    (set! z~self z)
    (set! file~self file)
    (set! port~self (open-input-file (path-settings file)))
    (set! header (read-header)))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a} {a}" x z))))
  
  
  (method override (destroy)
    (compact-region)
    (close-port port)
    (nextmethod))
  
  
  (method (refcount-increase)
    (increase! refcount)
    ;(debug '***** x z 'refcount refcount)
    )
  
  
  (method (refcount-decrease)
    (decrease! refcount)
    ;(debug '***** x z 'refcount refcount)
    (when (<= refcount 0)
      (release)))
  
  
  (method (release)
    ;(debug 'release 'Region x z)
    )
  
  
  (method (read-header)
    (let ((size region-header-size))
      (let ((content (make-u8vector size)))
        (read-subu8vector content 0 size port)
        content)))
  
  
  (method (with-chunks-mutex thunk)
    (mutex-lock! chunks-mutex)
    (prog1 (thunk)
      (mutex-unlock! chunks-mutex)))
  
  
  (method (get-chunk sector-x <fx> sector-z <fx>) <World-Chunk+>
    (get-chunk-at (chunk-index sector-x sector-z)))
  
  
  (method (get-chunk-at chunk-index <pair>) <World-Chunk+>
    (with-chunks-mutex
      (lambda ()
        (let ((value (table-ref chunks chunk-index #t)))
          (if (neq? value #t)
              value
            (let ((chunk (read-chunk chunk-index)))
              (refcount-increase)
              (table-set! chunks chunk-index chunk)
              chunk))))))
  
  
  (method (find-chunk sector-x <fx> sector-z <fx>) <World-Chunk+>
    (table-ref chunks (chunk-index sector-x sector-z) #f))
  
  
  (method (chunk-index sector-x <fx> sector-z <fx>) <pair>
    ;; looks like we don't need to adjust for negative values like stated on the wiki
    (cons (modulo sector-x 32)
          (modulo sector-z 32)))
  
  
  (method (chunk-header-offset chunk-x <fx> chunk-z <fx>) <fx>
    (* 4 (+ chunk-x (* chunk-z 32))))
  
  
  (method (get-chunk-offset header-offset <fx>) <fx+>
    (let ((b1 (u8vector-ref header header-offset))
          (b2 (u8vector-ref header (+ header-offset 1)))
          (b3 (u8vector-ref header (+ header-offset 2)))
          (b4 (u8vector-ref header (+ header-offset 3))))
      (if (and (= b1 0)
               (= b2 0)
               (= b3 0)
               (= b4 0))
          #f
        (* (+ (arithmetic-shift-left b1 16)
              (arithmetic-shift-left b2 8)
              b3)
           chunk-padding))))
  
  
  (method (get-chunk-size header-offset <fx>) <fx+>
    (* (u8vector-ref header (+ header-offset 3)) chunk-padding))
  
  
  (method (set-chunk-offset header-offset <fx> offset <fx+>)
    (if (not offset)
        (begin
          (u8vector-set! header header-offset 0)
          (u8vector-set! header (+ header-offset 1) 0)
          (u8vector-set! header (+ header-offset 2) 0)
          (u8vector-set! header (+ header-offset 3) 0))
      (let ((offset <fx> (/ offset chunk-padding)))
        (let ((b1 (modulo (arithmetic-shift offset -16) 256))
              (b2 (modulo (arithmetic-shift offset -8) 256))
              (b3 (modulo offset 256)))
          (u8vector-set! header header-offset b1)
          (u8vector-set! header (+ header-offset 1) b2)
          (u8vector-set! header (+ header-offset 2) b3)))))
  
  
  (method (set-chunk-size header-offset <fx> size <fx>)
    (let ((count (/ size chunk-padding)))
      (assert (fixnum? count))
      (u8vector-set! header (+ header-offset 3) count)))
  
  
  (method (read-chunk chunk-index <pair>) <World-Chunk+>
    (let ((chunk-x <fx> (car chunk-index))
          (chunk-z <fx> (cdr chunk-index)))
      (let ((header-offset (chunk-header-offset chunk-x chunk-z)))
        (assert (<= header-offset 4092))
        (let ((offset (get-chunk-offset header-offset)))
          (if (not offset)
              #f
            (site (read-chunk on?: #t parent: generate)
              (input-port-byte-position port offset)
              (let ((chunk-header (make-u8vector 5)))
                (read-subu8vector chunk-header 0 5 port)
                (let ((deflated-size <fx> (scan-u32-big-endian chunk-header 0))
                      (compression-type (u8vector-ref chunk-header 4)))
                  (assert (= compression-type 2))
                  (let ((chunk-raw (make-u8vector deflated-size)))
                    (read-subu8vector chunk-raw 0 deflated-size port)
                    (let ((chunk-data (zlib-inflate chunk-raw)))
                      (deserialize-chunk chunk-data)))))))))))
  
  
  (method protected virtual (deserialize-chunk chunk-data)
    (unimplemented))
  
  
  (method (get-section what sector-x <fx> sector-y <fx> sector-z <fx>) <World-Section+>
    (let ((zone (current-zone)))
      (let ((chunk (get-chunk sector-x sector-z)))
        (when chunk
          (let ((sections (get-sections~ chunk)))
            (when sections
              (let ((section (table-ref sections sector-y #f)))
                ;; until unification, associate section with sector
                (when (and section (not (get-sector~ section)))
                  (let ((sector (let ((index (vector sector-x sector-y sector-z)))
                                  (if (eq? what 'render)
                                      (find-eye-sector~ zone index)
                                    (index-sector~ zone index)))))
                    (set-sector~ section sector)
                    (when sector
                      (set-section~ sector section))))
                section)))))))
  
  
  (method (save-header port)
    (output-port-byte-position port 0)
    (let ((size (u8vector-length header)))
      (write-subu8vector header 0 size port)))
  
  
  (method (save-region)
    (define (save-chunks port)
      (iterate-chunks
        (lambda (x z)
          (let ((header-offset (chunk-header-offset x z))
                (chunk (find-chunk x z)))
            (when (and chunk (get-modified?~ chunk))
              (let ((content (serialize-chunk chunk)))
                (let ((deflated-size (u8vector-length content)))
                  (let ((chunk-size (pad-chunk (+ chunk-header-size deflated-size)))
                        (actual-size (allege (get-chunk-size header-offset))))
                    (define (position-port)
                      (if (<= chunk-size actual-size)
                          (let ((offset (allege (get-chunk-offset header-offset))))
                            (output-port-byte-position port offset)
                            offset)
                        (output-port-byte-position port 0 2)
                        (output-port-byte-position port)))
                    
                    (let ((offset (position-port)))
                      (let ((chunk-header (make-u8vector 5)))
                        (put-u32-big-endian chunk-header 0 deflated-size)
                        (u8vector-set! chunk-header 4 2)
                        (write-subu8vector chunk-header 0 5 port)
                        (write-subu8vector content 0 deflated-size port)
                        (set-chunk-offset header-offset offset)
                        (set-chunk-size header-offset chunk-size))))))
              (reset-modified~ chunk))))))
    
    (define (pad-chunk size <fx>) <fx>
      (let ((mod (modulo size chunk-padding)))
        (if (= mod 0)
            size
          (+ size (- chunk-padding mod)))))
    
    (define (needs-compaction? port)
      (define (actual-size)
        (output-port-byte-position port 0 2)
        (output-port-byte-position port))
      
      (define (compact-size)
        (let ((size region-header-size))
          (iterate-chunks
            (lambda (x z)
              (let ((header-offset (chunk-header-offset x z)))
                (increase! size (get-chunk-size header-offset)))))
          size))
      
      (let ((percentage-threshold 125.))
        (>= (percentage (actual-size) (compact-size)) percentage-threshold)))
    
    (define (save)
      (call-with-output-file (path-settings file truncate: #f)
        (lambda (port)
          (save-chunks port)
          (save-header port)
          (when (needs-compaction? port)
            (set! compact? #t)))))
    
    (when modified?
      (save)
      (reset-modified)))
  
  
  (method (compact-region)
    (define (read-chunks)
      (let ((chunks (new-queue)))
        (iterate-chunks
          (lambda (x z)
            (let ((header-offset (chunk-header-offset x z)))
              (let ((offset (get-chunk-offset header-offset)))
                (if (not offset)
                    (enqueue chunks #f)
                  (let ((size (get-chunk-size header-offset)))
                    (input-port-byte-position port offset)
                    (let ((chunk (make-u8vector size)))
                      (read-subu8vector chunk 0 size port)
                      (enqueue chunks chunk))))))))
        (queue-list chunks)))
    
    (define (save-chunks port chunks)
      (let ((offset region-header-size))
        (output-port-byte-position port offset)
        (iterate-chunks
          (lambda (x z)
            (let ((header-offset (chunk-header-offset x z))
                  (chunk (car chunks)))
              (set! chunks (cdr chunks))
              (if (not chunk)
                  (set-chunk-offset header-offset #f)
                (let ((size (u8vector-length chunk)))
                  (write-subu8vector chunk 0 size port)
                  (set-chunk-offset header-offset offset)
                  (set-chunk-size header-offset size)
                  (increase! offset size))))))))
    
    (define (compact)
      (let ((chunks (read-chunks)))
        (call-with-output-file (path-settings file truncate: #t)
          (lambda (port)
            (save-chunks port chunks)
            (save-header port)))))
    
    (when compact?
      (compact)
      (set! compact? #f)))
  
  
  (method protected (iterate-chunks proc)
    (loop (for z from 0 below 32)
          (loop (for x from 0 below 32)
                (proc x z))))
  
  
  (method protected virtual (serialize-chunk chunk) <u8vector>
    (unimplemented))
  
  
  (method protected (reset-modified)
    (set! modified? #f))
  
  
  (method protected (modified)
    (set! modified? #t))))
