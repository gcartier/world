;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Panes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.pane jazz


(import (jazz.window)
        (world)
        (world.autoload)
        (world.context)
        (world.geometry)
        (world.quad-texture)
        (world.syntax (phase syntax)))


(class World-Pane extends Element
  
  
  (property size       initialize #f accessors generate)
  (property resolution initialize #f accessors generate)
  (property billboard  initialize #f accessors generate)
  (property render?    initialize #t accessors generate)
  
  
  (slot quad-texture <Quad-Texture+> initialize #f getter generate)
  
  
  (method override (update-radiuses self)
    (when size
      (set-radiuses self (vertex (/ (get-width size) 2.) (/ (get-height size) 2.) .001))))
  
  
  (method protected virtual (make-texture self)
    (let ((w (get-width size))
          (h (get-height size))
          (width (get-width resolution))
          (height (get-height resolution)))
      (let ((v1 (vertex (/ w -2.) (/ h  2.) 0.))
            (v2 (vertex (/ w -2.) (/ h -2.) 0.))
            (v3 (vertex (/ w  2.) (/ h -2.) 0.))
            (v4 (vertex (/ w  2.) (/ h  2.) 0.)))
        (let ((quad-texture <Quad-Texture> (make-quad-texture width height v1 v2 v3 v4 mipmap?: (mipmap? self))))
          (let ((texture (get-texture quad-texture)))
            (let ((surface (get-surface texture)))
              (draw self surface)
              (map-texture texture)))
          quad-texture))))
  
  
  (method package virtual (mipmap? self)
    #f)
  
  
  (method package virtual (draw self surface)
    )
  
  
  (method package virtual (mouse-down self pos)
    (debug 'down (get-h pos) (get-v pos)))
  
  
  (method package virtual (mouse-move self pos)
    (debug 'move (get-h pos) (get-v pos)))
  
  
  (method package virtual (mouse-up self pos)
    (debug 'up (get-h pos) (get-v pos)))
  
  
  (method package (cache-quad-texture self)
    (when (not quad-texture)
      (set! quad-texture (make-texture self)))
    quad-texture)
  
  
  (method override (render self)
    (draw-texture (cache-quad-texture self))))


;;;
;;;; Player
;;;


(class Pane-Player extends View-Player))
