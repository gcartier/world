;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Motion Controllers
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.motion jazz


(import (jazz.geometry)
        (world)
        (world.autoload)
        (world.camera)
        (world.collision)
        (world.dye)
        (world.element)
        (world.generation.block)
        (world.geometry)
        (world.history)
        (world.protocol)
        (world.support)
        (world.syntax (phase syntax)))


(definition public (person-motion?)
  (is? (current-motion) Person-Motion))


(definition public (orbit-motion?)
  (is? (current-motion) Orbit-Motion))


(definition public (free-motion?)
  (is? (current-motion) Free-Motion))


(definition public (make-person-motion world camera person)
  (new Person-Motion world camera person))


(definition public (make-orbit-motion world camera target)
  (new Orbit-Motion world camera target))


(definition public (make-free-motion world camera)
  (new Free-Motion world camera))


;;;
;;;; Motion
;;;


(class Motion extends Object
  
  
  (slot world  <World>  getter generate)
  (slot camera <Camera> getter generate)
  
  
  (method override (initialize world camera)
    (set! world~self world)
    (set! camera~self camera))
  
  
  (method protected virtual (motion-target)
    #f)
  
  
  (method protected virtual (move-forward)
    )
  
  
  (method protected virtual (move-backward)
    )
  
  
  (method protected virtual (sprint-forward)
    )
  
  
  (method protected virtual (sprint-backward)
    )
  
  
  (method protected virtual (blink-forward)
    )
  
  
  (method protected virtual (blink-backward)
    )
  
  
  (method protected virtual (warp-forward)
    )
  
  
  (method protected virtual (warp-backward)
    )
  
  
  (method protected virtual (move-left)
    )
  
  
  (method protected virtual (move-right)
    )
  
  
  (method protected virtual (move-up)
    )
  
  
  (method protected virtual (move-down)
    )
  
  
  (method protected virtual (rotate-left)
    )
  
  
  (method protected virtual (rotate-right)
    )
  
  
  (method protected virtual (zoom-in)
    )
  
  
  (method protected virtual (zoom-out)
    )
  
  
  (method protected virtual (follow-player)
    )
  
  
  (method protected virtual (follow-actor actor)
    )
  
  
  (method protected virtual (eye-behind-player)
    )
  
  
  (method protected virtual (eye-behind-actor actor)
    )
  
  
  (method protected virtual (restoring-desired-distance?)
    #f)
  
  
  (method protected virtual (restore-desired-distance elapse)
    )
  
  
  (method protected virtual (eye-center-player)
    )
  
  
  (method protected virtual (restore-roll actor)
    )
  
  
  (method protected virtual (reset-roll actor)
    )
  
  
  (method protected virtual (rotate-camera yaw <fl> pitch <fl>)
    (rotate-horizontal~ camera yaw)
    (rotate-vertical~ camera pitch))
  
  
  (method (track-camera dx <fl> dy <fl>)
    (when (hook-eye~ world)
      (rotate-camera (/ (- dx) (get-track-sensitivity))
                     (/ (- dy) (get-track-sensitivity))))))


;;;
;;;; Target
;;;


(class Target-Motion extends Motion
  
  
  (slot target            <Element> initialize #f   accessors generate)
  (slot max-distance      <fl>      initialize 30.0 getter generate)
  (slot desired-distance  <fl>      initialize 3.5  getter generate)
  (slot distance-override <fl+>     initialize #f   getter generate)
  (slot distance-goal     <fl+>     initialize #f   getter generate)
  (slot distance-restore  <fl+>     initialize #f   getter generate)
  (slot relative-yaw      <fl>      initialize 0.   getter generate)
  (slot absolute-pitch    <fl>      initialize 0.   getter generate)
  
  
  (method override (initialize world camera target)
    (nextmethod world camera)
    (set! target~self target)
    (prepare-yaw/pitch))
  
  
  (method (prepare-yaw/pitch)
    (let ((world (current-world)))
      (let ((world-up (get-world-up~ world)))
        ;; do not consider roll as a first draft
        (set! relative-yaw (- (vector-signed-angle (remove-pitch-quick-hack (get-sight~ camera)) (get-sight~ (motion-target)) world-up)))
        (set! absolute-pitch (- (vertical-angle (get-sight~ camera) world-up))))))
  
  
  (method override (motion-target)
    target)
  
  
  (method (setup-motion distance)
    (set! desired-distance distance)
    (set! distance-override #f)
    (set! distance-goal #f)
    (align-eye-behind-no-occlusion (lens-center~ (motion-target)))
    (adjust-player-alpha))
  
  
  (method (effective-distance)
    (or distance-override desired-distance))
  
  
  (method (desired-distance-closer (speed: speed #f))
    (let ((speed (or speed .75)))
      (set! desired-distance (* desired-distance speed))
      (update-first-restore)))
  
  
  (method (desired-distance-further (speed: speed #f))
    (let ((speed (or speed .75)))
      (set! desired-distance (min (/ desired-distance speed) max-distance))
      (update-first-restore)))
  
  
  (method (update-first-restore)
    (when (and distance-restore distance-override)
      (set! distance-restore (/ (- desired-distance distance-override) eye-restore-duration))))
  
  
  (method (adjust-player-alpha)
    (let ((target (motion-target)))
      (let ((alpha (if (<= (vertex-distance (lens-center~ target) (get-position~ camera)) .8)
                       0.
                     (max 0.2 (min 1. (/ (effective-distance) 2.))))))
        (dye-alpha-set! (get-color~ target) alpha)
        (if (orb? (get-name~ (cache-model~ target)))
            (dye-alpha-set! (get-overlay~ target) 1.)
          (dye-alpha-set! (get-overlay~ target) alpha)))))
  
  
  (method override (follow-player)
    ;; until planets camera doesn't follow gravity fixed
    (unless (planets?~ (current-zone))
      (reposition-camera)))
  
  
  (method (reposition-camera)
    (let ((lookat (get-lookat~ camera))
          (target (motion-target)))
      (standardize-lookat! lookat)
      ;; quick not correct doesn't work if lookat has roll
      ;; quick not correct hack to remove pitch
      (let ((x (vertex-x (get-sight~ target)))
            (z (vertex-z (get-sight~ target))))
        (unless (and (= x 0.)
                     (= z 0.))
          (let ((sight (vertex-normalize& (vertex& x 0. z))))
            (let ((up (get-up~ lookat)))
              (init-lookat~ camera sight up)))))
      (set-lookat~ camera (rotate-lookat& lookat relative-yaw (get-up~ lookat)))
      (set-lookat~ camera (rotate-lookat& lookat absolute-pitch (get-right~ lookat)))
      (when target
        (align-eye-behind (lens-center~ target)))))
  
  
  (method override (rotate-camera yaw <fl> pitch <fl>)
    (nextmethod yaw pitch)
    (increase! relative-yaw yaw)
    (let ((epsilon .001))
      (set! absolute-pitch (between (- epsilon PI/2) (+ absolute-pitch pitch) (- PI/2 epsilon)))))
  
  
  (method override (follow-actor actor)
    (when (person-motion?)
      (when actor
        (align-eye-behind (lens-center~ actor)))))
  
  
  (method override (eye-behind-player)
    (eye-behind-actor (current-controlled~ world)))
  
  
  (method override (eye-behind-actor actor)
    (when (and actor (person-motion?))
      (set-lookat~ camera (realign-lookat& (get-lookat~ actor) (get-world-up~ world)))
      (camera-update~ camera)))
  
  
  (definition eye-restore-duration
    1.6)
  
  
  ;; mimimum radius determined by experimentation
  (definition occlusion-radius
    .1)
  
  (definition occlusion-radiuses
    (vertex occlusion-radius occlusion-radius occlusion-radius))
  
  
  (method override (restoring-desired-distance?)
    distance-goal)
  
  
  ;; lens   override   goal   distance
  ;; |      |          |      |
  ;; |      |          |      | - user desired distance
  ;; |      |          | - new override target
  ;; |      | - override to clear any occlusion
  ;; | - camera target
  ;;
  ;; distance-restore : constant restore velocity
  (method override (restore-desired-distance elapse)
    (let ((target (motion-target)))
      (when distance-goal
        (let ((augmented-override (+ distance-override (* elapse distance-restore))))
          (cond ((>= augmented-override desired-distance)
                 (set! distance-override #f)
                 (set! distance-goal #f))
                ((>= augmented-override distance-goal)
                 (set! distance-override distance-goal)
                 (set! distance-goal #f))
                (else
                 (set! distance-override augmented-override))))
        (align-eye-behind-no-occlusion (lens-center~ target))
        (adjust-player-alpha))))
  
  
  (method (remove-eye-occlusion lens)
    (let ((direction (vertex-negate& (get-sight~ camera))))
      (define (liquid-filter poly)
        (not (get-liquid-cube?~ (id->block (decode-id (get-data1~ poly))))))
      
      (define (determine-eye-occlusion lens)
        (bind-values (center direction collisions) (collide&stop$ lens (vertex+& lens (vertex-scalar*& direction desired-distance)) (get-lookat~ (motion-target)) occlusion-radiuses max-distance: desired-distance area-cube: (update-area-cube~ camera) polygon-filter: liquid-filter ignore-players?: #t)
          (if (null? collisions)
              #f
            (free-collisions$ collisions)
            (vertex-distance lens center))))
      
      (let ((occlusion (determine-eye-occlusion lens)))
        (let ((effective (effective-distance))
              (goal (or occlusion desired-distance)))
          (when (and (/= goal effective)
                     (or (not distance-goal)
                         (/= goal distance-goal)))
            (if (< goal effective)
                (begin
                  (set! distance-override goal)
                  (set! distance-goal #f)
                  (set! distance-restore (/ (- desired-distance goal) eye-restore-duration))
                  (align-eye-behind-no-occlusion lens)
                  (adjust-player-alpha))
              (set! distance-goal goal)))))))
  
  
  (method (align-eye-behind-no-occlusion pos)
    (let ((sight (get-sight~ camera)))
      (set-position~ camera (vertex-& pos (vertex-scalar*& sight (effective-distance))))
      (derive-target~ camera)
      (camera-update~ camera)))
  
  
  (method (align-eye-behind pos)
    (align-eye-behind-no-occlusion pos)
    (remove-eye-occlusion pos))
  
  
  (method (orbit-behind elem)
    (set! distance-override #f)
    (align-eye-behind-no-occlusion (transformed-center~ elem)))
  
  
  (method (orbit-behind-target)
    (orbit-behind (motion-target)))
  
  
  (method override (eye-center-player)
    (let ((history (current-history)))
      (unless (or (get-paused?~ history) simulate-protocol?)
        (follow-player))))
  
  
  (method override (zoom-in)
    (desired-distance-closer)
    (eye-center-player)
    (adjust-player-alpha))
  
  
  (method override (zoom-out)
    (desired-distance-further)
    (eye-center-player)
    (adjust-player-alpha)))


;;;
;;;; Person
;;;


(class Person-Motion extends Target-Motion
  
  
  (slot desired-roll   <fl>  initialize 0. getter generate)
  (slot roll-origin    <fl+> initialize #f getter generate)
  (slot roll-speed     <fl+> initialize #f getter generate)
  (slot last-roll-time <fl+> initialize #f getter generate)
  (slot last-track-fly <fl+> initialize #f getter generate)
  
  
  (method override (motion-target)
    (or target (current-me)))
  
  
  (method override (move-forward)
    (move-forward~ (motion-target) (get-commands~ world)))
  
  
  (method override (move-backward)
    (move-backward~ (motion-target) (get-commands~ world)))
  
  
  (method override (sprint-forward)
    (sprint-forward~ (motion-target) (get-commands~ world)))
  
  
  (method override (sprint-backward)
    (sprint-backward~ (motion-target) (get-commands~ world)))
  
  
  (method override (blink-forward)
    (blink-forward~ (motion-target) (get-commands~ world)))
  
  
  (method override (blink-backward)
    (blink-backward~ (motion-target) (get-commands~ world)))
  
  
  (method override (warp-forward)
    (warp-forward~ (motion-target)))
  
  
  (method override (warp-backward)
    (warp-backward~ (motion-target)))
  
  
  (method override (move-left)
    (move-left~ (motion-target) (get-commands~ world)))
  
  
  (method override (move-right)
    (move-right~ (motion-target) (get-commands~ world)))
  
  
  (method override (move-up)
    (move-up~ (motion-target) (get-commands~ world)))
  
  
  (method override (move-down)
    (move-down~ (motion-target) (get-commands~ world)))
  
  
  (method override (rotate-left)
    (rotate-left~ (motion-target) (get-commands~ world)))
  
  
  (method override (rotate-right)
    (rotate-right~ (motion-target) (get-commands~ world)))
  
  
  (method (force-up distance)
    (let ((target (motion-target))
          (position (get-position~ camera)))
      (set-position~ camera (vertex+& position (vertex-scalar*& (get-up~ (get-lookat~ target)) distance)))
      (derive-target~ camera)
      (camera-update~ camera)
      (set-position~ target (vertex+& (get-position~ target) (vertex& 0. distance 0.)))
      (follow-player)))
  
  
  (method (rotate-actor-horizontal actor angle (follow-actor?: follow-actor? #t))
    (let ((history (current-history)))
      (let ((me (current-controlled~ world)))
        (let ((lookat (get-lookat~ actor)))
          (let ((up (get-world-up~ world)))
            (set-lookat~ actor (rotate-lookat& lookat angle up))
            (when (and (get-down?~ world) (not (get-moving?~ world)))
              (decrease! relative-yaw angle))
            (update-matrix~ actor)
            (when (and (eq? actor me) (not (get-paused?~ history)) (not simulate-protocol?))
              (unless (and (get-down?~ world) (not (is-moving?~ world)))
                (set-lookat~ camera (rotate-lookat& (get-lookat~ camera) angle up)))
              (when follow-actor?
                (follow-actor actor))))))))
  
  
  (method (rotate-actor-vertical actor angle (follow-actor?: follow-actor? #t))
    (let ((history (current-history)))
      (let ((me (current-controlled~ world)))
        (let ((lookat (get-lookat~ actor)))
          (set-lookat~ actor (rotate-lookat& lookat angle (get-right~ lookat)))
          (update-matrix~ actor))
        (when (and (eq? actor me) (not (get-paused?~ history)) (not simulate-protocol?))
          (set-lookat~ camera (rotate-lookat& (get-lookat~ camera) angle (get-right~ camera)))
          (when follow-actor?
            (follow-actor actor))))))
  
  
  (method (track-actor actor dx dy local-sight local-up local-right first?)
    (let ((me (current-controlled~ world)))
      (if (flying?~ me)
          (track-fly actor dx dy local-sight local-up local-right first?)
        (track-ground actor dx dy local-sight local-up local-right first?))))
  
  
  (method (track-ground actor dx dy local-sight local-up local-right first?)
    (let ((me (current-controlled~ world))
          (interface (current-interface))
          (world (current-world))
          (camera (current-camera))
          (history (current-history)))
      (let ((me? (eq? actor me))
            (angleh (/ (- dx) (get-track-sensitivity)))
            (anglev (/ (- dy) (get-track-sensitivity)))
            (actor-lookat (get-lookat~ actor))
            (local-lookat (lookat local-sight local-up local-right))
            (old-lookat (lookat-copy& (get-lookat~ actor))))
        (let ((pitch (vertical-angle local-sight (get-world-up~ world))))
          ;; remove pitch
          (when (and me? (not (get-paused?~ history)) (not simulate-protocol?))
            (rotate-lookat-vertical! local-lookat local-lookat pitch))
          ;; start from camera lookat
          (when first?
            (set-lookat~ actor local-lookat))
          ;; adjust direction
          (rotate-actor-horizontal actor angleh follow-actor?: #f)
          ;; aec quicky to force desembed
          (let ((center (transformed-center~ actor))
                (lookat (get-lookat~ actor))
                (radiuses (transformed-radiuses~ actor))
                (area-cube (update-area-cube~ actor))
                (ignore-entities? (ignore-entities?~ actor))
                (ignore-players? (ignore-players?~ actor))
                (ignore-me? (ignore-me?~ actor)))
            (bind-values (new-center slide-direction slide-collisions) (collide&slide$ center center lookat radiuses area-cube: area-cube include-back-facing?: #t ignore-entities?: ignore-entities? ignore-players?: ignore-players? ignore-me?: ignore-me?)
              (let ((movement (vertex- new-center center))
                    (position (get-position~ actor)))
                (let ((new-position (vertex+& position movement))
                      (old-position (vertex-copy& position)))
                  (when (some? (lambda (collision)
                                 (eq? (get-type~ collision) 'embedded))
                               slide-collisions)
                    (set-position~ actor new-position))))
              (free-collisions$ slide-collisions)))
          ;; add pitch
          (when (and me? (not (get-paused?~ history)) (not simulate-protocol?))
            (rotate-vertical-up~ camera anglev (get-up~ (get-lookat~ actor))))
          (set! relative-yaw 0.)
          (set! absolute-pitch (- (vertical-angle (get-sight~ camera) (get-world-up~ world))))
          ;; camera follows actor
          (when (and me? (not (get-paused?~ history)) (not simulate-protocol?))
            (follow-actor actor))
          (update-compass~ world)
          (world.changes:lookat-change (current-seconds) actor #f #f old-lookat (get-lookat~ actor))))))
  
  
  (method (track-fly actor dx dy local-sight local-up local-right first?)
    (let ((me (current-controlled~ world))
          (camera (current-camera))
          (history (current-history)))
      (let ((me? (eq? actor me))
            (angleh (/ (- dx) (get-track-sensitivity)))
            (anglev (/ (- dy) (get-track-sensitivity)))
            (camera-lookat (get-lookat~ camera))
            (actor-lookat (get-lookat~ actor))
            (old-lookat (lookat-copy& (get-lookat~ actor)))
            (world-up (get-world-up~ (current-world))))
        (without-roll~ actor
          (lambda ()
            ;; start from camera lookat
            (when first?
              (set-lookat~ actor (lookat& local-sight local-up local-right)))
            ;; adjust pitch
            (set-lookat~ actor (rotate-lookat-vertical-up& actor-lookat anglev world-up))
            (when (not simulate-protocol?)
              (set-lookat~ camera (rotate-lookat-vertical-up& camera-lookat anglev world-up)))
            ;; adjust direction
            (set-lookat~ actor (rotate-lookat& actor-lookat angleh world-up))
            (when (not simulate-protocol?)
              (set-lookat~ camera (rotate-lookat& camera-lookat angleh world-up)))
            ;; update matrix
            (update-matrix~ me)
            (set! relative-yaw 0.)
            (set! absolute-pitch (- (vertical-angle (get-sight~ camera) (get-world-up~ world))))
            ;; camera follows actor
            (when (and me? (not (get-paused?~ history)) (not simulate-protocol?))
              (follow-actor actor))))
        ;; adjust roll
        (roll-actor actor angleh)
        (update-compass~ world)
        (world.changes:lookat-change (current-seconds) actor #f #f old-lookat (get-lookat~ actor)))))
  
  
  (method (roll-actor actor angle)
    (let ((max-roll .8))
      (let ((desired (max (- max-roll) (min max-roll (* (- angle) 25.)))))
        (set! desired-roll desired)
        (set! roll-origin 'track)
        (set! roll-speed .05)
        (set! last-track-fly (current-seconds))
        (process-roll actor))))
  
  
  (method override (restore-roll actor)
    (when roll-origin
      (if (and last-track-fly (> (- (current-seconds) last-track-fly) .1))
          (begin
            (set! last-track-fly #f)
            (reset-roll actor))
        (process-roll actor))))
  
  
  (method override (reset-roll actor)
    (unless (eq? roll-origin 'reset)
      (set! desired-roll 0.)
      (set! roll-origin 'reset)
      (let ((lookat (get-lookat~ actor)))
        (let ((dist (max (/ (abs (vertical-angle (get-right~ lookat) (roll-free-up (get-sight~ lookat)))) PI/4) .6)))
          ;; reset slower if less distance to go
          (set! roll-speed (* .02 dist))))
      (process-roll actor)))
  
  
  (method (process-roll actor)
    (define (increase-roll inc)
      (let ((lookat (get-lookat~ actor)))
        (when (or (not last-roll-time)
                  (> (abs inc) .01)
                  (> (- (current-seconds) last-roll-time) .05))
          (set! last-roll-time (current-seconds))
          (set-lookat~ actor (rotate-lookat& lookat inc (get-sight~ lookat))))))
    
    (when (and roll-origin (or (not last-roll-time) (> (- (current-seconds) last-roll-time) .01)))
      (let ((lookat (get-lookat~ actor)))
        (let ((current (vertical-angle (get-right~ lookat) (roll-free-up (get-sight~ lookat)))))
          (let ((distance (abs (- desired-roll current))))
            (if (float-near? distance 0.)
                (set! roll-origin #f)
              (if (eq? roll-origin 'reset)
                  (increase-roll (max (- roll-speed) (min roll-speed (- desired-roll current))))
                ;; increase damper for small distances
                (let ((damper (/ 15. distance)))
                  (let ((target-roll (/ (+ desired-roll (* damper current)) (+ 1. damper))))
                    (increase-roll (max (- roll-speed) (min roll-speed (- target-roll current)))))))))))))
  
  
  (method (exit-fly)
    (let ((target (motion-target)))
      (let ((lookat (get-lookat~ target)))
        (rotate-actor-vertical target (vertical-angle (get-sight~ lookat) (get-up~ lookat)))
        (follow-player)))))


;;;
;;;; Orbit
;;;


(class Orbit-Motion extends Target-Motion
  
  
  (method override (motion-target)
    (or target (first-target~ world) (current-me)))
  
  
  (method override (move-forward)
    (desired-distance-closer speed: .95)
    (orbit-behind (motion-target)))
  
  
  (method override (move-backward)
    (desired-distance-further speed: .95)
    (orbit-behind (motion-target)))
  
  
  (method override (move-left)
    (rotate-right~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (move-right)
    (rotate-left~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (move-up)
    (rotate-down~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (move-down)
    (rotate-up~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (rotate-left)
    (rotate-right~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (rotate-right)
    (rotate-left~ camera)
    (orbit-behind (motion-target))))


;;;
;;;; Free
;;;


(class Free-Motion extends Motion
  
  
  (method override (move-forward)
    (when (hook-eye~ world)
      (move-forward~ camera)))
  
  
  (method override (move-backward)
    (when (hook-eye~ world)
      (move-backward~ camera)))
  
  
  (method override (sprint-forward)
    (when (hook-eye~ world)
      (sprint-forward~ camera)))
  
  
  (method override (sprint-backward)
    (when (hook-eye~ world)
      (sprint-backward~ camera)))
  
  
  (method override (blink-forward)
    (when (hook-eye~ world)
      (blink-forward~ camera)))
  
  
  (method override (blink-backward)
    (when (hook-eye~ world)
      (blink-backward~ camera)))
  
  
  (method override (warp-forward)
    (when (hook-eye~ world)
      (warp-forward~ camera)))
  
  
  (method override (warp-backward)
    (when (hook-eye~ world)
      (warp-backward~ camera)))
  
  
  (method override (move-left)
    (when (hook-eye~ world)
      (move-left~ camera)))
  
  
  (method override (move-right)
    (when (hook-eye~ world)
      (move-right~ camera)))
  
  
  (method override (move-up)
    (when (hook-eye~ world)
      (move-up~ camera)))
  
  
  (method override (move-down)
    (when (hook-eye~ world)
      (move-down~ camera)))
  
  
  (method override (rotate-left)
    (when (hook-eye~ world)
      (rotate-left~ camera)))
  
  
  (method override (rotate-right)
    (when (hook-eye~ world)
      (rotate-right~ camera)))
  
  
  (method override (zoom-in)
    (zoom-in~ camera))
  
  
  (method override (zoom-out)
    (zoom-out~ camera))))
