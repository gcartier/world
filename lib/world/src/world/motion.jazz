;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Motion Controllers
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.motion jazz


(import (jazz.geometry)
        (world)
        (world.autoload)
        (world.camera)
        (world.collision)
        (world.dye)
        (world.element)
        (world.geometry)
        (world.syntax (phase syntax)))


(definition public (person-motion?)
  (is? (current-motion) Person-Motion))


(definition public (orbit-motion?)
  (is? (current-motion) Orbit-Motion))


(definition public (free-motion?)
  (is? (current-motion) Free-Motion))


(definition public (make-person-motion world camera person)
  (new Person-Motion world camera person))


(definition public (make-orbit-motion world camera target)
  (new Orbit-Motion world camera target))


(definition public (make-free-motion world camera)
  (new Free-Motion world camera))


(definition camera-smoothing? <bool>
  #t)

(definition public (get-camera-smoothing?) <bool>
  camera-smoothing?)

(definition public (set-camera-smoothing? flag <bool>)
  (set! camera-smoothing? flag))


(definition independant-camera? <bool>
  #t)

(definition public (get-independant-camera?) <bool>
  independant-camera?)

(definition public (set-independant-camera? flag <bool>)
  (set! independant-camera? flag))


(definition autotrack-camera? <bool>
  #t)

(definition public (get-autotrack-camera?) <bool>
  autotrack-camera?)

(definition public (set-autotrack-camera? flag <bool>)
  (set! autotrack-camera? flag))


(definition cursor-mode? <bool>
  #f)

(definition public (get-cursor-mode?) <bool>
  cursor-mode?)

(definition public (set-cursor-mode? flag <bool>)
  (set! cursor-mode? flag))


(definition track-sensitivity <fl>
  200.)

(definition public (get-track-sensitivity) <fl>
  track-sensitivity)

(definition public (set-track-sensitivity sensitivity <fl>)
  (set! track-sensitivity sensitivity))


;;;
;;;; Motion
;;;


(class Motion extends Object
  
  
  (slot world  <World>  getter generate)
  (slot camera <Camera> getter generate)
  
  
  (method override (initialize world camera)
    (set! world~self world)
    (set! camera~self camera))
  
  
  (method protected virtual (move-forward)
    )
  
  
  (method protected virtual (move-backward)
    )
  
  
  (method protected virtual (run-forward)
    )
  
  
  (method protected virtual (run-backward)
    )
  
  
  (method protected virtual (blink-forward)
    )
  
  
  (method protected virtual (blink-backward)
    )
  
  
  (method protected virtual (warp-forward)
    )
  
  
  (method protected virtual (warp-backward)
    )
  
  
  (method protected virtual (move-left)
    )
  
  
  (method protected virtual (move-right)
    )
  
  
  (method protected virtual (move-up)
    )
  
  
  (method protected virtual (move-down)
    )
  
  
  (method protected virtual (rotate-left)
    )
  
  
  (method protected virtual (rotate-right)
    )
  
  
  (method protected virtual (zoom-in)
    )
  
  
  (method protected virtual (zoom-out)
    )
  
  
  (method protected virtual (follow-player)
    )
  
  
  (method protected virtual (follow-actor actor)
    )
  
  
  (method protected virtual (eye-behind-player)
    )
  
  
  (method protected virtual (eye-behind-actor actor)
    )
  
  
  (method protected virtual (restoring-desired-distance?)
    #f)
  
  
  (method protected virtual (restore-desired-distance elapse)
    )
  
  
  (method protected virtual (eye-center-player)
    )
  
  
  (method protected virtual (reset-roll actor)
    )
  
  
  (method protected virtual (process-roll actor)
    )
  
  
  (method protected virtual (rotate-camera yaw <fl> pitch <fl>)
    (rotate-horizontal~ camera yaw)
    (rotate-vertical~ camera pitch))
  
  
  (method (track-camera dh <fl> dv <fl>)
    (when (hook-eye~ world)
      (rotate-camera (/ (- dh) track-sensitivity)
                     (/ (- dv) track-sensitivity)))))


;;;
;;;; Target
;;;


(class Target-Motion extends Motion
  
  
  (slot target            <Element> initialize #f   accessors generate)
  (slot max-distance      <fl>      initialize 30.0 getter generate)
  (slot desired-distance  <fl>      initialize 3.5  getter generate)
  (slot distance-override <fl+>     initialize #f   getter generate)
  (slot distance-goal     <fl+>     initialize #f   getter generate)
  (slot distance-restore  <fl+>     initialize #f   getter generate)
  
  
  (method override (initialize world camera target)
    (nextmethod world camera)
    (set! target~self target))
  
  
  (method protected virtual (motion-target)
    target)
  
  
  (method (setup-motion distance)
    (set! desired-distance distance)
    (set! distance-override #f)
    (set! distance-goal #f)
    (align-eye-behind-no-occlusion (player-lens (motion-target)))
    (adjust-player-alpha))
  
  
  (method (effective-distance)
    (or distance-override desired-distance))
  
  
  (method (desired-distance-closer (speed: speed #f))
    (let ((speed (or speed .75)))
      (set! desired-distance (* desired-distance speed))
      (update-first-restore)))
  
  
  (method (desired-distance-further (speed: speed #f))
    (let ((speed (or speed .75)))
      (set! desired-distance (min (/ desired-distance speed) max-distance))
      (update-first-restore)))
  
  
  (method (update-first-restore)
    (when (and distance-restore distance-override)
      (set! distance-restore (/ (- desired-distance distance-override) eye-restore-duration))))
  
  
  (method (adjust-player-alpha)
    (let ((me (current-me)))
      (let ((alpha (if (contains-vertex?~ me (get-position~ camera))
                       0.
                     (max 0.2 (min 1. (/ (effective-distance) 2.))))))
        (dye-alpha-set! (get-color~ me) alpha)
        (dye-alpha-set! (get-overlay~ me) alpha))))
  
  
  (method override (follow-player)
    (follow-actor (current-controlled~ world)))
  
  
  (method override (follow-actor actor)
    (when (person-motion?)
      @w(unless (or (get-viewing?~ world) (eq? (get-first-camera~ world) 'free))
        (eye-behind-actor actor))
      (when actor
        (align-eye-behind (player-lens actor)))))
  
  
  (method override (eye-behind-player)
    (eye-behind-actor (current-controlled~ world)))
  
  
  (method override (eye-behind-actor actor)
    (when (and actor (person-motion?))
      (set-lookat~ camera (realign-lookat& (get-lookat~ actor) (get-world-up~ world)))
      (camera-update~ camera)))
  
  
  (method (player-lens player)
    (let ((model (cache-model~ player)))
      (let ((center (get-center~ model))
            (bounds (get-bounds~ model))
            (matrix (get-matrix~ player))
            ;; quick try
            (eye-level (if (equal? (get-name~ model) "bloodelffemale")
                           .4
                         .15)))
        (let ((model-lens (vertex+ center (vertex 0. (* (cuboid-height bounds) eye-level) 0.))))
          (matrix-transform-3x4 matrix model-lens)))))
  
  
  (definition eye-restore-duration
    1.6)
  
  
  ;; mimimum radius determined by experimentation
  (definition occlusion-radius
    .1)
  
  (definition occlusion-radiuses
    (vertex occlusion-radius occlusion-radius occlusion-radius))
  
  
  (method override (restoring-desired-distance?)
    distance-goal)
  
  
  ;; lens   override   goal   distance
  ;; |      |          |      |
  ;; |      |          |      | - user desired distance
  ;; |      |          | - new override target
  ;; |      | - override to clear any occlusion
  ;; | - camera target
  ;;
  ;; distance-restore : constant restore velocity
  (method override (restore-desired-distance elapse)
    (let ((me (current-me)))
      (when distance-goal
        (let ((augmented-override (+ distance-override (* elapse distance-restore))))
          (cond ((>= augmented-override desired-distance)
                 (set! distance-override #f)
                 (set! distance-goal #f))
                ((>= augmented-override distance-goal)
                 (set! distance-override distance-goal)
                 (set! distance-goal #f))
                (else
                 (set! distance-override augmented-override))))
        (align-eye-behind-no-occlusion (player-lens me))
        (adjust-player-alpha))))
  
  
  (method (remove-eye-occlusion lens)
    (let ((direction (vertex-negate& (get-sight~ camera))))
      (define (liquid-filter poly)
        (not (get-water-cube?~ (world.external.minecraft:blockid->block (fxround (get-data1~ poly))))))
      
      (define (determine-eye-occlusion lens)
        (receive (center direction collisions) (collide&stop lens (vertex+ lens (vertex-scalar*& direction desired-distance)) (get-lookat~ (current-me)) occlusion-radiuses max-distance: desired-distance polygon-filter: liquid-filter ignore-players?: #t)
          (if (null? collisions)
              #f
            (vertex-distance lens center))))
      
      (let ((occlusion (determine-eye-occlusion lens)))
        (let ((effective (effective-distance))
              (goal (or occlusion desired-distance)))
          (when (and (/= goal effective)
                     (or (not distance-goal)
                         (/= goal distance-goal)))
            (if (< goal effective)
                (begin
                  (set! distance-override goal)
                  (set! distance-goal #f)
                  (set! distance-restore (/ (- desired-distance goal) eye-restore-duration))
                  (align-eye-behind-no-occlusion lens)
                  (adjust-player-alpha))
              (set! distance-goal goal)))))))
  
  
  (method (align-eye-behind-no-occlusion pos)
    (let ((sight (get-sight~ camera)))
      (set-position~ camera (vertex-& pos (vertex-scalar*& sight (effective-distance))))
      (derive-target~ camera)
      (camera-update~ camera)))
  
  
  (method (align-eye-behind pos)
    (align-eye-behind-no-occlusion pos)
    (remove-eye-occlusion pos))
  
  
  (method (orbit-behind elem)
    (set! distance-override #f)
    (align-eye-behind-no-occlusion (transformed-center~ elem)))
  
  
  (method (orbit-behind-target)
    (orbit-behind (motion-target)))
  
  
  (method override (eye-center-player)
    (unless (get-paused?~ world)
      (follow-player))))


;;;
;;;; Person
;;;


(class Person-Motion extends Target-Motion
  
  
  (slot relative-yaw   <fl>  initialize 0. getter generate)
  (slot absolute-pitch <fl>  initialize 0. getter generate)
  (slot desired-roll   <fl>  initialize 0. getter generate)
  (slot roll-speed     <fl+> initialize #f getter generate)
  (slot last-roll-time <fl+> initialize #f getter generate)
  
  
  (method override (initialize world camera target)
    (nextmethod world camera target)
    ;; do not consider pitch and roll to start
    (set! relative-yaw (vector-angle (get-sight~ camera) (get-sight~ (motion-target))))
    (set! absolute-pitch (- (pitch-angle (get-lookat~ camera) (get-world-up~ world)))))
  
  
  (method override (follow-player)
    (reposition-camera))
  
  
  (method (reposition-camera)
    (let ((lookat (get-lookat~ camera))
          (target (motion-target)))
      (standardize-lookat! lookat)
      ;; quick not correct doesn't work if lookat has roll
      ;; quick not correct hack to remove pitch
      (let ((sight (vertex-normalize (vertex (vertex-x (get-sight~ target)) 0. (vertex-z (get-sight~ target))))))
        (let ((up (get-up~ lookat)))
          (let ((right (cross-normal& sight up)))
            (lookat! lookat
                     sight
                     up
                     right))))
      (rotate-lookat! lookat lookat relative-yaw (get-up~ lookat))
      (rotate-lookat! lookat lookat absolute-pitch (get-right~ lookat))
      (when target
        (align-eye-behind (player-lens target)))))
  
  
  (method override (rotate-camera yaw <fl> pitch <fl>)
    (nextmethod yaw pitch)
    (increase! relative-yaw yaw)
    (increase! absolute-pitch pitch))
  
  
  (method override (motion-target)
    (or target (current-me)))
  
  
  (method override (move-forward)
    (move-forward~ (motion-target) (get-commands~ world)))
  
  
  (method override (move-backward)
    (move-backward~ (motion-target) (get-commands~ world)))
  
  
  (method override (run-forward)
    (run-forward~ (motion-target) (get-commands~ world)))
  
  
  (method override (run-backward)
    (run-backward~ (motion-target) (get-commands~ world)))
  
  
  (method override (blink-forward)
    (blink-forward~ (motion-target) (get-commands~ world)))
  
  
  (method override (blink-backward)
    (blink-backward~ (motion-target) (get-commands~ world)))
  
  
  (method override (warp-forward)
    (warp-forward~ (motion-target)))
  
  
  (method override (warp-backward)
    (warp-backward~ (motion-target)))
  
  
  (method override (move-left)
    (move-left~ (motion-target) (get-commands~ world)))
  
  
  (method override (move-right)
    (move-right~ (motion-target) (get-commands~ world)))
  
  
  (method override (move-up)
    (move-up~ (motion-target) (get-commands~ world)))
  
  
  (method override (move-down)
    (move-down~ (motion-target) (get-commands~ world)))
  
  
  (method override (rotate-left)
    (rotate-left~ (motion-target) (get-commands~ world)))
  
  
  (method override (rotate-right)
    (rotate-right~ (motion-target) (get-commands~ world)))
  
  
  (method override (zoom-in)
    (desired-distance-closer)
    (eye-center-player)
    (adjust-player-alpha))
  
  
  (method override (zoom-out)
    (desired-distance-further)
    (eye-center-player)
    (adjust-player-alpha))
  
  
  (method (force-up distance)
    (let ((target (motion-target))
          (position (get-position~ camera)))
      (set-position~ camera (vertex+& position (vertex-scalar*& (get-up~ (get-lookat~ target)) distance)))
      (derive-target~ camera)
      (camera-update~ camera)
      (set-y~ target (+ (get-y~ target) distance))
      (follow-player)))
  
  
  (method (rotate-actor-horizontal actor angle (follow-actor?: follow-actor? #t))
    (let ((me (current-controlled~ world)))
      (let ((lookat (get-lookat~ actor)))
        (let ((up (get-world-up~ world)))
          (set-lookat~ actor (rotate-lookat& lookat angle up))
          (update-matrix~ me)
          (when (and (eq? actor me) (not (get-paused?~ world)))
            (unless (and (get-down?~ world) (not (is-moving?~ world)))
              (set-lookat~ camera (rotate-lookat& (get-lookat~ camera) angle up)))
            (when follow-actor?
              (follow-actor actor)))))))
  
  
  (method (rotate-actor-vertical actor angle (follow-actor?: follow-actor? #t))
    (let ((me (current-controlled~ world)))
      (let ((lookat (get-lookat~ actor)))
        (set-lookat~ actor (rotate-lookat& lookat angle (get-right~ lookat)))
        (update-matrix~ me))
      (when (and (eq? actor me) (not (get-paused?~ world)))
        (set-lookat~ camera (rotate-lookat& (get-lookat~ camera) angle (get-right~ camera)))
        (when follow-actor?
          (follow-actor actor)))))
  
  
  (method (track-actor actor dx dy local-sight local-up local-right first?)
    (if (get-fly?~ world)
        (track-fly actor dx dy local-sight local-up local-right first?)
      (track-ground actor dx dy local-sight local-up local-right first?)))
  
  
  (method (track-ground actor dx dy camera-sight camera-up camera-right first?)
    (let ((me (current-controlled~ world))
          (camera (current-camera)))
      (let ((me? (eq? actor me))
            (angleh (/ (- dx) track-sensitivity))
            (anglev (/ (- dy) track-sensitivity))
            (actor-lookat (get-lookat~ actor))
            (camera-lookat (lookat camera-sight camera-up camera-right)))
        (let ((pitch (vertical-angle camera-sight (get-world-up~ world))))
          ;; remove pitch
          (when (and me? (not (get-paused?~ world)))
            (rotate-lookat-vertical! camera-lookat pitch))
          ;; start from camera lookat
          (when first?
            (lookat-copy! actor-lookat camera-lookat))
          ;; adjust direction
          (rotate-actor-horizontal actor angleh follow-actor?: #f)
          ;; add pitch
          (when (and me? (not (get-paused?~ world)))
            (rotate-vertical-up~ camera anglev (get-up~ (get-lookat~ actor))))
          (set! relative-yaw 0.)
          (set! absolute-pitch (- (vertical-angle (get-sight~ camera) (get-world-up~ world))))
          ;; camera follows actor
          (when (and me? (not (get-paused?~ world)))
            (follow-actor actor))))))
  
  
  (method (track-fly actor dx dy camera-sight camera-up camera-right first?)
    (let ((me (current-controlled~ world))
          (camera (current-camera)))
      (let ((me? (eq? actor me))
            (angleh (/ (- dx) track-sensitivity))
            (anglev (/ (- dy) track-sensitivity))
            (camera-lookat (get-lookat~ camera))
            (actor-lookat (get-lookat~ actor))
            (world-up (get-world-up~ (current-world))))
        ;; start from camera lookat
        (when first?
          (lookat-copy! actor-lookat (lookat camera-sight camera-up camera-right)))
        ;; adjust pitch
        (let ((actor-horizon (cross-normal& (get-sight~ actor-lookat) world-up)))
          (rotate-lookat-vertical-up! actor-lookat anglev world-up)
          (rotate-lookat-vertical-up! camera-lookat anglev world-up))
        ;; adjust roll
        (roll-actor actor angleh)
        ;; adjust direction
        (set-lookat~ actor (rotate-lookat& actor-lookat angleh world-up))
        (set-lookat~ camera (rotate-lookat& camera-lookat angleh world-up))
        ;; update matrix
        (update-matrix~ me)
        (set! relative-yaw 0.)
        (set! absolute-pitch (- (vertical-angle (get-sight~ camera) (get-world-up~ world))))
        ;; camera follows actor
        (when (and me? (not (get-paused?~ world)))
          (follow-actor actor)))))
  
  
  (method override (reset-roll actor)
    (set! desired-roll 0.)
    (set! roll-speed .25)
    (process-roll actor))
  
  
  (method (roll-actor actor angle)
    (let ((max-roll PI/4))
      (let ((desired (max (- max-roll) (min max-roll (* (- angle) 25.)))))
        (set! desired-roll desired)
        (set! roll-speed .25)
        (process-roll actor))))
  
  
  (method override (process-roll actor)
    (when (and roll-speed (or (not last-roll-time) (> (- (current-seconds) last-roll-time) .01)))
      (let ((lookat (get-lookat~ actor)))
        (let ((current (vertical-angle (get-right~ lookat) (roll-free-up (get-sight~ lookat)))))
          (if (near? current desired-roll .001)
              (set! roll-speed #f)
            (let ((damper 10.))
              (let ((target-roll (/ (+ desired-roll (* damper current)) (+ 1. damper))))
                (increase-roll actor (max (- roll-speed) (min roll-speed (- target-roll current)))))))))))
  
  
  (method (increase-roll actor inc)
    (let ((lookat (get-lookat~ actor)))
      (when (or (not last-roll-time)
                (> (abs inc) .01)
                (> (- (current-seconds) last-roll-time) .05))
        (set! last-roll-time (current-seconds))
        (set-lookat~ actor (rotate-lookat& lookat inc (get-sight~ lookat))))))
  
  
  (method (exit-fly)
    (let ((target (motion-target)))
      (let ((lookat (get-lookat~ target)))
        (rotate-actor-vertical target (vertical-angle (get-sight~ lookat) (get-up~ lookat)))
        (follow-player)))))


;;;
;;;; Orbit
;;;


(class Orbit-Motion extends Target-Motion
  
  
  (method override (motion-target)
    (or target (first-target~ world) (current-me)))
  
  
  (method override (move-forward)
    (desired-distance-closer speed: .95)
    (orbit-behind (motion-target)))
  
  
  (method override (move-backward)
    (desired-distance-further speed: .95)
    (orbit-behind (motion-target)))
  
  
  (method override (move-left)
    (rotate-right~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (move-right)
    (rotate-left~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (move-up)
    (rotate-down~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (move-down)
    (rotate-up~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (rotate-left)
    (rotate-right~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (rotate-right)
    (rotate-left~ camera)
    (orbit-behind (motion-target)))
  
  
  (method override (zoom-in)
    (desired-distance-closer)
    (orbit-behind (motion-target)))
  
  
  (method override (zoom-out)
    (desired-distance-further)
    (orbit-behind (motion-target)))
  
  
  (method (track-orbit dx <fl> dy <fl>)
    (rotate-horizontal~ camera (/ (- dx) track-sensitivity))
    (rotate-vertical~ camera (/ (- dy) track-sensitivity))
    (orbit-behind-target)))


;;;
;;;; Free
;;;


(class Free-Motion extends Motion
  
  
  (method override (move-forward)
    (when (hook-eye~ world)
      (move-forward~ camera)))
  
  
  (method override (move-backward)
    (when (hook-eye~ world)
      (move-backward~ camera)))
  
  
  (method override (run-forward)
    (when (hook-eye~ world)
      (run-forward~ camera)))
  
  
  (method override (run-backward)
    (when (hook-eye~ world)
      (run-backward~ camera)))
  
  
  (method override (blink-forward)
    (when (hook-eye~ world)
      (blink-forward~ camera)))
  
  
  (method override (blink-backward)
    (when (hook-eye~ world)
      (blink-backward~ camera)))
  
  
  (method override (warp-forward)
    (when (hook-eye~ world)
      (warp-forward~ camera)))
  
  
  (method override (warp-backward)
    (when (hook-eye~ world)
      (warp-backward~ camera)))
  
  
  (method override (move-left)
    (when (hook-eye~ world)
      (move-left~ camera)))
  
  
  (method override (move-right)
    (when (hook-eye~ world)
      (move-right~ camera)))
  
  
  (method override (move-up)
    (when (hook-eye~ world)
      (move-up~ camera)))
  
  
  (method override (move-down)
    (when (hook-eye~ world)
      (move-down~ camera)))
  
  
  (method override (rotate-left)
    (when (hook-eye~ world)
      (rotate-left~ camera)))
  
  
  (method override (rotate-right)
    (when (hook-eye~ world)
      (rotate-right~ camera)))
  
  
  (method override (zoom-in)
    (zoom-in~ camera))
  
  
  (method override (zoom-out)
    (zoom-out~ camera))))
