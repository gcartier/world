;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Time
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Barbara Samson


(module world.time jazz


(import (jazz.time)
        (world.event))


(definition public (present-duration duration (compact?: compact? #f))
  (let ((integer-duration (fxround duration)))
    (let ((date (time-monotonic->date (make-time 'time-monotonic 0 integer-duration) 0))
          (day (* 24 60 60)))
      (let ((days (quotient integer-duration day))
            (hours (date-hour date))
            (minutes (date-minute date))
            (seconds (date-second date))
            (spacing (if compact? "" " ")))
        (cond ((and (= days 0)
                    (= hours 0)
                    (= minutes 0))
               (format "{a}s" seconds))
              ((and (= days 0)
                    (= hours 0))
               (format "{a}m{a}{a}s" minutes spacing seconds))
              ((= days 0)
               (format "{a}h{a}{a}m" hours spacing minutes))
              (else
               (format "{a}d{a}{a}h{a}{a}m" days spacing hours spacing minutes)))))))


;;;
;;;; Time-Base
;;;


(definition protected udp-latency-no-change
  0.)

(definition protected udp-latency-nack-up
  1.)

(definition protected udp-latency-up
  2.)

(definition protected udp-latency-down
  3.)

(definition protected udp-latency-max
  4.)

(definition protected udp-latency-raw
  5.)


(class Time-Base extends Object
  
  
  (slot base        <fl+> getter generate)
  (slot raw-latency <fl>  getter generate)
  (slot latency     <fl>  getter generate)
  
  
  (method override (initialize self)
    (nextmethod self)
    (reset self))
  
  
  (method protected (reset self)
    (reset-base self))
  
  
  (method protected virtual (reset-base self)
    (set! self.base #f)
    (set! self.raw-latency 0.)
    (set! self.latency 0.))

  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a} {a}" base latency))))
  
  
  (method protected (visualizer-update self base latency)
    (set! self.base base)
    (set! self.latency latency))
  
  
  (method public (update-start self new-local new-remote)
    (when (not base)
      (let ((new-base (- new-local new-remote)))
        (set! base new-base)
        (set! raw-latency 0.)
        (set! latency 0.))))
  
  
  (method public virtual (update-time self new-local new-remote update-proc)
    (let ((new-base (- new-local new-remote)))
      (if (not base)
          (begin
            (set! base new-base)
            (set! raw-latency 0.)
            (set! latency 0.))
        (let ((raw (- new-base base)))
          (set! raw-latency raw)
          (set! latency raw)
          (update-proc raw-latency latency udp-latency-raw)))))
  
  
  (method public virtual (update-past self new-local new-remote update-proc)
    )
  
  
  (method public virtual (remote->local self remote) <fl>
    (+ base latency remote)))


;;;
;;;; Rate
;;;


(class Rate extends Object
  
  
  (slot start     initialize #f getter generate)
  (slot count     initialize #f getter generate)
  (slot frequency initialize #f getter generate)
  
  
  (method protected (update self sampling)
    (if (not start)
        (begin
          (set! start (current-monotonic))
          (set! count 0))
      (increase! count)
      (when (= count sampling)
        (let ((now (current-monotonic)))
          (let ((elapse (- now start)))
            (set! start now)
            (set! count 0)
            (set! frequency (/ (cast <fl> sampling) elapse)))))))))
