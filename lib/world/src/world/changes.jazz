;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Changes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.changes jazz


(import (jazz.component)
        (jazz.editor.jazz)
        (jazz.text)
        (world)
        (world.actor)
        (world.autoload)
        (world.change)
        (world.console)
        (world.context)
        (world.creature)
        (world.entity)
        (world.geometry)
        (world.history)
        (world.message)
        (world.parts)
        (world.script.asset)
        (world.syntax (phase syntax)))


;;;
;;;; Move
;;;


(define-change move
  ;; action
  action-layout: '(entity commands double)
  action-store: (lambda (push entity commands elapse)
                  (push-entity push entity)
                  (push-commands push commands)
                  (push-double push elapse))
  action-apply: (lambda (client-id pop)
                  (let* ((entity (pop-entity pop))
                         (commands (pop-commands pop))
                         (elapse (pop-double pop)))
                    ;; sejour quicky
                    (when entity
                      (tick-action entity commands (current-seconds) elapse))))
  ;; forward
  forward-layout: '(entity vertex vertex vertex boolean)
  forward-store: (lambda (push entity new-position new-velocity new-fall-velocity ground?)
                   (push-entity push entity)
                   (push-vertex push new-position)
                   (push-vertex push new-velocity)
                   (push-vertex push new-fall-velocity)
                   (push-boolean push ground?))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop))
                          (position (pop-vertex& pop))
                          (velocity (pop-vertex& pop))
                          (fall-velocity (pop-vertex& pop))
                          (ground? (pop-boolean pop)))
                     ;; sejour quicky
                     (when entity
                       (set-position entity position)
                       (set-effective-velocity entity velocity)
                       (set-fall-velocity entity fall-velocity)
                       (set-ground? entity ground?)
                       (adjust-motion-target entity))))
  ;; backward
  backward-layout: '(entity vertex vertex vertex boolean)
  backward-store: (lambda (push entity old-position old-velocity old-fall-velocity old-ground?)
                    (push-entity push entity)
                    (push-vertex push old-position)
                    (push-vertex push old-velocity)
                    (push-vertex push old-fall-velocity)
                    (push-boolean push old-ground?))
  backward-apply: (lambda (client-id pop)
                    (let* ((entity (pop-entity pop))
                           (old-position (pop-vertex& pop))
                           (old-velocity (pop-vertex& pop))
                           (old-fall-velocity (pop-vertex& pop))
                           (old-ground? (pop-boolean pop)))
                      ;; sejour quicky
                      (when entity
                        (set-position entity old-position)
                        (set-effective-velocity entity old-velocity)
                        (set-fall-velocity entity old-fall-velocity)
                        (set-ground? entity old-ground?)))))


(definition protected (move-change time entity commands elapse old-position new-position old-velocity new-velocity old-fall-velocity new-fall-velocity old-ground? new-ground?)
  (let ((world (current-world))
        (history (current-history)))
    (unless (or (not (get-id entity))
                (not (record-change? entity))
                (and (get-paused? history) (not (stepping-history?)))
                (and (vertex-near? new-position old-position)
                     (vertex-near? new-velocity old-velocity)
                     (vertex-near? new-fall-velocity new-fall-velocity)
                     (eq? new-ground? old-ground?))
                (admin-me? entity))
      (bidirectional-change 'move time
        (lambda (store push)
          (store push entity commands elapse))
        (lambda (store push)
          (store push entity new-position new-velocity new-fall-velocity new-ground?))
        (lambda (store push)
          (store push entity old-position old-velocity old-fall-velocity old-ground?))
        metadata: (snapshot-metadata world entity)))))


;;;
;;;; Lookat
;;;


(define-change lookat
  ;; action
  action-layout: '(entity commands double)
  action-store: (lambda (push entity commands elapse)
                  (push-entity push entity)
                  (push-commands push commands)
                  (push-double push (or elapse -1.)))
  action-apply: (lambda (client-id pop)
                  (let* ((entity (pop-entity pop))
                         (commands (pop-commands pop))
                         (elapse (pop-double pop)))
                     ;; sejour quicky
                     (when entity
                       ;; quick hack for track-ground
                       (when commands
                         (tick-action entity commands (current-seconds) elapse)))))
  ;; forward
  forward-layout: '(entity lookat)
  forward-store: (lambda (push entity new-lookat)
                   (push-entity push entity)
                   (push-lookat push new-lookat))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop))
                          (new-lookat (pop-lookat& pop)))
                     ;; sejour quicky
                     (when entity
                       (set-lookat entity new-lookat)
                       (adjust-motion-target entity))))
  ;; backward
  backward-layout: '(entity lookat)
  backward-store: (lambda (push entity old-lookat)
                    (push-entity push entity)
                    (push-lookat push old-lookat))
  backward-apply: (lambda (client-id pop)
                    (let* ((entity (pop-entity pop))
                           (old-lookat (pop-lookat& pop)))
                      ;; sejour quicky
                      (when entity
                        (set-lookat entity old-lookat)))))


(definition protected (lookat-change time entity commands elapse old-lookat new-lookat)
  (let ((world (current-world))
        (history (current-history)))
    (unless (or (not (get-id entity))
                (not (record-change? entity))
                (and (get-paused? history) (not (stepping-history?)))
                (lookat-near? new-lookat old-lookat)
                (admin-me? entity))
      (bidirectional-change 'lookat time
        (lambda (store push)
          (store push entity commands elapse))
        (lambda (store push)
          (store push entity new-lookat))
        (lambda (store push)
          (store push entity old-lookat))
        metadata: (snapshot-metadata world entity)))))


(definition protected (lookat-fly-change entity dir)
  (let ((old-lookat (lookat-copy& (get-lookat entity)))
        (new-lookat (direction-lookat& dir)))
    (set-lookat entity new-lookat)
    (lookat-change (current-seconds) entity #f #f old-lookat new-lookat)))
  
  
(definition protected (lookat-horizon-change entity dir)
  (let ((old-lookat (lookat-copy& (get-lookat entity))))
    (horizon-sight! entity dir)
    (let ((new-lookat (get-lookat entity)))
      (lookat-change (current-seconds) entity #f #f old-lookat new-lookat))))


;;;
;;;; Animate
;;;


(define-change animate
  ;; forward
  forward-layout: '(entity literal)
  forward-store: (lambda (push entity new-animation)
                   (push-entity push entity)
                   (push-literal push new-animation))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop))
                          (new-animation (pop-literal pop)))
                     ;; sejour quicky
                     (when entity
                       ;; sejour quicky
                       (when (and (get-morphing entity)
                                  (find-animation (cache-model entity) new-animation))
                         (change-morphing entity new-animation)))))
  ;; backward
  backward-layout: '(entity literal)
  backward-store: (lambda (push entity old-animation)
                    (push-entity push entity)
                    (push-literal push old-animation))
  backward-apply: (lambda (client-id pop)
                    (let* ((entity (pop-entity pop))
                           (old-animation (pop-literal pop)))
                      ;; sejour quicky
                      (when entity
                        ;; limit-case for first animate
                        (when old-animation
                          (change-morphing entity old-animation))))))


(definition protected (animate-change time entity old-animation new-animation)
  (let ((world (current-world))
        (history (current-history)))
    (unless (or (not (get-id entity))
                (not (record-change? entity))
                (and (get-paused? history) (not (stepping-history?)))
                (equal? new-animation old-animation)
                (admin-me? entity))
      (bidirectional-change 'animate time
        (lambda (store push)
          (store push entity new-animation))
        (lambda (store push)
          (store push entity new-animation))
        (lambda (store push)
          (store push entity old-animation))))))


;;;
;;;; Damage
;;;


(definition protected (damage-change actor amount critical?)
  (let ((history (current-history)))
    (unless (and (get-paused? history) (not (stepping-history?)))
      (apply-damage actor amount critical?)
      (bidirectional-change 'damage (current-seconds)
        (lambda (store push)
          )
        (lambda (store push)
          (store push actor amount critical?))
        (lambda (store push)
          (store push actor))))))


(definition protected (apply-damage actor amount critical?)
  (when (client?)
    (damage actor amount critical?)))


;; todo
(definition protected (apply-undamage actor)
  )


(define-change damage
  ;; forward
  forward-layout: '(entity double boolean)
  forward-store: (lambda (push actor amount critical?)
                   (push-entity push actor)
                   (push-double push amount)
                   (push-boolean push critical?))
  forward-apply: (lambda (client-id pop)
                   (let* ((actor (pop-entity pop))
                          (amount (pop-double pop))
                          (critical? (pop-boolean pop)))
                     ;; sejour quicky
                     (when actor
                       (apply-damage actor amount critical?))))
  ;; backward
  backward-layout: '(entity)
  backward-store: (lambda (push actor)
                    (push-entity push actor))
  backward-apply: (lambda (client-id pop)
                    (let* ((actor (pop-entity pop)))
                      ;; sejour quicky
                      (when actor
                        (apply-undamage actor)))))


;;;
;;;; Die
;;;


(definition protected (die-change actor)
  (let ((history (current-history)))
    (unless (and (get-paused? history) (not (stepping-history?)))
      (apply-die actor)
      (bidirectional-change 'die (current-seconds)
        (lambda (store push)
          )
        (lambda (store push)
          (store push actor))
        (lambda (store push)
          (store push actor))))))


(definition protected (apply-die actor)
  (die actor))


;; useful for navigating history as only server should apply die
(definition protected (apply-resurrect actor)
  (resurrect actor))


(define-change die
  ;; forward
  forward-layout: '(entity)
  forward-store: (lambda (push actor)
                   (push-entity push actor))
  forward-apply: (lambda (client-id pop)
                   (let* ((actor (pop-entity pop)))
                     ;; sejour quicky
                     (when actor
                       (apply-die actor))))
  ;; backward
  backward-layout: '(entity)
  backward-store: (lambda (push actor)
                    (push-entity push actor))
  backward-apply: (lambda (client-id pop)
                    (let* ((actor (pop-entity pop)))
                      ;; sejour quicky
                      (when actor
                        (apply-resurrect actor)))))


;;;
;;;; Trek
;;;


(definition protected (trek-change actor)
  (let ((world (current-world))
        (history (current-history)))
    (unless (and (get-paused? history) (not (stepping-history?)))
      (let ((old-mount (get-mount actor))
            (old-vehicle (vehicle-name actor)))
        (activate-trek actor)
        (bidirectional-change 'trek (current-seconds)
          (lambda (store push)
            )
          (lambda (store push)
            (store push actor))
          (lambda (store push)
            (store push actor old-mount old-vehicle)))))))


(define-change trek
  ;; forward
  forward-layout: '(entity)
  forward-store: (lambda (push actor)
                   (push-entity push actor))
  forward-apply: (lambda (client-id pop)
                   (let* ((actor (pop-entity pop)))
                     ;; sejour quicky
                     (when actor
                       (activate-trek actor))))
  ;; backward
  backward-layout: '(entity literal literal)
  backward-store: (lambda (push actor old-mount old-vehicle)
                    (push-entity push actor)
                    (push-literal push old-mount)
                    (push-literal push old-vehicle))
  backward-apply: (lambda (client-id pop)
                    (let* ((actor (pop-entity pop))
                           (old-mount (pop-literal pop))
                           (old-vehicle (pop-literal pop)))
                      ;; sejour quicky
                      (when actor
                        (activate-travel actor old-mount old-vehicle)))))


;;;
;;;; Ride
;;;


(definition protected (ride-change actor vehicle)
  (let ((world (current-world))
        (history (current-history)))
    (unless (and (get-paused? history) (not (stepping-history?)))
      (let ((old-mount (get-mount actor))
            (old-vehicle (vehicle-name actor)))
        (activate-ride actor vehicle)
        (bidirectional-change 'ride (current-seconds)
          (lambda (store push)
            )
          (lambda (store push)
            (store push actor vehicle))
          (lambda (store push)
            (store push actor old-mount old-vehicle)))))))


(define-change ride
  ;; forward
  forward-layout: '(entity literal)
  forward-store: (lambda (push actor vehicle)
                   (push-entity push actor)
                   (push-literal push vehicle))
  forward-apply: (lambda (client-id pop)
                   (let* ((actor (pop-entity pop))
                          (vehicle (pop-literal pop)))
                     ;; sejour quicky
                     (when actor
                       (activate-ride actor vehicle))))
  ;; backward
  backward-layout: '(entity literal literal)
  backward-store: (lambda (push actor old-mount old-vehicle)
                    (push-entity push actor)
                    (push-literal push old-mount)
                    (push-literal push old-vehicle))
  backward-apply: (lambda (client-id pop)
                    (let* ((actor (pop-entity pop))
                           (old-mount (pop-literal pop))
                           (old-vehicle (pop-literal pop)))
                      ;; sejour quicky
                      (when actor
                        (activate-travel actor old-mount old-vehicle)))))


;;;
;;;; Fly
;;;


(definition protected (fly-change actor vehicle)
  (let ((world (current-world))
        (history (current-history)))
    (unless (and (get-paused? history) (not (stepping-history?)))
      (let ((old-mount (get-mount actor))
            (old-vehicle (vehicle-name actor)))
        (activate-fly actor vehicle)
        (bidirectional-change 'fly (current-seconds)
          (lambda (store push)
            )
          (lambda (store push)
            (store push actor vehicle))
          (lambda (store push)
            (store push actor old-mount old-vehicle)))))))


(define-change fly
  ;; forward
  forward-layout: '(entity literal)
  forward-store: (lambda (push actor vehicle)
                   (push-entity push actor)
                   (push-literal push vehicle))
  forward-apply: (lambda (client-id pop)
                   (let* ((actor (pop-entity pop))
                          (vehicle (pop-literal pop)))
                     ;; sejour quicky
                     (when actor
                       (activate-fly actor vehicle))))
  ;; backward
  backward-layout: '(entity literal literal)
  backward-store: (lambda (push actor old-mount old-vehicle)
                    (push-entity push actor)
                    (push-literal push old-mount)
                    (push-literal push old-vehicle))
  backward-apply: (lambda (client-id pop)
                    (let* ((actor (pop-entity pop))
                           (old-mount (pop-literal pop))
                           (old-vehicle (pop-literal pop)))
                      ;; sejour quicky
                      (when actor
                        (activate-travel actor old-mount old-vehicle)))))


;;;
;;;; Vehicle
;;;


(definition protected (vehicle-change actor vehicle)
  (let ((world (current-world))
        (history (current-history)))
    (unless (and (get-paused? history) (not (stepping-history?)))
      (let ((old-mount (get-mount actor))
            (old-vehicle (vehicle-name actor)))
        (change-vehicle actor vehicle)
        (bidirectional-change 'vehicle (current-seconds)
          (lambda (store push)
            )
          (lambda (store push)
            (store push actor vehicle))
          (lambda (store push)
            (store push actor old-mount old-vehicle)))))))


(define-change vehicle
  ;; forward
  forward-layout: '(entity literal)
  forward-store: (lambda (push actor vehicle)
                   (push-entity push actor)
                   (push-literal push vehicle))
  forward-apply: (lambda (client-id pop)
                   (let* ((actor (pop-entity pop))
                          (vehicle (pop-literal pop)))
                     ;; sejour quicky
                     (when actor
                       (change-vehicle actor vehicle))))
  ;; backward
  backward-layout: '(entity literal literal)
  backward-store: (lambda (push actor old-mount old-vehicle)
                    (push-entity push actor)
                    (push-literal push old-mount)
                    (push-literal push old-vehicle))
  backward-apply: (lambda (client-id pop)
                    (let* ((actor (pop-entity pop))
                           (old-mount (pop-literal pop))
                           (old-vehicle (pop-literal pop)))
                      ;; sejour quicky
                      (when actor
                        (activate-travel actor old-mount old-vehicle)))))


;;;
;;;; Name
;;;


(definition protected (name-change entity name)
  (let ((world (current-world))
        (history (current-history)))
    (unless (and (get-paused? history) (not (stepping-history?)))
      (apply-name entity name)
      (bidirectional-change 'name (current-seconds)
        (lambda (store push)
          )
        (lambda (store push)
          (store push entity name))
        (lambda (store push)
          (store push))))))


(define-change name
  ;; forward
  forward-layout: '(entity literal)
  forward-store: (lambda (push entity name)
                   (push-entity push entity)
                   (push-literal push name))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop))
                          (name (pop-literal pop)))
                     ;; sejour quicky
                     (when entity
                       (apply-name entity name))))
  ;; backward
  backward-layout: '()
  backward-store: (lambda (push)
                    )
  backward-apply: (lambda (client-id pop)
                    ))


(definition protected (apply-name entity name)
  (set-name-pane entity name))


;;;
;;;; Say
;;;


(definition protected (say-change entity message (size: size #f))
  (let ((world (current-world))
        (history (current-history)))
    (unless (and (get-paused? history) (not (stepping-history?)))
      (bidirectional-change 'say (current-seconds)
        (lambda (store push)
          )
        (lambda (store push)
          (store push entity message size))
        (lambda (store push)
          (store push))))))


(define-change say
  ;; forward
  forward-layout: '(entity literal literal)
  forward-store: (lambda (push entity message size)
                   (push-entity push entity)
                   (push-literal push message)
                   (push-literal push size))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop))
                          (message (pop-literal pop))
                          (size (pop-literal pop)))
                     ;; sejour quicky
                     (when entity
                       (apply-say entity message size))))
  ;; backward
  backward-layout: '()
  backward-store: (lambda (push)
                    )
  backward-apply: (lambda (client-id pop)
                    ))


(definition protected (apply-say entity message size)
  (set-output entity (new Message (current-seconds) 'say (standardize-message message) size))
  (update-console-output entity)
  (world.interface.script:update-console entity))


;;;
;;;; Fire
;;;


(definition protected (fire-change pos dir kind actor)
  (let ((history (current-history)))
    (unless (or (and (get-paused? history) (not (stepping-history?))) (admin-me? actor))
      (let ((missile (apply-fire #f pos dir kind actor)))
        (let ((entity-id (get-id missile)))
          (bidirectional-change 'fire (current-seconds)
            (lambda (store push)
              )
            (lambda (store push)
              (store push entity-id pos dir kind actor))
            (lambda (store push)
              (store push missile))))))))


(definition protected (apply-fire id pos dir kind actor)
  (let ((zone (current-zone)))
    (let ((missile (new Missile id: id position: pos actor: actor kind: kind)))
      (increase-missiles actor)
      (set-velocity missile (vertex-scalar*& dir (missile-speed missile)))
      (when (client?)
        (set-effective-velocity missile (get-velocity missile)))
      (add-element zone missile)
      missile)))


(definition protected (apply-unfire missile)
  (let ((world (current-world))
        (zone (current-zone)))
    (decrease-missiles (get-actor missile))
    (remove-element world missile)))


(define-change fire
  ;; forward
  forward-layout: '(id vertex vertex literal entity)
  forward-store: (lambda (push entity-id position dir kind actor)
                   (push-id push entity-id)
                   (push-vertex push position)
                   (push-vertex push dir)
                   (push-literal push kind)
                   (push-entity push actor))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity-id (pop-id pop))
                          (position (pop-vertex& pop))
                          (dir (pop-vertex& pop))
                          (kind (pop-literal pop))
                          (actor (pop-entity pop)))
                     ;; sejour quicky
                     (when actor
                       (apply-fire entity-id position dir kind actor))))
  ;; backward
  backward-layout: '(entity)
  backward-store: (lambda (push entity)
                    (push-entity push entity))
  backward-apply: (lambda (client-id pop)
                    (let* ((entity (pop-entity pop)))
                      ;; sejour quicky
                      (when entity
                        (apply-unfire entity)))))


;;;
;;;; Spawn
;;;


(definition protected (spawn-change pos lookat spawner)
  (let ((history (current-history)))
    (unless (and (get-paused? history) (not (stepping-history?)))
      (let ((class (random-element (list Spider Wolf Jumper))))
        (let ((creature (apply-spawn class #f pos lookat spawner)))
          (let ((entity-id (get-id creature)))
            (bidirectional-change 'spawn (current-seconds)
              (lambda (store push)
                )
              (lambda (store push)
                (store push class entity-id pos lookat spawner))
              (lambda (store push)
                (store push creature)))))))))


(definition protected (apply-spawn class id pos lookat spawner)
  (let ((world (current-world))
        (zone (current-zone)))
    (let ((creature (new class id: id position: pos lookat: lookat)))
      (set-behavior creature 'hostile)
      (set-spawner creature spawner)
      (add-element zone creature)
      creature)))


(definition protected (apply-despawn entity)
  (let ((world (current-world)))
    (remove-element world entity)))


(define-change spawn
  ;; forward
  forward-layout: '(class id vertex lookat @wait-this-lags entity)
  forward-store: (lambda (push class entity-id position lookat spawner)
                   (push-class push class)
                   (push-id push entity-id)
                   (push-vertex push position)
                   (push-lookat push lookat)
                   @wait-this-lags (push-entity push spawner))
  forward-apply: (lambda (client-id pop)
                   (let* ((class (pop-class pop))
                          (entity-id (pop-id pop))
                          (position (pop-vertex& pop))
                          (lookat (pop-lookat& pop))
                          @wait-this-lags (spawner (pop-entity pop)))
                     (apply-spawn class entity-id position lookat #f @wait-this-lags spawner)))
  ;; backward
  backward-layout: '(entity)
  backward-store: (lambda (push entity)
                    (push-entity push entity))
  backward-apply: (lambda (client-id pop)
                    (let* ((entity (pop-entity pop)))
                      ;; sejour quicky
                      (when entity
                        (apply-despawn entity)))))


;;;
;;;; Block
;;;


(define-change add-block
  ;; forward
  forward-layout: '(vertex integer integer)
  forward-store: (lambda (push position block-id data-id)
                   (push-vertex push position)
                   (push-integer push block-id)
                   (push-integer push data-id))
  forward-apply: (lambda (client-id pop)
                   (let* ((position (pop-vertex pop))
                          (block-id (pop-integer pop))
                          (data-id (pop-integer pop)))
                     (apply-add-block (current-universe) position block-id data-id)))
  ;; backward
  backward-layout: '(vertex integer integer)
  backward-store: (lambda (push position replaced-block-id replaced-data-id)
                    (push-vertex push position)
                    (push-integer push replaced-block-id)
                    (push-integer push replaced-data-id))
  backward-apply: (lambda (client-id pop)
                    (let* ((position (pop-vertex pop))
                           (replaced-block-id (pop-integer pop))
                           (replaced-data-id (pop-integer pop)))
                      (apply-restore-block (current-universe) position replaced-block-id replaced-data-id))))


(define-change delete-block
  ;; forward
  forward-layout: '(vertex)
  forward-store: (lambda (push position)
                   (push-vertex push position))
  forward-apply: (lambda (client-id pop)
                   (let* ((position (pop-vertex pop)))
                     (apply-delete-block (current-universe) position)))
  ;; backward
  backward-layout: '(vertex integer integer)
  backward-store: (lambda (push position block-id data-id)
                    (push-vertex push position)
                    (push-integer push block-id)
                    (push-integer push data-id))
  backward-apply: (lambda (client-id pop)
                    (let* ((position (pop-vertex pop))
                           (block-id (pop-integer pop))
                           (data-id (pop-integer pop)))
                      (apply-restore-block (current-universe) position block-id data-id))))


;;;
;;;; Entity
;;;


(define-change add-entity
  ;; forward
  forward-layout: '(class id literal vertex lookat vertex literal literal)
  forward-store: (lambda (push entity name position lookat scale)
                   (push-class push (class-of entity))
                   (push-id push (get-id entity))
                   (push-literal push name)
                   (push-vertex push position)
                   (push-lookat push lookat)
                   (push-vertex push scale)
                   (push-literal push (get-model entity))
                   (push-literal push (get-script-text entity)))
  forward-apply: (lambda (client-id pop)
                   (let* ((class (pop-class pop))
                          (entity-id (pop-id pop))
                          (name (pop-literal pop))
                          (position (pop-vertex pop))
                          (lookat (pop-lookat pop))
                          (scale (pop-vertex pop))
                          (model (pop-literal pop))
                          (script-text (pop-literal pop)))
                     (apply-add-entity (current-universe) class entity-id name position lookat scale model script-text)))
  ;; backward
  backward-layout: '(entity)
  backward-store: (lambda (push entity)
                    (push-entity push entity))
  backward-apply: (lambda (client-id pop)
                    (let* ((entity (pop-entity pop)))
                      ;; sejour quicky
                      (when entity
                        (apply-remove-entity (current-universe) entity)))))


(define-change remove-entity
  ;; forward
  forward-layout: '(entity)
  forward-store: (lambda (push entity)
                   (push-entity push entity))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop)))
                     ;; sejour quicky
                     (when entity
                       (apply-remove-entity (current-universe) entity))))
  ;; backward
  backward-layout: '(class id literal vertex lookat vertex literal literal)
  backward-store: (lambda (push entity)
                    (push-class push (class-of entity))
                    (push-id push (get-id entity))
                    (push-literal push (get-name entity))
                    (push-vertex push (get-position entity))
                    (push-lookat push (get-lookat entity))
                    (push-vertex push (get-scale entity))
                    (push-literal push (get-model entity))
                    (push-literal push (get-script-text entity)))
  backward-apply: (lambda (client-id pop)
                    (let* ((class (pop-class pop))
                           (entity-id (pop-id pop))
                           (name (pop-literal pop))
                           (position (pop-vertex pop))
                           (lookat (pop-lookat pop))
                           (scale (pop-vertex pop))
                           (model (pop-literal pop))
                           (script-text (pop-literal pop)))
                      (apply-add-entity (current-universe) class entity-id name position lookat scale model script-text))))


;;;
;;;; Action
;;;


(define-change action
  ;; forward
  forward-layout: '(vertex)
  forward-store: (lambda (push position)
                   (push-vertex push position))
  forward-apply: (lambda (client-id pop)
                   (let* ((position (pop-vertex pop)))
                     (apply-action (current-universe) position)))
  ;; backward
  backward-layout: '()
  backward-store: (lambda (push)
                    )
  backward-apply: (lambda (client-id pop)
                    ))


;;;
;;;; Interact
;;;


(define-change interact
  ;; forward
  forward-layout: '(entity)
  forward-store: (lambda (push obj)
                   (push-entity push obj))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop)))
                     ;; sejour quicky
                     (when entity
                       (apply-interact (current-universe) entity))))
  ;; backward
  backward-layout: '()
  backward-store: (lambda (push)
                    )
  backward-apply: (lambda (client-id pop)
                    ))


;;;
;;;; Double-Click
;;;


(define-change double-click
  ;; forward
  forward-layout: '(entity)
  forward-store: (lambda (push obj)
                   (push-entity push obj))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop)))
                     ;; sejour quicky
                     (when entity
                       (apply-double-click (current-universe) entity))))
  ;; backward
  backward-layout: '()
  backward-store: (lambda (push)
                    )
  backward-apply: (lambda (client-id pop)
                    ))


;;;
;;;; Script
;;;


(definition protected (edit-script-change time entity edit?)
  (unless (server?)
    (let ((history (current-history)))
      ;; quicky
      (unless (is? entity Asset-Entity)
        (unless (get-paused? history)
          (bidirectional-change 'edit-script time
            (lambda (store push)
              (store push))
            (lambda (store push)
              (store push entity edit?))
            (lambda (store push)
              (store push entity edit?))))))))


(define-change edit-script
  ;; forward
  forward-layout: '(entity boolean)
  forward-store: (lambda (push entity edit?)
                   (push-entity push entity)
                   (push-boolean push edit?))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop))
                          (edit? (pop-literal pop)))
                     ;; sejour quicky
                     (when entity
                       (edit-script client-id entity edit?))))
  ;; backward
  backward-layout: '(entity boolean)
  backward-store: (lambda (push entity edit?)
                    (push-entity push entity)
                    (push-boolean push edit?))
  backward-apply: (lambda (client-id pop)
                    (let* ((entity (pop-entity pop))
                           (edit? (pop-literal pop)))
                      ;; sejour quicky
                      (when entity
                        (edit-script client-id entity (not edit?))))))


(definition protected (update-script-change time entity range strings undo-range undo-strings run?)
  (unless (server?)
    (let ((history (current-history)))
      (unless (get-paused? history)
        (bidirectional-change 'update-script time
          (lambda (store push)
            (store push))
          (lambda (store push)
            (store push entity range strings run?))
          (lambda (store push)
            (store push entity undo-range undo-strings)))))))


(define-change update-script
  ;; forward
  forward-layout: '(entity literal literal boolean)
  forward-store: (lambda (push entity range strings run?)
                   (push-entity push entity)
                   (push-literal push range)
                   (push-literal push strings)
                   (push-boolean push run?))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop))
                          (range (pop-literal pop))
                          (strings (pop-literal pop))
                          (run? (pop-boolean pop)))
                     ;; sejour quicky
                     (when entity
                       (apply-update client-id entity range strings run?))))
  ;; backward
  backward-layout: '(entity literal literal)
  backward-store: (lambda (push entity range strings)
                    (push-entity push entity)
                    (push-literal push range)
                    (push-literal push strings))
  backward-apply: (lambda (client-id pop)
                    (let* ((entity (pop-entity pop))
                           (range (pop-literal pop))
                           (strings (pop-literal pop)))
                      ;; sejour quicky
                      (when entity
                        (let ((manager (find-script-manager entity)))
                          (when manager
                            (let ((text (locate manager '(script text))))
                              ;; collabo quick hack for tests
                              (when (nu=? (valid-range text range) range)
                                (replace text range strings)))))))))


(definition protected (run-script-change time entity)
  (unless (or (server?)
              (and (client?) (client-script? entity)))
    (let ((history (current-history)))
      (unless (get-paused? history)
        (bidirectional-change 'run-script time
          (lambda (store push)
            (store push))
          (lambda (store push)
            (store push entity))
          (lambda (store push)
            (store push)))))))


(define-change run-script
  ;; forward
  forward-layout: '(entity)
  forward-store: (lambda (push entity)
                   (push-entity push entity))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop)))
                     ;; sejour quicky
                     (when entity
                       (when (processing?)
                         (apply-run entity)))))
  ;; backward
  backward-layout: '()
  backward-store: (lambda (push)
                    )
  backward-apply: (lambda (client-id pop)
                    ))


(definition protected (state-script-change time entity state data)
  (let ((history (current-history)))
    (unless (get-paused? history)
      (bidirectional-change 'state-script time
        (lambda (store push)
          (store push))
        (lambda (store push)
          (store push entity state data))
        (lambda (store push)
          (store push))))))


(define-change state-script
  ;; forward
  forward-layout: '(entity literal literal)
  forward-store: (lambda (push entity state data)
                   (push-entity push entity)
                   (push-literal push state)
                   (push-literal push data))
  forward-apply: (lambda (client-id pop)
                   (let* ((entity (pop-entity pop))
                          (state (pop-literal pop))
                          (data (pop-literal pop)))
                     ;; sejour quicky
                     (when entity
                       (apply-state entity state data))))
  ;; backward
  backward-layout: '()
  backward-store: (lambda (push)
                    )
  backward-apply: (lambda (client-id pop)
                    ))


(definition protected edited-views
  (make-table test: eq?))


(class Server-Text-View extends Jazz-Text-View)


(definition (edit-script client-id entity edit?)
  (when (processing?)
    (let ((views (table-ref edited-views entity '())))
      (if edit?
          (let ((text (new Server-Text-View)))
            (if (null? views)
                (let ((script (get-script entity)))
                  (when script
                    (set-string-content text (get-text script))))
              (set-model text (get-model (cdar views))))
            (table-set! edited-views entity (cons (cons client-id text) views)))
        (let ((pair (assq client-id views)))
          (when pair
            (table-set! edited-views entity (remove! pair views))))))))


(definition protected inhibit-content-changes?
  (make-parameter #f))


(definition (apply-update client-id entity range strings run?)
  (let ((zone (current-zone)))
    (when (processing?)
      (let ((views (table-ref edited-views entity '())))
        (let ((pair (assq client-id views)))
          (when pair
            (let ((text (cdr pair)))
              ;; collabo quick hack for tests
              (when (nu=? (valid-range text range) range)
                (parameterize ((inhibit-content-changes? #t))
                  (replace text range strings))))))))
    (let ((manager (find-script-manager entity)))
      (if manager
          (let ((text (locate manager '(script text))))
            ;; collabo quick hack for tests
            (when (nu=? (valid-range text range) range)
              (parameterize ((inhibit-content-changes? #t))
                (replace (get-model text) range strings))
              (save-script manager)
              (when run?
                (when (processing?)
                  (let ((script (need-script zone entity)))
                    ;; sejour quicky
                    (when script
                      (evaluate-script (get-edited manager) script)))))))
        ;; collabo mega quick test
        (let ((text (new Server-Text-View))
              (script (need-script zone entity)))
          ;; sejour quicky
          (when script
            (when (get-text script)
              (set-string-content text (get-text script)))
            ;; collabo quick hack for tests
            (when (nu=? (valid-range text range) range)
              (parameterize ((inhibit-content-changes? #t))
                (replace text range strings)))
            (write-element zone script 'text (get-string-content text))
            (when run?
              (when (processing?)
                (evaluate-script entity script)))))))))


(definition (apply-run entity)
  (let ((script (get-script entity)))
    (when script
      (evaluate-script entity script evaluate-run?: #t))))


(definition (apply-state entity state data)
  (let ((script (get-script entity)))
    (when script
      (update-state script entity state data)))))
