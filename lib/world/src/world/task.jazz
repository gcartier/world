;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Tasks
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.task jazz


(import (jazz.debuggee)
        (jazz.event)
        (jazz.window)
        (world)
        (world.configure)
        (world.support))


;;;
;;;; Threads
;;;


(definition protected (setup-threads)
  ;; heartbeat
  (when heartbeat-interval
    (set-heartbeat-interval! heartbeat-interval))
  ;; event
  (thread-base-priority-set! (event-thread) event-priority)
  (when quantum-interval
    (thread-quantum-set! (event-thread) quantum-interval))
  ;; message
  (when (window?)
    (thread-base-priority-set! (message-thread) message-priority)
    (when quantum-interval
      (thread-quantum-set! (message-thread) quantum-interval))
    (message-period-set! message-period)
    (message-sleep-set! message-sleep)))


;;;
;;;; Tasks
;;;


(definition public current-task
  (make-parameter #f))


(definition public current-exit
  (make-parameter #f))


(definition package named-tasks
  (make-table test: eq?))


(definition public (register-task name procedure)
  (let ((task (new Task name procedure)))
    (table-set! named-tasks name task)
    task))


(definition public (find-task name)
  (table-ref named-tasks name #f))


(definition protected (start-task name/task (synchronous?: synchronous? #f))
  (let ((task (if (symbol? name/task) (find-task name/task) name/task)))
    (when task
      (start task synchronous?))))


(definition protected (stop-task name/task (needs-stop? #t))
  (let ((task (if (symbol? name/task) (find-task name/task) name/task)))
    (when (and task (eq? (get-state task) 'started))
      (stop task needs-stop?))))


(definition protected (halt-task name/task)
  (let ((task (if (symbol? name/task) (find-task name/task) name/task)))
    (when (and task (eq? (get-state task) 'started))
      (halt task))))


(definition protected (task-thread name)
  (get-thread (find-task name)))


(definition public (with-task task proc)
  (continuation-capture
    (lambda (exit)
      (ready-task task)
      (let (restart-loop)
        (with-current-loop
          (lambda ()
            (with-restart-catcher 'resume-loop (present-current-loop-restart)
              (lambda ()
                (parameterize ((current-task task)
                               (current-exit exit))
                  (proc exit))))))
        (restart-loop))))
  (stopped task))


(definition public (with-script-task task proc)
  (continuation-capture
    (lambda (exit)
      (parameterize ((current-task task)
                     (current-exit exit))
        (ready-task task)
        (proc exit))))
  (stopped task))


;;;
;;;; Task
;;;


;; quick hack for together init script and
;; i should definitely investigate just running
;; the script code in the current thread instead
(definition protected synchronous-task?
  (make-parameter #f))


(class Task extends Object
  
  
  (slot name      getter generate)
  (slot procedure getter generate)
  (slot ready     getter generate)
  (slot mutex     getter generate)
  (slot thread    getter generate)
  (slot state     getter generate)
  
  
  (method override (initialize self name procedure (priority: priority #f))
    (set! self.name name)
    (set! self.procedure procedure)
    (set! self.ready (make-mutex))
    (set! self.mutex (make-mutex))
    (set! self.thread (new-thread (lambda ()
                                    (procedure self))
                                  name))
    (when quantum-interval
      (thread-quantum-set! thread quantum-interval))
    (when priority
      (thread-base-priority-set! thread priority))
    (set! self.state 'initialized))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" name))))
  
  
  (method package (with-mutex self thunk)
    (mutex-lock! mutex)
    (prog1 (thunk)
      (mutex-unlock! mutex)))
  
  
  (method package (start self synchronous?)
    (mutex-lock! ready)
    (thread-start! thread)
    (when synchronous?
      (thread-join! thread))
    (set! state 'started)
    (mutex-lock! ready)
    (mutex-unlock! ready))
  
  
  (method package (ready-task self)
    (mutex-unlock! ready))
  
  
  (method package (stop self needs-stop?)
    (mutex-lock! mutex)
    (unless (eq? state 'stop)
      (when needs-stop?
        (task-stop thread))
      (set! state 'stopping)
      (if (eq? thread (current-thread))
          (mutex-unlock! mutex)
        (thread-base-priority-set! thread +infinity)
        (without-task-mutex
          (lambda ()
            (mutex-unlock! mutex)
            (thread-join! thread))))))
  
  
  (method package (halt self)
    (mutex-lock! mutex)
    (unless (eq? state 'stop)
      (task-stop thread)
      (set! state 'stopping)
      (mutex-unlock! mutex)))
  
  
  (method package (stopped self)
    (with-mutex self
      (lambda ()
        (set! state 'stopped))))
  
  
  (method package (stopping? self)
    (with-mutex self
      (lambda ()
        (or shutdown?
            (eq? state 'stopping)))))
  
  
  (method package (running? self)
    (and (not (stopping? self))
         (not sleeping?))))


;;;
;;;; Mutex
;;;


(definition public task-mutex
  (make-mutex 'task))


(definition public (with-task-mutex thunk)
  (when debug-deadlocks?
    (assert (neq? (mutex-owner task-mutex) (current-thread))))
  (unwind-protect
      (begin
        (mutex-lock! task-mutex)
        (thunk))
    (mutex-unlock! task-mutex)))


(definition public (with-maybe-task-mutex thunk)
  (if (task-mutex-owner?)
      (thunk)
    (with-task-mutex thunk)))


(definition public (without-task-mutex thunk)
  (unwind-protect
      (begin
        (mutex-unlock! task-mutex)
        (thunk))
    (mutex-lock! task-mutex)))


(definition public (assert-task-mutex)
  (let ((thread (current-thread))
        (owner (mutex-owner task-mutex)))
    (when (neq? owner thread)
      (error "Mutex task is owned by {a} instead of {a}" owner thread))))


(definition public (task-mutex-owner)
  (mutex-owner task-mutex))


(definition public (task-mutex-owner?)
  (eq? (mutex-owner task-mutex) (current-thread)))


;;;
;;;; Shutdown
;;;


(definition protected shutdown?
  #f)

(definition protected shutdown-mutex
  (make-mutex 'shutdown))


(definition protected (request-shutdown)
  (set! shutdown? #t))

(definition protected (reset-shutdown)
  (set! shutdown? #f))


(definition protected (shutting-down thunk)
  (when debug-deadlocks?
    (assert (neq? (mutex-owner shutdown-mutex) (current-thread))))
  (dynamic-wind
    (lambda ()
      (mutex-lock! shutdown-mutex)
      (set! shutdown? #t))
    thunk
    (lambda ()
      (set! shutdown? #f)
      (mutex-unlock! shutdown-mutex))))


;;;
;;;; Control
;;;


(proclaim (warn optimizations))


;; hack around sleep having a huge drift on windows
(definition protected (adjust-wait wait)
  (cond-expand
    (windows (/ wait 2.))
    (else wait)))


;; rate = 60
;; period = 1/60
;; 0                      1/90             1/60
;; |-----------------------|----------------|-----------------------------
;;     period - duration       period / 3            duration / 3
(definition public (throttle-wait name period <fl> duration <fl>)
  (let ((objective (- period duration))
        (period/3 (/ period 3.)))
    ;; waiting at least period / 3 ensures a 2 / 3 | 1 / 3 ratio
    ;; between running and sleeping so we never starve other tasks
    (if (>= objective period/3)
        objective
      period/3
      ;; this needs more thinking through
      ;; maybe we should only duration throttle the render task!?
      (let ((duration/3 (/ duration 10.))) ;; start with a very conservative factor to make sure we do not
                                           ;; impact fast machines. until a more sophisticated algorithm...
        ;; waiting at least duration / 3 is essential on slow machines
        ;; where waiting period / 3 would be too small to be of consequence
        (if (>= period/3 duration/3)
            (begin
              (when debug-throttle?
                (terminal 'throttle name 'period duration))
              period/3)
          (when debug-throttle?
            (terminal 'throttle name 'duration duration))
          duration/3)))))


(definition public (message-sleep period <fl> duration <fl>)
  (let ((wait (throttle-wait 'message period duration)))
    (sleep (adjust-wait wait))))


(definition public (task-sleep task <Task> period <fl> duration <fl> exit)
  (let ((wait (throttle-wait (get-name task) period duration)))
    (let ((proc (thread-receive (adjust-wait wait) #f)))
      (when proc
        (proc exit)))))


(definition public (task-wakeup thread proc)
  (thread-send thread
    proc))


(definition public (task-stop thread)
  (thread-send thread
    (lambda (exit)
      ;; trying to debug a crash as this should not happen
      (assert exit)
      (continuation-return exit #f))))


(proclaim (not warn optimizations))


;;;
;;;; Debug
;;;


(definition protected (snapshot-tasks)
  (define (snapshot name task)
    (let ((thread (get-thread task)))
      (list name (present-thread-state (thread-state thread)))))
  
  (define (snapshot-tasks)
    (let ((queue (new-queue)))
      (iterate-table named-tasks
        (lambda (name task)
          (enqueue queue (snapshot name task))))
      (queue-list queue)))
  
  (list
    (list 'task-mutex (mutex-owner task-mutex))
    (list 'generate-mutex (mutex-owner world.generation:generate-mutex))
    (list 'collide-mutex (mutex-owner world.collision:collide-mutex))
    (snapshot-tasks))))
