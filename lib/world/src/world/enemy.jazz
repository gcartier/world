;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Enemy
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.enemy jazz


(import (world)
        (world.actor)
        (world.autoload)
        (world.block)
        (world.commands)
        (world.dye)
        (world.dyes)
        (world.geometry)
        (world.serialization)
        (world.syntax (phase syntax)))


;;;
;;;; Enemy
;;;


(definition move-forward-commands
  (let ((commands (new Commands)))
    (move-forward~ commands)
    commands))


(class Enemy extends Actor
  
  
  (properties (;; component
               name visible?
               ;; element
               position lookat scale radiuses radius color hit-behavior user-data
               ;; entity
               model animate? animation
               ;; actor
               velocity fall-velocity life global-cooldown spell adjustments rapid-fire-acceleration rapid-fire-duration rapid-fire-cooldown last-missile max-missiles missiles-count history-color jumping? jump-ready? jump-grace? jump-time ground? rotation orientation sprint sprint-duration animation-active animation-start animation-end animation-next)
    (slot speed <fl>     initialize 0.    accessors generate)
    (slot tick? <bool>   initialize #t    accessors generate))
  
  
  (method (move-enemy time elapse exit (gravity?: gravity? #t) (response: response 'slide) (ignore-entities?: ignore-entities? #t) (ignore-players?: ignore-players? #f))
    (tick-actor move-forward-commands time elapse exit gravity? response ignore-entities?: ignore-entities? ignore-players?: ignore-players?))
  
  
  (method override (hit collisions exit)
    @unify-with-die
    (let ((world (current-world))
          (me (current-me)))
      (when (memq? me collisions key: get-element~)
        (lose~ world me)
        (continuation-return exit))))
  
  
  (method protected virtual (rebound-x)
    (vertex-x-set! velocity (- (vertex-x velocity)))
    (update-lookat velocity))
  
  
  (method protected virtual (rebound-z)
    (vertex-z-set! velocity (- (vertex-z velocity)))
    (update-lookat velocity))
  
  
  (method protected virtual (walk-speed)
    .01)
  
  
  (method protected virtual (run-speed)
    .02)
  
  
  (method override (move-animation)
    (let ((effective-speed (* speed sprint)))
      (cond ((= effective-speed 0.) "Idle")
            ((<= effective-speed (walk-speed)) "Walk")
            (else "Run"))))
  
  
  (method (update-lookat dir)
    (let ((world (current-world)))
      (init-lookat (vertex-normalize& dir)
                   (get-world-up~ world)))))


;;;
;;;; Creature
;;;


(definition attack-distance <fl>
  .1)


(class Creature extends Enemy
  
  
  (properties (;; component
               name visible?
               ;; element
               position lookat scale radiuses radius color hit-behavior user-data
               ;; entity
               model animate? animation
               ;; actor
               velocity fall-velocity life global-cooldown spell adjustments rapid-fire-acceleration rapid-fire-duration rapid-fire-cooldown last-missile max-missiles missiles-count history-color jumping? jump-ready? jump-grace? jump-time ground? rotation orientation sprint sprint-duration animation-active animation-start animation-end animation-next
               ;; enemy
               speed tick?)
    (slot path-duration initialize #f accessors generate)
    (slot anim-duration initialize #f accessors generate)
    (slot aggro-radius  initialize #f accessors generate)
    (slot aggro-target  initialize #f accessors generate))
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (prepare-pathing))
  
  
  (method override (tick commands time elapse exit)
    (let ((me (current-me)))
      (when (and tick? (neq? self (get-controlled~ me)))
        (or (players-aggro)
            (if (<= path-duration 0.)
                (path-creature)
              (decrease! path-duration elapse)))
        (if (and aggro-target (< (vertex-distance position (get-position~ aggro-target)) (+ (get-radius) (get-radius~ aggro-target) attack-distance)))
            (attack-player aggro-target)
          (move-enemy time elapse exit)))))
  
  
  (method override (hit collisions exit)
    (for-each (lambda (collision)
                (let ((element (get-element~ collision)))
                  (cond ((is? element Block)
                         (case axis
                           ((x) (rebound-x))
                           ((z) (rebound-z)))))))
              collisions))
  
  
  (method override (ground-speed)
    speed)
  
  
  (method public virtual (aggro-sound)
    #f)
  
  
  (method public virtual (attack-sound)
    #f)
  
  
  (method (players-aggro)
    (when aggro-radius
      (let ((zone (current-zone)))
        (let (iter (scan (get-players~ zone)))
          (if (null? scan)
              (begin
                (set! aggro-target #f)
                #f)
            (let ((player (car scan)))
              (let ((dist (vertex-distance position (get-position~ player))))
                (if (< dist aggro-radius)
                    (begin
                      (aggro-player player)
                      #t)
                  (iter (cdr scan))))))))))
  
  
  (method (aggro-player player)
    (let ((world (current-world)))
      (when (neq? player aggro-target)
        (set! aggro-target player)
        (play-sound-file~ world (aggro-sound)))
      (update-lookat (vertex-normalize& (vertex-& (get-position~ player) position)))))
  
  
  (method (attack-player player)
    (let ((world (current-world)))
      (if (or (not anim-duration) (<= anim-duration 0))
          (begin
            (play-sound-file~ world (attack-sound))
            (wound~ player)
            (set! anim-duration 100))
        (decrease! anim-duration))))
  
  
  (method (prepare-pathing)
    (set! path-duration (random-between 1. 10.)))
  
  
  (method protected virtual (path-creature)
    (cond ((< (random-real) .333)
           (set! sprint 0.))
          (else
           (set! sprint 1.)
           (update-lookat (vertex (random-in 1.) 0. (random-in 1.)))))
    (prepare-pathing)))


;;;
;;;; Wolf
;;;


(definition wolf-speed <fl>
  .04)


(class Wolf extends Creature
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-scaling .4)
    (set-color (dye .157 .157 .157 1.))
    (set-model 'Wolf)
    (set! speed wolf-speed)
    (set! aggro-radius 40.))
  
  
  (method override (element-radiuses)
    (vertex .4 .4 .4))
  
  
  (method override (element-radius)
    .4)
  
  
  (method override (target-radius)
    (* (average-scaling) 1.8))
  
  
  (method override (aggro-sound)
    "sound/wolf/aggro")
  
  (method override (attack-sound)
    "sound/wolf/attack")
  
  (method override (wound-sound)
    "sound/wolf/wound")
  
  (method override (wound-critical-sound)
    "sound/wolf/woundCritical")
  
  (method override (death-sound)
    "sound/wolf/death"))


;;;
;;;; Spider
;;;


(definition spider-speed <fl>
  .02)


(class Spider extends Creature
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-scaling .4)
    (set-color (dye 0. .235 0. 1.))
    (set-model 'Spider)
    (set! speed spider-speed)
    (set! aggro-radius 25.))
  
  
  (method override (element-radiuses)
    (vertex .4 .4 .4))
  
  
  (method override (element-radius)
    .4)
  
  
  (method override (target-radius)
    (* (average-scaling) 1.8))
  
  
  (method override (aggro-sound)
    "sound/spider/aggro")
  
  (method override (attack-sound)
    "sound/spider/attack")
  
  (method override (wound-sound)
    "sound/spider/wound")
  
  (method override (wound-critical-sound)
    "sound/spider/woundCritical")
  
  (method override (death-sound)
    "sound/spider/death"))


;;;
;;;; Turtle
;;;


@wait-minecraft-doesnt-have-turf
(definition turtle-speed <fl>
  .005)


@wait-minecraft-doesnt-have-turf
(class Turtle extends Creature
  
  
  (slot stomp-time <fl> initialize 0. accessors generate)
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-color green-dye)
    (set-model 'Turtle)
    (set! speed turtle-speed))
  
  
  (method override (element-radiuses)
    (vertex .2 .2 .2))
  
  
  (method override (element-radius)
    .2)
  
  
  (method override (target-radius)
    .3)
  
  
  (method override (path-creature)
    (let ((zone (current-zone)))
      (if (getf (get-properties~ zone) victor?:)
          (vertex-x-set! velocity turtle-speed)
        (nextmethod))))
  
  
  (method override (tick commands time elapse exit)
    (let ((zone (current-zone)))
      (when tick?
        (if stomp-time
            (when (>= time stomp-time)
              (set! stomp-time #f))
          (move-enemy time elapse exit)
          (when (and (getf (get-properties~ zone) victor?:)
                     (or (< (get-x) -35.)
                         (> (get-x) -27.)))
            (rebound-x))))))
  
  
  (method override (stomp)
    (set! stomp-time (+ (current-seconds) 3.))
    #t))


;;;
;;;; MOB
;;;


(class MOB extends Creature
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set! speed .01)))


;;;
;;;; Cow
;;;


(class Cow extends MOB
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set! speed (walk-speed))
    (set! orientation 'horizontal))
  
  
  (method override (walk-speed)
    .01)
  
  
  (method override (run-speed)
    .02))


;;;
;;;; Camel
;;;


(class Camel extends MOB
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set! speed (walk-speed))
    (set! orientation 'horizontal))
  
  
  (method override (walk-speed)
    .01)
  
  
  (method override (run-speed)
    .02))


;;;
;;;; DireWolf
;;;


(class DireWolf extends MOB
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set! speed (run-speed))
    (set! orientation 'horizontal))
  
  
  (method override (walk-speed)
    .03)
  
  
  (method override (run-speed)
    .06))


;;;
;;;; Missile
;;;


(class Missile extends Enemy
  
  (properties (;; component
               name visible?
               ;; element
               position lookat scale radiuses radius color hit-behavior user-data
               ;; entity
               model animate? animation
               ;; actor
               velocity fall-velocity life global-cooldown spell adjustments rapid-fire-acceleration rapid-fire-duration rapid-fire-cooldown last-missile max-missiles missiles-count history-color jumping? jump-ready? jump-grace? jump-time ground? rotation orientation sprint sprint-duration animation-active animation-start animation-end animation-next
               ;; enemy
               speed tick?)
    (property actor <object> initialize #f accessors generate)
    (property kind  <object> initialize #f getter generate setter explicit)
    
    
    (slot alive    <fl>     initialize 0.    accessors generate)
    (slot response <object> initialize 'stop accessors generate))
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-color red-dye))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (setup-model))
  
  
  (method public (set-kind k)
    (set! kind k)
    (setup-model))
  
  
  (method (setup-model)
    (when kind
      (set-model (case kind
                   ((bullet) 'Bullet)
                   ((bomb) 'Bomb)))))
  
  
  (method override (element-radiuses)
    (vertex .05 .05 .05))
  
  
  (method override (element-radius)
    .05)
  
  
  (method override (get-friction)
    #f)
  
  
  (method (missile-speed) <fl>
    1.)
  
  (method (missile-life) <fl>
    2.5)
  
  
  (method override (tick commands time elapse exit)
    (move-enemy time elapse exit gravity?: #f response: response ignore-entities?: #f ignore-players?: #f)
    (set! alive (+ alive elapse))
    (when (> alive (missile-life))
      (remove-self)))
  
  
  (method override (hit collisions exit)
    (nextmethod collisions exit)
    (let ((world (current-world)))
      (let (iter (scan collisions))
        (when (not-null? scan)
          (let ((collision (car scan)))
            (case (missile-hit~ world self collision)
              ((stop))
              ((remove)
               (remove-self)
               (continuation-return exit))
              ((slide)
               (set! response 'slide))
              (else
               (iter (cdr scan)))))))))
  
  
  (method override (hit-floor exit)
    (remove-self)
    (continuation-return exit))
  
  
  (method (remove-self)
    (let ((world (current-world)))
      (remove-element~ world self)
      ;; quick hack for serialization not preserving actor
      (when actor
        (decrease-missiles~ actor))))
  
  
  (method override (rebound-x)
    )
  
  (method override (rebound-z)
    )))
