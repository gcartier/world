;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Scenes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.scene jazz


(import (jazz.component)
        (jazz.syntax (phase syntax))
        (world.task))


(class Scene extends Component
  
  
  (slot tick-procedures   <object> initialize '() getter generate)
  (slot animated-entities <object> initialize '() getter generate)
  
  
  ;;;
  ;;;; Tick
  ;;;
  
  
  (method package virtual (tick-scene self elapse)
    (let ((duration 0.))
      (when (not-null? tick-procedures)
        (for-each (lambda (proc)
                    (with-task-mutex
                      (lambda ()
                        (when (memq? proc tick-procedures)
                          (let ((start (current-monotonic)))
                            (proc elapse)
                            (increase! duration (- (current-monotonic) start)))))))
                  tick-procedures))
      duration))
  
  
  (method package (register-tick self proc)
    (set! tick-procedures (cons proc tick-procedures)))
  
  
  (method package (unregister-tick self proc)
    (set! tick-procedures (remove! proc tick-procedures)))
  
  
  ;;;
  ;;;; Animate
  ;;;
  
  
  (method (animate self elapse)
    (site animate
      (let ((duration 0.))
        (when (not-null? animated-entities)
          (for-each (lambda (entity)
                      (let ((start (current-monotonic)))
                        (animate entity elapse)
                        (increase! duration (- (current-monotonic) start))))
                    animated-entities))
        duration)))
  
  
  (method package (register-animated self entity)
    (set! animated-entities (cons entity animated-entities)))
  
  
  (method package (unregister-animated self entity)
    (set! animated-entities (remove! entity animated-entities)))))
