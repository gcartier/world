;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Instance
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.instance jazz


(import (jazz.component)
        (jazz.io)
        (jazz.network)
        (jazz.presence)
        (world)
        (world.autoload)
        (world.context)
        (world.master)
        (world.settings)
        (world.syntax (phase syntax))
        (world.work))


;;;
;;;; Instance
;;;


(class Instance extends Component
  
  
  (property template initialize #f accessors generate)
  (property rank     initialize #f accessors generate))


;;;
;;;; Processor
;;;


(definition protected processors
  (make-table test: equal?))


(definition protected processor-rank
  0)

(definition protected (next-processor-rank!)
  (prog1 processor-rank
    (increase! processor-rank)))


(definition protected (spawn-processor server-host server-service zone)
  (define (spawn)
    (let ((debug? (processor-debug?))
          (host server-host)
          (service (+ server-service (next-processor-rank!) 1)))
      (spawn-slave "processor" (get-processor-filename)
        (lambda (master slave cookie)
          (let ((server (load-object (get-local-register) 'world.server.remote 'world-remote-server))
                (server-path (subpath {Directory Settings "servers"} (get-directory (server-tier)))))
            (processor-setup slave server server-path zone)))
        (lambda (master slave cookie)
          slave)
        arguments: `("-processor" "true" "-host" ,(host->string host) "-service" ,(port->string service))
        debug?: debug?
        wait?: #t)))
  
  (or (table-ref processors zone #f)
      (let ((processor (spawn)))
        (table-set! processors zone processor)
        processor)))


(definition protected (kill-processors)
  (iterate-table-safe processors
    (lambda (zone processor)
      (kill-processor zone))))


(definition protected (kill-processor zone)
  (let ((processor (table-ref processors zone)))
    (processor-quit processor)
    (table-clear processors zone)))


(definition (processor-debug?)
  (world-setting 'world.processor-debug? #t))


;;;
;;;; Exit
;;;


(add-exit-job! kill-processors))
