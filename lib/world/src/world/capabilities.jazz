;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Capabilities
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.capabilities jazz


(import (jazz.io)
        (jazz.opengl.glew)
        (jazz.settings)
        (jazz.snapshot)
        (world))


(definition public caps-version
  1.0)


(definition public (estimate-computer-speed)
  (declare (proper-tail-calls))
  (let ((before (current-monotonic)))
    (let (loop (c 10000))
      (when (>= c 0)
        (loop (- c 1))))
    (let ((after (current-monotonic)))
      (let ((elapse (- after before)))
        ;; the division is so my macbook pro gets around 100
        (/ 1. elapse 3300.)))))


(definition public (estimate-garbage-collect)
  (gc)
  (let ((before (current-monotonic)))
    (gc)
    (let ((after (current-monotonic)))
      (- after before))))


(definition protected (estimate-speed-profile speed)
  (cond ((>= speed 90.) 'high)
        ((>= speed 50.) 'medium)
        (else 'low)))


(definition public (estimate-base-profile speed)
  (case (estimate-speed-profile speed)
    ((high) 'medium)
    ((medium) 'low)
    ((low) 'poor)))


(definition public (describe-capabilities output)
  (let ((computer-speed (estimate-computer-speed))
        (garbage-collect (estimate-garbage-collect)))
    (let ((base-profile (estimate-base-profile computer-speed)))
      (format output "{a} 1.0{%}" 'caps.version)
      (format output "{a} {r precision: 3}{%}" 'caps.computer-speed computer-speed)
      (format output "{a} {r precision: 3}{%}" 'caps.garbage-collect garbage-collect)
      (format output "{a} {a}{%}" 'caps.profile.base base-profile))))


(definition public (create-capabilities file)
  (create-directories file)
  (call-with-output-file (path-settings file)
    (lambda (output)
      (describe-capabilities output))))


;;;
;;;; OpenGL
;;;


(definition public (check-extension ext)
  (format "{a} {a} supported"
          ext
          (if (glewIsSupported ext)
              "is"
            "not")))


(definition public (update-opengl-capabilities caps-settings)
  (unless (get-setting caps-settings 'caps.opengl.version)
    (set-setting caps-settings 'caps.opengl.version (glGetString GL_VERSION))
    (set-setting caps-settings 'caps.opengl.support.texture-array? (glewIsSupported "GL_EXT_texture_array"))
    (set-setting caps-settings 'caps.opengl.support.framebuffer-object? (glewIsSupported "GL_EXT_framebuffer_object"))
    (set-setting caps-settings 'caps.opengl.support.framebuffer-blit? (glewIsSupported "GL_EXT_framebuffer_blit"))
    (set-setting caps-settings 'caps.opengl.support.framebuffer-multisample? (glewIsSupported "GL_EXT_framebuffer_multisample"))
    (set-setting caps-settings 'caps.opengl.support.multisample? (glewIsSupported "GL_ARB_multisample"))
    (set-setting caps-settings 'caps.opengl.support.debug-output? (glewIsSupported "GL_ARB_debug_output"))
    (set-setting caps-settings 'caps.opengl.max-texture-size (glGetInteger* GL_MAX_TEXTURE_SIZE))
    (set-setting caps-settings 'caps.opengl.max-3d-texture-size (glGetInteger* GL_MAX_3D_TEXTURE_SIZE))
    (set-setting caps-settings 'caps.opengl.max-texture-image-units (glGetInteger* GL_MAX_TEXTURE_IMAGE_UNITS))
    (set-setting caps-settings 'caps.opengl.max-vertex-uniform-components (glGetInteger* GL_MAX_VERTEX_UNIFORM_COMPONENTS))
    (set-setting caps-settings 'caps.opengl.max-fragment-uniform-components (glGetInteger* GL_MAX_FRAGMENT_UNIFORM_COMPONENTS))
    (save-content caps-settings)))


;;;
;;;; Update
;;;


(definition public (update-capabilities caps-settings)
  (update-opengl-capabilities caps-settings))


;;;
;;;; Snapshot
;;;


(definition public (add-capabilities-to-snapshot caps-settings)
  (add-snapshot-settings caps-settings)))
