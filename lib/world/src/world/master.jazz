;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Remote Master Implementation
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.master jazz


(import (jazz.debuggee)
        (jazz.io)
        (jazz.network)
        (jazz.presence))


;;;
;;;; Slave
;;;


(definition *slave-cookie*
  0)

(definition *slave-procedures*
  (make-table test: eqv?))


(definition public (register-slave-ready-cookie)
  (prog1 *slave-cookie*
    (increase! *slave-cookie*)))


(definition public (register-slave-procedures setup ready)
  (let ((cookie (register-slave-ready-cookie))
        (mutex (make-mutex 'ready)))
    (mutex-lock! mutex)
    (table-set! *slave-procedures* cookie (list mutex setup ready))
    cookie))


(definition public (registered-slave-ready cookie)
  (table-ref *slave-procedures* cookie))


(definition public (wait-slave-ready cookie)
  (bind (mutex) (registered-slave-ready cookie)
    (mutex-lock! mutex)
    (let ((result (table-ref *slave-procedures* cookie)))
      (table-clear *slave-procedures* cookie)
      (mutex-unlock! mutex)
      result)))


(definition public (invoke-slave-setup-procedure master slave cookie)
  (bind (mutex setup ready) (registered-slave-ready cookie)
    (setup master slave cookie)))


(definition public (invoke-slave-ready-procedure master slave cookie)
  (bind (mutex setup ready) (registered-slave-ready cookie)
    (thread-yield!) ; why is this necessary?
    (table-set! *slave-procedures* cookie (ready master slave cookie))
    (mutex-unlock! mutex)))


(definition public (spawn-slave name filename setup ready (purpose: purpose #f) (arguments: arguments #f) (debug?: debug? #f) (wait?: wait? #f))
  (define (determine-slave-path)
    (let ((filename (add-extension filename (executable-extension kernel-platform))))
      (or (find-kernel-file filename)
          (error "Unable to find {a} executable" name))))
  
  (start-remote-presence purpose: purpose)
  (let ((slave-path (determine-slave-path))
        (listener (get-remote-listener purpose: purpose))
        (cookie (register-slave-procedures setup ready)))
    (let ((arguments `("-:darD" @maybe-use-in-production!? "-:daqQ-"
                       ;; it is not completly correct at the moment to always
                       ;; debug the slave because of the evaluate always hack
                       ,@(let ((arg (debugger-argument)))
                           (if (and debug? arg)
                               (list "-debugger" arg)
                             '()))
                       ,@(or arguments '())
                       "-master" ,(encode-host/port (or @creates-problems-with-systemd-service (listening-host listener) "localhost") (listening-port listener))
                       "-master-cookie" ,(->string cookie))))
      (let ((process-port (open-process
                            (list
                              path: slave-path
                              arguments: arguments
                              stdin-redirection: #f
                              stdout-redirection: #f
                              stderr-redirection: #f
                              show-console: #f))))
        (if (not wait?)
            process-port
          (values process-port (wait-slave-ready cookie)))))))


;;;
;;;; Master
;;;


(class World-Remote-Master extends Object
  
  
  (method public (slave-setup self slave cookie)
    (invoke-slave-setup-procedure self slave cookie))
  
  
  (method public (slave-ready self slave cookie)
    (invoke-slave-ready-procedure self slave cookie))))
