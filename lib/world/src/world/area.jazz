;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Areas
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.area jazz


(import (world)
        (world.autoload)
        (world.geometry)
        (world.index)
        (world.polygon)
        (world.retain)
        (world.syntax (phase syntax)))


;;;
;;;; Area
;;;


(class Area extends Object
  
  
  (slot refcount           <fx>         initialize 0   getter generate)
  (slot index              <vector>                    getter generate)
  (slot header             <table>                     getter generate)
  (slot entities           <list>       initialize '() accessors generate)
  (slot polygons           <f64vector+> initialize #f  accessors generate)
  (slot polygons-elements  <vector+>    initialize #f  accessors generate)
  (slot physical-uptodate? <bool>       initialize #f  accessors generate)
  (slot physical-revision  <fx>         initialize -1  accessors generate)
  (slot visit              <table>                     getter generate)
  
  
  (method override (initialize index)
    (set! index~self index)
    (set! header~self (make-table test: equal?))
    (set! visit~self (make-table test: equal?)))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}, {a}, {a}{?: with {a} entities~}" (index-x index) (index-y index) (index-z index) (not-null? entities) (length entities)))))
  
  
  (method (retain retainer)
    (increase! refcount)
    (when debug-retain?
      (add-retainer self retainer)
      (validate-retainers self refcount)))
  
  
  (method (release retainer)
    (decrease! refcount)
    (when debug-retain?
      (remove-retainer self retainer)
      (validate-retainers self refcount))
    (when (<= refcount 0)
      (free)))
  
  
  (method (free)
    (let ((zone (current-zone)))
      (unlink-area~ zone index)))
  
  
  (method (visit-ref index)
    (table-ref visit index -1))
  
  
  (method (visit-set! index value)
    (table-set! visit index value))
  
  
  (method (get-position&) <vertex>
    (vertex& (* (cast <fl> (index-x index)) 16.)
             (* (cast <fl> (index-y index)) 16.)
             (* (cast <fl> (index-z index)) 16.)))
  
  
  (method (update-physical)
    (let ((zone (current-zone)))
      (let ((generator-revision (get-revision~ (get-generator~ zone))))
        (when (or (not physical-uptodate?)
                  (< physical-revision generator-revision))
          (generate-physical~ zone self physical-uptodate?))
        (set! physical-uptodate? #t)
        (set! physical-revision generator-revision))))
  
  
  (method (local-index i <fx> j <fx> k <fx>) <fx>
    (+ (* j 64) (* k 8) i))
  
  
  (method (index->coordinates index)
    (let ((i (modulo index 8))
          (k (modulo (quotient index 8) 8))
          (j (modulo (quotient (quotient index 8) 8) 8)))
      (vector i j k)))
  
  
  (method (position-index pos <vertex>) <fx>
    (let ((zone (current-zone)))
      (let ((size (get-area-size~ zone))
            (ratio (get-sector/area-ratio~ zone)))
        (local-index (between-interval 0 (fxfloor (/ (vertex-x pos) size)) ratio)
                     (between-interval 0 (fxfloor (/ (vertex-y pos) size)) ratio)
                     (between-interval 0 (fxfloor (/ (vertex-z pos) size)) ratio)))))
  
  
  (method (subarea-index i <fx> j <fx> k <fx>) <fx>
    (local-index (modulo i 8)
                 (modulo j 8)
                 (modulo k 8)))
  
  
  (method (iterate-entities proc)
    (for-each proc entities))
  
  
  (method (iterate-polygons env index proc)
    (let ((polygons (table-ref header index #f)))
      (when polygons
        (loop (for i from 0 below (fxround/ (f64vector-length polygons) polygon-float-size))
              (let ((offset (* i polygon-float-size)))
                (proc env
                      (and polygons-elements (vector-ref polygons-elements i))
                      polygons
                      offset))))))
  
  
  (method (add-polygons element index lst matrix)
    (define (add-at vect offset)
      (for-each (lambda (poly)
                  (polygon-move!~ poly vect offset matrix)
                  (increase! offset polygon-float-size))
                lst))
    
    (let ((count (length lst)))
      (let ((add (* count polygon-float-size)))
        (let ((vect (make-f64vector add)))
          (add-at vect 0)
          (table-set! header index vect)))))
  
  
  (method (clear-polygons)
    (set! header (make-table test: equal?))
    (set! polygons #f)
    (set! polygons-elements #f)))


;;;
;;;; Area Old
;;;


@old^^^
(class Area-Old extends Object
  
  
  (slot index             <vector>                    getter generate)
  (slot sector            <Sector>                    getter generate)
  (slot polygons          <f64vector+> initialize #f  accessors generate)
  (slot polygons-elements <vector+>    initialize #f  accessors generate)
  (slot actors            <list>       initialize '() accessors generate)
  (slot entities          <list>       initialize '() accessors generate)
  (slot gadgets           <list>       initialize '() accessors generate)
  (slot visit             <fx>         initialize -1  accessors generate)
  
  
  (method override (initialize index sector)
    (set! index~self index)
    (set! sector~self sector))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}, {a}, {a}" (index-x index) (index-y index) (index-z index))
        @$$$-buggy
        (let ((polygons (if (not polygons) 0 (fxround/ (vector-length polygons) polygon-float-size)))
              (actors (length actors))
              (gadgets (length gadgets)))
          (format output
                  "{a}{a}{a}{a}"
                  (format "{a}, {a}, {a}{a}" (vector-ref index 0) (vector-ref index 1) (vector-ref index 2) (if (or (/= polygons 0) (/= actors 0)) " with " ""))
                  (if (= polygons 0) "" (format "{a} polygon{a}" polygons (format-plural polygons)))
                  (if (= actors 0) "" (format "{a}{a} actor{a}" (if (= polygons 0) "" ", ") actors (format-plural actors)))
                  (if (= gadgets 0) "" (format "{a}{a} gadget{a}" (if (and (= polygons 0) (= actors 0)) "" ", ") gadgets (format-plural gadgets))))))))
  
  
  (proclaim (warn optimizations))
  
  
  (method (iterate-polygons env proc)
    (declare (proper-tail-calls))
    (declare (optimize-dead-local-variables))
    (declare (inline))
    (declare (inlining-limit 1000))
    (when polygons
      (loop (for i from 0 below (fxround/ (f64vector-length polygons) polygon-float-size))
            (let ((offset (* i polygon-float-size)))
              (proc env
                    (and polygons-elements (vector-ref polygons-elements i))
                    polygons
                    offset)))))


  (proclaim (not warn optimizations))
  
  
  (method (add-polygons element lst matrix)
    (define (add-at vect offset)
      (for-each (lambda (poly)
                  (polygon-move!~ poly vect offset matrix)
                  (increase! offset polygon-float-size))
                lst))
    
    (let ((count (length lst)))
      (let ((add (* count polygon-float-size)))
        (cond ((not polygons)
               (let ((vect (make-f64vector add)))
                 (add-at vect 0)
                 (set! polygons vect))
               (when element
                 (set! polygons-elements (make-vector count element))))
              (else
               (let ((actual (f64vector-length polygons)))
                 (let ((vect (make-f64vector (+ actual add))))
                   (subf64vector-move! polygons 0 actual vect 0)
                   (add-at vect actual)
                   (set! polygons vect))
                 (when (or polygons-elements element)
                   (let ((actual-count (fxround/ actual polygon-float-size)))
                     (let ((vect (make-vector (+ actual-count count) element)))
                       (if (not polygons-elements)
                           (subvector-fill! vect 0 actual-count #f)
                         (subvector-move! polygons-elements 0 actual-count vect 0))
                       (set! polygons-elements vect))))))))))
  
  
  (method (remove-polygons element)
    (when polygons
      (let ((count 0))
        (iterate-polygons #f
          (lambda (env elem vect offset)
            (when (neq? elem element)
              (increase! count))))
        (let ((vect (make-f64vector (* count polygon-float-size)))
              (vect-offset 0)
              (vect-elements (and polygons-elements (make-vector count)))
              (vect-elements-offset 0))
          (iterate-polygons #f
            (lambda (env elem polygons offset)
              (when (neq? elem element)
                (subf64vector-move! polygons offset (+ offset polygon-float-size) vect vect-offset)
                (increase! vect-offset polygon-float-size)
                (when vect-elements
                  (vector-set! vect-elements vect-elements-offset elem)
                  (increase! vect-elements-offset)))))
          (set! polygons vect)
          (set! polygons-elements vect-elements)))))
  
  
  (method (clear-polygons)
    (set! polygons #f)
    (set! polygons-elements #f))
  
  
  (method (iterate-entities proc)
    (for-each proc actors)
    (for-each proc entities)
    (for-each proc gadgets)))


;;;
;;;; Area Retainer
;;;


(definition public (with-area-retainer retainer proc)
  (let ((queue #f))
    (define (retain area)
      (retain~ area retainer)
      (when (not queue)
        (set! queue (new-queue)))
      (enqueue queue area))
    
    (define (release)
      (when queue
        (for-each (lambda (area)
                    (release~ area retainer))
                  (queue-list queue))))
    
    (unwind-protect (proc retain)
      (release))))


;;;
;;;; Area Cube
;;;


(proclaim (warn optimizations))


(class Area-Cube extends Object
  
  
  (slot cube-radius     <fx>     getter generate)
  (slot cube-diameter   <fx>     getter generate)
  (slot cube-diameter^2 <fx>     getter generate)
  (slot cube-diameter^3 <fx>     getter generate)
  (slot cube-areas      <vector> getter generate)
  (slot cube-indexes    <vector> getter generate)
  (slot cube-index      <vector> getter generate)
  (slot cube-inited?    <bool>   getter generate)
  
  
  (method override (initialize radius)
    (set! cube-radius radius)
    (set! cube-diameter (+ cube-radius 1 cube-radius))
    (set! cube-diameter^2 (expt cube-diameter 2))
    (set! cube-diameter^3 (expt cube-diameter 3))
    (set! cube-areas (make-vector cube-diameter^3 #f))
    (set! cube-indexes (make-vector cube-diameter^3 #f))
    (set! cube-index (make-index))
    (set! cube-inited? #f))
  
  
  (method (areas-index i <fx> j <fx> k <fx>) <fx>
    (+ (* j cube-diameter^2) (* k cube-diameter) i))
  
  
  (method (update-cube pos <vertex>) <void>
    (declare (proper-tail-calls))
    (declare (optimize-dead-local-variables))
    (declare (inline))
    (declare (inlining-limit 1000))
    (let ((zone (current-zone)))
      (let ((index (position-area-index&~ zone pos)))
        (unless (and cube-inited? (equal? index cube-index))
          ;; update areas
          (let ((areas cube-areas)
                (indexes cube-indexes)
                (a (- (index-x index) cube-radius))
                (b (- (index-y index) cube-radius))
                (c (- (index-z index) cube-radius)))
            (define (area-new index i j k)
              (let ((area (indexed-area~ zone i j k)))
                (retain~ area self)
                (vector-set! areas index area)
                (vector-set! indexes index (subarea-index~ area i j k))))
            
            (define (area-free area)
              (release~ area self))
            
            (if (not cube-inited?)
                (begin
                  (loop (for i from 0 below cube-diameter)
                        (loop (for j from 0 below cube-diameter)
                              (loop (for k from 0 below cube-diameter)
                                    (area-new (areas-index i j k)
                                              (+ a i)
                                              (+ b j)
                                              (+ c k)))))
                  (set! cube-inited? #t))
              (let ((last (- cube-diameter 1))
                    (dx (- (index-x index) (index-x cube-index)))
                    (dy (- (index-y index) (index-y cube-index)))
                    (dz (- (index-z index) (index-z cube-index))))
                (define (by delta <fx>)
                  (if (>= delta 0)
                      1
                    -1))
                
                (loop (for x start 0 end last by (by dx))
                      (let ((fx (+ x dx))
                            (tx x))
                        (loop (for y start 0 end last by (by dy))
                              (let ((fy (+ y dy))
                                    (ty y))
                                (loop (for z start 0 end last by (by dz))
                                      (let ((fz (+ z dz))
                                            (tz z))
                                        (let ((fi (and (between? fx 0 last)
                                                       (between? fy 0 last)
                                                       (between? fz 0 last)
                                                       (areas-index fx fy fz)))
                                              (ti (areas-index tx ty tz)))
                                          (let ((area (vector-ref areas ti)))
                                            (when area
                                              ;; free
                                              (area-free area)))
                                          (if fi
                                              (begin
                                                ;; move
                                                (vector-set! areas ti (vector-ref areas fi))
                                                (vector-set! areas fi #f)
                                                (vector-set! indexes ti (vector-ref indexes fi))
                                                (vector-set! indexes fi #f))
                                            ;; new
                                            (area-new ti (+ a tx) (+ b ty) (+ c tz)))))))))))))
          ;; update index
          (index-copy! cube-index index)))))
  
  
  (method (iterate-neighbor-areas pos distance <fx> proc)
    (declare (proper-tail-calls))
    (declare (optimize-dead-local-variables))
    (declare (inline))
    (declare (inlining-limit 1000))
    (let ((zone (current-zone)))
      (let ((index (position-area-index&~ zone pos)))
        (let ((di (- (index-x index) (index-x cube-index)))
              (dj (- (index-y index) (index-y cube-index)))
              (dk (- (index-z index) (index-z cube-index)))
              (lower (- cube-radius distance))
              (upper (+ cube-radius distance))
              (last (- cube-diameter 1)))
          (loop (for i from lower to upper)
                (loop (for j from lower to upper)
                      (loop (for k from lower to upper)
                            (let ((a (+ i di))
                                  (b (+ j dj))
                                  (c (+ k dk)))
                              (let ((in? (and (between? a 0 last)
                                              (between? b 0 last)
                                              (between? c 0 last)))
                                    (idx (areas-index a b c)))
                                (let ((area (if in?
                                                (vector-ref cube-areas idx)
                                              (indexed-area~ zone a b c))))
                                  (let ((index (if in?
                                                   (vector-ref cube-indexes idx)
                                                 (subarea-index~ area a b c))))
                                    (update-physical~ area)
                                    (proc area index (not in?)))))))))))))))
