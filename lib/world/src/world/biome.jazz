;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Biomes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.biome jazz


(import (jazz.geometry)
        (jazz.graphic)
        (world)
        (world.autoload)
        (world.foreign)
        (world.geometry)
        (world.syntax (phase syntax)))


;;;
;;;; Biome
;;;


(class Biome extends Object
  
  
  (slot name             getter generate)
  (slot title            getter generate)
  (slot id               getter generate)
  (slot top              accessors generate)
  (slot fill             accessors generate)
  (slot color            getter generate)
  (slot base        <fl> getter generate)
  (slot variation   <fl> getter generate)
  (slot temperature <fl> getter generate)
  (slot rain        <fl> getter generate)
  (slot trees       <fx> getter generate)
  
  
  (method override (initialize name title id top fill color base variation temperature rain trees)
    (set! name~self name)
    (set! title~self title)
    (set! id~self id)
    (set! top~self top)
    (set! fill~self fill)
    (set! color~self color)
    (set! base~self base)
    (set! variation~self variation)
    (set! temperature~self temperature)
    (set! rain~self rain)
    (set! trees~self trees))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" name))))
  
  
  (method virtual (replace-blocks x z rand block-ref block-set! blockstate-set!)
    (let ((replace-amount -1))
      (let (iter (y 255))
        (if (<= y (randomInt rand 5))
            (block-set! x y z 7)
          (let ((block-id <fx> (block-ref x y z)))
            (if (= block-id 0)
                (set! replace-amount -1)
              (when (= block-id 1)
                (if (= replace-amount -1)
                    (begin
                      (blockstate-set! x y z top)
                      (set! replace-amount (randomIntBetween rand 3 5)))
                  (when (> replace-amount 0)
                    (blockstate-set! x y z fill)
                    (decrease! replace-amount)))))))
        (when (> y 0)
          (iter (- y 1)))))))


(definition protected biome-names <table>
  (make-table test: eq?))

(definition protected biome-ids <vector>
  (make-vector 256 #f))


(definition protected (register-biome name class title id top fill color (base: base 0.1) (variation: variation 0.2) (temperature: temperature 0.5) (rain: rain 0.5) (trees: trees 0))
  (bind (red green blue) color
    (let ((color (new Color red: red green: green blue: blue)))
      (let ((biome (new class name title id top fill color base variation temperature rain trees)))
        (table-set! biome-names name biome)
        (vector-set! biome-ids id biome)
        (unspecified)))))


(definition public (name->biome name) <Biome>
  (table-ref biome-names name #f))

(definition public (id->biome id <fx>) <Biome>
  (vector-ref biome-ids id))


;;;
;;;; Biome classes
;;;


(class BiomeMesa extends Biome
  
  
  (slot bands               <vector+> initialize #f    getter generate)
  (slot white-stained-clay  <fl>      initialize 2704. getter explicit)
  (slot orange-stained-clay <fl>      initialize 2705. getter explicit)
  (slot yellow-stained-clay <fl>      initialize 2708. getter explicit)
  (slot silver-stained-clay <fl>      initialize 2712. getter explicit)
  (slot brown-stained-clay  <fl>      initialize 2716. getter explicit)
  (slot red-stained-clay    <fl>      initialize 2718. getter explicit)
  (slot hardened-clay       <fl>      initialize 2924. getter explicit)
  
  
  (method (generate-bands)
    (let ((rand (newRandom (worldSeed))))
      (set! bands (make-vector 64 hardened-clay))
      (let ((i2 (+ (randomInt rand 4) 2))
            (j2 (+ (randomInt rand 4) 2))
            (l2 (+ (randomInt rand 4) 2))
            (k3 (+ (randomInt rand 3) 3))
            (j4 0))
        (loop (for l1 <fx> from 0 below 64)
              (increase! l1 (+ (randomInt rand 5) 1))
              (when (< l1 64)
                (vector-set! bands l1 orange-stained-clay)))
        (loop (for i <fx> from 0 below i2)
              (let ((j (+ (randomInt rand 3) 1))
                    (k (randomInt rand 64)))
                (loop (for l <fx> init 0 test (and (< (+ k l) 64) (< l j)) iter (+ l 1))
                      (vector-set! bands (+ k l) yellow-stained-clay))))
        (loop (for k2 <fx> from 0 below j2)
              (let ((i3 (+ (randomInt rand 3) 2))
                    (l3 (randomInt rand 64)))
                (loop (for i1 <fx> init 0 test (and (< (+ l3 i1) 64) (< i1 i3)) iter (+ i1 1))
                      (vector-set! bands (+ l3 i1) brown-stained-clay))))
        (loop (for j3 <fx> from 0 below l2)
              (let ((i4 (+ (randomInt rand 3) 1))
                    (k4 (randomInt rand 64)))
                (loop (for j1 <fx> init 0 test (and (< (+ k4 j1) 64) (< j1 i4)) iter (+ j1 1))
                      (vector-set! bands (+ k4 j1) red-stained-clay))))
        (loop (for l4 from 0 below k3)
              (do
                    (increase! j4 (+ (randomInt rand 16) 4))
                  (when (< j4 64)
                    (vector-set! bands j4 white-stained-clay)
                    (when (and (> j4 1) (randomBool rand))
                      (vector-set! bands (- j4 1) silver-stained-clay))
                    (when (and (< j4 63) (randomBool rand))
                      (vector-set! bands (+ j4 1) silver-stained-clay))))))))
  
  
  (method override (replace-blocks x z rand block-ref block-set! blockstate-set!)
    (unless bands
      (generate-bands))
    (let ((flag <bool> (randomBool rand))
          (l <fx> -1)
          (flag1 <bool> #f))
      (loop (for y from 255 downto 0)
            (if (< y (randomInt rand 5))
                (block-set! x y z 7)
              (let ((iblockstate1 (block-ref x y z)))
                (if (= iblockstate1 0)
                    (set! l -1)
                  (when (= iblockstate1 1)
                    (if (= l -1) ;; i. e. there is air above this stone block
                        (begin
                          (set! flag1 #f)
                          (set! l (+ 3 (max 0 (- y 63))))
                          (cond ((< y 62)
                                 (blockstate-set! x y z orange-stained-clay))
                                ((<= y 69)
                                 (blockstate-set! x y z top)
                                 (set! flag1 #t))
                                (else
                                 (let ((iblockstate2 orange-stained-clay))
                                   (when (and (>= y 64) (<= y 127))
                                     (set! iblockstate2 (if #f hardened-clay (vector-ref bands (modulo y 64)))))
                                   (blockstate-set! x y z iblockstate2)))))
                      (when (> l 0)
                        (decrease! l)
                        (blockstate-set! x y z (if flag1 orange-stained-clay (vector-ref bands (modulo y 64))))))))))))))


(class BiomeTaiga extends Biome
  
  
  (method override (replace-blocks x z rand block-ref block-set! blockstate-set!)
    (let ((replace-amount -1))
      (let (iter (y 255))
        (if (<= y (randomInt rand 5))
            (block-set! x y z 7)
          (let ((block-id <fx> (block-ref x y z)))
            (if (= block-id 0)
                (set! replace-amount -1)
              (when (= block-id 1)
                (if (= replace-amount -1)
                    (begin
                      (when (< y 255)
                        (blockstate-set! x (+ y 1) z 1327.))
                      (blockstate-set! x y z top)
                      (set! replace-amount (randomIntBetween rand 3 5)))
                  (when (> replace-amount 0)
                    (blockstate-set! x y z fill)
                    (decrease! replace-amount)))))))
        (when (> y 0)
          (iter (- y 1)))))))

(class BiomeOcean extends Biome
  )

(class BiomePlains extends Biome
  )

(class BiomeDesert extends Biome
  )

(class BiomeHills extends Biome
  )

(class BiomeForest extends Biome
  )

(class BiomeSwamp extends Biome
  )

(class BiomeRiver extends Biome
  )

(class BiomeHell extends Biome
  )

(class BiomeEnd extends Biome
  )

(class BiomeSnow extends Biome
  )

(class BiomeMushroomIsland extends Biome
  )

(class BiomeJungle extends Biome
  )

(class BiomeBeach extends Biome
  )

(class BiomeStoneBeach extends Biome
  )

(class BiomeSavanna extends Biome
  )

(class BiomeForestMutated extends Biome
  )

(class BiomeSavannaMutated extends Biome
  )

@not-necessary-for-now
(class BiomeVoid extends Biome
  )


;;;
;;;; Biomes
;;;


(define-biome Ocean                BiomeOcean          "Ocean"                        0  Gravel       Gravel       (0 0 112) base: -1.0 variation: 0.1)
(define-biome Plains               BiomePlains         "Plains"                       1  Grass        Dirt         (141 179 96) base: 0.125 variation: 0.05 temperature: 0.8 rain: 0.4 trees: 5)
(define-biome Desert               BiomeDesert         "Desert"                       2  Sand         Sand         (250 148 24) base: 0.125 variation: 0.05 temperature: 2.0 rain: 0.0 trees: 1)
(define-biome ExtremeHills         BiomeHills          "Extreme Hills"                3  Grass        Dirt         (96 96 96) base: 1.0 variation: 0.5 temperature: 0.2 rain: 0.3 trees: 1)
(define-biome Forest               BiomeForest         "Forest"                       4  Grass        Dirt         (5 102 33) temperature: 0.7 rain: 0.8 trees: 5)
(define-biome Taiga                BiomeTaiga          "Taiga"                        5  Grass        Dirt         (11 102 89) base: 0.2 variation: 0.2 temperature: 0.25 rain: 0.8 trees: 5)
(define-biome Swampland            BiomeSwamp          "Swampland"                    6  Grass        Dirt         (7 249 178) base: -0.2 variation: 0.1 temperature: 0.8 rain: 0.9 trees: 4)
(define-biome River                BiomeRiver          "River"                        7  Sand         Sand         (0 0 255) base: -0.5 variation: 0.0)
(define-biome Hell                 BiomeHell           "Hell"                         8  Grass        Dirt         (255 0 0) temperature: 2.0 rain: 0.0)
(define-biome Sky                  BiomeEnd            "End"                          9  Grass        Dirt         (128 128 255))
(define-biome FrozenOcean          BiomeOcean          "Frozen Ocean"                10  Grass        Dirt         (144 144 160) base: -1.0 variation: 0.1 temperature: 0.0 rain: 0.5)
(define-biome FrozenRiver          BiomeRiver          "Frozen River"                11  Grass        Dirt         (160 160 255) base: -0.5 variation: 0.0 temperature: 0.0 rain: 0.5)
(define-biome IcePlains            BiomeSnow           "Ice Plains"                  12  Snow         Dirt         (255 255 255) base: 0.125 variation: 0.05 temperature: 0.0 rain: 0.5)
(define-biome IceMountains         BiomeSnow           "Ice Mountains"               13  Snow         Dirt         (160 160 160) base: 0.45 variation: 0.3 temperature: 0.0 rain: 0.5)
(define-biome MushroomIsland       BiomeMushroomIsland "Mushroom Island"             14  Mycelium     Dirt         (255 0 255) base: 0.2 variation: 0.3 temperature: 0.9 rain: 1.0)
(define-biome MushroomIslandShore  BiomeMushroomIsland "Mushroom Island Shore"       15  Mycelium     Dirt         (160 0 255) base: 0.0 variation: 0.025 temperature: 0.9 rain: 1.0)
(define-biome Beach                BiomeBeach          "Beach"                       16  Sand         Sand         (250 222 85) base: 0.0 variation: 0.025 temperature: 0.8 rain: 0.4)
(define-biome DesertHills          BiomeDesert         "Desert Hills"                17  Sand         Sand         (210 95 18) base: 0.45 variation: 0.3 temperature: 2.0 rain: 0.0)
(define-biome ForestHills          BiomeForest         "Forest Hills"                18  Grass        Dirt         (34 85 28) base: 0.45 variation: 0.3 temperature: 0.7 rain: 0.8)
(define-biome TaigaHills           BiomeTaiga          "Taiga Hills"                 19  Grass        Dirt         (22 57 51) temperature: 0.25 rain: 0.8 base: 0.45 variation: 0.3)
(define-biome ExtremeHillsEdge     BiomeHills          "Extreme Hills Edge"          20  Grass        Dirt         (114 120 154) base: 0.8 variation: 0.3 temperature: 0.2 rain: 0.3)
(define-biome Jungle               BiomeJungle         "Jungle"                      21  Grass        Dirt         (83 123 9) temperature: 0.95 rain: 0.9 trees: 4)
(define-biome JungleHills          BiomeJungle         "Jungle Hills"                22  Grass        Dirt         (44 66 5) base: 0.45 variation: 0.3 temperature: 0.95 rain: 0.9)
(define-biome JungleEdge           BiomeJungle         "Jungle Edge"                 23  Grass        Dirt         (98 139 23) temperature: 0.95 rain: 0.8)
(define-biome DeepOcean            BiomeOcean          "Deep Ocean"                  24  Grass        Dirt         (0 0 48) base: -1.8 variation: 0.1)
(define-biome StoneBeach           BiomeStoneBeach     "Stone Beach"                 25  Stone        Stone        (162 162 132) base: 0.1 variation: 0.8 temperature: 0.2 rain: 0.3)
(define-biome ColdBeach            BiomeBeach          "Cold Beach"                  26  Sand         Sand         (250 240 192) base: 0.0 variation: 0.025 temperature: 0.05 rain: 0.3)
(define-biome BirchForest          BiomeForest         "Birch Forest"                27  Grass        Dirt         (48 116 68) temperature: 0.6 rain: 0.6)
(define-biome BirchForestHills     BiomeForest         "Birch Forest Hills"          28  Grass        Dirt         (31 95 50) base: 0.45 variation: 0.3 temperature: 0.6 rain: 0.6)
(define-biome RoofedForest         BiomeForest         "Roofed Forest"               29  Grass        Dirt         (64 81 26) temperature: 0.7 rain: 0.8)
(define-biome ColdTaiga            BiomeTaiga          "Cold Taiga"                  30  Grass        Dirt         (49 85 74) base: 0.2 variation: 0.2 temperature: -0.5 rain: 0.4)
(define-biome ColdTaigaHills       BiomeTaiga          "Cold Taiga Hills"            31  Grass        Dirt         (36 63 54) base: 0.45 variation: 0.3 temperature: -0.5 rain: 0.4)
(define-biome MegaTaiga            BiomeTaiga          "Mega Taiga"                  32  Grass        Dirt         (89 102 81) temperature: 0.3 rain: 0.8 base: 0.2 variation: 0.2)
(define-biome MegaTaigaHills       BiomeTaiga          "Mega Taiga Hills"            33  Grass        Dirt         (69 79 62) base: 0.45 variation: 0.3 temperature: 0.3 rain: 0.8)
(define-biome ExtremeHillsPlus     BiomeHills          "Extreme Hills+"              34  Grass        Dirt         (80 112 80) base: 1.0 variation: 0.5 temperature: 0.2 rain: 0.3)
(define-biome Savanna              BiomeSavanna        "Savanna"                     35  Grass        Dirt         (189 178 95) base: 0.125 variation: 0.05 temperature: 1.2 rain: 0.0)
(define-biome SavannaPlateau       BiomeSavanna        "Savanna Plateau"             36  Grass        Dirt         (167 157 100) base: 1.5 variation: 0.025 temperature: 1.0 rain: 0.0)
(define-biome Mesa                 BiomeMesa           "Mesa"                        37  RedSand      OrangeStainedClay (217 69 21) temperature: 2.0 rain: 0.0 trees: 1)
(define-biome MesaPlateauF         BiomeMesa           "Mesa Plateau F"              38  HardenedClay HardenedClay (176 151 101) base: 1.5 variation: 0.025 temperature: 2.0 rain: 0.0)
(define-biome MesaPlateau          BiomeMesa           "Mesa Plateau"                39  HardenedClay HardenedClay (202 140 101) base: 1.5 variation: 0.025 temperature: 2.0 rain: 0.0)
@w(define-biome Void               BiomeVoid           "Void"                       127  Grass        Dirt         (0 0 0))
(define-biome SunflowerPlains      BiomePlains         "Sunflower Plains"           129  Grass        Dirt         (141 179 96) base: 0.125 variation: 0.05 temperature: 0.8 rain: 0.4)
(define-biome DesertM              BiomeDesert         "Desert M"                   130  Sand         Sand         (250 148 24) base: 0.225 variation: 0.25 temperature: 2.0 rain: 0.0)
(define-biome ExtremeHillsM        BiomeHills          "Extreme Hills M"            131  Grass        Dirt         (96 96 96) base: 1.0 variation: 0.5 temperature: 0.2 rain: 0.3)
(define-biome FlowerForest         BiomeForest         "Flower Forest"              132  Grass        Dirt         (5 102 33) variation: 0.4 temperature: 0.7 rain: 0.8)
(define-biome TaigaM               BiomeTaiga          "Taiga M"                    133  Grass        Dirt         (11 102 89) base: 0.3 variation: 0.4 temperature: 0.25 rain: 0.8)
(define-biome SwamplandM           BiomeSwamp          "Swampland M"                134  Grass        Dirt         (7 249 178) base: -0.1 variation: 0.3 temperature: 0.8 rain: 0.9)
(define-biome IcePlainsSpikes      BiomeSnow           "Ice Plains Spikes"          140  Snow         Dirt         (140 180 180) base: 0.425 variation: 0.45 temperature: 0.0 rain: 0.5)
(define-biome JungleM              BiomeJungle         "Jungle M"                   149  Grass        Dirt         (83 123 9) base: 0.2 variation: 0.4 temperature: 0.95 rain: 0.9)
(define-biome JungleEdgeM          BiomeJungle         "Jungle Edge M"              151  Grass        Dirt         (98 139 23) base: 0.2 variation: 0.4 temperature: 0.95 rain: 0.8)
(define-biome BirchForestM         BiomeForestMutated  "Birch Forest M"             155  Grass        Dirt         (48 116 68) base: 0.2 variation: 0.4 temperature: 0.6 rain: 0.6)
(define-biome BirchForestHillsM    BiomeForestMutated  "Birch Forest Hills M"       156  Grass        Dirt         (31 95 50) base: 0.55 variation: 0.5 temperature: 0.6 rain: 0.6)
(define-biome RoofedForestM        BiomeForest         "Roofed Forest M"            157  Grass        Dirt         (64 81 26) base: 0.2 variation: 0.4 temperature: 0.7 rain: 0.8)
(define-biome ColdTaigaM           BiomeTaiga          "Cold Taiga M"               158  Grass        Dirt         (49 85 74) base: 0.3 variation: 0.4 temperature: -0.5 rain: 0.4)
(define-biome MegaSpruceTaiga      BiomeTaiga          "Mega Spruce Taiga"          160  Grass        Dirt         (89 102 81) base: 0.2 variation: 0.2 temperature: 0.25 rain: 0.8)
(define-biome MegaSpurceTaigaHills BiomeTaiga          "Mega Spruce Taiga (Hills)"  161  Grass        Dirt         (69 79 62) base: 0.2 variation: 0.2 temperature: 0.25 rain: 0.8)
(define-biome ExtremeHillsPlusM    BiomeHills          "Extreme Hills+ M"           162  Grass        Dirt         (80 112 80) base: 1.0 variation: 0.5 temperature: 0.2 rain: 0.3)
(define-biome SavannaM             BiomeSavannaMutated "Savanna M"                  163  Grass        Dirt         (189 178 95) base: 0.3625 variation: 1.225 temperature: 1.1 rain: 0.0)
(define-biome SavannaPlateauM      BiomeSavannaMutated "Savanna Plateau M"          164  Grass        Dirt         (167 157 100) base: 1.05 variation: 1.2125001 temperature: 1.0 rain: 0.0)
(define-biome MesaBryce            BiomeMesa           "Mesa (Bryce)"               165  HardenedClay HardenedClay (217 69 21) temperature: 2.0 rain: 0.0)
(define-biome MesaPlateauFM        BiomeMesa           "Mesa Plateau F M"           166  HardenedClay HardenedClay (176 151 101) base: 0.45 variation: 0.3 temperature: 2.0 rain: 0.0)
(define-biome MesaPlateauM         BiomeMesa           "Mesa Plateau M"             167  HardenedClay HardenedClay (202 140 101) base: 0.45 variation: 0.3 temperature: 2.0 rain: 0.0)


;;;
;;;; Primary
;;;


(definition public Primary-Biomes
  (vector Ocean Plains Desert ExtremeHills Forest Taiga Swampland Jungle Savanna Mesa)))