;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Player
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.player jazz


(import (jazz.graphic)
        (jazz.syntax (phase syntax))
        (world)
        (world.actor)
        (world.area)
        (world.audio)
        (world.autoload)
        (world.camera)
        (world.configure)
        (world.dye)
        (world.generation.block)
        (world.geometry)
        (world.models)
        (world.settings)
        (world.syntax (phase syntax))
        (world.task))


(definition protected floor-deceleration <fl>
  .25)

(definition protected air-deceleration <fl>
  .05)


(definition protected (default-avatar)
  (let ((world (current-world)))
    (define (try path)
      (and (find-model~ world path error?: #f)
           path))
    
    (if (world-setting 'world.orb-avatar? #f)
        'Orb
      (or (try (if young-child?
                   "character/clockworkgnome"
                 "character/bloodelffemale"))
          'Orb))))


(class Player extends Actor
  
  
  (method meta override (package entity)
    (error "Players should not be packaged"))
  
  
  (method meta override (unpackage info)
    (error "Players should not be unpackaged"))
  
  
  (property me?           <bool>   initialize #t  accessors generate)
  (property avatar        <object> initialize #f  getter generate setter explicit)
  
  (slot aggroed-creatures <list>   initialize '() accessors generate)
  (slot eating            <fl+>    initialize #f  accessors generate)
  (slot eating-ticks      <fx+>    initialize #f  accessors generate)
  (slot controlled        <object> initialize #f  accessors generate)
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-color (dye .678 .047 .902 1.))
    (update-model))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (set! motion 'run))
  
  
  (method override (setup-id)
    )
  
  
  (method override (setup-area-cube)
    (unless (and (client?) (not me?))
      (set! area-cube (new Area-Cube 7))))
  
  
  (method override (update-area-cube)
    (assert area-cube
      (nextmethod)))
  
  
  (method override (simulated?)
    (not me?))
  
  
  (method override (task-name)
    'bot)
  
  
  (method (set-avatar avat)
    (set! avatar avat)
    (update-model))
  
  
  (method (update-model)
    (let ((world (current-world))
          (zone (current-zone)))
      (let ((new-model (if avatar
                           (if (dye? avatar)
                               (begin
                                 (set-color avatar)
                                 'Orb)
                             avatar)
                         (default-avatar))))
        (when (neq? new-model model)
          (free-morphing)
          (set-model new-model)
          ;; reset defaults
          (set-scaling 1.)
          (set-animate? #f)
          (when (string? new-model)
            (metadata-install)
            (when (window?)
              (setup-morphing)
              (set-animate? #t))
            (set-orientation 'vertical))))))
  
  
  (method (player-camera)
    (find-type Camera))
  
  
  (method override (effective-pumps)
    (get-pumps~ (cache-model)))
  
  
  (method override (walk-speed)
    (/ (run-speed) 3.))
  
  
  (method override (run-speed)
    8.)
  
  
  (method override (admin-me?)
    (and (admin?)
         me?))
  
  
  (method (unaggro)
    (for-each (lambda (creature)
                (set-aggro-target~ creature #f))
              aggroed-creatures)
    (set! aggroed-creatures '()))
  
  
  (method (verify-water)
    (when (get-water?)
      (exit-water))
    (let ((block (block-at-center)))
      (when (and block (get-liquid-cube?~ (id->block block)))
        (enter-water (memq? block '(10 11))))))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a} {a}" name position))))
  
  
  (method override (is-player?)
    #t)
  
  
  ;;;
  ;;;; History
  ;;;
  
  
  (method override (history-draw-trail?)
    #t)
  
  
  ;;;
  ;;;; Actions
  ;;;
  
  
  (method override (wound inflictor factor (amount #f))
    ;; until client is 100% smooth
    (unless (and (not (local?)) (is? inflictor Player))
      (let ((world (current-world)))
        (let ((critical? (> (random-real) .8)))
          (damage (if amount
                      amount
                    (attack-amount~ inflictor factor (wound-amount critical?)))
                  (if amount #f critical?))))))
  
  
  (method override (damage amount critical?)
    (when (server?)
      (damage-change self amount critical?))
    (nextmethod amount critical?)
    (let ((interface (current-interface+)))
      (when (and interface me?)
        (let ((pane (child~ interface 'combat)))
          (set-visible?~ pane #t)
          (invalidate-view~ pane))
        (let ((pane (child~ interface 'character)))
          (when (get-visible?~ pane)
            (view-health~ pane))
          (damage-armor~ pane critical?))))
    (stop-eating))
  
  
  (method override (attack kind)
    (let ((interface (current-interface+)))
      (when (and interface me?)
        (let ((pane (child~ interface 'character)))
          (case kind
            ((bullet) (damage-weapon~ pane))
            ((arrow) (damage-bow~ pane))))))
    (stop-eating))
  
  
  (method override (wound-amount critical?)
    (/ (nextmethod critical?) armor-factor))
  
  
  (method override (die)
    (let ((interface (current-interface+))
          (history (current-history))
          (world (current-world))
          (zone (current-zone))
          (tier (current-tier)))
      (when (and me? (not (get-paused?~ history)))
        (sleep .5))
      (play-3d-sound-if (death-sound) position)
      (display-message~ world (format "R.I.P. {a}" (or name (anonymous~ tier))) color: {Color Orange})
      (unaggro)
      (set-life 100.)
      (set-alive? #t)
      (set-lava? #f)
      (set-water? #f)
      (when (and interface me?)
        (invalidate-view~ (child~ interface 'combat)))
      (resurrect-player~ zone self)
      (update-compass~ world)
      (when me?
        (eye-behind-player~ world)
        (follow-player~ world))))
  
  
  (method override (resurrect)
    (nextmethod)
    (update-compass~ (current-world)))
  
  
  (method (stop-eating)
    (set! eating #f)
    (set! eating-ticks #f))
  
  
  (method (control actor)
    (let ((world (current-world)))
      (set! controlled actor)
      (eye-behind-actor~ world actor)
      (follow-actor~ world actor)))
  
  
  (method (relinquish-control)
    (let ((world (current-world)))
      (set! controlled #f)
      (eye-behind-player~ world)
      (follow-player~ world)))
  
  
  (method (effective-controlled)
    (or controlled self))
  
  
  (method override (tick-slice)
    .04)
  
  
  (method override (tick commands time elapse)
    (unless (= elapse 0.)
      (tick-actor~ (effective-controlled) commands time elapse #t 'slide)))
  
  
  (method override (hit collisions)
    )
  
  
  (method (move-backward commands)
    (move-backward~ commands))
  
  
  (method (move-forward commands)
    (move-forward~ commands))
  
  
  (method (sprint-forward commands)
    (sprint-forward~ commands))
  
  
  (method (sprint-backward commands)
    (sprint-backward~ commands))
  
  
  (method (blink-forward commands)
    (blink-forward~ commands))
  
  
  (method (blink-backward commands)
    (blink-backward~ commands))
  
  
  (method (move-left commands)
    (move-left~ commands))
  
  
  (method (move-right commands)
    (move-right~ commands))
  
  
  (method (move-up commands)
    (move-up~ commands))
  
  
  (method (move-down commands)
    (move-down~ commands))
  
  
  (method (rotate-left commands)
    (rotate-left~ commands))
  
  
  (method (rotate-right commands)
    (rotate-right~ commands))
  
  
  (method (space commands)
    (jump commands))
  
  
  (method (jump commands)
    (jump~ commands))))
