;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Player
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.player jazz


(import (jazz.graphic)
        (world)
        (world.actor)
        (world.audio)
        (world.autoload)
        (world.dye)
        (world.geometry)
        (world.model)
        (world.settings)
        (world.syntax (phase syntax))
        (world.tag))


(definition protected floor-deceleration <fl>
  .25)

(definition protected air-deceleration <fl>
  .05)


(class Player extends Actor
  
  
  (properties (;; component
               name visible?
               ;; element
               position lookat scale radiuses radius color user-data
               ;; entity
               model animate? animation
               ;; actor
               velocity fall-velocity life alive? global-cooldown spell adjustments rapid-fire-acceleration rapid-fire-duration rapid-fire-cooldown last-missile max-missiles missiles-count history-color jumping? jump-ready? jump-grace? jump-time ground? water? swim-timer water-jump? rotation orientation sprint sprint-duration animation-active)
    
    (slot aggroed-enemies <list> initialize '() accessors generate)
    (slot eating          <fl+>  initialize #f  accessors generate)
    (slot eating-ticks    <fx+>  initialize #f  accessors generate)
    
    :custom-marshalling)
  
  
  (method meta override (marshall-object obj)
    (let ((size (marshall-size)))
      (let ((content (make-vector (+ size 2))))
        (marshall-to obj content)
        (let ((morphing (get-morphing~ obj)))
          (let ((animation (and morphing (get-animation~ morphing)))
                (transition (and morphing (get-transition~ morphing))))
            (vector-set! content size (and animation (list (get-name~ animation)
                                                           (get-current-frame~ morphing))))
            (vector-set! content (+ size 1) (and transition (list (get-name~ (get-animation~ transition))
                                                                  (get-transition-frame~ morphing)
                                                                  (get-transition-elapsed~ morphing)
                                                                  (get-transition-duration~ morphing))))))
        (serialize-object (class-of obj) content))))
  
  
  (method meta override (unmarshall-object content)
    (let ((obj (new self))
          (size (marshall-size)))
      (unmarshall-from obj content)
      (let ((animation (vector-ref content size)))
        (when animation
          (bind (animation-name current-frame) animation
            (change-morphing~ obj animation-name frame: current-frame)
            (goto-frame~ (get-morphing~ obj) current-frame)
            (let ((transition (vector-ref content (+ size 1))))
              (when transition
                (bind (name frame elapsed duration) transition
                  (transition-morphing~ obj name frame: frame duration: duration elapsed: elapsed)))))))
      obj))
  
  
  (slot controlled <object> initialize #f accessors generate)
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (let ((zone (current-zone)))
      (set-color (dye .678 .047 .902 1.))
      (update-model)))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (standardize-lookat! lookat))
  
  
  (method (update-model)
    (let ((world (current-world))
          (zone (current-zone)))
      (let ((new-model (case (get-vehicle~ world)
                         ((wheels) 'OrbOnWheels)
                         ((hummer) 'OrbOnHummer)
                         ((saucer) 'OrbOnSaucer)
                         ((rocket) 'OrbOnRocket)
                         (else (get-avatar~ zone)))))
        (when (neq? new-model model)
          (free-morphing)
          (set-model new-model)
          ;; reset defaults
          (set-scaling 1.)
          (set-animate? #f)
          (when (string? new-model)
            (metadata-install)
            (setup-morphing)
            (set-animate? #t)
            (set-orientation 'vertical))))))
  
  
  (method (unaggro)
    (for-each (lambda (enemy)
                (set-aggro-target~ enemy #f))
              aggroed-enemies)
    (set! aggroed-enemies '()))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a} {a}" name position))))
  
  
  (method override (is-player?)
    #t)
  
  
  ;;;
  ;;;; History
  ;;;
  
  
  (method override (history-draw-trail?)
    #t)
  
  
  ;;;
  ;;;; Actions
  ;;;
  
  
  (method override (damage amount critical?)
    (nextmethod amount critical?)
    (let ((pane (child~ (get-interface~ (current-world)) 'combat)))
      (set-visible?~ pane #t)
      (invalidate-view~ pane))
    (let ((pane (child~ (get-interface~ (current-world)) 'character)))
      (when (get-visible?~ pane)
        (view-health~ pane))
      (damage-armor~ pane critical?))
    (stop-eating))
  
  
  (method override (attack kind)
    (let ((pane (child~ (get-interface~ (current-world)) 'character)))
      (case kind
        ((bullet) (damage-weapon~ pane))
        ((arrow) (damage-bow~ pane))))
    (stop-eating))
  
  
  (method override (wound-amount critical?)
    (/ (nextmethod critical?) armor-factor))
  
  
  (method override (die)
    (let ((world (current-world))
          (zone (current-zone))
          (me (current-me)))
      (sleep .5)
      (play-3d-sound (death-sound) position)
      (display-message~ world (format "R.I.P. {a}" (get-name)) color: {Color Orange})
      (unaggro)
      (set-life 100.)
      (set-alive? #t)
      (set-lava? #f)
      (set-water? #f)
      (invalidate-view~ (child~ (get-interface~ world) 'combat))
      (resurrect-player~ zone me)
      (when (eq? self me)
        (eye-behind-player~ world)
        (follow-player~ world))))
  
  
  (method (stop-eating)
    (set! eating #f)
    (set! eating-ticks #f))
  
  
  (method (control actor)
    (let ((world (current-world)))
      (set! controlled actor)
      (eye-behind-actor~ world actor)
      (follow-actor~ world actor)))
  
  
  (method (relinquish-control)
    (let ((world (current-world)))
      (set! controlled #f)
      (eye-behind-player~ world)
      (follow-player~ world)))
  
  
  (method (effective-controlled)
    (or controlled self))
  
  
  (method override (tick-slice)
    .04)
  
  
  (method override (tick-actions commands)
    (tick-actor-actions~ (effective-controlled) commands))
  
  
  (method override (tick commands time elapse exit)
    (unless (= elapse 0.)
      (tick-actor~ (effective-controlled) commands time elapse exit #t 'slide)))
  
  
  (method override (hit collisions exit)
    )
  
  
  (method (move-backward commands)
    (move-backward~ commands))
  
  
  (method (move-forward commands)
    (move-forward~ commands))
  
  
  (method (run-forward commands)
    (run-forward~ commands))
  
  
  (method (run-backward commands)
    (run-backward~ commands))
  
  
  (method (blink-forward commands)
    (blink-forward~ commands))
  
  
  (method (blink-backward commands)
    (blink-backward~ commands))
  
  
  (method (move-left commands)
    (move-left~ commands))
  
  
  (method (move-right commands)
    (move-right~ commands))
  
  
  (method (move-up commands)
    (move-up~ commands))
  
  
  (method (move-down commands)
    (move-down~ commands))
  
  
  (method (rotate-left commands)
    (rotate-left~ commands))
  
  
  (method (rotate-right commands)
    (rotate-right~ commands))
  
  
  (method (space commands)
    (jump commands))
  
  
  (method (jump commands)
    (jump~ commands))
  
  
  (method (reset-position)
    (let ((zone (current-zone)))
      (set-position (or (get-start-position~ zone) (vertex -9.5 62.5 0.0)))
      (set-velocity (vertex 0. 0. 0.))))))
