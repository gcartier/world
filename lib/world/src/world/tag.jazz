;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; NBT Tags
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.tag jazz


(import (world.binary))


(proclaim (warn optimizations))

(declare (proper-tail-calls))


;;;
;;;; Tag
;;;


(constant inline TAG_End        <fx>  0)
(constant inline TAG_Byte       <fx>  1)
(constant inline TAG_Short      <fx>  2)
(constant inline TAG_Int        <fx>  3)
(constant inline TAG_Long       <fx>  4)
(constant inline TAG_Float      <fx>  5)
(constant inline TAG_Double     <fx>  6)
(constant inline TAG_Byte_Array <fx>  7)
(constant inline TAG_String     <fx>  8)
(constant inline TAG_List       <fx>  9)
(constant inline TAG_Compound   <fx> 10)
(constant inline TAG_Int_Array  <fx> 11)


(definition public inline (make-tag name type value) <vector>
  (vector name type value))

(definition public inline (tag-name tag <vector>)
  (vector-ref tag 0))

(definition public inline (tag-type tag <vector>)
  (vector-ref tag 1))

(definition public inline (tag-value tag <vector>)
  (vector-ref tag 2))

(definition public inline (tag-set tag <vector> value)
  (vector-set! tag 2 value))

(definition public (tag-find name tags)
  (loop (for tag in tags)
        (when (equal? (tag-name tag) name)
          (return tag))
        (finally #f)))

(definition public (tag-assoc name tags)
  (let ((tag (tag-find name tags)))
    (and tag (tag-value tag))))

(definition public (tag-add compound name type value)
  (let ((tags (tag-value compound)))
    (let ((tag (tag-find name tags)))
      (if tag
          (tag-set tag value)
        (tag-set compound (append tags (list (make-tag name type value))))))))

(definition public (tag-remove compound name)
  (let ((tags (tag-value compound)))
    (tag-set compound (remove name tags key: tag-name test: equal?))))


(definition public inline (make-list-payload type <fx> content <list>) <pair>
  (cons type content))

(definition public inline (list-payload-type payload <pair>) <fx>
  (car payload))

(definition public inline (list-payload-content payload <pair>) <list>
  (cdr payload))

(definition public inline (set-list-payload-type payload <pair> type)
  (set-car! payload type))

;; requiring the type is a work around a quirck in minecraft where empty lists
;; can sometimes have their type set to Byte or End instead of the correct type
(definition public (set-list-payload-content payload <pair> type content)
  (set-car! payload type)
  (set-cdr! payload content))


;;;
;;;; Reader
;;;


(class NBT-Reader extends Binary-Reader
  
  
  (method package (read-tag self)
    (define (read-tag)
      (let ((type <fx> (<-u8 self)))
        (if (= type TAG_End)
            #f
          (let ((name (read-name)))
            (make-tag name type (read-data type))))))
    
    (define (read-data type)
      (ecase type
        ((TAG_End)        (read-end))
        ((TAG_Byte)       (read-byte))
        ((TAG_Short)      (read-short))
        ((TAG_Int)        (read-int))
        ((TAG_Long)       (read-long))
        ((TAG_Float)      (read-float))
        ((TAG_Double)     (read-double))
        ((TAG_Byte_Array) (read-byte-array))
        ((TAG_String)     (read-name))
        ((TAG_List)       (read-list))
        ((TAG_Compound)   (read-compound))
        ((TAG_Int_Array)  (read-int-array))
        (else (error "Unknown NBT tag: {s}" type))))
    
    (define (read-end)
      (error "Invalid NBT tag: TAG_End"))
    
    (define (read-byte)
      (<-s8 self))
    
    (define (read-short)
      (<-s16-big-endian self))
    
    (define (read-int)
      (<-s32-big-endian self))
    
    (define (read-long)
      (<-s64-big-endian self))
    
    (define (read-float)
      (<-float-big-endian self))
    
    (define (read-double)
      (<-double-big-endian self))
    
    (define (read-byte-array)
      (let ((size <fx> (read-int)))
        (<-u8vector self size)))
    
    (define (read-name)
      (let ((size <fx> (read-short)))
        (<-utf-8-string self size)))
    
    (define (read-list)
      (let ((type <fx> (read-byte))
            (size <fx> (read-int)))
        (let ((content (loop (repeat size)
                             (collect (read-data type)))))
          (make-list-payload type content))))
    
    (define (read-compound)
      (let ((tag (read-tag)))
        (if (not tag)
            '()
          (cons tag (read-compound)))))
    
    (define (read-int-array)
      (let ((size <fx> (read-int)))
        (let ((data (make-vector size)))
          (loop (for i from 0 below size)
                (vector-set! data i (read-int)))
          data)))
    
    (read-tag)))


;;;
;;;; Writer
;;;


(class NBT-Writer extends Binary-Writer
  
  
  (method package (write-tag self tag)
    (define (write-tag tag)
      (let ((name (tag-name tag))
            (type (tag-type tag))
            (val (tag-value tag)))
        (->u8 self type)
        (write-name name)
        (write-data type val)))
    
    (define (write-data type val)
      (ecase type
        ((TAG_End)        (error "Invalid NBT"))
        ((TAG_Byte)       (write-byte val))
        ((TAG_Short)      (write-short val))
        ((TAG_Int)        (write-int val))
        ((TAG_Long)       (write-long val))
        ((TAG_Float)      (write-float val))
        ((TAG_Double)     (write-double val))
        ((TAG_Byte_Array) (write-byte-array val))
        ((TAG_String)     (write-name val))
        ((TAG_List)       (write-list val))
        ((TAG_Compound)   (write-compound val))
        ((TAG_Int_Array)  (write-int-array val))
        (else (error "Unknown NBT tag: {s}" type))))
    
    (define (write-end)
      (->u8 self TAG_End))
    
    (define (write-byte val)
      (->s8 self val))
    
    (define (write-short val)
      (->s16-big-endian self val))
    
    (define (write-int val)
      (->s32-big-endian self val))
    
    (define (write-long val)
      (->s64-big-endian self val))
    
    (define (write-float val)
      (->float-big-endian self val))
    
    (define (write-double val)
      (->double-big-endian self val))
    
    (define (write-byte-array val)
      (let ((size (u8vector-length val)))
        (write-int size)
        (->u8vector self val)))
    
    (define (write-name val)
      (let ((port (open-u8vector)))
        (write-string val port)
        (let ((u8vec (get-output-u8vector port)))
          (close-port port)
          (let ((size (u8vector-length u8vec)))
            (write-short size)
            (->u8vector self u8vec)))))
    
    (define (write-list val)
      (let ((type (list-payload-type val))
            (content (list-payload-content val)))
        (let ((size (length content)))
          (write-byte type)
          (write-int size)
          (loop (for elem in content)
                (write-data type elem)))))
    
    (define (write-compound val)
      (loop (for tag in val)
            (write-tag tag))
      (write-end))
    
    (define (write-int-array val)
      (let ((size (vector-length val)))
        (write-int size)
        (loop (for i from 0 below size)
              (write-int (vector-ref val i)))))
    
    (write-tag tag))))
