;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Target
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.target jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.draw)
        (world.dye)
        (world.dyes)
        (world.foreign)
        (world.geometry)
        (world.opengl)
        (world.quad)
        (world.shape)
        (world.syntax (phase syntax))
        (world.widget))


;;;
;;;; Widget
;;;


(class Target-Widget extends Widget
  
  
  (slot last-point initialize #f)
  
  
  (method (colorize-dye name default (alpha #f))
    (let ((world (current-world)))
      (let ((down (get-widget-down~ world))
            (part (get-widget-part~ world)))
        (gl-colorize-dye
          (if (and (eq? self down) (eq? part name))
              (if alpha
                  (dye 1. 1. 0. alpha)
                yellow-dye)
            default))))))


;;;
;;;; Mover
;;;


(class Mover extends Target-Widget
  
  
  (slot start-point)
  (slot start-position)


  (method override (draw-widget)
    (let ((world (current-world)))
      (let ((target (first-target~ world)))
        (let ((x (get-x~ target))
              (y (get-y~ target))
              (z (get-z~ target))
              (sx (+ 1.5 (get-radius-x~ target)))
              (sy (+ 1.5 (get-radius-y~ target)))
              (sz (+ 1.5 (get-radius-z~ target)))
              (ps .5)
              (pa .4))
          (glLineWidth 2.)
          (glDisable GL_LIGHTING)
          (glDisable GL_DEPTH_TEST)
          (glBegin GL_LINES)
          ;; Axis X
          (colorize-dye 'x red-dye)
          (glVertex3f (+ x 0.0) y (+ z 0.0))
          (glVertex3f (+ x  sx) y (+ z 0.0))
          
          ;; Axis Y
          (colorize-dye 'y green-dye)
          (glVertex3f (+ x 0.0) (+ y 0.0) z)
          (glVertex3f (+ x 0.0) (+ y  sy) z)
          
          ;; Axis Z
          (colorize-dye 'z blue-dye)
          (glVertex3f (+ x 0.0) y (+ z 0.0))
          (glVertex3f (+ x 0.0) y (+ z  sz))
          (glEnd)
          (glLineWidth 1.)
          
          ;; Plane X
          (colorize-dye 'plane-x (dye 1. 0. 0. pa) pa)
          (glBegin GL_QUADS)
          (glVertex3f x y z)
          (glVertex3f x y (+ z ps))
          (glVertex3f x (+ y ps) (+ z ps))
          (glVertex3f x (+ y ps) z)
          (glEnd)
          (colorize-dye 'plane-x red-dye)
          (glBegin GL_LINE_STRIP)
          (glVertex3f x y (+ z ps))
          (glVertex3f x (+ y ps) (+ z ps))
          (glVertex3f x (+ y ps) z)
          (glEnd)
          
          ;; Plane Y
          (colorize-dye 'plane-y (dye 0. 1. 0. pa) pa)
          (glBegin GL_QUADS)
          (glVertex3f x y z)
          (glVertex3f x y (+ z ps))
          (glVertex3f (+ x ps) y (+ z ps))
          (glVertex3f (+ x ps) y z)
          (glEnd)
          (colorize-dye 'plane-y green-dye)
          (glBegin GL_LINE_STRIP)
          (glVertex3f x y (+ z ps))
          (glVertex3f (+ x ps) y (+ z ps))
          (glVertex3f (+ x ps) y z)
          (glEnd)
          
          ;; Plane Z
          (colorize-dye 'plane-z (dye 0. 0. 1. pa) pa)
          (glBegin GL_QUADS)
          (glVertex3f x y z)
          (glVertex3f x (+ y ps) z)
          (glVertex3f (+ x ps) (+ y ps) z)
          (glVertex3f (+ x ps) y z)
          (glEnd)
          (colorize-dye 'plane-z blue-dye)
          (glBegin GL_LINE_STRIP)
          (glVertex3f x (+ y ps) z)
          (glVertex3f (+ x ps) (+ y ps) z)
          (glVertex3f (+ x ps) y z)
          (glEnd)
          
          ;; Arrow X
          (glPushMatrix)
          (glTranslatef (+ x sx) y z)
          (glRotatef 90.0 0.0 1.0 0.0)
          (glScalef 0.1 0.1 0.1)
          (colorize-dye 'x red-dye)
          (gl-cylinder 1. 0. 2. 26 52)
          (glPopMatrix)
          
          ;; Arrow Y
          (glPushMatrix)
          (glTranslatef x (+ y sy) z)
          (glRotatef -90.0 1.0 0.0 0.0)
          (glScalef 0.1 0.1 0.1)
          (colorize-dye 'y green-dye)
          (gl-cylinder 1. 0. 2. 26 52)
          (glPopMatrix)
          
          ;; Arrow Z
          (glPushMatrix)
          (glTranslatef x y (+ z sz))
          (glRotatef -90.0 0.0 0.0 1.0)
          (glScalef 0.1 0.1 0.1)
          (colorize-dye 'z blue-dye)
          (gl-cylinder 1. 0. 2. 26 52)
          (glPopMatrix)
          (glEnable GL_LIGHTING)
          (glEnable GL_DEPTH_TEST)))))
  
  
  (method override (iterate-polygons proc)
    (let ((world (current-world)))
      (let ((target (first-target~ world)))
        (when target
          (let ((x (get-x~ target))
                (y (get-y~ target))
                (z (get-z~ target))
                (sx (+ 1.5 (get-radius-x~ target)))
                (sy (+ 1.5 (get-radius-y~ target)))
                (sz (+ 1.5 (get-radius-z~ target)))
                (ps .5)
                (cuboid (make-cuboid))
                (cr .1)
                (ar .05))
            (define (iterate-plane v1 v2 v3 v4 part)
              (proc (make-quad (cons self part)
                               v1 v2 v3 v4)))
            
            (define (iterate-arrow x y z part)
              (cuboid-init! cuboid (- x cr) (- y cr) (- z cr) (+ x cr) (+ y cr) (+ z cr))
              (iterate-cuboid-quads cuboid #f #f proc (cons self part)))
            
            (define (iterate-axis left bottom back right top front part)
              (cuboid-init! cuboid left bottom back right top front)
              (iterate-cuboid-quads cuboid #f #f proc (cons self part)))
            
            (iterate-plane (vertex x y z)
                           (vertex x y (+ z ps))
                           (vertex x (+ y ps) (+ z ps))
                           (vertex x (+ y ps) z)
                           'plane-x)
            (iterate-plane (vertex x y z)
                           (vertex x y (+ z ps))
                           (vertex (+ x ps) y (+ z ps))
                           (vertex (+ x ps) y z)
                           'plane-y)
            (iterate-plane (vertex x y z)
                           (vertex x (+ y ps) z)
                           (vertex (+ x ps) (+ y ps) z)
                           (vertex (+ x ps) y z)
                           'plane-z)
            (iterate-arrow (+ x sx) y z 'x)
            (iterate-arrow x (+ y sy) z 'y)
            (iterate-arrow x y (+ z sz) 'z)
            (iterate-axis (+ x ps ar) (- y ar) (- z ar) (+ x sx) (+ y ar) (+ z ar) 'x)
            (iterate-axis (- x ar) (+ y ps ar) (- z ar) (+ x ar) (+ y sy) (+ z ar) 'y)
            (iterate-axis (- x ar) (- y ar) (+ z ps ar) (+ x ar) (+ y ar) (+ z sz) 'z))))))
  
  
  (method override (widget-mouse-down h v)
    (let ((world (current-world)))
      (set! start-point (new Point h v))
      (set! start-position (copy-vertex (get-position~ (first-target~ world))))))
  
  
  (method override (widget-mouse-move h v)
    (define (dot-product-2d u v)
      (+ (* (cast <fl> (get-h~ u)) (cast <fl> (get-h~ v)))
         (* (cast <fl> (get-v~ u)) (cast <fl> (get-v~ v)))))
    
    (define (screen-nu- pt1 pt2)
      (new Point
        (- (get-h~ pt1) (get-h~ pt2))
        (- (get-v~ pt2) (get-v~ pt1))))
    
    (let ((world (current-world)))
      (let ((point (new Point h v))
            (eye (get-eye~ world))
            (target (first-target~ world)))
        (when target
          (let ((delta (nu- point start-point))
                (target-center (get-center~ target)))
            (let ((x-dir (vertex 1. 0. 0.))
                  (y-dir (vertex 0. 1. 0.))
                  (z-dir (vertex 0. 0. 1.)))
             (define (movement part)
               (let ((direction (case part
                                  ((x) x-dir)
                                  ((y) y-dir)
                                  ((z) z-dir))))
                 (let ((tip (world->screen~ eye (vertex+ start-position direction)))
                       (origin (world->screen~ eye start-position)))
                   (let ((unit (nu- tip origin)))
                     @w(debug (dot-product-2d unit delta)
                            (dot-product-2d unit unit))
                     (/ (dot-product-2d unit delta)
                        (dot-product-2d unit unit))))))
             
             ;(debug start-position)
             (let ((target (get-target~ world)))
               (case (get-widget-part~ world)
                 ((x) @w(debug (movement 'x)) (move-target target (vertex-scalar* x-dir (movement 'x))))
                 ((y) @w(debug (movement 'y)) (move-target target (vertex-scalar* y-dir (movement 'y))))
                 ((z) @w(debug (movement 'z)) (move-target target (vertex-scalar* z-dir (movement 'z))))
                 ((plane-x) (move-target target (vertex+ (vertex-scalar* y-dir (movement 'y))
                                                         (vertex-scalar* z-dir (movement 'z)))))
                 ((plane-y) (move-target target (vertex+ (vertex-scalar* x-dir (movement 'x))
                                                         (vertex-scalar* z-dir (movement 'z)))))
                 ((plane-z) (move-target target (vertex+ (vertex-scalar* x-dir (movement 'x))
                                                         (vertex-scalar* y-dir (movement 'y)))))))
             (set! start-point point)
             (set! start-position (copy-vertex (get-position~ (first-target~ world))))))))))
  
  
  (method (move-target target delta)
    (for-each (lambda (elem)
                (move-element elem delta))
              target))
  
  
  (method (move-element elem delta)
    (let ((zone (current-zone)))
      (let ((designer (get-designer~ zone))
            (old-position (get-position~ elem)))
        (let ((new-position (vertex+ start-position delta)))
          (set-property~ designer elem 'position new-position)
          (update-area~ zone elem old-position)
          (update-sector~ zone elem old-position)
          (invalidate-lightmaps~ zone elem))))))


;;;
;;;; Rotater
;;;


(class Rotater extends Target-Widget


  (method override (draw-widget)
    (let ((world (current-world)))
      (let ((target (first-target~ world)))
        (let ((x (get-x~ target))
              (y (get-y~ target))
              (z (get-z~ target))
              ;; quick hack
              (sx (+ 1.5 (get-radius-x~ target)))
              (sy (+ 1.5 (get-radius-y~ target)))
              (sz (+ 1.5 (get-radius-z~ target)))
              (radius (+ .5 (get-radius~ target))))
          (glLineWidth 2.)
          (glDisable GL_LIGHTING)
          (glDisable GL_DEPTH_TEST)
          @wait (
          ;; Rotate X
          (glPushMatrix)
          (glTranslatef x y z)
          (glRotatef 90.0 0.0 1.0 0.0)
          (gl-segment-circle {Point 0 0} radius {Color Red} 100)
          (glPopMatrix)
          
          ;; Rotate Y
          (glPushMatrix)
          (glTranslatef x y z)
          (glRotatef 90.0 1.0 0.0 0.0)
          (gl-segment-circle {Point 0 0} radius {Color Green} 100)
          (glPopMatrix)
          
          ;; Rotate Z
          (glPushMatrix)
          (glTranslatef x y z)
          (glRotatef 90.0 0.0 0.0 1.0)
          (gl-segment-circle {Point 0 0} radius {Color Blue} 100)
          (glPopMatrix))
          
          ;; quick hack
          (glBegin GL_LINES)
          
          ;; Axis X
          (colorize-dye 'x red-dye)
          (glVertex3f (+ x 0.0) y (+ z 0.0))
          (glVertex3f (+ x  sx) y (+ z 0.0))
          
          ;; Axis Y
          (colorize-dye 'y green-dye)
          (glVertex3f (+ x 0.0) (+ y 0.0) z)
          (glVertex3f (+ x 0.0) (+ y  sy) z)
          
          ;; Axis Z
          (colorize-dye 'z blue-dye)
          (glVertex3f (+ x 0.0) y (+ z 0.0))
          (glVertex3f (+ x 0.0) y (+ z  sz))
          (glEnd)
          ;(glEnable GL_LIGHTING)
          (glLineWidth 1.)
          
          ;; Sphere X
          (glPushMatrix)
          (glTranslatef (+ x sx) y z)
          (glRotatef 90.0 0.0 1.0 0.0)
          (glScalef 0.1 0.1 0.1)
          (colorize-dye 'x red-dye)
          (gl-sphere 1. 26 52)
          (glPopMatrix)
          
          ;; Sphere Y
          (glPushMatrix)
          (glTranslatef x (+ y sy) z)
          (glRotatef -90.0 1.0 0.0 0.0)
          (glScalef 0.1 0.1 0.1)
          (colorize-dye 'y green-dye)
          (gl-sphere 1. 26 52)
          (glPopMatrix)
          
          ;; Sphere Z
          (glPushMatrix)
          (glTranslatef x y (+ z sz))
          (glRotatef -90.0 0.0 0.0 1.0)
          (glScalef 0.1 0.1 0.1)
          (colorize-dye 'z blue-dye)
          (gl-sphere 1. 26 52)
          (glPopMatrix)
          
          (glEnable GL_LIGHTING)
          (glEnable GL_DEPTH_TEST)
          (glLineWidth 1.)))))
  
  
  ;; quick hack
  (method override (iterate-polygons proc)
    (let ((world (current-world)))
      (let ((target (first-target~ world)))
        (when target
          (let ((x (get-x~ target))
                (y (get-y~ target))
                (z (get-z~ target))
                (sx (+ 1.5 (get-radius-x~ target)))
                (sy (+ 1.5 (get-radius-y~ target)))
                (sz (+ 1.5 (get-radius-z~ target)))
                (cuboid (make-cuboid))
                (cr .1))
            (define (iterate x y z part)
              (cuboid-init! cuboid (- x cr) (- y cr) (- z cr) (+ x cr) (+ y cr) (+ z cr))
              (iterate-cuboid-quads cuboid #f #f proc (cons self part)))
            
            (iterate x y z 'global)
            (iterate (+ x sx) y z 'x)
            (iterate x (+ y sy) z 'y)
            (iterate x y (+ z sz) 'z))))))
  
  
  ;; quick hack
  (method override (widget-mouse-down h v)
    (set! last-point (new Point h v)))
  
  
  ;; quick hack
  (method override (widget-mouse-move h v)
    (let ((world (current-world)))
      (let ((target (get-target~ world))
            (part (get-widget-part~ world)))
        (let ((delta (- h (get-h~ last-point))))
          (set! last-point (new Point h v))
          (rotate-target target delta part)))))
  
  
  (method (rotate-target target delta part)
    (for-each (lambda (elem)
                (rotate-element elem delta part))
              target))
  
  
  (method (rotate-element elem delta part)
    (let ((zone (current-zone)))
      (let ((designer (get-designer~ zone))
            (d (cast <fl> delta)))
        (let ((inc (* d (/ PI/8 2.))))
          (case part
            ((x) (rotate-lookat! (get-lookat~ elem) inc (get-right~ (get-lookat~ elem))))
            ((y) (rotate-lookat! (get-lookat~ elem) inc (get-sight~ (get-lookat~ elem))))
            ((z) (rotate-lookat! (get-lookat~ elem) inc (get-up~ (get-lookat~ elem)))))
          (set-property~ designer elem 'lookat (get-lookat~ elem)))))))


;;;
;;;; Scaler
;;;


(class Scaler extends Target-Widget


  (method override (draw-widget)
    (let ((world (current-world)))
      (let ((target (first-target~ world)))
        (let ((x (get-x~ target))
              (y (get-y~ target))
              (z (get-z~ target))
              (sx (+ 1.5 (get-radius-x~ target)))
              (sy (+ 1.5 (get-radius-y~ target)))
              (sz (+ 1.5 (get-radius-z~ target))))
          (glLineWidth 2.)
          (glDisable GL_LIGHTING)
          (glDisable GL_DEPTH_TEST)
          (glBegin GL_LINES)
          
          ;; Axis X
          (colorize-dye 'x red-dye)
          (glVertex3f (+ x 0.0) y (+ z 0.0))
          (glVertex3f (+ x  sx) y (+ z 0.0))
          
          ;; Axis Y
          (colorize-dye 'y green-dye)
          (glVertex3f (+ x 0.0) (+ y 0.0) z)
          (glVertex3f (+ x 0.0) (+ y  sy) z)
          
          ;; Axis Z
          (colorize-dye 'z blue-dye)
          (glVertex3f (+ x 0.0) y (+ z 0.0))
          (glVertex3f (+ x 0.0) y (+ z  sz))
          (glEnd)
          (glLineWidth 1.)
          
          ;; Sphere X
          (glPushMatrix)
          (glTranslatef (+ x sx) y z)
          (glRotatef 90.0 0.0 1.0 0.0)
          (glScalef 0.1 0.1 0.1)
          (colorize-dye 'x red-dye)
          (gl-sphere 1. 26 52)
          (glPopMatrix)
          
          ;; Sphere Y
          (glPushMatrix)
          (glTranslatef x (+ y sy) z)
          (glRotatef -90.0 1.0 0.0 0.0)
          (glScalef 0.1 0.1 0.1)
          (colorize-dye 'y green-dye)
          (gl-sphere 1. 26 52)
          (glPopMatrix)
          
          ;; Sphere Z
          (glPushMatrix)
          (glTranslatef x y (+ z sz))
          (glRotatef -90.0 0.0 0.0 1.0)
          (glScalef 0.1 0.1 0.1)
          (colorize-dye 'z blue-dye)
          (gl-sphere 1. 26 52)
          (glPopMatrix)
          
          ;; Sphere Global
          (glPushMatrix)
          (glTranslatef x y z)
          (glRotatef 90.0 0.0 1.0 0.0)
          (glScalef 0.15 0.15 0.15)
          (colorize-dye 'global (dye .5 .5 .5 1.))
          (gl-sphere 1. 26 52)
          (glPopMatrix)
          (glEnable GL_LIGHTING)
          (glEnable GL_DEPTH_TEST)))))
  
  
  (method override (iterate-polygons proc)
    (let ((world (current-world)))
      (let ((target (first-target~ world)))
        (when target
          (let ((x (get-x~ target))
                (y (get-y~ target))
                (z (get-z~ target))
                (sx (+ 1.5 (get-radius-x~ target)))
                (sy (+ 1.5 (get-radius-y~ target)))
                (sz (+ 1.5 (get-radius-z~ target)))
                (cuboid (make-cuboid))
                (cr .1))
            (define (iterate x y z part)
              (cuboid-init! cuboid (- x cr) (- y cr) (- z cr) (+ x cr) (+ y cr) (+ z cr))
              (iterate-cuboid-quads cuboid #f #f proc (cons self part)))
            
            (iterate x y z 'global)
            (iterate (+ x sx) y z 'x)
            (iterate x (+ y sy) z 'y)
            (iterate x y (+ z sz) 'z))))))
  
  
  (method override (widget-mouse-down h v)
    (set! last-point (new Point h v)))
  
  
  (method override (widget-mouse-move h v)
    (let ((world (current-world)))
      (let ((target (get-target~ world))
            (part (get-widget-part~ world)))
        (let ((delta (- h (get-h~ last-point))))
          (set! last-point (new Point h v))
          (scale-target target delta part)))))
  
  
  (method (scale-target target delta part)
    (for-each (lambda (elem)
                (scale-element elem delta part))
              target))
  
  
  (method (scale-element elem delta part)
    (let ((zone (current-zone)))
      (let ((designer (get-designer~ zone))
            (d (cast <fl> delta))
            (f 1.025))
        (let ((factor (if (> d 0.) f (/ f))))
          (let ((s (case part
                     ((global) (vertex factor factor factor))
                     ((x) (vertex factor 1. 1.))
                     ((y) (vertex 1. factor 1.))
                     ((z) (vertex 1. 1. factor)))))
            (set-property~ designer elem 'scale (vertex* (get-scale~ elem) s)))))))))
