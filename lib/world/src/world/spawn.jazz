;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Spawn
;;;
;;;  The Initial Developer of the Original Code is Edouard Hebert.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.spawn jazz


(import (jazz.syntax (phase syntax))
        (world)
        (world.autoload)
        (world.change)
        (world.configure)
        (world.context)
        (world.creature)
        (world.entity)
        (world.geometry)
        (world.history)
        (world.homogeneous)
        (world.generation)
        (world.index)
        (world.math)
        (world.settings)
        (world.syntax (phase syntax))
        (world.task))


(definition protected spawn-creatures?    <bool> (world-setting 'world.spawn-creatures? #f @aec-quicky-and-no-generate-on-server-for-now (not young-child?)))
(definition protected spawn-initial-delay <pair> (world-setting 'world.spawn-initial-delay '(5. 10.)))
(definition protected spawn-delay         <pair> (world-setting 'world.spawn-delay '(10. 40.)))
(definition protected spawn-minimum       <fx>   (world-setting 'world.spawn-minimum 2))
(definition protected spawn-maximum       <fx>   (world-setting 'world.spawn-maximum 4))
(definition protected max-spawner-count   <fx>   (world-setting 'world.max-spawner-count 10))
(definition protected max-spawned-count   <fx>   (world-setting 'world.max-spawned-count 40))

(definition protected spawn-initial-min-delay <fl> (first spawn-initial-delay))
(definition protected spawn-initial-max-delay <fl> (second spawn-initial-delay))
(definition protected spawn-min-delay         <fl> (first spawn-delay))
(definition protected spawn-max-delay         <fl> (second spawn-delay))

(definition protected (set-spawn-creatures? flag)
  (set! spawn-creatures? flag))


(definition protected spawned-count      0)
(definition protected spawned-creatures  '())
(definition protected player-indexes     #f)
(definition protected player-spawners    #f)
(definition protected modified-spawners? #f)

(definition protected (set-spawned-count count)
  (set! spawned-count count))

(definition protected (set-spawned-creatures creatures)
  (set! spawned-creatures creatures))

;; should not be global but only affect modified section
(definition protected (set-modified-spawners? modified?)
  (set! modified-spawners? modified?))


(constant max-spawn-light <fx>
  8)


(definition protected (initialize-spawned)
  (set! spawned-count 0)
  (set! spawned-creatures '())
  (set! player-indexes (make-table test: eq? weak-keys: #t))
  (set! player-spawners (make-table test: eq? weak-keys: #t))
  (set! modified-spawners? #f))


(definition (sorted-spawned-creatures player)
  (let ((pos (get-position player)))
    (sort > spawned-creatures key: (lambda (creature)
                                     (vertex-distance (get-position creature) pos)))))


(definition public (spawn-task task <Task>)
  (declare (proper-tail-calls))
  (let ((zone (current-zone))
        (history (current-history)))
    (with-task task
      (lambda (exit)
        (let (iter (previous (current-monotonic)))
          (let ((time (current-monotonic)))
            (let ((elapse (- time previous)))
              ;; only server spawns
              (unless #f @gambit30 (client?)
                (unless (get-paused? history)
                  (unless (stopping? task)
                    (for-each (lambda (player)
                                (unless (admin-me? player)
                                  (let ((current-section-index (element-section-index zone player)))
                                    (let ((current-spawners (with-task-mutex
                                                              (lambda ()
                                                                (if (and (equal? (table-ref player-indexes player #f) current-section-index)
                                                                         (not modified-spawners?))
                                                                    (table-ref player-spawners player)
                                                                  (find-spawners current-section-index))))))
                                      (table-set! player-indexes player current-section-index)
                                      (table-set! player-spawners player current-spawners)
                                      (set! modified-spawners? #f)
                                      (tick-spawn player elapse current-spawners)))))
                              (get-players zone)))))
              (let ((duration (- (current-monotonic) time)))
                (task-wait task spawn-period duration exit))
              (iter time))))))))


(definition (tick-spawn player elapse spawners)
  (site (spawn)
    (when spawn-creatures?
      (spawn-creatures player elapse spawners))))


(definition protected (find-spawners section-index)
  (let ((zone (current-zone))
        (queue (new-queue)))
    (iterate-section-neighbors zone section-index 1
      (lambda (section)
        (section-iterate-blocks zone section '(52)
          (lambda (index)
            (let ((coords (index->coordinates index)))
              (let ((spawner (find-if (lambda (creature)
                                        (let ((spawner (get-spawner creature)))
                                          (and (eq? (get-section spawner) section)
                                               (equal? (get-coordinates spawner) coords)
                                               spawner)))
                                      spawned-creatures
                                      return: 'result)))
                (if spawner
                    (enqueue queue spawner)
                  ;; spawn faster the first time to compensate for not having natural spawning
                  (enqueue queue (new Spawner section: section coordinates: coords)))))))))
    (queue-list queue)))


(definition (spawn-creatures player elapse spawners)
  (let ((zone (current-zone))
        (universe (current-universe))
        (history (current-history)))
    (for-each (lambda (spawner)
                (let ((time-now (+ (get-time-since-last spawner) elapse)))
                  (if (>= time-now (get-time-for-spawn spawner))
                      (with-task-mutex
                        (lambda ()
                          (unless (get-paused? history)
                            (let ((player-position (get-position player))
                                  (section (get-section spawner))
                                  (coordinates (get-coordinates spawner)))
                              (bind-index (i j k) coordinates
                                (call-with-section zone section i j k
                                  (lambda (section block-id <fx> data-id <fx> i <fx> j <fx> k <fx> x <fl> y <fl> z <fl> with-field block-ref block-set! data-ref data-set! blocklight-ref blocklight-set! skylight-ref skylight-set!)
                                    (define (find-spawning-places needed)
                                      (define (random-x/z)
                                        (random-element '(-1 1)))
                                      
                                      (define (random-y)
                                        (random-element '(-1 0 1)))
                                      
                                      (let ((places '()))
                                        (set! needed (* needed 4))
                                        (let (iter-distance (dist 1))
                                          (let (iter-circle (max-places (max 2 (/ needed 2))))
                                            (let ((a (* dist (if (= dist 1) (random-y) (random-x/z))))
                                                  (b (* dist (random-y)))
                                                  (c (* dist (if (= dist 1) (random-y) (random-x/z)))))
                                              (with-field (+ i a) (+ j b) (+ k c)
                                                (lambda (section index)
                                                  (when (and section (= (block-ref section index) 0))
                                                    (decrease! needed)
                                                    (decrease! max-places)
                                                    (set! places (cons (cons (section/index->position section index) (blocklight-ref section index)) places)) ;; maybe include skylight
                                                    (cond ((> max-places 0)
                                                           (iter-circle max-places))
                                                          ((and (< dist 4) (= max-places 0) (> needed 0))
                                                           (iter-distance (+ dist 1)))
                                                          (else
                                                           #f))))))))
                                        places))
                                    
                                    (let ((spawn-count (random-integer-between spawn-minimum spawn-maximum)))
                                      (let ((sorted-creatures (and (> (+ spawned-count spawn-count) max-spawned-count) (sorted-spawned-creatures player)))
                                            (possible-spawn-places (find-spawning-places spawn-count)))
                                        (loop (for number from 0 below spawn-count)
                                              (unless (or (null? possible-spawn-places) (>= (get-creature-count spawner) max-spawner-count))
                                                (bind (pos . light) (random-element possible-spawn-places)
                                                  (when (< light max-spawn-light)
                                                    (set! possible-spawn-places (remove! position possible-spawn-places test: equal?))
                                                    (let ((farthest-to-remove (and (> (+ spawned-count 1) max-spawned-count) (not-null? sorted-creatures) (car sorted-creatures))))
                                                      ;; do not remove farthest if we are spawning farthest away than it
                                                      (unless (and farthest-to-remove (>= (vertex-distance& pos player-position) (vertex-distance& (get-position farthest-to-remove) player-position)))
                                                        (let ((lookat (horizon-lookat (random-horizon))))
                                                          (when farthest-to-remove
                                                            (world.changes:die-change farthest-to-remove)
                                                            (set! sorted-creatures (cdr sorted-creatures)))
                                                          (world.changes:spawn-change pos lookat spawner))))))))
                                        (set-time-since-last spawner 0.)
                                        (set-time-for-spawn spawner (random-between spawn-min-delay spawn-max-delay)))))))))))
                      (set-time-since-last spawner time-now))))
              spawners)))


(definition protected (add-spawned creature)
  (let ((spawner (get-spawner creature)))
    (when spawner
      (set-spawned-count (+ spawned-count 1))
      (set-spawned-creatures (cons creature spawned-creatures))
      (set-creature-count spawner (+ (get-creature-count spawner) 1)))))


(definition protected (remove-spawned creature)
  (let ((spawner (get-spawner creature)))
    (when spawner
      (set-spawned-count (- spawned-count 1))
      (set-spawned-creatures (remove! creature spawned-creatures))
      (set-creature-count spawner (- (get-creature-count spawner) 1)))))


(definition protected (validate-spawn)
  (let ((spawned (collect-if get-spawner spawned-creatures)))
    (assert (<= spawned-count max-spawned-count))
    (assert (= spawned-count (length spawned) (length spawned-creatures)))
    (assert (= (length spawned-creatures) (length (remove-duplicates spawned-creatures))))
    (assert (= (length player-spawners) (length (remove-duplicates player-spawners))))
    (let ((spawners (remove-duplicates (map get-spawner spawned-creatures))))
      (for-each (lambda (spawner count)
                  (assert (= (get-creature-count spawner) count))
                  (assert (<= count max-spawner-count)))
                spawners
                (map (lambda (spawner)
                       (length (collect-if (lambda (creature)
                                             (eq? (get-spawner creature) spawner))
                                           spawned-creatures)))
                     spawners)))))


;;;
;;;; Spawner
;;;


(class Spawner extends Entity
  
  
  (method meta override (package self spawner)
    (let ((section (get-section spawner))
          (coordinates (get-coordinates spawner)))
      (list (get-id spawner) (section/coordinates->location section coordinates))))
  
  
  (method meta override (unpackage self info)
    (let ((universe (current-universe)))
      (bind (id location) info
        (receive (section coordinates) (location->section/coordinates location)
          (new Spawner id: id section: section coordinates: coordinates)))))
  
  
  (property section     accessors generate)
  (property coordinates accessors generate)
  
  (slot time-since-last accessors generate)
  (slot time-for-spawn  accessors generate)
  (slot creature-count  accessors generate)
  
  
  (method override (prepare self rest)
    (nextmethod self rest)
    (set! self.time-since-last 0.)
    (set! self.time-for-spawn (random-between spawn-initial-min-delay spawn-initial-max-delay))
    (set! self.creature-count 0))))
