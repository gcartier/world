;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Planets
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.planet jazz


(import (jazz.jml)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.draw)
        (world.dyes)
        (world.element)
        (world.entity)
        (world.foreign)
        (world.gadget)
        (world.generate)
        (world.geometry)
        (world.gravity)
        (world.object)
        (world.serialization)
        (world.shader)
        (world.shape)
        (world.syntax (phase syntax)))


;;;
;;;; Planet
;;;


(class Planet extends Element implements Gravity-Well
  
  
  (property size    <fl>     initialize 1.      accessors generate)
  (property gravity <fl>     initialize .018    getter generate setter explicit)
  (property image   <object> initialize "dirty" accessors generate)
  
  
  (slot mass <fl>)
  
  
  (method override (finish rest)
    (nextmethod rest)
    (set-radiuses (vertex size size size))
    (set-scale (vertex size size size))
    (set-color red-dye)
    (update-mass)
    (generate-planet))
  
  
  (method public (set-gravity grav)
    (set! gravity grav)
    (update-mass))
  
  
  (method (update-mass)
    (set! mass (* gravity (* radius radius))))
  
  
  (method (generate-planet)
    (jazz.ui.window:delay-event
      (lambda ()
        (let ((world (current-world))
              (zone (current-zone))
              (slices (fxfloor (* size 4.8)))
              (stacks (fxfloor (* size 2.4))))
          (let ((matrix (matrix-multiply (make-translation-matrix& (get-x) (get-y) (get-z)) (make-scaling-matrix& (vertex-x scale) (vertex-y scale) (vertex-z scale))))
                (material (get-tile-material~ world))
                (uv (image-rect~ (get-tile-atlas~ world) image)))
            (let ((generator
                    (ico-sphere-generator 50 uv)
                    ;;(uv-sphere-generator slices stacks uv)
                    ))
              (generate-faces generator matrix: matrix smooth-normals?: #t material: material)
              (generate-polygons generator matrix: matrix)))))))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{r precision: 3}" mass))))
  
  
  (method meta override (marshall-object obj)
    (serialize-object (class-of obj)
                      (vector
                        ;; component
                        (serialize (get-name~ obj))
                        (serialize (get-visible?~ obj))
                        ;; element
                        (serialize (get-position~ obj))
                        (serialize (get-radiuses~ obj))
                        (serialize (get-radius~ obj))
                        (serialize (get-scale~ obj))
                        (serialize (get-color~ obj))
                        (serialize (get-texture~ obj))
                        (serialize (get-hit-behavior~ obj))
                        (serialize (get-user-data~ obj))
                        ;; entity
                        ;; planet
                        (serialize (get-gravity~ obj)))))
  
  
  (method meta override (unmarshall-object content)
    (bind-vector (;; component
                  name
                  visible?
                  ;; element
                  position
                  radiuses
                  radius
                  scale
                  color
                  texture
                  hit-behavior
                  user-data
                  ;; entity
                  ;; planet
                  gravity)
        content
      (let ((obj (new self name: name visible?: visible?)))
        ;; element
        (set-position~ obj (deserialize position))
        (set-radiuses~ obj (deserialize radiuses))
        (set-radius~ obj (deserialize radius))
        (set-scale~ obj (deserialize scale))
        (set-color~ obj (deserialize color))
        (set-texture~ obj (deserialize texture))
        (set-hit-behavior~ obj (deserialize hit-behavior))
        (set-user-data~ obj (deserialize user-data))
        ;; entity
        ;; planet
        (set-gravity~ obj (deserialize gravity)))))
  
  
  ;; done in generate-planet
  (method override (element-faces)
    '())
  
  
  (method override (element-radius)
    size)
  
  
  (method override (well-mass)
    mass)
  
  
  (method override (inside? r vert)
    (let ((distance (vertex-norm (vertex- vert center))))
      (<= distance (+ radius r))))))
