;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Planets
;;;


(module world.planet jazz


(import (jazz.jml)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.draw)
        (world.dyes)
        (world.element)
        (world.entity)
        (world.foreign)
        (world.gadget)
        (world.generate)
        (world.geometry)
        (world.gravity)
        (world.object)
        (world.serialization)
        (world.shader)
        (world.shape)
        (world.syntax (phase syntax)))


;;;
;;;; Planet
;;;


(class Planet extends Element implements Gravity-Well
  
  
  (property size  <fl>     initialize 1.      accessors generate)
  (property pull  <fl>     initialize .01     accessors generate)
  (property image <object> initialize "dirty" accessors generate)
  
  
  (method override (finish rest)
    (nextmethod rest)
    (set-radiuses (vertex size size size))
    (set-scale (vertex size size size))
    (set-color red-dye)
    (generate-planet))
  
  
  (method (generate-planet)
    (jazz.ui.window:delay-event
      (lambda ()
        (let ((world (current-world))
              (zone (current-zone))
              (slices (fxfloor (* size 4.8)))
              (stacks (fxfloor (* size 2.4))))
          (let ((matrix (matrix-multiply (make-translation-matrix& (get-x) (get-y) (get-z)) (make-scaling-matrix& (vertex-x scale) (vertex-y scale) (vertex-z scale))))
                (material (get-tile-material~ world))
                (uv (image-rect~ (get-tile-atlas~ world) image)))
            (let ((generator (uv-sphere-generator slices stacks uv matrix: matrix)))
              (let ((faces (generate-mesh generator @smooth-normals?: #t return-faces?: #t))
                    (sectors (make-table test: eq?)))
                (for-each (lambda (face)
                            (let ((sector (position-sector~ zone (get-center~ (get-polygon~ face)))))
                              (add-face~ sector face)
                              (table-set! sectors sector #t)))
                          faces)
                (iterate-table sectors
                  (lambda (sector ignore)
                    (set-vertices-uptodate?~ sector #f)))
                (generate-sphere slices stacks uv 'physical matrix: matrix))))))))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{r precision: 1}" pull))))
  
  
  (method meta override (marshall-object obj)
    (serialize-object (class-of obj)
                      (vector
                        ;; component
                        (serialize (get-name~ obj))
                        (serialize (get-visible?~ obj))
                        ;; element
                        (serialize (get-position~ obj))
                        (serialize (get-radiuses~ obj))
                        (serialize (get-radius~ obj))
                        (serialize (get-scale~ obj))
                        (serialize (get-color~ obj))
                        (serialize (get-texture~ obj))
                        (serialize (get-hit-behavior~ obj))
                        (serialize (get-user-data~ obj))
                        ;; entity
                        ;; planet
                        (serialize (get-pull~ obj)))))
  
  
  (method meta override (unmarshall-object content)
    (bind-vector (;; component
                  name
                  visible?
                  ;; element
                  position
                  radiuses
                  radius
                  scale
                  color
                  texture
                  hit-behavior
                  user-data
                  ;; entity
                  ;; planet
                  pull)
        content
      (let ((obj (new self name: name visible?: visible?)))
        ;; element
        (set-position~ obj (deserialize position))
        (set-radiuses~ obj (deserialize radiuses))
        (set-radius~ obj (deserialize radius))
        (set-scale~ obj (deserialize scale))
        (set-color~ obj (deserialize color))
        (set-texture~ obj (deserialize texture))
        (set-hit-behavior~ obj (deserialize hit-behavior))
        (set-user-data~ obj (deserialize user-data))
        ;; entity
        ;; planet
        (set-pull~ obj (deserialize pull)))))
  
  
  ;; done in generate-planet
  (method override (element-faces)
    '())
  
  
  (method override (element-radius)
    size)
  
  
  (method override (gravity-pull)
    pull)
  
  
  (method override (inside? r vert)
    (let ((distance (vertex-norm (vertex- vert center))))
      (<= distance (+ radius r))))))
