;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Server UDP
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Barbara Samson


(module world.server.udp jazz


(import (jazz.network)
        (jazz.presence)
        (jazz.stream)
        (jazz.syntax (phase syntax))
        (world)
        (world.configure)
        (world.context)
        (world.event)
        (world.ring)
        (world.settings)
        (world.support)
        (world.task)
        (world.udp))


(class UDP-Server extends Object
  
  
  (slot udp-port              initialize #f getter generate)
  (slot udp-stopping?         initialize #f getter generate)
  (slot origin-connections    initialize #f getter generate)
  (slot host/port-connections initialize #f getter generate)
  ;; mutex was not needed before server streaming...
  (slot server-mutex          initialize #f getter generate)
  (slot server-channels       initialize #f getter generate)
  (slot send-channels         initialize #f getter generate)
  (slot receive-task          initialize #f getter generate)
  
  
  (method package (start self host service)
    (if (simulation?)
        (begin
          (set! server-channels (make-table))
          (set! send-channels (make-table)))
    (set! udp-port (open-udp (list
                               local-address: host
                               local-port-number: service)))
    (set! udp-stopping? #f)
    (set! origin-connections (make-table))
    (set! host/port-connections (make-table))
    (set! server-mutex (make-mutex 'server))
    (set! server-channels (make-table))
    (set! send-channels (make-table))
    (unless (simulation?)
      (set! receive-task (new Task 'udp-receive (lambda (task)
                                                  (receive-loop self task))
                              priority: udp-priority))
      (start-task receive-task))))
  
  
  (method package (stop self)
    (set! udp-stopping? #t)
    (close-port udp-port)
    (thread-join! (get-thread receive-task))
    (set! udp-port #f)
    (set! receive-task #f))
  
  
  ;;;
  ;;;; Connection
  ;;;
  
  
  (method (register-connection self origin host port connection)
    (table-set! origin-connections origin connection)
    (table-set! host/port-connections (cons host port) connection))
  
  
  (method (unregister-connection self origin host port)
    (table-clear origin-connections origin)
    (table-clear host/port-connections (cons host port)))
  
  
  (method (origin->connection self origin)
    (table-ref origin-connections origin #f))
  
  
  (method (host/port->connection self host port)
    (table-ref host/port-connections (cons host port) #f))
  
  
  ;;;
  ;;;; Channel
  ;;;
  
  
  (method package (find-server-channel self channel-no)
    (table-ref server-channels channel-no #f))
  
  
  (method package (register-server-channel self name no origin media-kind resolution recipient exclude-delegate)
    (let ((channel (new UDP-Server-Channel name no origin media-kind resolution recipient exclude-delegate)))
      (table-set! server-channels no channel)
      channel))
  
  
  (method package (close-server-channel self no)
    (let ((channel (table-ref server-channels no #f)))
      ;; lets be robust for now
      (when channel
        (close channel)
        (table-clear server-channels no))))
  
  
  (method public (find-send-channel self channel-no)
    (table-ref send-channels channel-no #f))
  
  
  (method package (register-send-channel self name channel-no media-kind resolution)
    (let ((channel (new UDP-Send-Channel name channel-no client-no media-kind resolution)))
      (table-set! send-channels channel-no channel)
      channel))
  
  
  (method package (close-send-channel self channel-no)
    (let ((channel (table-ref send-channels channel-no #f)))
      (close channel)
      (server-unregister-channel (current-server) channel-no)
      (table-clear send-channels channel-no)))
  
  
  (method package (close-send-channels self)
    (iterate-table-safe send-channels
      (lambda (channel-no channel)
        (close-send-channel self channel-no))))
  
  
  ;;;
  ;;;; Send
  ;;;
  
  
  (method package (send-media self channel-no buffer header? timestamp duration)
    (mutex-lock! server-mutex)
    (let ((channel (find-send-channel self channel-no)))
      (when channel
        (let ((origin (get-origin channel))
              (kind (get-media-kind channel)))
          ;; quick test really not sure about this
          (let ((server-channel (find-server-channel self channel-no)))
            (send-media channel buffer header? timestamp duration
              (lambda (data)
                (broadcast-media self server-channel origin kind data)))))))
    (mutex-unlock! server-mutex))
  
  
  ;;;
  ;;;; Receive
  ;;;
  
  
  (method (receive-loop self task)
    (declare (proper-tail-calls))
    (with-task task
      (lambda (exit)
        (let (loop)
          (let ((data (with-exception-filter
                        (lambda (exc)
                          udp-stopping?)
                        (lambda (exc)
                          (continuation-return exit #f))
                        (lambda ()
                          (read udp-port)))))
            (when data
              (site (udp-receive on?: #t)
                (when udp-record-read/write?
                  (let ((channel (read-udp-channel data))
                        (origin (read-udp-origin data))
                        (media-kind (read-udp-kind data))
                        (sequence (read-udp-sequence data))
                        (frame (read-udp-frame data)))
                    (record-event udp-id-read-data
                                  (fixnum->flonum channel)
                                  (fixnum->flonum origin)
                                  (fixnum->flonum media-kind)
                                  (fixnum->flonum sequence)
                                  (fixnum->flonum frame)
                                  -1.
                                  (fixnum->flonum (u8vector-length data)))))
                (receive-data self data))
              (loop)))))))
  
  
  (method (receive-data self data)
    (mutex-lock! server-mutex)
    (let ((origin (read-udp-origin data)))
      (let ((connection (origin->connection self origin)))
        (when connection
          (increase-packets-received connection))))
    (let ((kind (read-udp-kind data)))
      (ecase kind
        ((udp-connect)
         (connect-source self data))
        ((udp-state)
         (receive-state self data))
        ((udp-nack)
         (receive-nack self data))
        ((udp-audio udp-video)
         (receive-media self kind data))
        (else
         (error "Unknown UDP kind: {s}" kind))))
    (mutex-unlock! server-mutex))
  
  
  (method (connect-source self data)
    (let ((sent (read-udp-sent data))
          (origin (read-udp-origin data))
          (local-host (read-udp-connect-local-host data))
          (local-port (read-udp-connect-local-port data))
          (latency (read-udp-connect-latency data))
          (lag (read-udp-connect-lag data))
          (drop (read-udp-connect-drop data))
          (source-info (udp-source-socket-info udp-port)))
      (define (udp-simulator)
        (let ((latency (and (/= latency -1.) latency))
              (lag (and (/= lag -1.) lag))
              (drop (and (/= drop -1.) drop)))
          (if (or latency lag drop)
              (new UDP-Simulator latency lag drop
                   (lambda (info drop?)
                     (bind (connection . data) info
                       (if drop?
                           (increase-packets-sent connection)
                         (send-connection self connection data)))))
            #f)))
      
      (let ((source-host (socket-info-address source-info))
            (source-port (socket-info-port-number source-info)))
        (define (send-connected connection)
          (udp-destination-set! source-host source-port udp-port)
          (let ((size (+ 8 4 4)))
            (let ((data (make-u8vector size)))
              (write-udp-sent data sent)
              (write-udp-kind data udp-connected)
              (write-udp-origin data origin)
              (retrying-write data udp-port)
              (increase-packets-sent connection))))
        
        (let ((existing-connection (origin->connection self origin)))
          (if existing-connection
              (send-connected existing-connection)
            (let ((connection (new UDP-Connection source-host source-port local-host local-port (udp-simulator))))
              (register-connection self origin source-host source-port connection)
              (send-connected connection)))))))
  
  
  (method (receive-state self data)
    (let ((origin (read-udp-origin data)))
      ;; ack origin
      (send-ack self origin data)
      ;; throttling
      (let ((connection (origin->connection self origin)))
        (when connection
          (set-last-received-state connection (current-seconds))
          (when (get-throttle-media? connection)
            (set-throttle-media? connection #f)
            (record-event udp-id-throttle-off
                          -1. ;; CHANNELTODO
                          (fixnum->flonum origin)
                          -1.
                          -1.
                          -1.
                          -1.
                          -1.)
            (when udp-show-throttle?
              (let ((delegate (find-delegate-by-no (current-server) origin)))
                (format :terminal "{s} {s} {a} {s}{%}" remote-name 'THROTTLE (if delegate (get-name delegate) origin) 'OFF))))))
      (broadcast-state self origin data)))
  
  
  (method (receive-nack self data)
    (let ((channel-no (read-udp-channel data))
          (origin (read-udp-origin data))
          (media-kind (read-udp-nack-media-kind data))
          (missing (u8vector->object (read-udp-nack-missing data))))
      (send-missing self channel-no origin media-kind missing)))
  
  
  (method (receive-media self kind data)
    (let ((channel-no (read-udp-channel data))
          (origin (read-udp-origin data)))
      (let ((channel (find-server-channel self channel-no)))
        (when channel
          (retain-media channel data)
          (broadcast-media self channel origin kind data)
          (let ((frame (read-udp-frame data))
                (last-nacked-frame (get-last-nacked-frame channel)))
            (when (or (not last-nacked-frame)
                      (> frame last-nacked-frame))
              (send-nacks self channel-no origin kind channel)
              (set-last-nacked-frame channel frame)))))))
  
  
  (method (send-nacks self channel-no origin kind channel)
    (let ((missing (collect-missing channel)))
      (unless (null? missing)
        (let ((source-info (udp-source-socket-info udp-port)))
          (let ((source-host (socket-info-address source-info))
                (source-port (socket-info-port-number source-info)))
            (let ((connection (host/port->connection self source-host source-port)))
              (send-nack self connection channel-no origin kind missing)))))))
  
  
  (method (send-missing self channel-no origin media-kind missing)
    (let ((channel (find-server-channel self channel-no)))
      (when channel
        (let ((ring (get-retain-ring channel)))
          (for-each (lambda (sequence)
                      (let ((data (locate-data ring sequence)))
                        (if data
                            (let ((source-info (udp-source-socket-info udp-port)))
                              (let ((source-host (socket-info-address source-info))
                                    (source-port (socket-info-port-number source-info)))
                                (let ((connection (host/port->connection self source-host source-port)))
                                  (when udp-show-nacks?
                                    (terminal remote-name (present-media-kind media-kind) '***nack-found-on-server*** sequence))
                                  (increase-udp-resent data)
                                  (send-data self connection data)
                                  (let ((frame (read-udp-frame data)))
                                    (record-event udp-id-resend-packet
                                                  (fixnum->flonum channel-no)
                                                  (fixnum->flonum origin)
                                                  (fixnum->flonum media-kind)
                                                  (fixnum->flonum sequence)
                                                  (fixnum->flonum frame)
                                                  -1.
                                                  -1.)))))
                          (record-event udp-id-absent-packet
                                        (fixnum->flonum channel-no)
                                        (fixnum->flonum origin)
                                        (fixnum->flonum media-kind)
                                        (fixnum->flonum sequence)
                                        -1.
                                        -1.
                                        -1.)
                          ;; we already nacked the source early and will broadcast
                          ;; when we receive the resent so we can ignore this nack
                          )))
                    missing)))))
  
  
  (method (send-nack self connection channel-no origin media-kind missing)
    (when udp-show-nacks?
      (terminal remote-name (present-media-kind media-kind) '***send-nack*** missing))
    (let ((missing-vector (object->u8vector missing)))
      (let ((size (+ 8 4 4 4 4 (u8vector-length missing-vector))))
        (let ((data (make-u8vector size)))
          (write-udp-sent data (current-seconds))
          (write-udp-kind data udp-nack)
          (write-udp-origin data origin)
          (write-udp-channel data channel-no)
          (write-udp-nack-media-kind data media-kind)
          (write-udp-nack-missing data missing-vector)
          (send-data self connection data)))))
  
  
  (method (send-ack self origin data)
    (let ((connection (origin->connection self origin)))
      (when connection
        (let ((packets-sent (get-packets-sent connection))
              (packets-received (get-packets-received connection)))
          ;(reset-packets-sent connection)
          ;(reset-packets-received connection)
          (let ((size (+ 8 4 4 4 4))
                (sent (read-udp-sent data)))
            (let ((data (make-u8vector size)))
              (write-udp-sent data sent)
              (write-udp-kind data udp-ack)
              (write-udp-origin data origin)
              (write-udp-ack-packets-sent data packets-sent)
              (write-udp-ack-packets-received data packets-received)
              (send-data self connection data)))))))
  
  
  (method (broadcast-state self origin data)
    (let ((delegate (find-delegate-by-no (current-server) origin)))
      (when delegate
        (let ((group (get-group delegate)))
          (when group
            (let ((self? (read-udp-state-self? data)))
              (for-each (lambda (member)
                          (let ((no (get-no member)))
                            (let ((connection (table-ref origin-connections no #f)))
                              (when connection
                                (when (or (neq? member delegate)
                                          self?)
                                  (send-data self connection data))))))
                        (get-members group))))))))
  
  
  (method (broadcast-media self channel origin kind data)
    (let ((recipient (get-recipient channel))
          (exclude-delegate (get-exclude-delegate channel)))
      (let ((recipients (recipient-delegates (current-server) recipient)))
        (for-each (lambda (member)
                    (let ((no (get-no member)))
                      (let ((connection (table-ref origin-connections no #f)))
                        (when connection
                          (unless (eq? member exclude-delegate)
                            (let ((last-received-state (get-last-received-state connection)))
                              (when last-received-state
                                (unless (get-throttle-media? connection)
                                  (let ((now (current-seconds)))
                                    (if (and udp-throttle (> (- now last-received-state) udp-throttle))
                                        (begin
                                          (set-throttle-media? connection #t)
                                          (record-event udp-id-throttle-on
                                                        (fixnum->flonum (get-no channel))
                                                        (fixnum->flonum no)
                                                        -1.
                                                        -1.
                                                        -1.
                                                        -1.
                                                        -1.)
                                          (when udp-show-throttle?
                                            (format :terminal "{s} {s} {a} {s}{%}" remote-name 'THROTTLE (get-name member) 'ON)))
                                      (send-data self connection data)))))))))))
                  recipients))))
  
  
  (method (send-data self connection data)
    (let ((simulator (get-simulator connection)))
      (if simulator
          (send simulator (cons connection data))
        (send-connection self connection data))))
  
  
  (method (send-connection self connection data)
    (udp-destination-set! (get-host connection) (get-port connection) udp-port)
    (retrying-write data udp-port)
    (increase-packets-sent connection)))


;;;
;;;; UDP Connection
;;;


(class UDP-Connection extends Object
  
  
  (slot host                              getter generate)
  (slot port                              getter generate)
  (slot local-host                        getter generate)
  (slot local-port                        getter generate)
  (slot simulator                         getter generate)
  (slot packets-sent        initialize 0  getter generate)
  (slot packets-received    initialize 0  getter generate)
  (slot last-received-state initialize #f accessors generate)
  (slot throttle-media?     initialize #f accessors generate)
  
  
  (method override (initialize self host port local-host local-port simulator)
    (nextmethod self)
    (set! self.host host)
    (set! self.port port)
    (set! self.local-host local-host)
    (set! self.local-port local-port)
    (set! self.simulator simulator))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a} {a}" host port))))
  
  
  (method protected (reset-packets-sent self)
    (set! packets-sent 0))
  
  
  (method protected (reset-packets-received self)
    (set! packets-received 0))
  
  
  (method protected (increase-packets-sent self)
    (increase! packets-sent))
  
  
  (method protected (increase-packets-received self)
    (increase! packets-received)))


;;;
;;;; Server Channel
;;;


(class UDP-Server-Channel extends UDP-Channel
  
  
  (slot recipient         getter generate)
  (slot exclude-delegate  getter generate)
  (slot last-nacked-frame accessors generate)
  (slot retain-ring       getter generate)
  (slot release-task      getter generate)
  
  
  (method override (initialize self name no origin media-kind resolution recipient exclude-delegate)
    (nextmethod self name no origin media-kind resolution)
    (set! self.recipient recipient)
    (set! self.exclude-delegate exclude-delegate)
    (set! self.last-nacked-frame #f)
    (set! self.retain-ring (new UDP-Ring no origin media-kind udp-retain-size max-size: udp-retain-max-size overflow-proc: (udp-ring-overflow 'retain udp-ring-kind-retain)))
    (unless (simulation?)
      (set! self.release-task (new Task 'udp-release (lambda (task)
                                                       (release-loop self task))
                                   priority: udp-priority))
      (start-task release-task)))
  
  
  (method override (destroy self)
    (terminate retain-ring)
    (thread-join! (get-thread release-task))
    (nextmethod self))
  
  
  (method protected (collect-missing self)
    (let ((missing (new-queue)))
      (iterate-latest retain-ring
        (lambda (packet)
          (when (not (get-data packet))
            (enqueue missing (get-sequence packet)))))
      (queue-list missing)))
  
  
  (method protected (retain-media self data)
    (insert retain-ring data udp-id-retain-packet))
  
  
  (method protected (release-loop self task)
    (declare (proper-tail-calls))
    (with-task task
      (lambda (exit)
        (let (loop)
          (let ((packet (retain-wait retain-ring)))
            (if (eq? packet 'terminate)
                (continuation-return exit #f)
              (site (udp-release on?: #t)
                (release-media self packet))
              (loop)))))))
  
  
  (method protected (release-media self packet)
    (let ((data (get-data packet)))
      (if (not data)
          (let ((removed-sequence (get-sequence packet)))
            (record-event udp-id-giveup-packet
                          (fixnum->flonum no)
                          (fixnum->flonum origin)
                          (fixnum->flonum media-kind)
                          (fixnum->flonum removed-sequence)
                          -1.
                          -1.
                          -1.))
        (let ((origin (read-udp-origin data))
              (sequence (read-udp-sequence data))
              (frame (read-udp-frame data))
              (timestamp (read-udp-media-timestamp data)))
          (record-event udp-id-release-packet
                        (fixnum->flonum no)
                        (fixnum->flonum origin)
                        (fixnum->flonum media-kind)
                        (fixnum->flonum sequence)
                        (fixnum->flonum frame)
                        -1.
                        (timestamp->flonum timestamp))))))))
