;;;============
;;;  Together
;;;============
;;;
;;;; Server Reports
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.server.report jazz


(import (jazz.io)
        (jazz.pack))


(definition curl-path
  "/usr/bin/curl")


(definition monitor-directory
  (cond-expand
    (devel  {Directory Home ".together" "devel" "1.0.0" "work" "snapshots"})
    (trial  {Directory Home ".together" "trial" "1.0.0" "work" "snapshots"})
    (test   {Directory Home ".together" "test" "1.0.0" "work" "snapshots"})
    (triage {Directory Home ".together" "triage" "1.0.0" "work" "snapshots"})
    (stable {Directory Home ".together" "stable" "1.0.0" "work" "snapshots"})
    (stage  {Directory Home ".together" "stage" "1.0.0" "work" "snapshots"})
    (prod   {Directory Home ".together" "prod" "1.0.0" "work" "snapshots"})
    (live   {Directory Home ".together" "live" "1.0.0" "work" "snapshots"})))

(definition monitor-remote
  (cond-expand
    (devel  "http://logs.togethersphere.com/devel")
    (trial  "http://logs.togethersphere.com/trial")
    (test   "http://logs.togethersphere.com/test")
    (triage "http://logs.togethersphere.com/triage")
    (stable "http://logs.togethersphere.com/stable")
    (stage  "http://logs.togethersphere.com/stage")
    (prod   "http://logs.togethersphere.com/prod")
    (live   "http://logs.togethersphere.com/live")))


(cond-expand
  (windows
   (definition public (send-snapshot-reports)
     ))
  (else
   (definition public (send-snapshot-reports)
     (define (send-snapshot snapshot)
       (bind (dir remote) snapshot
         (let ((report (new-file (get-parent dir) (add-extension (get-name dir) "report"))))
           (pack-directory dir report)
           (delete dir)
           (let ((port (open-process
                         (list
                           path: curl-path
                           arguments: (list "--silent" "-T" (parse report) "-H" "Content-Type: text/plain" (string-append remote "/snapshots/" (get-name report)))
                           show-console: #f))))
             (let ((status (process-status port)))
               (when (= status 0)
                 ;; robust to report still in use
                 (catch (os-exception? exc)
                   (delete report))))))))
     
     (define (send-snapshots snapshots)
       (for-each send-snapshot snapshots))
     
     (define (collect-snapshots)
       (let ((queue (new-queue)))
         (when (exists? monitor-directory)
           (iterate-directory monitor-directory
             (lambda (dir)
               (when (starts-with? (get-name dir) "snapshot_")
                 (enqueue queue (list dir monitor-remote))))
             files?: #f
             directories?: #t
             recursive?: #f))
         (queue-list queue)))
     
     (let ((snapshots (collect-snapshots)))
       (let ((count (length snapshots)))
         (when (> count 0)
           (thread-start!
             (new-thread
               (lambda ()
                 (send-snapshots snapshots))
               'send-snapshot-reports)))
         count))))))
