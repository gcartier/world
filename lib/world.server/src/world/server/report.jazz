;;;============
;;;  Together
;;;============
;;;
;;;; Server Reports
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.server.report jazz


(import (jazz.io))


(definition curl-path
  "/usr/bin/curl")


(definition monitor-directory
  (cond-expand
    (devel  {Directory Home ".together" "devel" "1.0.0" "work" "snapshots"})
    (trial  {Directory Home ".together" "trial" "1.0.0" "work" "snapshots"})
    (test   {Directory Home ".together" "test" "1.0.0" "work" "snapshots"})
    (triage {Directory Home ".together" "triage" "1.0.0" "work" "snapshots"})
    (stable {Directory Home ".together" "stable" "1.0.0" "work" "snapshots"})
    (stage  {Directory Home ".together" "stage" "1.0.0" "work" "snapshots"})
    (prod   {Directory Home ".together" "prod" "1.0.0" "work" "snapshots"})))

(definition monitor-remote
  (cond-expand
    (devel  "http://logs.togethersphere.com/devel")
    (trial  "http://logs.togethersphere.com/trial")
    (test   "http://logs.togethersphere.com/test")
    (triage "http://logs.togethersphere.com/triage")
    (stable "http://logs.togethersphere.com/stable")
    (stage  "http://logs.togethersphere.com/stage")
    (prod   "http://logs.togethersphere.com/prod")))


(cond-expand
  (windows
   (definition public (send-snapshot-reports)
     ))
  (else
   (definition public (send-snapshot-reports)
     (define (send-snapshot snapshot)
       (bind (file remote) snapshot
         (define (normalize-file file)
           (let ((name (get-name file)))
             (if (find name #\space)
                 (let ((brother-name (substitute #\space #\_ name)))
                   (let ((brother (new-brother file brother-name)))
                     (rename file brother)
                     brother))
               file)))
         
         (let ((file (normalize-file file)))
           (let ((port (open-process
                         (list
                           path: curl-path
                           arguments: (list "--silent" "-T" (parse file) "-H" "Content-Type: text/plain" (string-append remote "/snapshots/" (get-name file)))
                           show-console: #f))))
             (let ((status (process-status port)))
               (when (= status 0)
                 ;; robust to file still in use
                 (catch (os-exception? exc)
                   (delete file))))))))
     
     (define (send-snapshots snapshots)
       (for-each send-snapshot snapshots))
     
     (define (collect-snapshots)
       (let ((queue (new-queue)))
         (when (exists? monitor-directory)
           (iterate-directory monitor-directory
             (lambda (file)
               (when (extension=? (get-extension file) "report")
                 (enqueue queue (list file monitor-remote))))
             files?: #t
             directories?: #f
             recursive?: #f))
         (queue-list queue)))
     
     (let ((snapshots (collect-snapshots)))
       (let ((count (length snapshots)))
         (when (> count 0)
           (thread-start!
             (new-thread
               (lambda ()
                 (send-snapshots snapshots))
               'send-snapshot-reports)))
         count))))))
