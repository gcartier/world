;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Mobile Server
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.server.mobile jazz


(import (jazz.io)
        (jazz.network)
        (jazz.network.websocket)
        (world)
        (world.server)
        (world.server.client)
        (world.settings))


(definition package mobile-server
  #f)

(definition package mobile-server-connect
  #f)

(definition package mobile-service-offset
  50)


(definition package mobile-tls-context
  #f)

(definition package (cache-mobile-tls-context certificates-dir)
  (or mobile-tls-context
      (let ((cert (new-file certificates-dir "cert.pem"))
            (key (new-file certificates-dir "key.pem")))
        (assert (and (exists? cert)
                     (exists? key))
          (let ((tls-context (make-tls-context options: '(server-mode)
                               certificate: (parse cert)
                               private-key: (parse key))))
            (set! mobile-tls-context tls-context)
            tls-context)))))


(definition package (start-mobile-server certificates-dir mobile-connect)
  (let ((configuration (get-server-configuration)))
    (let ((host (effective-host configuration))
          (service (+ (effective-service configuration) mobile-service-offset))
          (tls-context (cache-mobile-tls-context certificates-dir)))
      (let ((server (new Mobile-Server host: host service: service tls-context: tls-context)))
        (start server)
        (set! mobile-server server)
        (set! mobile-server-connect mobile-connect)))))


(definition package (stop-mobile-server)
  (when mobile-server
    (stop mobile-server)
    (set! mobile-server #f)))


;;;
;;;; Server
;;;


(class Mobile-Server extends WebSocket-Server
  
  
  (method override (server-name self)
    'mobile-server)
  
  
  (method override (connection-name self)
    'mobile-connection)
  
  
  (method override (process-connection self port)
    (mobile-server-connect self port)))


;;;
;;;; Delegate
;;;


(class Mobile-Delegate extends Client-Delegate
  
  
  (slot server accessors generate)
  (slot port   accessors generate)
  
  
  (method override (initialize self server port state id no name role admin? verified? verifier)
    (nextmethod self #f state #f id no name #f #f #f role admin? verified? verifier #f)
    (set! self.server server)
    (set! self.port port))
  
  
  (method override (mobile? self)
    #t)
  
  
  ;; quick solution never disconnect for now
  (method override (still-alive? self)
    #t)
  
  
  ;; quick solution never disconnect for now
  (method override (disconnected-giveup? self)
    #f)))
