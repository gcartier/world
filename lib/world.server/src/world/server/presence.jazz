;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Server Presence
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.server.presence jazz


(import (jazz.presence)
        (jazz.presence.transmission)
        (world)
        (world.context)
        (world.server.problem))


(definition protected connection-disconnecter
  #f)

(definition protected (set-connection-disconnecter proc)
  (set! connection-disconnecter proc))


(definition (serving-already-connected presence remote-uuid existing-connection accept)
  (problem "Already connected")
  (close-connection presence remote-uuid existing-connection)
  (accept))


(definition (serving-accept-handler proc presence)
  (handle-exception-filter
    connection-exception?
    (lambda (exc)
      (problem "Accepting remote: {a}" (exception-reason exc)))
    (lambda ()
      (proc presence))))


(definition (serving-process-handler proc connection)
  (handle-exception-filter
    connection-exception?
    (lambda (exc)
      (problem "Processing remote: {a}" (exception-reason exc))
      (when connection-disconnecter
        (connection-disconnecter connection exc)))
    (lambda ()
      (proc connection))))


(definition (serving-execute-handler proc connection)
  (handle-exception-filter
    connection-exception?
    (lambda (exc)
      (problem "Executing remote: {a}" (exception-reason exc)))
    (lambda ()
      (proc connection))))


(definition protected (setup-serving-presence)
  (if (get-remote-listener)
      (error "Remote listener already started")
    (set-presence-already-connected serving-already-connected)
    (set-presence-accept-handler serving-accept-handler)
    ;; set-presence-invoke-handler is not needed
    ;; at the moment it is made safe by calling-client
    (set-presence-process-handler serving-process-handler)
    (set-presence-execute-handler serving-execute-handler))))
