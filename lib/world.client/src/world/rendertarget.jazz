;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Render-Target
;;;


(module world.rendertarget jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world.foreign)
        (time))


;;;
;;;; Render-Target
;;;


(class Render-Target extends Object
  
  
  (slot fbo          <int>    initialize #f    getter generate)
  (slot depth-buffer <int>    initialize #f    getter generate)
  (slot texture      <int>    initialize #f    getter generate)
  (slot width        <int>    initialize #f)
  (slot height       <int>    initialize #f)
  
  
  ;; Assign width, height and generate the necessary buffers if none are passed
  (method override (initialize width height (fbo: fbo #f) (depth-buffer: depth-buffer #f) (texture: texture #f))
    (nextmethod)
    (set! fbo~self fbo)
    (set! depth-buffer~self depth-buffer)
    (set! texture~self texture)
    (set! width~self width)
    (set! height~self height)

    (when (not texture~self)
      (initialize-texture))

    (when (not depth-buffer~self)
      (initialize-depth-buffer))

    (when (not fbo~self)
      (initialize-fbo)))
  
  
  ;; Initialize a texture where the scene will be rendered
  (method (initialize-texture)
    (set! texture (gl-generate-texture))
    (glBindTexture GL_TEXTURE_2D texture)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER GL_LINEAR)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MIN_FILTER GL_LINEAR)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_S GL_CLAMP_TO_EDGE)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_T GL_CLAMP_TO_EDGE)
    (glTexImage2D GL_TEXTURE_2D 0 GL_RGBA width height 0 GL_RGBA GL_UNSIGNED_BYTE #f)
    (glBindTexture GL_TEXTURE_2D 0))
  
  
  ;; Initialize a depth buffer for rendering
  ;; Two or more render targets can share the same depth buffer
  (method (initialize-depth-buffer)
    (set! depth-buffer (glGenRenderbuffersEXT*))
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT depth-buffer)
    (glRenderbufferStorageEXT GL_RENDERBUFFER_EXT GL_DEPTH_COMPONENT32 width height)  
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT 0))
  
  
  ;; Bind the texture and the depth buffer in a single framebuffer object
  (method (initialize-fbo)
    (when (and texture depth-buffer)
      (set! fbo (glGenFramebuffersEXT*))
      (glBindFramebufferEXT GL_FRAMEBUFFER_EXT fbo)
      (glFramebufferTexture2DEXT GL_FRAMEBUFFER_EXT GL_COLOR_ATTACHMENT0_EXT GL_TEXTURE_2D texture 0)
      (glFramebufferRenderbufferEXT GL_FRAMEBUFFER_EXT GL_DEPTH_ATTACHMENT_EXT GL_RENDERBUFFER_EXT depth-buffer)
      (glBindFramebufferEXT GL_FRAMEBUFFER_EXT 0)))
  
  
  ;;; Sets the render target for rendering
  (method (activate)
    (when fbo
      (glBindFramebufferEXT GL_FRAMEBUFFER_EXT fbo)))
  
  
  ;; Unsets the current render-target for rendering, and sets the default back (opengl's native buffers)
  (method (deactivate)
    (glBindFramebufferEXT GL_FRAMEBUFFER_EXT 0))
  
  
  ;; Resize buffers (useful when the window is resized)
  (method (resize width height)
    (set! width~self width)
    (set! height~self height)
    
    (glBindTexture GL_TEXTURE_2D texture)
    (glTexImage2D GL_TEXTURE_2D 0 GL_RGBA width height 0 GL_RGBA GL_UNSIGNED_BYTE #f)
    (glBindTexture GL_TEXTURE_2D 0)
    
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT depth-buffer)
    (glRenderbufferStorageEXT GL_RENDERBUFFER_EXT GL_DEPTH_COMPONENT32 width height)
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT 0))
  
  
  ;; Unsets everything
  (method (free-resources)
    (glDeleteRenderbuffersEXT 1 depth-buffer)
    (glDeleteTextures 1 texture)
    (glDeleteFramebuffersEXT 1 fbo)
    (set! fbo #f)
    (set! texture #f)
    (set! depth-buffer #f))))
