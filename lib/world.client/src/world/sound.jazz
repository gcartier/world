;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Sound
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.sound jazz


(import (jazz.sound)
        (jazz.syntax (phase syntax))
        (world)
        (world.autoload)
        (world.configure)
        (world.settings)
        (world.syntax (phase syntax))
        (world.task))


(definition default-rate
  44100)

(definition default-format
  SoundIoFormatFloat32NE)

(definition default-channels
  2)

(definition default-latency
  .2)


;;;
;;;; Task
;;;


(definition public (mic-task task <Task>)
  (declare (proper-tail-calls))
  (let ((mic (current-mic)))
    (with-task task
      (lambda (exit)
        (let (iter (previous (current-seconds)))
          (let ((time (current-seconds)))
            (let ((elapse (- time previous)))
              (unless (stopping?~ task)
                (site (mic on?: #t)
                  (process-mic~ mic)))
              (let ((duration (- (current-seconds) time)))
                (task-sleep task mic-period duration exit))
              (iter time))))))))


;;;
;;;; Mic
;;;


(class World-Mic extends Object
  
  
  (slot mic?   <bool>     initialize #f accessors generate)
  (slot buffer <u8vector> initialize #f accessors generate)
  
  
  (method override (initialize)
    (set! mic? (world-setting 'world.mic? #f))
    (when (and mic? (client?))
      (start-mic)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-toggle-mic evt)
    (toggle-mic))
  
  
  (method (toggle-mic)
    (when (client?)
      (let ((world (current-world)))
        (if (not mic?)
            (start-mic)
          (stop-mic))
        (update-parameter~ world 'mic)
        (display-on/off~ world "Mic" mic?))))
  
  
  ;;;
  ;;;; Process
  ;;;
  
  
  (method (start-mic)
    (unless mic?
      (startup-sound)
      (let ((in-device (sound-default-input-device))
            (rate default-rate)
            (format default-format)
            (layout (sound-get-default-layout default-channels))
            (latency default-latency))
        (start-sound-input in-device rate format layout latency)
        (set! buffer (make-u8vector (sound-input-ring-capacity)))
        (set! mic? #t))))
  
  
  (method (stop-mic)
    (when mic?
      (stop-sound-input)
      ;(shutdown-sound)
      (set! buffer #f)
      (set! mic? #f)))
  
  
  (method (process-mic)
    (when mic?
      (let ((read (sound-read-input-ring buffer)))
        (when (> read 0)
          (send-client-audio~ (client-tier) (subu8vector buffer 0 read)))))))


;;;
;;;; Speaker
;;;


(definition speaker?
  #f)


(definition public (start-speaker)
  (unless speaker?
    (startup-sound)
    (let ((out-device (sound-default-output-device))
          (rate default-rate)
          (format default-format)
          (layout (sound-get-default-layout default-channels))
          (latency default-latency))
      (start-sound-output out-device rate format layout latency)
      (set! speaker? #t))))


(definition public (stop-speaker)
  (when speaker?
    (stop-sound-output)
    ;(shutdown-sound)
    (set! speaker? #f)))


(definition public (write-server-audio bytes)
  (start-speaker)
  (sound-write-output-ring bytes (u8vector-length bytes))))
