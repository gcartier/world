;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Billboards
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.billboard jazz


(import (jazz.opengl.glew)
        (world)
        (world.autoload)
        (world.foreign)
        (world.geometry)
        (world.quad-texture)
        (world.syntax (phase syntax))
        (world.texture))


(definition public (make-billboard-texture width height v1 v2 v3 v4)
  (let ((world (current-world)))
    (let ((quad-texture (new Quad-Texture))
          (program (get-billboard-program~ world)))
      (set-program~ quad-texture program)
      (set-array~ quad-texture (let ((vertices (f32vector (vertex-x v1) (vertex-y v1) (vertex-z v1) 0. 0. 0.
                                                          (vertex-x v4) (vertex-y v4) (vertex-z v4) 1. 0. 0.
                                                          (vertex-x v2) (vertex-y v2) (vertex-z v2) 0. 1. 0.
                                                          (vertex-x v3) (vertex-y v3) (vertex-z v3) 1. 1. 0.
                                                          (vertex-x v2) (vertex-y v2) (vertex-z v2) 0. 1. 0.
                                                          (vertex-x v4) (vertex-y v4) (vertex-z v4) 1. 0. 0.)))
                                 (create-vertices-array~ program vertices)))
      (set-texture~ quad-texture (make-cairo-texture width height))
      quad-texture)))


(definition public (make-billboard-element billboard (position: position #f))
  (new Billboard-Element
    position: (or position (vertex& 0. 0. 0.))
    billboard: billboard))


(class Billboard-Element extends Element
  
  
  (property billboard initialize #f accessors generate)
  
  
  (slot quad-texture <Quad-Texture+> initialize #f)
  
  
  (method (make-text-billboard)
    (bind (text font color w) billboard
      (let ((extent (text-extent~ font text)))
        (let ((width (get-width~ extent))
              (height (get-height~ extent)))
          (let ((h (/ (* w height) width)))
            (let ((v1 (vertex (/ w -2.) (/ h  2.) 0.))
                  (v2 (vertex (/ w -2.) (/ h -2.) 0.))
                  (v3 (vertex (/ w  2.) (/ h -2.) 0.))
                  (v4 (vertex (/ w  2.) (/ h  2.) 0.)))
              (let ((quad-texture <Quad-Texture> (make-billboard-texture width height v1 v2 v3 v4)))
                (let ((texture (get-texture~ quad-texture)))
                  (let ((surface (get-surface~ texture)))
                    (clear~ surface)
                    (set-font~ surface font)
                    (draw-text~ surface 0 0 text color)
                    (map-texture~ texture)))
                quad-texture)))))))
  
  
  (method override (draw)
    (when (not quad-texture)
      (set! quad-texture (make-text-billboard)))
    (render-texture~ quad-texture))))
