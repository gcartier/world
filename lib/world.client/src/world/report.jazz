;;;============
;;;  Together
;;;============
;;;
;;;; Client Reports
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.report jazz


(import (jazz.io)
        (jazz.pack)
        (jazz.snapshot)
        (world)
        (world.autoload)
        (world.context))


(definition reports-directory
  {Directory Settings "work" "reports"})

(definition reports-bucket
  "reports")

(definition send-directory
  {Directory Settings "work" "send"})


(definition snapshots-bucket
  "snapshots")


(definition remote
  (cond-expand
    (devel  "http://logs.togethersphere.com/devel")
    (trial  "http://logs.togethersphere.com/trial")
    (test   "http://logs.togethersphere.com/test")
    (triage "http://logs.togethersphere.com/triage")
    (stable "http://logs.togethersphere.com/stable")
    (stage  "http://logs.togethersphere.com/stage")
    (prod   "http://logs.togethersphere.com/prod")
    (live   "http://logs.togethersphere.com/live")))


(definition public (send-reports (feedback?: feedback? #t))
  (let ((world (and feedback? (current-world)))
        (curl (curl-path)))
    (define (normalize-file file)
      (let ((name (get-name file)))
        (if (find name #\space)
            (let ((brother-name (substitute #\space #\_ name)))
              (let ((brother (new-brother file brother-name)))
                (rename file brother)
                brother))
          file)))
    
    (define (send-unsent unsent rank total)
      (for-each (lambda (report)
                  (when feedback?
                    (working-progress world (format "Sending logs ({a} of {a})" (+ rank 1) total) #f))
                  (send-file report reports-bucket)
                  (increase! rank))
                unsent))
    
    (define (send-file file bucket)
      (let ((file (normalize-file file)))
        (let ((port (open-process
                      (list
                        path: curl
                        arguments: (list "--silent" "-T" (parse file) "-H" "Content-Type: text/plain" (string-append remote "/" bucket "/" (get-name file)))
                        show-console: #f))))
          (let ((status (process-status port)))
            (when (= status 0)
              ;; robust to file still in use
              (catch (os-exception? exc)
                (delete file)))))))
    
    (define (send-dirs dirs bucket rank total)
      (for-each (lambda (dir)
                  (when feedback?
                    (working-progress world (format "Sending logs ({a} of {a})" (+ rank 1) total) #f))
                  (send-dir dir bucket)
                  (increase! rank))
                dirs))
    
    (define (send-dir dir bucket)
      (create-directories send-directory)
      (let ((report (new-file send-directory (add-extension (get-name dir) "report"))))
        (pack-directory dir report)
        (delete dir)
        (send-file report bucket)))
    
    (define (send-snapshots snapshots rank total)
      (for-each (lambda (snapshot)
                  (when feedback?
                    (working-progress world (format "Sending logs ({a} of {a})" (+ rank 1) total) #f))
                  (send-snapshot snapshot)
                  (increase! rank))
                snapshots))
    
    (define (send-snapshot snapshot)
      (send-dir snapshot snapshots-bucket))
    
    (define (collect-unsent)
      (let ((queue (new-queue)))
        (when (exists? send-directory)
          (iterate-directory send-directory
            (lambda (file)
              (when (extension=? (get-extension file) "report")
                (enqueue queue file)))
            files?: #t
            directories?: #f
            recursive?: #f))
        (queue-list queue)))
    
    (define (collect-dirs)
      (let ((queue (new-queue)))
        (when (exists? reports-directory)
          (iterate-directory reports-directory
            (lambda (dir)
              (let ((name (get-name dir)))
                (when (or (starts-with? name "profile_")
                          (starts-with? name "replay_"))
                  (enqueue queue dir))))
            files?: #f
            directories?: #t
            recursive?: #f))
        (queue-list queue)))
    
    (define (collect-snapshots)
      (let ((queue (new-queue))
            (snapshots-dir (snapshots-directory)))
        (when (exists? snapshots-dir)
          (iterate-directory snapshots-dir
            (lambda (dir)
              (when (starts-with? (get-name dir) "snapshot_")
                (enqueue queue dir)))
            files?: #f
            directories?: #t
            recursive?: #f))
        (queue-list queue)))
    
    (let ((unsent (collect-unsent))
          (dirs (collect-dirs))
          (snapshots (collect-snapshots)))
      (let ((total (+ (length unsent) (length dirs) (length snapshots))))
        (define (send-all)
          (send-unsent unsent 0 total)
          (send-dirs dirs reports-bucket (length unsent) total)
          (send-snapshots snapshots (+ (length unsent) (length dirs)) total))
        
        (when (> total 0)
          (if (not feedback?)
              (send-all)
            (working world
              (lambda ()
                (send-all)))))))))


;;;
;;;; Curl
;;;


(cond-expand
  (windows
   (definition public (curl-path)
     (parse {File Build "curl.exe"})))
  (else
   (definition public (curl-path)
     "/usr/bin/curl"))))
