;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; BBR State
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Barbara Samson


(module world.bbr.state jazz


(import (world.bbr)
        (world.event)
        (world.format)
        (world.record (phase syntax)))


;; closest to a 1432 divisor near 50
(definition protected udp-mss <fx>
  53)


(definition protected (present-bbr-mode mode)
  (vector-ref #(Startup Drain ProbeBW ProbeRTT) mode))

(definition protected (present-bbr-phase phase)
  (vector-ref #(Invalid Startup Drain ProbeRTT Up Down Cruise Refill) phase))


;;;
;;;; State
;;;


(class BBR-State extends Object
  
  
  (slot current-mode         getter generate)
  (slot current-phase        getter generate)
  (slot inflight-buffers     getter generate)
  (slot inflight-datas       getter generate)
  (slot delivered-last-order getter generate)
  (slot last-sent-time       getter generate)
  
  
  (method override (initialize self)
    (nextmethod self)
    (set! current-mode -1)
    (set! current-phase -1)
    (set! inflight-buffers (make-table test: equal?))
    (set! inflight-datas (make-table test: equal?))
    (set! delivered-last-order -1)
    (set! last-sent-time 0))
  
  
  (method protected (reset self)
    (set! current-mode -1)
    (set! current-phase -1)
    (set! inflight-buffers (make-table test: equal?))
    (set! inflight-datas (make-table test: equal?))
    (set! delivered-last-order -1)
    (set! last-sent-time 0))
  
  
  (method protected (record-state self socket order)
    (let ((mode (bbr_get_mode socket))
          (phase (bbr_get_phase socket)))
      (when (or (/= mode current-mode)
                (/= phase current-phase))
        (record-event udp-id-bbr-state
                      -1.
                      -1.
                      -1.
                      -1.
                      (fixnum->flonum order)
                      (fixnum->flonum mode)
                      (fixnum->flonum phase))
        (set! current-mode mode)
        (set! current-phase phase))))
  
  
  (method protected (packet-sent self sent-time order data)
    (let ((segs (max 1 (fxround/ (u8vector-length data) udp-mss))))
      (let ((skb (new_skbuff segs)))
        (table-set! inflight-buffers order skb)
        (table-set! inflight-datas order data)
        (set! last-sent-time sent-time)
        skb)))
  
  
  (method protected (pulse-sent self sent-time)
    (set! last-sent-time sent-time))
  
  
  (method protected (ack-received self socket order srtt queue-empty? timer)
    (if (<= order delivered-last-order)
        #f
      (let ((lost 0))
        (loop (for ord from (+ delivered-last-order 1) below order)
              (update-lost self socket ord srtt queue-empty? timer)
              (increase! lost))
        (set! delivered-last-order order)
        (let ((skb (table-ref inflight-buffers order #f))
              (data (table-ref inflight-datas order #f)))
          ;; pulses are not put in the inflight table
          (if (not skb)
              #f
            (table-clear inflight-buffers order)
            (values skb data lost))))))
  
  
  (method protected (update-lost self socket ord srtt queue-empty? timer)
    (let ((skb (table-ref inflight-buffers ord #f))
          (data (table-ref inflight-datas ord #f)))
      (when skb
        (table-clear inflight-buffers ord)
        (table-clear inflight-datas ord)
        (tcp_skb_lost socket skb)
        (free_skbuff skb)
        (with-record media
          (record-event udp-id-bbr-lost
                        (fixnum->flonum (read-media-channel data))
                        (fixnum->flonum (read-header-sender data))
                        (fixnum->flonum (read-header-kind data))
                        (fixnum->flonum (read-media-sequence data))
                        (fixnum->flonum ord)
                        -1.
                        -1.)))))))
