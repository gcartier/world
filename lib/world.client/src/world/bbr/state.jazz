;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; BBR State
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;    Barbara Samson


(module world.bbr.state jazz


(import (world.bbr)
        (world.event)
        (world.format)
        (world.record-syntax (phase syntax))
        (world.udp))


;; closest to a 1392 divisor near 50
(definition protected udp-mss <fx>
  48)


(definition protected (present-bbr-mode mode)
  (vector-ref #(Startup Drain ProbeBW ProbeRTT) mode))

(definition protected (present-bbr-phase phase)
  (vector-ref #(Invalid Startup Drain ProbeRTT Up Down Cruise Refill) phase))


;;;
;;;; State
;;;


(class BBR-State extends Object
  
  
  (slot inflight-buffers     getter generate)
  (slot delivered-last-order getter generate)
  (slot last-sent-time       getter generate)
  (slot current-mode         getter generate)
  (slot current-phase        getter generate)
  (slot current-pacing       getter generate)
  (slot current-window       getter generate)
  (slot current-congested    getter generate)
  (slot current-limited      getter generate)
  
  
  (method override (initialize self)
    (nextmethod self)
    (set! inflight-buffers (make-table test: equal?))
    (set! delivered-last-order -1)
    (set! last-sent-time 0)
    (reset-current self))
  
  
  (method protected (reset self)
    (set! inflight-buffers (make-table test: equal?))
    (set! delivered-last-order -1)
    (set! last-sent-time 0)
    (reset-current self))
  
  
  (method (reset-current self)
    (set! current-mode -1)
    (set! current-phase -1)
    (set! current-pacing -1)
    (set! current-window -1)
    (set! current-congested -1.)
    (set! current-limited -1.))
  
  
  (method package (packet-sent self sent-time order parcel)
    (let ((segs (max 1 (fxround/ (parcel-length parcel) udp-mss))))
      (let ((skb (new_skbuff segs)))
        (table-set! inflight-buffers order skb)
        (set! last-sent-time sent-time)
        (values skb segs))))
  
  
  ;; for the visualizer
  (method package (segments-sent self order segs)
    (let ((skb (new_skbuff segs)))
      (table-set! inflight-buffers order skb)
      skb))
  
  
  (method package (pulse-sent self sent-time)
    (set! last-sent-time sent-time))
  
  
  (method package (ack-received self socket order)
    (if (<= order delivered-last-order)
        #f
      (let ((lost 0))
        (loop (for ord from (+ delivered-last-order 1) below order)
              (update-lost self socket ord)
              (increase! lost))
        (set! delivered-last-order order)
        (let ((skb (table-ref inflight-buffers order #f)))
          ;; pulses are not put in the inflight table
          (if (not skb)
              #f
            (table-clear inflight-buffers order)
            (values skb lost))))))
  
  
  (method (update-lost self socket ord)
    (let ((skb (table-ref inflight-buffers ord #f)))
      (when skb
        (table-clear inflight-buffers ord)
        (tcp_skb_lost socket skb)
        (free_skbuff skb)
        (unless (emulation?)
          (when record-events?
            (record-event udp-id-bbr-lost
                          -1.
                          -1.
                          -1.
                          -1.
                          (fixnum->flonum ord)
                          -1.
                          -1.))))))
  
  
  (method package (record-state self socket order)
    (let ((mode (bbr_get_mode socket))
          (phase (bbr_get_phase socket)))
      (when (or (/= mode current-mode)
                (/= phase current-phase))
        (when record-events?
          (record-event udp-id-bbr-state
                        -1.
                        -1.
                        -1.
                        -1.
                        (fixnum->flonum order)
                        (fixnum->flonum mode)
                        (fixnum->flonum phase)))
        (set! current-mode mode)
        (set! current-phase phase))))
  
  
  (method package (record-pacing self socket order)
    (let ((pacing (tcp_pacing_rate socket)))
      (when (/= pacing current-pacing)
        (when record-events?
          (record-event udp-id-bbr-pacing
                        -1.
                        -1.
                        -1.
                        -1.
                        (fixnum->flonum order)
                        (bytes->mbps pacing)
                        -1.))
        (set! current-pacing pacing))))
  
  
  (method package (record-window self socket order)
    (let ((window (tcp_snd_cwnd socket)))
      (when (/= window current-window)
        (when record-events?
          (record-event udp-id-bbr-window
                        -1.
                        -1.
                        -1.
                        -1.
                        (fixnum->flonum order)
                        (fixnum->flonum window)
                        -1.))
        (set! current-window window))))
  
  
  (method package (record-congested self congested?)
    (let ((congested (if congested? 1. 0.)))
      (when (/= congested current-congested)
        (when record-events?
          (record-event udp-id-bbr-congested
                        -1.
                        -1.
                        -1.
                        -1.
                        -1.
                        congested
                        -1.))
        (set! current-congested congested))))
  
  
  (method package (record-limited self limited? order)
    (let ((limited (if limited? 1. 0.)))
      (when (/= limited current-limited)
        (when record-events?
          (record-event udp-id-bbr-limited
                        -1.
                        -1.
                        -1.
                        -1.
                        (fixnum->flonum order)
                        limited
                        -1.))
        (set! current-limited limited))))))
