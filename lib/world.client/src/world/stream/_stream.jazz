;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Streams
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.stream jazz


(import (jazz.cairo)
        (jazz.geometry)
        (jazz.gstreamer)
        (jazz.syntax (phase syntax))
        (world)
        (world.autoload)
        (world.geometry)
        (world.quad-texture)
        (world.syntax (phase syntax))
        (world.task))


;;;
;;;; Element
;;;


(class Stream-Element extends Object
  
  
  (slot state initialize #f getter generate)
  (slot sink  initialize #f accessors generate)
  (slot src   initialize #f accessors generate)
  
  
  (method (ready?)
    (eq? state 'ready))
  
  
  (method (playing?)
    (eq? state 'playing))
  
  
  (method public (setup)
    (when (not state)
      (startup-gstreamer)
      (setup-element)
      (set! state 'ready)))
  
  
  (method public (start)
    (setup)
    (unless (eq? state 'playing)
      (start-element)
      (set! state 'playing)))
  
  
  (method public (stop)
    (when (eq? state 'playing)
      (stop-element)
      (set! state 'ready)))
  
  
  (method public (conclude)
    (stop)
    (when (eq? state 'ready)
      (conclude-element)
      (set! state #f)))
  
  
  (method protected virtual (setup-element)
    )
  
  
  (method protected virtual (start-element)
    )
  
  
  (method protected virtual (stop-element)
    )
  
  
  (method protected virtual (conclude-element)
    )
  
  
  (method protected virtual (process)
    )
  
  
  (method protected virtual (consume buffer timestamp duration)
    ))


;;;
;;;; Src
;;;


(class Stream-Src extends Stream-Element)


;;;
;;;; Sink
;;;


(class Stream-Sink extends Stream-Element
  
  
  (slot consumer initialize #f accessors generate)
  
  
  (method override (initialize (consumer: consumer #f))
    (nextmethod)
    (set! consumer~self consumer))
  
  
  (method override (consume buffer timestamp duration)
    (consumer buffer timestamp duration)))


;;;
;;;; Debug-Sink
;;;


(class Debug-Sink extends Stream-Sink
  
  
  (method override (consume buffer timestamp duration)
    (debug (u8vector-length buffer) timestamp duration)))


;;;
;;;; Pipeline
;;;


(definition public (pipeline . elements)
  (let ((len (length elements)))
    (if (< len 2)
        (error "Invalid pipeline length: {s}" len)
      (let (loop (previous #f) (scan elements))
        (when (not-null? scan)
          (let ((elem (car scan)))
            (when previous
              (set-src~ elem previous)
              (set-sink~ previous elem))
            (loop elem (cdr scan)))))))
  (new Stream-Pipeline elements))


(class Stream-Pipeline extends Stream-Element
  
  
  (slot elements initialize #f accessors generate)
  
  
  (method override (initialize elements)
    (nextmethod)
    (set! elements~self elements))
  
  
  (method override (setup-element)
    (for-each setup~ elements))
  
  
  (method override (start-element)
    (for-each start~ elements))
  
  
  (method override (stop-element)
    (for-each stop~ elements))
  
  
  (method override (conclude-element)
    (for-each conclude~ elements))))
