;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Geometry Tests
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module protected world.validate.suite.Geometry-Tester jazz


(import (jazz.jml)
        (jazz.locale)
        (jazz.validation)
        (world.geometry))


(class Geometry-Tester extends Tester
  
  
  (form
    (<install> title: "Geometry"))


  (method override (test)
    (invoke #f test-vertex)
    (invoke #f test-matrix)
    (invoke #f test-cross-product)
    (invoke #f test-ray-intersects-triangle?)
    (invoke #f test-ray-intersects-sphere?))
  
  
  ;;;
  ;;;; vertex
  ;;;
  
  
  (method (test-vertex)
    (validate (vertex=? (vertex 1. 2. 3.) (vertex 1. 2. 3.)))
    (validate (vertex=? (make-vertex) (vertex 0. 0. 0.)))
    (let ((v (vertex 1. 2. 3.)))
      (validate (= (vertex-x v) 1.))
      (validate (= (vertex-y v) 2.))
      (validate (= (vertex-z v) 3.))
      (vertex-x-set! v 10.)
      (vertex-y-set! v 20.)
      (vertex-z-set! v 30.)
      (validate (= (vertex-x v) 10.))
      (validate (= (vertex-y v) 20.))
      (validate (= (vertex-z v) 30.))
      (vertex-init! v 4. 5. 6.)
      (validate (= (vertex-x v) 4.))
      (validate (= (vertex-y v) 5.))
      (validate (= (vertex-z v) 6.))
      (vertex-copy! v (vertex 7. 8. 9.))
      (validate (= (vertex-x v) 7.))
      (validate (= (vertex-y v) 8.))
      (validate (= (vertex-z v) 9.)))
    ;; functional
    (validate (vertex=? (vertex+ (vertex 1. 2. 3.) (vertex 1. 2. 3.)) (vertex 2. 4. 6.)))
    (validate (vertex=? (vertex- (vertex 1. 2. 3.) (vertex 1. 2. 3.)) (vertex 0. 0. 0.)))
    (validate (vertex=? (vertex-negate (vertex 1. 2. 3.)) (vertex -1. -2. -3.)))
    (validate (vertex=? (vertex-scalar* (vertex 1. 2. 3.) 5.) (vertex 5. 10. 15.0)))
    (validate (= (vertex-norm (vertex 1. 0. 0.)) 1.))
    (validate (vertex=? (vertex-normalize (vertex 5. 0. 0.)) (vertex 1. 0. 0.)))
    (validate (= (vertex-distance (vertex 1. 0. 0.) (vertex 5. 0. 0.)) 4.))
    ;; destructive
    (let ((v (make-vertex)))
      (vertex+! v (vertex 1. 2. 3.) (vertex 1. 2. 3.))
      (validate (vertex=? v (vertex 2. 4. 6.)))
      (vertex-! v (vertex 1. 2. 3.) (vertex 1. 2. 3.))
      (validate (vertex=? v (vertex 0. 0. 0.)))
      (vertex-init! v 1. 2. 3.)
      (vertex-negate! v v)
      (validate (vertex=? v (vertex -1. -2. -3.)))
      (vertex-init! v 1. 2. 3.)
      (vertex-scalar*! v v 5.)
      (validate (vertex=? v (vertex 5. 10. 15.0)))
      (vertex-init! v 5. 0. 0.)
      (vertex-normalize! v v)
      (validate (vertex=? v (vertex 1. 0. 0.)))))
  
  
  ;;;
  ;;;; matrix
  ;;;
  
  
  (method (test-matrix)
    (validate (matrix=? (matrix 1. 0. 0. 0.
                                0. 1. 0. 0.
                                0. 0. 1. 0.
                                0. 0. 0. 1.)
                        (make-identity-matrix)))
    (validate (matrix=? (matrix-multiply (make-identity-matrix) (make-identity-matrix)) (make-identity-matrix)))
    (let ((t (make-translation-matrix (vertex 1. 2. 3.))))
      (validate (matrix=? t (matrix 1. 0. 0. 1.
                                    0. 1. 0. 2.
                                    0. 0. 1. 3.
                                    0. 0. 0. 1.)))
      (make-identity-matrix! t)
      (validate (matrix=? t (make-identity-matrix))))
    (let ((p (make-projection-matrix 45.0 1.5 1.0 30.0)))
      (matrix-copy! p (make-identity-matrix))
      (validate (matrix=? p (make-identity-matrix))))
    (let ((v (make-view-matrix (vertex 10. 2. 10.) (make-standard-lookat))))
      ))
  
  
  ;;;
  ;;;; cross-product
  ;;;


  (method (test-cross-product)
    (let ((x (vertex 1. 0. 0.))
          (y (vertex 0. 1. 0.))
          (z (vertex 0. 0. 1.))
          (sight (vertex 0. 0. -1.))
          (up (vertex 0. 1. 0.))
          (right (vertex 1. 0. 0.)))
      ;; axis
      (validate (vertex=? (cross-product x y) z))
      (validate (vertex=? (cross-product y x) (vertex-negate z)))
      
      ;; lookat
      (validate (vertex=? (cross-product sight up) right))))
  
  
  ;;;
  ;;;; ray-intersects-triangle?
  ;;;
  
  
  (method (test-ray-intersects-triangle?)
    (validate (ray-intersects-triangle? (vertex 0. 0. 0.) (vertex 0. 0. 1.) (vertex 0. 1. 1.) (vertex -1. -1. 1.) (vertex 1. -1. 1.)))
    (validate (not (ray-intersects-triangle? (vertex 0. 0. 0.) (vertex 0. 0. -1.) (vertex 0. 1. 1.) (vertex -1. -1. 1.) (vertex 1. -1. 1.))))
    (validate (ray-intersects-triangle? (vertex 0. 0. 0.) (vertex 0. 0. 1.) (vertex 0. 1. 1.) (vertex -1. 0. 1.) (vertex 1. 0. 1.)))
    (validate (not (ray-intersects-triangle? (vertex 0. 0. 0.) (vertex 0. 0. 1.) (vertex 0. 1. 1.) (vertex -1. 0.1 1.) (vertex 1. 0. 1.)))))
    
  
  ;;;
  ;;;; test-ray-intersects-sphere?
  ;;;

  
  (method (test-ray-intersects-sphere?)
    (validate (ray-intersects-sphere? (vertex 0. 0. 0.) (vertex 0. 0. 1.) (vertex 0. 0. 1.) 1.))
    (validate (ray-intersects-sphere? (vertex 0. 0. 0.) (vertex 0. 0. -1.) (vertex 0. 0. 1.) 1.))
    (validate (not (ray-intersects-sphere? (vertex 0. 0. 0.) (vertex 0. 0. -1.) (vertex 0. 0. 1.) .9))))))
