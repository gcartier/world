;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Triangles
;;;


(module world.triangle jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.foreign)
        (world.geometry)
        (world.plane)
        (world.polygon)
        (world.texture)
        (time))


(definition public (make-triangle element normal v1 v2 v3 radius)
  (new Triangle element normal v1 v2 v3 radius))


(definition public (make-smooth-triangle element normal v1 v2 v3 radius n1 n2 n3)
  (new Smooth-Triangle element normal v1 v2 v3 radius n1 n2 n3))


;;;
;;;; Triangle
;;;


(class Triangle extends Polygon
  
  
  (method override (initialize element normal v1 v2 v3 radius)
    (nextmethod element (vector v1 v2 v3) radius normal))
  
  
  (method override (triangle-count)
    1)
  
  
  (method override (add-triangles uv add)
    (let ((v1 (get-v1))
          (v2 (get-v2))
          (v3 (get-v3))
          (n1 (get-normal1))
          (n2 (get-normal2))
          (n3 (get-normal3)))
      (add v1 v2 v3 n1 n2 n3 (f32vector-ref uv 0) (f32vector-ref uv 1) (f32vector-ref uv 2) (f32vector-ref uv 3) (f32vector-ref uv 4) (f32vector-ref uv 5))))
  
  
  (method override (iterate-triangles proc)
    (proc (get-v1)
          (get-v2)
          (get-v3)))
  
  
  (method public (get-v1)
    (vector-ref vertices 0))
  
  (method public (get-v2)
    (vector-ref vertices 1))
  
  (method public (get-v3)
    (vector-ref vertices 2))
  
  
  (method public virtual (get-normal1)
    normal)
  
  (method public virtual (get-normal2)
    normal)
  
  (method public virtual (get-normal3)
    normal))


;;;
;;;; Smooth-Triangle
;;;


(class Smooth-Triangle extends Triangle
  
  
  (slot n1 <f32vector> initialize #f getter generate)
  (slot n2 <f32vector> initialize #f getter generate)
  (slot n3 <f32vector> initialize #f getter generate)
  
  
  (method override (initialize element normal v1 v2 v3 radius n1 n2 n3)
    (nextmethod element normal v1 v2 v3 radius)
    (set! n1~self n1)
    (set! n2~self n2)
    (set! n3~self n3))
  
  
  (method override (get-normal1)
    n1)
  
  (method override (get-normal2)
    n2)
  
  (method override (get-normal3)
    n3)))
