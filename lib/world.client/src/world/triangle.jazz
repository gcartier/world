;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Triangles
;;;


(module world.triangle jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.foreign)
        (world.geometry)
        (world.plane)
        (world.polygon)
        (world.texture)
        (time))


(definition public (make-triangle element normal v1 v2 v3)
  (new Triangle element normal v1 v2 v3))


(definition public (make-smooth-triangle element normal v1 v2 v3 n1 n2 n3)
  (new Smooth-Triangle element normal v1 v2 v3 n1 n2 n3))


(definition protected (f32vector->triangle elem vect <f32vector> offset <fx>)
  (allocate Triangle
    (vertex (f32vector-ref vect (+ offset 0)) (f32vector-ref vect (+ offset 1)) (f32vector-ref vect (+ offset 2)))
    (vertex (f32vector-ref vect (+ offset 3)) (f32vector-ref vect (+ offset 4)) (f32vector-ref vect (+ offset 5)))
    elem
    (vector (vertex (f32vector-ref vect (+ offset  6)) (f32vector-ref vect (+ offset  7)) (f32vector-ref vect (+ offset  8)))
            (vertex (f32vector-ref vect (+ offset  9)) (f32vector-ref vect (+ offset 10)) (f32vector-ref vect (+ offset 11)))
            (vertex (f32vector-ref vect (+ offset 12)) (f32vector-ref vect (+ offset 13)) (f32vector-ref vect (+ offset 14))))
    (f32vector-ref vect (+ offset 18))))


;;;
;;;; Triangle
;;;


(class Triangle extends Polygon
  
  
  (method override (initialize element normal v1 v2 v3)
    (nextmethod element (vector v1 v2 v3) normal))
  
  
  (method override (triangle-count) <fx>
    1)
  
  
  (method override (add-triangles uv add)
    (let ((v1 (get-v1))
          (v2 (get-v2))
          (v3 (get-v3))
          (n1 (get-normal1))
          (n2 (get-normal2))
          (n3 (get-normal3)))
      (add v1 v2 v3 n1 n2 n3 (f32vector-ref uv 0) (f32vector-ref uv 1) (f32vector-ref uv 2) (f32vector-ref uv 3) (f32vector-ref uv 4) (f32vector-ref uv 5))))
  
  
  (method override (iterate-triangles proc)
    (proc (get-v1)
          (get-v2)
          (get-v3)))
  
  
  (method public (get-v1)
    (vector-ref vertices 0))
  
  (method public (get-v2)
    (vector-ref vertices 1))
  
  (method public (get-v3)
    (vector-ref vertices 2))
  
  
  (method public virtual (get-normal1)
    normal)
  
  (method public virtual (get-normal2)
    normal)
  
  (method public virtual (get-normal3)
    normal)
  
  
  (method override (polygon-move! vect <f32vector> offset <fx>) <void>
    (let ((v1 (get-v1))
          (v2 (get-v2))
          (v3 (get-v3)))
      (f32vector-set! vect (+ offset  0) (vertex-x origin))
      (f32vector-set! vect (+ offset  1) (vertex-y origin))
      (f32vector-set! vect (+ offset  2) (vertex-z origin))
      (f32vector-set! vect (+ offset  3) (vertex-x normal))
      (f32vector-set! vect (+ offset  4) (vertex-y normal))
      (f32vector-set! vect (+ offset  5) (vertex-z normal))
      (f32vector-set! vect (+ offset  6) (vertex-x v1))
      (f32vector-set! vect (+ offset  7) (vertex-y v1))
      (f32vector-set! vect (+ offset  8) (vertex-z v1))
      (f32vector-set! vect (+ offset  9) (vertex-x v2))
      (f32vector-set! vect (+ offset 10) (vertex-y v2))
      (f32vector-set! vect (+ offset 11) (vertex-z v2))
      (f32vector-set! vect (+ offset 12) (vertex-x v3))
      (f32vector-set! vect (+ offset 13) (vertex-y v3))
      (f32vector-set! vect (+ offset 14) (vertex-z v3))
      (f32vector-set! vect (+ offset 15) +nan.0)
      (f32vector-set! vect (+ offset 16) +nan.0)
      (f32vector-set! vect (+ offset 17) +nan.0)
      (f32vector-set! vect (+ offset 18) radius))))


;;;
;;;; Smooth-Triangle
;;;


(class Smooth-Triangle extends Triangle
  
  
  (slot n1 <f32vector> getter generate)
  (slot n2 <f32vector> getter generate)
  (slot n3 <f32vector> getter generate)
  
  
  (method override (initialize element normal v1 v2 v3 n1 n2 n3)
    (nextmethod element normal v1 v2 v3)
    (set! n1~self n1)
    (set! n2~self n2)
    (set! n3~self n3))
  
  
  (method override (get-normal1)
    n1)
  
  (method override (get-normal2)
    n2)
  
  (method override (get-normal3)
    n3)))
