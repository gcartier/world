;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World I/O
;;;


(module world.io jazz


(import (jazz.designer)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.library.component)
        (jazz.system)
        (jazz.ui.window)
        (jazz.version)
        (world)
        (world.autoload)
        (world.dye)
        (world.geometry)
        (world.settings)
        (world.syntax (phase syntax))
        (world.window))


;;;
;;;; Default
;;;


(definition public (select-default-world)
  (let ((start-world (find-setting 'world.start-world #f)))
    (if (not start-world)
        (new-world)
      (let ((dir (find-world start-world)))
        (if (not dir)
            (let ((world (new-world)))
              (display-error~ world (format "Unable to find world: {s}" start-world)))
          (open-world (new-file~ dir (format "_{a}.world" start-world))))))))


(definition public (find-world name)
  (continuation-capture
    (lambda (return)
      (for-each (lambda (worlds-dir)
                  (for-each (lambda (world-dir)
                              (when (filename=? (get-base~ world-dir) name)
                                (continuation-return return world-dir)))
                            (collect-directories~ worlds-dir)))
                (worlds-directories))
      #f)))


(definition public (worlds-directories)
  (or (find-setting 'world.worlds-directories #f)
      (let ((queue (new-queue)))
        (define (add-if-exists dir)
          (when (exists?~ dir)
            (enqueue queue dir)
            #t))
        
        (add-if-exists {Directory Settings "worlds"})
        (or (add-if-exists (new-brother~ (world-directory '()) "worlds"))
            (add-if-exists {Directory Home "worlds"}))
        
        (queue-list queue))))


;;;
;;;; World
;;;


(definition public (new-world)
  (let ((model 'World)
        (unit 'world.data))
    (let ((tag (string->symbol (format "<{a}>" model))))
      (let ((form (construct-form (list tag) (imports-locator (list unit)) #f)))
        (make-world form)))))


(definition public (open-world file)
  (make-world (convert-world (read-form file))))


(definition public (make-world form)
  (let ((current (current-world+))
        (world (instantiate~ form)))
    (when current
      (close~ current))
    (unless (get-title~ world)
      (let ((origin (get-origin~ form)))
        (when origin
          (set-title~ world (get-name~ (get-parent~ origin))))))
    (set-designer~ world (new Designer form: form reference: world))
    (set-window~ world (current-world-window))
    (setup-world~ world)
    (setup-opengl~ world)
    (resize-scene~ world)
    (initialize-world~ world)
    (let ((start-zone (or (let ((start-zones (find-setting 'world.start-zones #f)))
                            (and start-zones
                                 (let ((pair (assoc (get-title~ world) start-zones)))
                                   (and pair
                                        (cdr pair)))))
                          (get-start-zone~ world))))
      (if (not start-zone)
          (new-zone)
        (open-zone (new-file~ (get-parent~ (get-moniker~ world)) (list "zones" (format "{a}.zone" start-zone))))))
    (delay-event
      (lambda ()
        (gc)
        (reset-last-tick~ world)
        (set-render-ready?~ world #t)
        (start-world~ world)))
    world))


(definition (convert-world form)
  (let ((version (or (get-version~ (get-data~ form)) {Version 1 0})))
    (cond ((nu=? version {Version 1 0})
           form)
          (else
           (error "Unsupported world version: {a}" (present-string~ version))))))


;;;
;;;; Zone
;;;


(definition public (new-zone)
  (let ((model 'Zone)
        (unit 'world.data))
    (let ((tag (string->symbol (format "<{a}>" model))))
      (let ((form (construct-form (list tag) (imports-locator (list unit)) #f)))
        (make-zone form)))))


(definition public (open-zone file)
  (make-zone (convert-zone (read-form file))))


(definition public (make-zone form)
  (let ((world (current-world)))
    (let ((current (current-zone+))
          (zone (instantiate~ form)))
      (when current
        (close~ current))
      (set-target~ world #f)
      (unless (get-title~ zone)
        (let ((origin (get-origin~ form)))
          (when origin
            (set-title~ zone (get-base~ origin)))))
      (set-designer~ zone (new Designer form: form reference: zone))
      (initialize-zone~ zone)
      (update-children~ zone)
      (zone-update~ world)
      (when (eq? (get-start-mode~ world) 'first-person)
        (activate-first-person~ world)
        (camera-behind-player~ world)
        (follow-player~ world))
      (when (get-sun-cycle?~ zone)
        (position-sun~ zone -.20))
      zone)))


(definition (convert-zone form)
  (define (convert->1.1)
    (define (convert-children form)
      (for-each convert (get-children~ form)))
    
    (define (convert form)
      (let ((x (get-property~ form x:)))
        (when x
          (let ((y (get-property~ form y:))
                (z (get-property~ form z:)))
            (remove-property~ form x:)
            (remove-property~ form y:)
            (remove-property~ form z:)
            (set-property~ form position: (vertex x y z)))))
      (let ((width (get-property~ form width:)))
        (when width
          (let ((height (get-property~ form height:))
                (depth (get-property~ form depth:)))
            (remove-property~ form width:)
            (remove-property~ form height:)
            (remove-property~ form depth:)
            (set-property~ form size: (vertex width height depth)))))
      (let ((color (get-property~ form color:)))
        (when color
          (set-property~ form color: (color->dye color))))
      (convert-children form))
    
    (convert-children form)
    form)
  
  (let ((version (or (get-version~ (get-data~ form)) {Version 1 0})))
    (cond ((nu=? version {Version 1 0})
           (convert->1.1))
          ((nu=? version {Version 1 1})
           form)
          (else
           (error "Unsupported zone version: {a}" (present-string~ version)))))))
