;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World I/O
;;;


(module world.io jazz


(import (jazz.designer)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (jazz.library.component)
        (jazz.system)
        (world)
        (world.autoload))


;;;
;;;; Default
;;;


(definition public (select-default-world)
  (let ((start-world (find-setting 'world.start-world #f)))
    (if (not start-world)
        (new-world)
      (let ((dir (find-world start-world)))
        (if (not dir)
            (let ((world (new-world)))
              (display-error~ world (format "Unable to find world: {s}" start-world)))
          (open-world (new-file~ dir (format "_{a}.world" start-world))))))))


(definition public (find-world name)
  (continuation-capture
    (lambda (return)
      (for-each (lambda (worlds-dir)
                  (for-each (lambda (world-dir)
                              (when (filename=? (get-base~ world-dir) name)
                                (continuation-return return world-dir)))
                            (collect-directories~ worlds-dir)))
                (find-setting 'world.worlds-directories '()))
      #f)))


(definition public (worlds-directories)
  (or (find-setting 'world.worlds-directories #f)
      (list {Directory Settings "worlds"} {Directory Home "worlds"})))


;;;
;;;; World
;;;


(definition public (new-world)
  (let ((model 'World)
        (unit 'world.data))
    (let ((tag (string->symbol (format "<{a}>" model))))
      (let ((form (construct-form (list tag) (imports-locator (list unit)) #f)))
        (make-world form)))))


(definition public (open-world file)
  (make-world (read-form file)))


(definition public (make-world form)
  (let ((current world)
        (world (instantiate~ form)))
    (when current
      (close~ current))
    (unless (get-title~ world)
      (let ((origin (get-origin~ form)))
        (when origin
          (set-title~ world (get-name~ (get-parent~ origin))))))
    (set-designer~ world (new Designer form: form reference: world))
    (set-window~ world world-window)
    (setup-world~ world)
    (setup-opengl~ world)
    (resize-scene~ world)
    (initialize-world~ world)
    (let ((start-zone (or (let ((start-zones (find-setting 'world.start-zones #f)))
                            (and start-zones
                                 (let ((pair (assoc (get-title~ world) start-zones)))
                                   (and pair
                                        (cdr pair)))))
                          (get-start-zone~ world))))
      (if (not start-zone)
          (new-zone)
        (open-zone (new-file~ (get-parent~ (get-moniker~ world)) (list "zones" (format "{a}.zone" start-zone))))))
    (start-world~ world)
    world))


;;;
;;;; Zone
;;;


(definition public (new-zone)
  (let ((model 'Zone)
        (unit 'world.data))
    (let ((tag (string->symbol (format "<{a}>" model))))
      (let ((form (construct-form (list tag) (imports-locator (list unit)) #f)))
        (make-zone form)))))


(definition public (open-zone file)
  (make-zone (read-form file)))


(definition public (make-zone form)
  (let ((current zone)
        (zone (instantiate~ form)))
    (when current
      (close~ current))
    (set-target~ world #f)
    (unless (get-title~ zone)
      (let ((origin (get-origin~ form)))
        (when origin
          (set-title~ zone (get-base~ origin)))))
    (set-designer~ zone (new Designer form: form reference: zone))
    (initialize-zone~ zone)
    (update-children~ zone)
    (zone-update~ world)
    (when (eq? (get-start-mode~ world) 'first-person)
      (activate-first-person~ world)
      (follow-player~ world))
    zone)))
