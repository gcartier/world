;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Renderer
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.renderer jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.library)
        (world)
        (world.autoload)
        (world.geometry)
        (world.support)
        (world.syntax (phase syntax)))


;;;
;;;; Renderers
;;;


(definition renderers
  (make-table test: eq?))

(definition public (get-renderers)
  renderers)

(definition public (register-renderer name renderer)
  (table-set! renderers name renderer))

(definition public (find-renderer name)
  (table-ref renderers name))


(definition public opaque-renderer
  #f)

(definition public transparent-renderer
  #f)

(definition public culled-transparent-renderer
  #f)

(definition public wireframe-renderer
  #f)

(definition public color-renderer
  #f)

(definition public character-renderer
  #f)

(definition public character2-renderer
  #f)

(definition public animation-opaque-renderer
  #f)

(definition public animation-transparent-renderer
  #f)

(definition public animation-culled-transparent-renderer
  #f)


(definition public (prepare-renderes)
  (define (register-renderers)
    (register-renderer 'opaque opaque-renderer)
    (register-renderer 'transparent transparent-renderer)
    (register-renderer 'culled-transparent culled-transparent-renderer)
    (register-renderer 'wireframe wireframe-renderer)
    (register-renderer 'color color-renderer)
    (register-renderer 'character character-renderer)
    (register-renderer 'character2 character2-renderer)
    (when (get-animations?)
      (register-renderer 'animation-opaque animation-opaque-renderer)
      (register-renderer 'animation-transparent animation-transparent-renderer)
      (register-renderer 'animation-culled-transparent animation-culled-transparent-renderer)))
  
  (set! opaque-renderer (new Opaque-Renderer 'dynamic-phong))
  (set! transparent-renderer (new Transparent-Renderer 'dynamic-phong))
  (set! culled-transparent-renderer (new Culled-Transparent-Renderer 'dynamic-phong))
  (set! wireframe-renderer (new Wireframe-Renderer 'wireframe))
  (set! color-renderer (new Color-Renderer 'color))
  (set! character-renderer (new Character-Renderer 'playerframe))
  (set! character2-renderer (new Character2-Renderer 'color))
  (when (get-animations?)
    (set! animation-opaque-renderer (new Opaque-Renderer 'skeletal-phong animation?: #t))
    (set! animation-transparent-renderer (new Transparent-Renderer 'skeletal-phong animation?: #t))
    (set! animation-culled-transparent-renderer (new Culled-Transparent-Renderer 'skeletal-phong animation?: #t)))
  (register-renderers))


(definition public (reset-renderes)
  (reset~ opaque-renderer)
  (reset~ transparent-renderer)
  (reset~ culled-transparent-renderer)
  (reset~ wireframe-renderer)
  (reset~ color-renderer)
  (reset~ character-renderer)
  (reset~ character2-renderer)
  (when (get-animations?)
    (reset~ animation-opaque-renderer)
    (reset~ animation-transparent-renderer)
    (reset~ animation-culled-transparent-renderer)))


(definition protected (report-renderers)
  (define (report name renderer)
    (debug name
           (get-size~ (get-meshes~ renderer))
           (get-size~ (get-entities~ renderer))))
  
  (report opaque: opaque-renderer)
  (report transparent: transparent-renderer)
  (report culled-transparent: culled-transparent-renderer)
  (report wireframe: wireframe-renderer)
  (report color: color-renderer)
  (report character: character-renderer)
  (report character2: character2-renderer)
  (when (get-animations?)
    (report animation-opaque: animation-opaque-renderer)
    (report animation-transparent: animation-transparent-renderer)
    (report animation-culled-transparent: animation-culled-transparent-renderer)))


;;;
;;;; Renderer
;;;


(class Mesh-Renderer extends Object
  
  
  (slot program    getter generate)
  (slot animation? getter generate)
  (slot meshes     getter generate)
  (slot entities   getter generate)
  
  
  (method override (initialize program (animation?: animation? #f))
    (let ((world (current-world)))
      (set! program~self (find-mesh-program~ world program))
      (set! animation?~self animation?)
      (set! meshes~self (new Axis 0))
      (set! entities~self (new Axis 0))))
  
  
  (method (reset)
    (set-size~ meshes 0)
    (set-size~ entities 0))
  
  
  (method (add-mesh mesh)
    (axis-add meshes mesh))
  
  
  (method (add-entity entity model mesh)
    (axis-add entities entity)
    (axis-add entities model)
    (axis-add entities mesh))
  
  
  (method (draw-meshes)
    (let ((size (get-size~ meshes))
          (content (get-content~ meshes)))
      (loop (for i from 0 below size)
            (let ((mesh (vector-ref content i)))
              (draw-mesh~ program mesh)))))
  
  
  (method (draw-entities)
    (let ((size (get-size~ entities))
          (content (get-content~ entities)))
      (loop (for i from 0 below size by 3)
            (let ((entity (vector-ref content i))
                  (model (vector-ref content (+ i 1)))
                  (mesh (vector-ref content (+ i 2))))
              (let ((matrix (matrix-multiply (get-matrix~ entity) (get-global-matrix~ model)))
                    (material (get-material~ mesh)))
                (bind-render-uniforms~ program material)
                (bind-uniforms entity model mesh matrix)
                (draw-mesh~ program mesh)
                (unbind-render-uniforms)
                (unbind-render-uniforms~ program))))))
  
  
  (method protected virtual (bind-uniforms entity model mesh matrix)
    (bind-uniforms~ program mesh matrix (and animation? (get-bone-matrices~ (get-morphing~ entity)))))
  
  
  (method protected virtual (unbind-render-uniforms)
    (unbind-render-uniforms~ program)))


;;;
;;;; Opaque-Renderer
;;;


(class Opaque-Renderer extends Mesh-Renderer
  
  
  (method (render-entities)
    (use~ program)
    (glDisable GL_BLEND)
    (glDisable GL_ALPHA_TEST)
    (draw-entities)
    (unuse~ program)))


;;;
;;;; Transparent-Renderer
;;;


(class Transparent-Renderer extends Mesh-Renderer
  
  
  (method (render-entities)
    (use~ program)
    (glDisable GL_BLEND)
    (glEnable GL_ALPHA_TEST)
    (glAlphaFunc GL_GREATER .7)
    (draw-entities)
    (glDisable GL_ALPHA_TEST)
    (glDisable GL_BLEND)
    (unuse~ program)))


;;;
;;;; Culled-Transparent-Renderer
;;;


(class Culled-Transparent-Renderer extends Mesh-Renderer
  
  
  (method (render-entities)
    (use~ program)
    (glEnable GL_CULL_FACE)
    (glCullFace GL_FRONT)
    (glDisable GL_BLEND)
    (glEnable GL_ALPHA_TEST)
    (glAlphaFunc GL_GREATER .7)
    (draw-entities)
    (glDisable GL_ALPHA_TEST)
    (glDisable GL_BLEND)
    (glCullFace GL_BACK)
    (glDisable GL_CULL_FACE)
    (unuse~ program)))


;;;
;;;; Wireframe-Renderer
;;;


(class Wireframe-Renderer extends Mesh-Renderer
  
  
  (method (render-entities)
    (use~ program)
    (glDisable GL_BLEND)
    (glEnable GL_ALPHA_TEST)
    (glAlphaFunc GL_GREATER .7)
    (draw-entities)
    (glDisable GL_ALPHA_TEST)
    (glDisable GL_BLEND)
    (unuse~ program)))


;;;
;;;; Color-Renderer
;;;


(class Color-Renderer extends Mesh-Renderer
  
  
  (method (render-entities)
    (use~ program)
    (glDisable GL_BLEND)
    (glEnable GL_ALPHA_TEST)
    (glAlphaFunc GL_GREATER .7)
    (draw-entities)
    (glDisable GL_ALPHA_TEST)
    (glDisable GL_BLEND)
    (unuse~ program))
  
  
  (method override (bind-uniforms entity model mesh matrix)
    (model-bind-uniforms~ model entity program mesh matrix)))


;;;
;;;; Wait
;;;


;; alpha-blend
@wait
((alpha-blend)
 (glEnable GL_BLEND)
 (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
 (glDisable GL_ALPHA_TEST)
 (draw-entities)
 (glDisable GL_BLEND))

;; additive
@wait
((additive)
 (glEnable GL_BLEND)
 (glBlendFunc GL_SRC_COLOR GL_ONE)
 (draw-entities)
 (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
 (glDisable GL_BLEND))

;; additive-alpha
@wait
((additive-alpha)
 (glEnable GL_BLEND)
 (glBlendFunc GL_SRC_ALPHA GL_ONE)
 (draw-entities)
 (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
 (glDisable GL_BLEND))

;; modulate
@wait
((modulate)
 (glEnable GL_BLEND)
 (glBlendFunc GL_DST_COLOR GL_SRC_COLOR)
 (draw-entities)
 (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
 (glDisable GL_BLEND))


;;;
;;;; Vegetation
;;;


@wait
((vegetation)
 (let ((texture (get-texture~ material)))
   ;; alpha test
   (glBindTexture GL_TEXTURE_2D (get-id~ texture))
   (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER GL_LINEAR)
   (glEnable GL_BLEND)
   (glBlendFunc GL_ONE GL_ONE_MINUS_SRC_ALPHA)
   (glEnable GL_ALPHA_TEST)
   (glAlphaFunc GL_GREATER .7)
   (draw-entities)
   (glDisable GL_ALPHA_TEST)
   (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
   ;; blend - doesn't seem to work for vegetation
   (when #f
     (glEnable GL_BLEND)
     (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
     (glDepthMask GL_FALSE)
     (draw-entities)
     (glDepthMask GL_TRUE))
   (glBindTexture GL_TEXTURE_2D 0)))


;;;
;;;; Character-Renderer
;;;


(class Character-Renderer extends Mesh-Renderer
  
  
  (method (render-entities)
    (use~ program)
    (glEnable GL_CULL_FACE)
    (glCullFace GL_FRONT)
    (glEnable GL_BLEND)
    (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
    (glEnable GL_ALPHA_TEST)
    (glAlphaFunc GL_GEQUAL .1)
    (draw-entities)
    (glDisable GL_ALPHA_TEST)
    (glCullFace GL_BACK)
    (glDisable GL_CULL_FACE)
    (unuse~ program))
  
  
  (method override (bind-uniforms entity model mesh matrix)
    (model-bind-uniforms~ model entity program mesh matrix)))


;;;
;;;; Character2-Renderer
;;;


(class Character2-Renderer extends Mesh-Renderer
  
  
  (method (render-entities)
    (use~ program)
    (glEnable GL_CULL_FACE)
    (glCullFace GL_FRONT)
    (glDisable GL_LIGHTING)
    (glEnable GL_BLEND)
    (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
    (glDisable GL_ALPHA_TEST)
    (draw-entities)
    (glDisable GL_ALPHA_TEST)
    (glDisable GL_BLEND)
    (glCullFace GL_BACK)
    (glDisable GL_CULL_FACE)
    (unuse~ program))
  
  
  (method override (bind-uniforms entity model mesh matrix)
    (model-bind-uniforms~ model entity program mesh matrix)))


;;;
;;;; Register
;;;


(definition public (register-entities entities)
  (for-each register-entity entities))


(definition public (register-entity entity (renderer #f))
  (let ((animate? (entity-animate?~ entity)))
    (define (add-model model)
      (let ((meshes (entity-meshes~ entity model)))
        (loop (for mesh in-vector meshes)
              (let ((renderer (or renderer (effective-renderer model mesh))))
                ;; quicky for test
                (when animate?
                  (cond ((eq? renderer opaque-renderer) (set! renderer animation-opaque-renderer))
                        ((eq? renderer transparent-renderer) (set! renderer animation-transparent-renderer))
                        ((eq? renderer culled-transparent-renderer) (set! renderer animation-culled-transparent-renderer))))
                (add-entity~ renderer entity model mesh)))
        (for-each add-model (get-children~ model))))
    
    (define (effective-renderer model mesh)
      (let ((material (get-material~ mesh)))
        ;; or is a quicky for test
        (or (and material (get-renderer~ material))
            (model-renderer~ model animate?))))
    
    (when (get-visible?~ entity)
      (add-model (cache-model~ entity)))))


;;;
;;;; Render
;;;


(definition public (render-opaque)
  (render-entities~ opaque-renderer)
  (when (get-animations?)
    (render-entities~ animation-opaque-renderer)))


(definition public (render-transparent)
  (render-entities~ wireframe-renderer)
  (render-entities~ color-renderer)
  (render-entities~ transparent-renderer)
  (render-entities~ culled-transparent-renderer)
  (when (get-animations?)
    (render-entities~ animation-transparent-renderer)
    (render-entities~ animation-culled-transparent-renderer)))


(definition public (render-character)
  (render-entities~ character2-renderer)
  (render-entities~ character-renderer)))
