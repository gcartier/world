;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Buffers
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.buffer jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.foreign)
        (time))


;;;
;;;; Buffer
;;;


(class Buffer extends Object
  
  
  (slot buffer  <int+> initialize #f getter generate)
  (slot floats  <int+> initialize #f getter generate)
  (slot indices <int+> initialize #f getter generate)
  
  
  (method public (bind)
    (when buffer
      (glBindBuffer GL_ARRAY_BUFFER buffer)))
  
  (method public (unbind)
    (glBindBuffer GL_ARRAY_BUFFER 0))
  
  
  (method public (empty?)
    (not buffer))
  
  
  ;; Upload data to video card
  (method public (set-data content floats-per-indice)
    (set! floats (f32vector-length content))
    (set! indices (/ floats floats-per-indice))
    (glBindBuffer GL_ARRAY_BUFFER buffer)
    (glBufferDataFloat* GL_ARRAY_BUFFER (f32vector-length content) content GL_STATIC_DRAW)
    (glBindBuffer GL_ARRAY_BUFFER 0))
  
  
  ;; Enable vertex attrib array before passing values to a shader
  (method public (enable-vertex-attrib location)
    (when (or (/= location -1) validate-shaders?)
      (glEnableVertexAttribArray location)))
  
  ;; Disable vertex attrib array
  (method public (disable-vertex-attrib location)
    (when (or (/= location -1) validate-shaders?)
      (glDisableVertexAttribArray location)))

  
  (method public (draw)
    (when buffer
      (glDrawArrays GL_TRIANGLES 0 indices)))
  
  
  ;; Set the vertex attrib pointer to pass to the vertex shader
  (method public (set-attrib-pointer location size type stride offset)
    (when (or (/= location -1) validate-shaders?)
      (glVertexAttribPointer* location size type GL_FALSE stride offset)))
  
  
  (method public (generate-buffer)
    (when buffer
      (free-buffer))
    (set! buffer (glGenBuffers*)))
  
  
  (method public (free-buffer)
    (when buffer
      (glDeleteBuffers* buffer)
      (set! indices #f)
      (set! floats #f)
      (set! buffer #f)))
  
  
  (method override (destroy)
    (free-buffer))))
