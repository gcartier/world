;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Buffers
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.buffer jazz


(import (jazz.opengl.glew)
        (world.foreign))


(proclaim (warn optimizations))


;;;
;;;; Buffer
;;;


(class Buffer extends Object
  
  
  (slot buffer  <int+> initialize #f getter generate)
  (slot floats  <fx+>  initialize #f getter generate)
  (slot indices <fx+>  initialize #f getter generate)
  
  
  (method public (bind)
    (glBindBuffer GL_ARRAY_BUFFER buffer))
  
  (method public (unbind)
    (glBindBuffer GL_ARRAY_BUFFER 0))
  
  
  (method public (empty?)
    (not buffer))
  
  
  ;; Upload data to video card
  (method public (set-data content size <fx> floats-per-indice <fx>)
    (set! floats size)
    (set! indices (fxround/ floats floats-per-indice))
    (glBindBuffer GL_ARRAY_BUFFER buffer)
    (glBufferDataFloat* GL_ARRAY_BUFFER size content GL_STATIC_DRAW)
    (glBindBuffer GL_ARRAY_BUFFER 0))
  
  
  ;; Enable vertex attrib array before passing values to a shader
  (method public (enable-vertex-attrib location)
    (glEnableVertexAttribArray location))
  
  ;; Disable vertex attrib array
  (method public (disable-vertex-attrib location)
    (glDisableVertexAttribArray location))

  
  (method public (draw)
    (glDrawArrays GL_TRIANGLES 0 indices))
  
  
  ;; Set the vertex attrib pointer to pass to the vertex shader
  (method public (set-attrib-pointer location size type stride offset)
    (glVertexAttribPointer* location size type GL_FALSE stride offset))
  
  
  (method public (generate-buffer)
    (free-buffer)
    (set! buffer (glGenBuffers*)))
  
  
  (method public (free-buffer)
    (when buffer
      (glDeleteBuffers* buffer)
      (set! indices #f)
      (set! floats #f)
      (set! buffer #f)))
  
  
  (method override (destroy)
    (free-buffer))))
