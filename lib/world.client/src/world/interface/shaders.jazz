;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Shaders
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.shaders jazz


(import (jazz.geometry)
        (world)
        (world.autoload)
        (world.entity)
        (world.face)
        (world.geometry)
        (world.interface.showcase)
        (world.interface.tiles)
        (world.quad)
        (world.renderers)
        (world.syntax (phase syntax)))


(class Shaders-Showcase extends World-Showcase
  
  
  (method override (showcase-showing)
    (let ((world (current-world))
          (zone (current-zone)))
      (let ((target (first-target~ world)))
        (if (is? target Entity)
            target
          zone))))
  
  
  (method override (showcase-front)
    (if (is? showing Entity)
        (nextmethod)
      0.))
  
  
  (method override (showcase-back)
    (if (is? showing Entity)
        (nextmethod)
      0.))
  
  
  (method override (showcase-top)
    (if (is? showing Entity)
        1.
      (nextmethod)))
  
  
  (method override (showcase-bottom)
    (if (is? showing Entity)
        .15
      (nextmethod)))
  
  
  (method override (showcase-left)
    (if (is? showing Entity)
        0.
      (nextmethod)))
  
  
  (method override (showcase-right)
    (if (is? showing Entity)
        .25
      (nextmethod)))
  
  
  (method override (showcase-radius)
    (if (is? showing Entity)
        .5
      .35))
  
  
  (method override (showcase-spacing)
    (if (is? showing Entity)
        .75
      .85))
  
  
  (method override (showcase-elements showing)
    (define (rotate entity)
      (rotate~ entity PI)
      entity)
    
    (let ((world (current-world))
          (zone (current-zone)))
      (if (or (is? showing Zone) (is? showing Face))
          (let ((camera (cache-camera))
                (spacing .85)
                (x 0.)
                (y 0.)
                (z 0.))
            (map (lambda (program)
                   (let ((material (find-block-material~ world program)))
                     (let ((uv (image-rect~ (get-atlas~ material) (sample-program-image~ zone program))))
                       (let ((model (make-tile-model material uv))
                             (scaling .7))
                         (let ((entity (rotate (new Entity parent: self position: (vertex x y z) scale: (vertex scaling scaling scaling) user-data: program))))
                           (set-camera~ entity camera)
                           (set-renderer~ entity (require-renderer program))
                           (set-model~ entity model)
                           (increase! z spacing)
                           entity)))))
                 '(opaque transparent wireframe squareframe radial-blur motion-blur water julia sierpinski multitexture kaleidoscope tunnel square-tunnel fly @buggy pulse)))
        (let ((model (cache-model~ showing)))
          (let ((path (get-model~ showing))
                (bounds (get-bounds~ model)))
            (let ((camera (cache-camera))
                  (spacing (showcase-spacing))
                  (width (cuboid-width bounds))
                  (height (cuboid-height bounds))
                  (x 0.)
                  (y 0.)
                  (z 0.))
              (map (lambda (program)
                     ;; ignore depth
                     (let ((max (max width height)))
                       (let ((scaling (if (> max 1.) (/ 1. max) 1.)))
                         (let ((entity (new Entity parent: self model: path position: (vertex x y z) scale: (vertex scaling scaling scaling) animate?: #t user-data: program)))
                           (set-camera~ entity camera)
                           (set-shaders~ entity (and (neq? program 'phong) program))
                           (let ((lookat (get-lookat~ entity)))
                             (set-lookat~ entity (rotate-lookat& lookat PI/2 (get-up~ lookat))))
                           (increase! z spacing)
                           entity))))
                   '(phong wireframe squareframe radial-blur motion-blur water julia sierpinski multitexture kaleidoscope tunnel square-tunnel fly @buggy pulse))))))))
  
  
  (method override (showcase-selection entity entities)
    (define (determine-selection shaders)
      (find-if (lambda (entity)
                 (eq? shaders (get-shaders~ entity)))
               entities))
    
    (and (is? entity Entity) (determine-selection (get-shaders~ entity))))
  
  
  (method override (update-top new-top)
    (let ((world (current-world)))
      (unless (equal? new-top top)
        (set! top new-top)
        (interface-layout~ world))))
  
  
  (method override (draw-selection)
    (when selection
      (if (is? showing Entity)
          (draw-target-circle~ selection)
        (draw-selection-quad (texture-quad selection)))))
  
  
  (method override (selection-addable?)
    (is? showing Entity))
  
  
  (method override (selection-update selection)
    (let ((world (current-world))
          (zone (current-zone)))
      (let ((shaders (get-shaders~ selection)))
        (for-each (lambda (entity)
                    (when (is? entity Entity)
                      (with-designer~ zone entity
                        (lambda (designer container)
                          (set-property~ designer entity 'shaders shaders)))))
                  (get-selection~ world)))))
  
  
  (method (texture-quad entity)
    (let ((pos (get-position~ entity))
          (half .35))
      (let ((v1 (vertex+ pos (vertex 0. (- half) (- half))))
            (v2 (vertex+ pos (vertex 0. half (- half))))
            (v3 (vertex+ pos (vertex 0. half half)))
            (v4 (vertex+ pos (vertex 0. (- half) half))))
        (make-quad entity v1 v2 v3 v4))))))
