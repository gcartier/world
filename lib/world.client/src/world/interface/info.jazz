;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Info Panel
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.info jazz


(import (jazz.graphic)
        (jazz.markup)
        (jazz.outline)
        (jazz.tree)
        (jazz.ui)
        (world.info)
        (world.interface.panel)
        (world.settings))


(class Info-Panel extends World-Panel
  
  
  (form
    (<install>                                        size: {Dimension 420 639} background: {Color red: .000 green: .000 blue: .000 alpha: .450} layout-type: fill
      (<Border-View>                                  border-color: {Color Medium-Gray} border-type: edge style: entry layout-type: fill
        (<Scroller-View>                              style: document hscroll?: #t vscroll?: #t
          (<content~>                                 layout-type: fill
            (<Tree-Header>                            style: document shown?: #f
              (<content~>
                (<Info-Tree>           name: info     portfolio: :images filled-column: value show-unfocused-selection?: #f background: #f
                  (<Tree-Node-Column>  name: property title: "Property" width: 150 controls?: #f toplevel-controls?: #f display-images?: #f display-levels?: #f)
                  (<Tree-Label-Column> name: value    title: "Value" width: 65)))))))))
  
  
  (method package (view-info self)
    (let ((tree (locate self 'info))
          (section-color {Color red: 220 green: 0 blue: 0})
          (content-color {Color Light-Gray})
          (font (new Font font-name: 'tahoma point-size: (if (developer?) 13.5 16.) shadow: 'thin antialias: 'none))
          (row-height (if (developer?) 18 22)))
      (remove-every-row tree)
      (with-update-locked self
        (lambda ()
          (define (add-section title (info #f))
            (add-row tree
              state: 'expanded
              row-height: row-height
              children: (list (new Tree-Node title: title font: font color: section-color)
                              (new Tree-Label title: (if info (->string info) "") font: font color: content-color))))
          
          (define (add-info father title info)
            (add-row tree
              father: father
              state: 'expanded
              row-height: row-height
              children: (list (new Tree-Node title: title font: font color: content-color)
                              (new Tree-Label title: (->string info) font: font color: content-color))))
          
          (let ((sections (map car (registered-infos)))
                (display (world-setting 'world.info-display #f)))
            (let ((effective-sections (if display (collect-if registered-info? display) sections)))
              (for-each (lambda (section)
                          (let ((proc (registered-info section)))
                            (proc add-section add-info)))
                        effective-sections))))))))


;;;
;;;; Info-Tree
;;;


(class Info-Tree extends Tree-View
  
  
  (method override (mouse-down self evt)
    )))
