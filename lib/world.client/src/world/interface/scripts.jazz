;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Scripts
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is WorldScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2014
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See http://github.com/gcartier/world for details.


(module world.interface.scripts jazz


(import (jazz.io)
        (jazz.jml)
        (jazz.graphic)
        (jazz.library)
        (jazz.system)
        (jazz.ui)
        (world)
        (world.autoload)
        (world.interface.frame)
        (world.syntax (phase syntax)))


(class Script-Run extends World-Frame
  
  
  (form
    (<install>                                      size: {Dimension 420 639} layout-type: border
      (<caption~>                                   title: "Scripts")
      (<content~>                                   layout-type: fill
        (<Scroller-View>                            style: document hscroll?: #t vscroll?: #t
          (<content~>                               layout-type: fill
            (<Tree-Header>                          style: document shown?: #f
              (<content~>
                (<Tree-View>          name: scripts portfolio: :images filled-column: script double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
                  (<Tree-Node-Column> name: script  title: "Script" width: 235 toplevel-controls?: #f display-images?: #f)))))))))
  
  
  (method override (conclude initargs)
    (nextmethod initargs)
    (view-scripts))
  
  
  (method override (focus-default)
    (acquire-focus~ (get-tree)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-tree)
    (locate 'scripts))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-double-click evt)
    (add-selected))
  
  
  (method override (on-return-press evt)
    (add-selected))
  
  
  (method override (on-escape-press evt)
    (close))
  
  
  (method (add-selected)
    (let ((world (current-world)))
      (let ((script (get-single-selected-data~ (get-tree))))
        (when script
          (close)
          (run-script~ world script)))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (view-scripts)
    (let ((world (current-world)))
      (define (partition-scripts scripts)
        (partition scripts key: (lambda (script)
                                  (bind (dir . path) script
                                    (if (> (length path) 1)
                                        (car path)
                                      "")))
          test: filename=?))
      
      (let ((user? (user-role?~ (get-application)))
            (scripts (partition-scripts (collect-scripts~ world)))
            (tree (get-tree)))
        (remove-every-row~ tree)
        (with-update-locked
          (lambda ()
            (define (add-children father children)
              (for-each (lambda (script)
                          (bind (dir . path) script
                            (let ((path (join path "/"))
                                  (title (extract-base (join (if (> (length path) 1) (cdr path) path) "."))))
                              (add-row~ tree
                                father: father
                                children: (list (new Tree-Node title: title font: {Font Label-User^} color: {Color White}))
                                user-data: script))))
                        children))
            
            (for-each (lambda (partition)
                        (bind (title . children) partition
                          (unless (and user? (filename=? title "tutorial"))
                            (if (empty-string? title)
                                (add-children #f children)
                              (let ((father (add-row~ tree
                                              state: 'expanded
                                              children: (list (new Tree-Node title: title font: {Font Label-User^} color: {Color red: 220 green: 0 blue: 0})))))
                                (add-children father children))))))
                      scripts))))))))
