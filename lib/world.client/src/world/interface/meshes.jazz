;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Meshes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.meshes jazz


(import (jazz.geometry)
        (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.block)
        (world.draw)
        (world.dye)
        (world.dyes)
        (world.entity)
        (world.generate)
        (world.geometry)
        (world.interface)
        (world.interface.showcase)
        (world.material)
        (world.object)
        (world.opengl)
        (world.syntax (phase syntax))
        (world.texture))


(class Meshes-Showcase extends World-Showcase
  
  
  (method override (showcase-showing)
    (let ((world (current-world)))
      (let ((target (first-target~ world)))
        (when (is? target Entity)
          target))))
  
  
  (method override (showcase-margin)
    .25)
  
  
  (method override (showcase-spacing)
    .75)
  
  
  (method override (showcase-z)
    -.25)
  
  
  (method override (showcase-elements showing)
    (let ((world (current-world)))
      (if (not showing)
          '()
        (let ((model (get-model~ showing)))
          (let ((path (get-path~ model))
                (bounds (get-bounds~ model)))
            (define (collect-meshes)
              (cons #f (vector->list (get-meshes~ model))))
            
            (let ((camera (cache-camera))
                  (spacing (showcase-spacing))
                  (width (cuboid-width bounds))
                  (height (cuboid-height bounds))
                  (scaling .25)
                  (x (showcase-x))
                  (y (showcase-y))
                  (z 0.))
              (map (lambda (mesh)
                     ;; ignore depth
                     (let ((block? (is? showing Block))
                           (max (max width height)))
                       (let ((scaling (if block? .5 (if (> max 1.) (/ 1. max) 1.))))
                         (let ((entity (new Entity path: path position: (vertex x y z) scale: (vertex scaling scaling scaling) animate?: #t)))
                           (when (not path)
                             (set-model~ entity model))
                           (cache-model~ entity)
                           (when mesh
                             (set-meshes~ entity (vector mesh)))
                           (set-camera~ entity camera)
                           (let ((lookat (get-lookat~ entity)))
                             (rotate-lookat! lookat PI/2 (get-up~ lookat))
                             (update-matrix~ entity))
                           (increase! z spacing)
                           entity))))
                   (collect-meshes))))))))
  
  
  (method override (showcase-selection entity entities)
    (define (determine-selection meshes)
      (find-if (lambda (entity)
                 (equal? meshes (get-meshes~ entity)))
               entities))
    
    (and entity (determine-selection (get-meshes~ entity))))
  
  
  (method override (draw-selection)
    (when selection
      (draw-target-circle~ selection)))
  
  
  (method override (selection-addable?)
    #t)
  
  
  (method override (selection-update selection)
    (let ((world (current-world)))
      (let ((entity (first-target~ world))
            (meshes (get-meshes~ selection)))
        (set-meshes~ entity meshes))))))
