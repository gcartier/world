;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Meshes
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.meshes jazz


(import (jazz.geometry)
        (gaea.geometry)
        (world)
        (world.autoload)
        (world.context)
        (world.element)
        (world.entity)
        (world.interface.showcase)
        (world.model))


(class Meshes-Showcase extends World-Showcase
  
  
  (method override (showcase-showing self)
    (let ((world (current-world))
          (me (current-me)))
      (let ((target (first-target world)))
        (cond ((is? target Entity)
               target)
              ((orb? (get-model me))
               #f)
              (else
               me)))))
  
  
  (method override (showcase-top self)
    1.)
  
  
  (method override (showcase-bottom self)
    .15)
  
  
  (method override (showcase-left self)
    0.)
  
  
  (method override (showcase-right self)
    .25)
  
  
  (method override (showcase-radius self)
    .5)
  
  
  (method override (showcase-spacing self)
    .75)
  
  
  (method override (showcase-elements self showing)
    (let ((world (current-world)))
      (if (not showing)
          '()
        (let ((model (cache-model showing)))
          (let ((path (get-model showing))
                (bounds (get-bounds model)))
            (define (collect-masks)
              (let ((meshes (vector->list (get-meshes model))))
                (if (>= (length meshes) 2)
                    (cons #f (map (lambda (mesh)
                                    (map (lambda (m)
                                           (eq? m mesh))
                                         meshes))
                                  meshes))
                  (list #f))))
            
            (let ((camera (cache-camera self))
                  (spacing (showcase-spacing self))
                  (width (cuboid-width bounds))
                  (height (cuboid-height bounds))
                  (scaling .25)
                  (x 0.)
                  (y 0.)
                  (z 0.))
              (map (lambda (mask)
                     ;; ignore depth
                     (let ((max (max width height)))
                       (let ((scaling (if (> max 1.) (/ 1. max) 1.)))
                         (let ((entity (new Entity parent: self model: path position: (vertex x y z) scale: (vertex scaling scaling scaling))))
                           (when (not path)
                             (set-model entity model))
                           (cache-model entity)
                           (set-meshes-mask entity mask)
                           (set-camera entity camera)
                           (let ((lookat (get-lookat entity)))
                             (set-lookat entity (rotate-lookat& lookat PI/2 (get-up lookat))))
                           (increase! z spacing)
                           entity))))
                   (collect-masks))))))))
  
  
  (method override (showcase-selection self entity entities)
    (define (determine-selection meshes-mask)
      (find-if (lambda (entity)
                 (equal? meshes-mask (get-meshes-mask entity)))
               entities))
    
    (and entity (determine-selection (get-meshes-mask entity))))
  
  
  (method override (draw-selection self)
    (when selection
      (draw-target-circle selection)))
  
  
  (method override (selection-addable? self)
    #t)
  
  
  (method override (selection-update self selection)
    (let ((world (current-world))
          (zone (current-zone)))
      (let ((model (cache-model selection))
            (meshes-mask (get-meshes-mask selection)))
        (for-each (lambda (entity)
                    (when (and (is? entity Entity)
                               (eq? (cache-model entity) model))
                      (with-designer zone entity
                        (lambda (designer container)
                          (set-property designer entity 'meshes-mask meshes-mask)))))
                  (get-selection world)))))))
