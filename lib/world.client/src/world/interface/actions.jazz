;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Actions
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.actions jazz


(import (jazz.action)
        (jazz.geometry)
        (jazz.graphic)
        (jazz.markup)
        (jazz.resource)
        (jazz.ui)
        (world)
        (world.context)
        (world.interface)
        (world.interface.panel)
        (world.interface.tooltip)
        (world.parameters))


;;;
;;;; World-Actionbar
;;;


(class World-Actionbar extends World-Panel
  
  
  ;; YOWNU quick try
  (property auto-show/hide? initialize #f accessors generate)
  
  
  (form
    (<install> background: {Color Medium alpha: .7} layout-type: flow layout-padding: 2))
  
  
  (method package virtual (bar-height self)
    38))


;;;
;;;; World-Iconbar
;;;


(class World-Iconbar extends World-Actionbar
  
  
  (form
    (<install> background: #f))
  
  
  (method override (bar-height self)
    38))


;;;
;;;; World-Radial-Menu
;;;


(class World-Radial-Menu extends World-Iconbar
  
  
  (form
    (<install> layout-type: radial))
  
  
  (method override (get-transparent? self)
    #t)
  
  
  (method protected (setup-visibility self)
    (for-each (lambda (child)
                (when (is? child World-Action-Button)
                  (let ((action-item (require-action-item (get-action-handler child))))
                    (set-visible? child (effective-visible? action-item)))))
              children))
  
  
  (method override (draw-children self surface nodes context)
    (set-font surface {Font Label-User})
    (let ((visible (get-visible-children self)))
      (let ((images? (every? (lambda (child)
                              (is? (get-resource child) Image-Resource))
                            visible)))
        (for-each (lambda (child)
                    (let ((resource (get-resource child))
                          (bounds (inflate-rect (acquire self child (get-bounds child)) 9 9)))
                      (if images?
                          (ellipse surface bounds {Color Medium alpha: 0.} {Color Medium alpha: .7})
                        (ellipse surface bounds {Color Black alpha: .0} {Color Black alpha: .45}))
                      (let ((action-item (require-action-item (get-action-handler child))))
                        (let ((title (get-title action-item)))
                          (let ((width (get-text-width surface title)))
                            (let ((h (+ (get-left bounds) (center width (rect-width bounds))))
                                  (offset (if images? 4 0)))
                              (draw-text surface h (+ (get-bottom bounds) offset) title {Color World-Ochre})))))))
                  visible)))
    (nextmethod self surface nodes context)))


;;;
;;;; World-Central-Menu
;;;


(class World-Central-Menu extends Layout-View implements User-Closable
  
  
  (form
    (<install> layout-type: center))
  
  
  (method override (user-close self)
    (let ((world (current-world)))
      (reset-central-menu world)))
  
  
  (method override (get-transparent? self)
    #t)
  
  
  (method protected (setup-visibility self)
    (let ((wheel (child self 'wheel)))
      (define (wheel-button name)
        (child wheel name))
      
      (for-each (lambda (child)
                  (when (is? child World-Radial-Menu)
                    (set-visible? (wheel-button (get-name child))
                                  (some? (lambda (child)
                                           (and (is? child World-Action-Button)
                                                (let ((action-item (require-action-item (get-action-handler child))))
                                                  (effective-visible? action-item))))
                                         (get-children child)))))
                children))))


;;;
;;;; World-Central-Wheel
;;;


(class World-Central-Wheel extends World-Iconbar
  
  
  (form
    (<install> layout-type: central))
  
  
  (method protected (setup-alpha self bar)
    (let ((name (get-name bar)))
      (for-each (lambda (child)
                  (set-alpha child (if (eq? (get-name child) name)
                                       #f
                                     .5)))
                (cdr children))))
  
  
  (method override (draw-children self surface nodes context)
    (set-font surface {Font Label-User})
    (let ((bounds (inflate-rect (get-bounds self) -10 -10)))
      (ellipse surface bounds {Color Black alpha: .0} {Color Black alpha: .45})
      (let ((menu (get-radial-menu (current-world))))
        (when menu
          (let ((title (get-title menu)))
            (let ((width (get-text-width surface title)))
              (let ((h (+ (get-left bounds) (center width (rect-width bounds)))))
                (draw-text surface h (- (get-bottom bounds) 92) title {Color World-Ochre})))))))
    (nextmethod self surface nodes context)))


;;;
;;;; World-Separator
;;;


(class World-Separator extends Separator-View
  
  
  (form
    (<install> size: {Dimension 38 0})))


;;;
;;;; World-Action-Button
;;;


(class World-Action-Button extends Action-Icon
  

  (property selected? initialize #f accessors explicit)
  
  
  (form
    (<install> size: {Dimension 38 38} portfolio: :icons))
  
  
  (method override (process-action self sender . rest)
    (apply nextmethod self sender rest)
    ;; bring central menu to front in case
    ;; the action brought up a view over it
    (let ((world (current-world)))
      (when (is? parent World-Radial-Menu)
        (let ((central (get-central-menu world)))
          (when (eq? (get-parent parent) central)
            (bring-to-front central))))))
               
  
  
  ;;;
  ;;;; Tooltip
  ;;;
  
  
  (method override (get-tooltip-context self pos)
    (define (present-text)
      (let ((action-item (require-action-item action-handler)))
        (let ((title (get-title action-item))
              (shortcut (get-shortcut action-item))
              (alternate-shortcut (first-alternate-shortcut action-item))
              (description (get-description action-item)))
          `(<span>
             (<paragraph>
               ,@(if shortcut
                     `((<text> ,title)
                       (<text> " ")
                       (<text> color: World-Yellow ,(format "({a})" (shortcuts-present shortcut alternate-shortcut))))
                   `(,title)))
             ,@(if description
                   (if (string? description)
                       `(""
                         (<paragraph> (<text> color: World-Green ,description)))
                     `(""
                       ,@(map (lambda (par)
                                (if (string? par)
                                    `(<paragraph> (<text> color: World-Green ,par))
                                  par))
                              description)))
                 '())))))
    
    ;; quick hack
    (and action-handler
    (let ((text (present-text))
          (bounds (get-tooltip-bounds self pos)))
      (tooltip-context self text bounds))))
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw-frame self surface)
    (define frame-color
      {Color red: .938 green: .938 blue: .005})
    
    (if (and selected? (memq? (get-state self) '(inactive highlighted out-pushed)))
        (draw-border self surface frame-color frame-color)
      (nextmethod self surface)))
  
  
  ;;;
  ;;;; Selected
  ;;;


  (method public (get-selected? self)
    selected?)


  (method public (set-selected? self flag)
    (when (neq? flag selected?)
      (set! selected? flag)
      (invalidate-view self))))


;;;
;;;; World-Parameter-Button
;;;


(class World-Parameter-Button extends World-Action-Button
  
  
  (property parameter initialize #f accessors generate)
  
  
  ;;;
  ;;;; Drawing
  ;;;
  
  
  (method override (draw-frame self surface)
    (define frame-color
      {Color red: .938 green: .938 blue: .005})
    
    (if (world-parameter parameter)
        (draw-border self surface frame-color frame-color)
      (nextmethod self surface))))


;;;
;;;; World-Ongoing-Button
;;;


(class World-Ongoing-Button extends World-Action-Button
  
  
  (property key initialize #f accessors generate)
  
  
  (method override (set-state self value)
    (nextmethod self value)
    (let ((world (current-world)))
      (set-ongoing-key world
        (if (memq? value '(pushed out-pushed))
            key
          #f)))))


;;;
;;;; World-Central-Button
;;;


(class World-Central-Button extends World-Action-Button
  
  
  (property angle initialize #f accessors generate)))
