;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Who Panel
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.who jazz


(import (jazz.event)
        (jazz.graphic)
        (jazz.handler)
        (jazz.markup)
        (jazz.outline)
        (jazz.tree)
        (jazz.ui)
        (world)
        (world.autoload)
        (world.context)
        (world.interface.frame)
        (world.settings))


(class Who-Panel extends World-Frame
  
  
  (form
    (<install>                                            size: {Dimension 420 639} layout-type: border
      (<caption~>                                         title: "Who")
      (<content~>                                         layout-type: fill
        (<Tab-View>                         name: tab     selection-handler: {Event-Handler :form on-selection-change}
          (<Tab-Sheet>                      name: zone    title: "Zone"
            (<!>                            name: content layout-type: fill
              (<Who-Zone>)))
          (<Tab-Sheet>                      name: server  title: "Server"
            (<!>                            name: content layout-type: fill
              (<Who-Server>)))))))
  
  
  (method override (conclude self initargs)
    (nextmethod self initargs)
    (update-players self 'zone))
  
  
  (method override (focus-default self)
    )
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (on-escape-press self evt)
    (close self))
  
  
  (method package (on-selection-change self evt)
    (let ((sender (get-sender evt)))
      (update-players self (get-selection-name sender))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (update-players self sheet-name)
    (let ((view (first-child (get-content (locate self sheet-name)))))
      (view-players view))))


;;;
;;;; Who-View
;;;


(class Who-View extends Scroller-View
  
  
  (form
    (<install>                                 style: document hscroll?: #t vscroll?: #t
      (<content~>                                    layout-type: fill
        (<Tree-Header>                               style: document shown?: #f
          (<content~>
            (<Tree-View>          name: players      portfolio: :images filled-column: player background: #f double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
              (<Tree-Node-Column> name: player       title: "Player" width: 235 toplevel-controls?: #f display-images?: #f display-levels?: #f)))))))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method package (get-tree self)
    (locate self 'players))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-double-click self evt)
    (travel-to self))
  
  
  (method override (on-return-press self evt)
    (travel-to self))
  
  
  ;;;
  ;;;; Travel
  ;;;
  
  
  (method protected virtual (travel-to self)
    )
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method package virtual (view-players self)
    ))


;;;
;;;; Who-Zone
;;;


(class Who-Zone extends Who-View
  
  
  (method override (travel-to self)
    (when (admin?)
      (let ((zone (current-zone)))
        (let ((player (get-single-selected-data (get-tree self))))
          (when player
            (close (find-ascendant self Who-Panel))
            (teleport zone (get-position player) (get-sight player)))))))
  
  
  (method override (view-players self)
    (let ((zone (current-zone))
          (me (current-me)))
      (let ((players (collect-players zone))
            (tree (get-tree self)))
        (remove-every-row tree)
        (with-update-locked self
          (lambda ()
            (for-each (lambda (player)
                        (unless (eq? player me)
                          (let ((title (get-name player)))
                            (add-row tree
                              children: (list (new Tree-Node title: title font: {Font Label-User} color: {Color World-Ochre}))
                              user-data: player))))
                      (sort nu<? players key: get-name))))))))


;;;
;;;; Who-Server
;;;


(class Who-Server extends Who-View
  
  
  (method override (travel-to self)
    (let ((player (get-single-selected-data (get-tree self))))
      (when player
        (bind (name zone) player
          (close (find-ascendant self Who-Panel))
          (change-zone (current-client) zone)))))
  
  
  (method override (view-players self)
    (let ((zone (current-zone))
          (me (current-me)))
      (let ((players (connected-players (current-tier)))
            (tree (get-tree self)))
        (remove-every-row tree)
        (with-update-locked self
          (lambda ()
            (for-each (lambda (player)
                        (bind (name zone presentation) player
                          (unless (equal? name (get-name me))
                            (let ((title (format "{a} @ {a}" name presentation)))
                              (add-row tree
                                children: (list (new Tree-Node title: title font: {Font Label-User} color: {Color World-Ochre}))
                                user-data: player)))))
                      (sort nu<? players key: first)))))))))
