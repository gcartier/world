;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Interface
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface jazz


(import (jazz.action)
        (jazz.graphic)
        (jazz.markup)
        (jazz.window)
        (world)
        (world.autoload)
        (world.context)
        (world.interface.sheet)
        (world.settings)
        (world.syntax (phase syntax))
        (world.texture))


;;;
;;;; Layout
;;;


(definition (showcase)
  (let ((world (current-world)))
    (get-showcase world)))


(definition (showcase-top)
  (let ((world (current-world)))
    (let ((showcase (get-showcase world)))
      (and showcase (get-top showcase)))))


(definition (effective-top (blocks? #f))
  (let ((interface (current-interface)))
    (let ((top (showcase-top)))
      (if top
          (- (get-height interface) top (if blocks? -52 0))
        ;; top of second row bars
        (- 58 (case current-role
                ((user creator)
                 26)
                (else
                 0)))))))


(definition public (info-panel-layouter panel)
  (set-size panel {Dimension 350 1200})
  (set-location panel 'head))


(definition public (profiler-panel-layouter panel)
  (let ((top (effective-top)))
    (set-size panel {Dimension 690 565})
    (set-location panel (list 6 (- (- top) (if (showcase-top) 80 7))))))


(definition public (chronology-panel-layouter panel)
  (let ((top (effective-top)))
    (set-size panel {Dimension 600 105})
    (set-location panel '(center 110))))


(definition public (color-panel-layouter panel)
  (set-size panel (new Dimension 430 110))
  (set-location panel '(center 10)))


(definition public (target-panel-layouter panel)
  (let ((top (effective-top)))
    (set-size panel {Dimension 300 178})
    (set-location panel (list -6 (- (- top) (if (showcase-top) 12 7))))))


(definition public (compass-panel-layouter panel)
  (set-size panel {Dimension 170 170})
  (set-location panel '(-5 5)))


(definition public (map-panel-layouter panel)
  (set-size panel {Dimension 1024 512})
  (set-location panel '(center center)))


(definition public (combat-panel-layouter panel)
  (set-size panel {Dimension 160 40})
  (set-location panel '(10 10)))


(definition public (character-panel-layouter panel)
  (let ((top (effective-top)))
    (set-size panel {Dimension 330 190})
    (set-location panel (list 10 (- (- top) 250)))))


(definition public (belt-panel-layouter panel)
  (let ((slot-size 48)
        (top (effective-top)))
    (set-size panel (new Dimension (+ (* slot-size 9) 1) (+ slot-size 1)))
    (set-location panel (list 'center (- (- top) 27)))))


(definition public (bag-panel-layouter panel)
  (let ((slot-size 48)
        (top (effective-top)))
    (set-size panel (new Dimension (+ (* slot-size 9) 1) (+ (* slot-size 3) 1)))
    (set-location panel (list 'center (- (- top) 102)))))


(definition public (chest-panel-layouter panel)
  (let ((slot-size 48)
        (top (effective-top)))
    (set-size panel (new Dimension (+ (* slot-size 9) 1) (+ (* slot-size 3) 1)))
    (set-location panel (list 'center (- (- top) 272)))))


(definition public (crafting-panel-layouter panel)
  (let ((top (effective-top)))
    (set-size panel {Dimension 550 300})
    (set-location panel (list 'center (- (- top) 272)))))


(definition public (chat-panel-layouter panel)
  (let ((interface (current-interface)))
    (let ((width (get-width interface))
          (height (get-height interface))
          (top (effective-top #t)))
      (set-size panel (new Dimension (fxround (percent 28. width)) (fxround (percent 12. height))))
      (set-location panel (list 10 (- (- top) 27))))))


(definition public (command-bar-layouter bar)
  (let ((interface (current-interface)))
    (let ((width (get-width interface))
          (height (get-height interface))
          (top (effective-top #t)))
      (set-size bar (new Dimension (fxround (percent 28. width)) 19))
      (set-location bar (list 10 (- (- top) 7))))))


(definition public (script-panel-layouter panel)
  (let ((interface (current-interface)))
    (let ((width (get-width interface))
          (height (get-height interface))
          (top (effective-top))
          (border 63))
      (let ((fullscreen? (world-setting 'world.script-fullscreen? #f))
            (horizontal? (world-setting 'world.script-horizontal? #f))
            (vertical? (world-setting 'world.script-vertical? #f)))
        (if fullscreen?
            (begin
              (set-size panel (new Dimension (- width border) (- height border)))
              (set-location panel (list 0 0)))
          (let ((chat (if vertical? 0 (+ (fxround (percent 12. height)) 30))))
            (set-size panel (new Dimension (if horizontal? (- width border 10) (fxround (percent 37. width))) (max 10 (- height 120 chat))))
            (set-location panel (list 10 (- (- top) (if (showcase-top) 80 7) chat)))))))))


(definition public (models-bar-layouter bar)
  (let ((top (showcase-top)))
    (when top
      (set-position bar (new Point 10 (- top (get-height bar) 15))))))


(definition public (blocks-bar-layouter bar)
  (let ((top (showcase-top)))
    (when top
      (set-position bar (new Point 10 (- top (get-height bar) 15))))))


;;;
;;;; Interface
;;;


(class World-Interface extends World-Sheet
  
  
  (form
    (<install> layout-type: justify))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (slot draw-hooks initialize '() getter generate)
  
  
  (method (register-draw-hook self proc)
    (set! draw-hooks (cons proc draw-hooks)))
  
  
  (method (unregister-draw-hook self proc)
    (set! draw-hooks (remove! proc draw-hooks)))
  
  
  (method override (draw-player self)
    (let ((surface (get-surface texture))
          (handle (get-handle player)))
      (when (offscreen-region-invalid? handle)
        (set-clipper surface (get-box (get-offscreen-invalid-region handle)))
        (clear surface)
        (paint-drawing self surface '())
        (for-each (lambda (proc)
                    (proc surface))
                  draw-hooks)
        (map-texture texture)
        (validate-offscreen handle)))))


;;;
;;;; Shortcut
;;;


(definition public (shortcut-present shortcut)
  (let ((modifiers (get-modifiers shortcut))
        (key (get-key shortcut)))
    (format "{?:{l} ~}{a}" (not-null? modifiers) (map enumerator->symbol modifiers) key)))


(definition public (shortcuts-present shortcut alternate-shortcut)
  (if (not shortcut)
      ""
    (let ((shortcut (shortcut-present shortcut)))
      (if (not alternate-shortcut)
          shortcut
        (format "{a}, {a}" shortcut (shortcut-present alternate-shortcut)))))))
