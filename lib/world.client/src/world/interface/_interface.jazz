;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Interface
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface jazz


(import (jazz.jml)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.ui)
        (jazz.ui.window)
        (world.interface.player)
        (world.syntax (phase syntax))
        (world.texture))


;;;
;;;; Properties
;;;


(definition public (info-pane-properties)
  (list size: {Dimension 350 1200} location: 'head))

(definition public (profiler-pane-properties)
  (list size: {Dimension 690 480} location: '(head -69)))

(definition public (target-pane-properties)
  (list size: {Dimension 300 178} location: '(tail -69)))

(definition public (script-pane-properties)
  (list size: {Dimension 800 500} location: '(6 -65)))

(definition public (character-pane-properties)
  (list size: {Dimension 200 50} location: '(10 10)))

(definition public (chest-pane-properties)
  (let ((slot-size 48))
    (list size: (new Dimension (+ (* slot-size 9) 1) (+ (* slot-size 3) 1)) location: '(center -400))))

(definition public (bag-pane-properties)
  (let ((slot-size 48))
    (list size: (new Dimension (+ (* slot-size 9) 1) (+ (* slot-size 3) 1)) location: '(center -200))))

(definition public (belt-pane-properties)
  (let ((slot-size 48))
    (list size: (new Dimension (+ (* slot-size 9) 1) (+ slot-size 1)) location: '(center -100))))


;;;
;;;; Interface
;;;


(class World-Interface extends World-Player
  
  
  (form
    (<install> layout-type: justify))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (size-change size)
    (nextmethod size)
    (when texture
      (close~ texture))
    (set! texture (make-cairo-texture (get-width~ size) (get-height~ size))))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (slot draw-hooks initialize '() getter generate)
  
  
  (method (register-draw-hook proc)
    (set! draw-hooks (cons proc draw-hooks)))
  
  
  (method (unregister-draw-hook proc)
    (set! draw-hooks (remove! proc draw-hooks)))
  
  
  (method override (draw-player)
    (let ((surface (get-surface~ texture))
          (handle (get-handle~ player)))
      (when (offscreen-region-invalid? handle)
        (set-clipper~ surface (get-box~ (get-offscreen-invalid-region handle)))
        (clear~ surface)
        (paint-drawing surface '())
        (for-each (lambda (proc)
                    (proc surface))
                  draw-hooks)
        (map-texture~ texture)
        (validate-offscreen handle)))))


;;;
;;;; Shortcut
;;;


(definition public (present-shortcut shortcut)
  (let ((modifiers (get-modifiers~ shortcut))
        (key (get-key~ shortcut)))
    (format "{?:{l} ~}{a}" (not-null? modifiers) (map enumerator->symbol modifiers) key)))


(definition public (present-shortcuts shortcut alternate-shortcut)
  (if (not shortcut)
      ""
    (let ((shortcut (present-shortcut shortcut)))
      (if (not alternate-shortcut)
          shortcut
        (format "{a}, {a}" shortcut (present-shortcut alternate-shortcut)))))))
