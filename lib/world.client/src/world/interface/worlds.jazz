;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Open
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.worlds jazz


(import (jazz.graphic)
        (jazz.handler)
        (jazz.io)
        (jazz.markup)
        (jazz.outline)
        (jazz.resource)
        (jazz.tree)
        (jazz.ui)
        (world)
        (world.context)
        (world.io)
        (world.interface.frame)
        (world.window))


(class World-Open extends World-Frame
  
  
  (form
    (<install>                                     size: {Dimension 420 639} layout-type: border
      (<caption~>                                  title: "Worlds")
      (<content~>                                  layout-type: fill
        (<Scroller-View>                           style: document hscroll?: #t vscroll?: #t
          (<content~>                              layout-type: fill
            (<Tree-Header>                         style: document shown?: #f
              (<content~>
                (<Tree-View>          name: worlds portfolio: :images filled-column: world background: #f double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
                  (<Tree-Node-Column> name: world  title: "World" width: 235 toplevel-controls?: #f)))))))))
  
  
  (method override (conclude self initargs)
    (nextmethod self initargs)
    (view-worlds self))
  
  
  (method override (focus-default self)
    (acquire-focus (get-tree self)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method package (get-tree self)
    (locate self 'worlds))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-double-click self evt)
    (open-selected self))
  
  
  (method override (on-return-press self evt)
    (open-selected self))
  
  
  (method override (on-escape-press self evt)
    (close self))
  
  
  (method package (open-selected self)
    (let ((world (current-world)))
      (let ((info (get-single-selected-data (get-tree self))))
        (when info
          (bind (name . dir) info
            (close self)
            (open-directory self name dir))))))
  
  
  (method (open-directory self name dir)
    (with-window-cursor :wait
      (lambda ()
        (let ((zone (current-zone)))
          (let ((world (open-world 'standalone (new-file dir ".world"))))
            (display-message world (format "{a}{a}"
                                            (get-title world)
                                            (let ((title (get-title zone)))
                                              (if (not title)
                                                  ""
                                                (format " / {a}" title))))))))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (view-worlds self)
    (let ((tree (get-tree self)))
      (remove-every-row tree)
      (with-update-locked self
        (lambda ()
          (for-each (lambda (info)
                      (bind (name . dir) info
                        (add-row tree children: (list (new Tree-Node image: {Image-Resource "Document"} title: name font: {Font Label-User} color: {Color White}))
                          user-data: info)))
                    (sort ci<? (world-directories) key: car))))))))
