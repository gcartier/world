;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Server Open
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.servers jazz


(import (jazz.graphic)
        (jazz.handler)
        (jazz.io)
        (jazz.jml)
        (jazz.tree)
        (jazz.ui)
        (world)
        (world.autoload)
        (world.io)
        (world.interface.frame)
        (world.syntax (phase syntax))
        (world.task)
        (world.window))


(class Server-Open extends World-Frame
  
  
  (form
    (<install>                        size: {Dimension 420 639} layout-type: border
      (<caption~>                     title: "Servers")
      (<content~>                     layout-type: fill
        (<Servers-View> name: servers))))
  
  
  (method override (focus-default)
    (acquire-focus~ (get-tree)))
  
  
  (method (get-tree)
    (locate '(servers servers))))


;;;
;;;; Servers-View
;;;


(class Servers-View extends Scroller-View
  
  
  (form
    (<install>                                  style: document hscroll?: #t vscroll?: #t
      (<content~>                               layout-type: fill
        (<Tree-Header>                          style: document shown?: #f
          (<content~>
            (<Tree-View>          name: servers portfolio: :images filled-column: server multiple-selection?: #t background: #f double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
              (<Tree-Node-Column> name: server  title: "Server" width: 235 toplevel-controls?: #f display-images?: #f display-levels?: #f)))))))
    
  
  (method override (conclude initargs)
    (nextmethod initargs)
    (view-servers))

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-tree)
    (locate 'servers))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-double-click evt)
    (open-selected))
  
  
  (method override (on-return-press evt)
    (open-selected))
  
  
  (method override (on-escape-press evt)
    (close~ (find-ascendant World-Frame)))
  
  
  (method (open-selected)
    (let ((world (current-world)))
      (let ((file (get-single-selected-data~ (get-tree))))
        (when file
          (close~ (find-ascendant World-Frame))
          (open-file file)))))
  
  
  (method (open-file file)
    @w
    (with-window-cursor :wait
      (lambda ()
        (let ((world (current-world)))
          (let ((zone (open-zone file)))
            (display-message~ world (get-title~ zone)))))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (view-servers)
    (let ((world (current-world)))
      (define (collect-servers)
        (let ((servers-directories (get-servers-directories~ world))
              (queue (new-queue)))
          (for-each (lambda (directory)
                      (when (exists?~ directory)
                        (iterate-directory~ directory
                          (lambda (dir)
                            (let ((file (new-file~ dir ".client")))
                              (when (exists?~ file)
                                (enqueue queue file))))
                          files?: #f
                          directories?: #t
                          recursive?: #f)))
                    servers-directories)
          (queue-list queue)))
      
      (let ((tree (get-tree)))
        (remove-every-row~ tree)
        (with-update-locked
          (lambda ()
            (for-each (lambda (file)
                        (let ((title (get-name~ (get-parent~ file))))
                          (add-row~ tree
                            children: (list (new Tree-Node title: title font: {Font Label-User} color: {Color White}))
                            user-data: file)))
                      (sort ci<? (collect-servers) key: (lambda (file) (get-name~ (get-parent~ file)))))
            (select-first-data~ tree))))))))
