;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Zone Open
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.zones jazz


(import (jazz.graphic)
        (jazz.handler)
        (jazz.jml)
        (jazz.tree)
        (jazz.ui)
        (world)
        (world.autoload)
        (world.interface.frame)
        (world.syntax (phase syntax))
        (world.window))


(class Zone-Open extends World-Frame
  
  
  (property start-script? initialize #t accessors generate)
  
  
  (form
    (<install>                    size: {Dimension 420 639} layout-type: border
      (<caption~>                 title: "Zones")
      (<content~>                 layout-type: fill
        (<Zones-View> name: zones))))
  
  
  (method override (focus-default)
    (acquire-focus~ (get-tree)))
  
  
  (method (get-tree)
    (locate '(zones zones))))


;;;
;;;; Zones-View
;;;


(class Zones-View extends Scroller-View
  
  
  (form
    (<install>                                    style: document hscroll?: #t vscroll?: #t
      (<content~>                                 layout-type: fill
        (<Tree-Header>                            style: document shown?: #f
          (<content~>
            (<Tree-View>          name: zones portfolio: :images filled-column: zone multiple-selection?: #t background: #f double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
              (<Tree-Node-Column> name: zone  title: "Zone" width: 235 toplevel-controls?: #f display-images?: #f display-levels?: #f)))))))
    
  
  (method override (conclude initargs)
    (nextmethod initargs)
    (view-zones))

  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-tree)
    (locate 'zones))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-double-click evt)
    (open-selected))
  
  
  (method override (on-return-press evt)
    (open-selected))
  
  
  (method override (on-escape-press evt)
    (close~ (find-ascendant World-Frame)))
  
  
  ;;;
  ;;;; Open
  ;;;
  
  
  (method (open-selected)
    (let ((zone (get-single-selected-data~ (get-tree))))
      (when zone
        (close~ (find-ascendant World-Frame))
        (open-zone zone))))
  
  
  (method (open-zone zone)
    (with-window-cursor :wait
      (lambda ()
        (change-zone~ (current-tier) zone))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (view-zones)
    (update-zones))
  
  
  (method (update-zones)
    (define (partition-zones)
      (partition (available-zones~ (current-tier)) key: caar))
    
    (let ((tree (get-tree)))
      (remove-every-row~ tree)
      (with-update-locked
        (lambda ()
          (define (add-zones father zones)
            (for-each (lambda (info)
                        (bind (zone . presentation) info
                          (add-row~ tree
                            father: father
                            children: (list (new Tree-Node title: presentation font: {Font Label-User} color: {Color White}))
                            user-data: zone)))
                      (sort ci<? zones key: cdr)))
          
          (let ((father (add-row~ tree
                          state: 'expanded
                          children: (list (new Tree-Node title: "start" font: {Font Label-User} color: {Color red: 220 green: 0 blue: 0})))))
            (add-row~ tree
              father: father
              children: (list (new Tree-Node title: "<Start Zone>" font: {Font Label-User} color: {Color White}))
              user-data: 'start))
          (for-each (lambda (partition)
                      (bind (kind . zones) partition
                        (let ((title (case kind
                                       ((instance) "instances")
                                       (else "zones"))))
                          (let ((father (add-row~ tree
                                          state: 'expanded
                                          children: (list (new Tree-Node title: title font: {Font Label-User} color: {Color red: 220 green: 0 blue: 0})))))
                            (add-zones father zones)))))
                    (sort < (partition-zones) key: (lambda (partition) (if (eq? (car partition) 'instance) 1 0))))
          (select-first-data~ tree)))))))
