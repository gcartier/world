;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Showcase
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.showcase jazz


(import (jazz.component)
        (jazz.event)
        (jazz.opengl.glew)
        (jazz.view)
        (gaea.dye)
        (gaea.geometry)
        (gaea.movement)
        (gaea.plane)
        (world)
        (world.asset)
        (world.camera)
        (world.context)
        (world.draw)
        (world.dyes)
        (world.interface.actions)
        (world.light)
        (world.lighting)
        (world.renderer)
        (world.renderers)
        (world.scene)
        (world.settings)
        (world.sound)
        (world.window))


(class World-Showcase extends Scene
  
  
  (property category initialize #f accessors generate)
  
  
  (slot showing      initialize #f getter generate)
  (slot elements     initialize #f getter generate)
  (slot selection    initialize #f getter generate)
  (slot camera       initialize #f getter generate)
  (slot baked        initialize (make-table test: eq?) getter generate)
  (slot head         initialize #f getter generate)
  (slot tail         initialize #f getter generate)
  (slot top          initialize #f getter generate)
  (slot bottom       initialize #f getter generate)
  (slot point        initialize #f getter generate)
  (slot moved?       initialize #f getter generate)
  (slot scrolled?    initialize #f getter generate)
  (slot last-z       initialize #f getter generate)
  (slot last-seconds initialize #f getter generate)
  (slot last-speed   initialize #f getter generate)
  (slot pulse        initialize #f getter generate)
  (slot speed        initialize #f getter generate)
  
  
  ;;;
  ;;;; Activation
  ;;;
  
  
  (method package virtual (setup self)
    )
  
  
  (method package virtual (activate self)
    ;; so elements are there on first draw
    (cache-elements self))
  
  
  (method package virtual (deactivate self)
    )
  
  
  ;;;
  ;;;; Category
  ;;;
  
  
  (method protected virtual  (showcase-categories self)
    #f)
  
  
  (method package (previous-category self)
    (let ((categories (showcase-categories self)))
      (when categories
        (update-category self (previous-element categories category)))))
  
  
  (method package (next-category self)
    (let ((categories (showcase-categories self)))
      (when categories
        (update-category self (next-element categories category)))))
  
  
  (method package (update-category self cat)
    (set! category cat)
    (reset-elements self)
    (play-interface-sound "warcraft/user/selectTarget")
    (let ((bar (showcase-categories-bar)))
      (when (get-visible? bar)
        (invalidate-view bar))))
  
  
  ;;;
  ;;;; Elements
  ;;;
  
  
  (definition elements-mutex
    (make-mutex 'update-elements))
  
  
  (method package (cache-elements self)
    (let ((new-showing (showcase-showing self)))
      (define (update-elements)
        (parameterize ((inhibit-working? #t))
          (mutex-lock! elements-mutex)
          (let ((new-elements (list->vector (showcase-elements self new-showing))))
            (set! showing new-showing)
            (set! elements new-elements)
            (set-selection self (showcase-selection self new-showing new-elements))
            (update-camera self)
            (mutex-unlock! elements-mutex)
            elements)))
      
      (if (and elements (eq? showing new-showing))
          elements
        (if (showcase-wait? self)
            (with-window-cursor :wait
              (lambda ()
                (update-elements)))
          (update-elements)))))
  
  
  (method package (reset-elements self)
    (set! elements #f)
    (set-selection self #f))
  
  
  (method protected virtual (showcase-showing self)
    #f)
  
  
  (method protected virtual (showcase-vertical self)
    10)
  
  
  (method protected virtual (showcase-front self)
    (- (showcase-radius self)))
  
  
  (method protected virtual (showcase-back self)
    (showcase-radius self))
  
  
  (method protected virtual (showcase-top self)
    (showcase-radius self))
  
  
  (method protected virtual (showcase-bottom self)
    (- (showcase-radius self)))
  
  
  (method protected virtual (showcase-left self)
    (- (showcase-radius self)))
  
  
  (method protected virtual (showcase-right self)
    (showcase-radius self))
  
  
  (method protected virtual (showcase-radius self)
    .5)
  
  
  (method protected virtual (showcase-spacing self)
    1.)
  
  
  (method protected virtual (showcase-wait? self)
    #f)
  
  
  (method protected virtual (showcase-elements self showing)
    #f)
  
  
  (method protected virtual (showcase-selection self showing elements)
    #f)
  
  
  (method protected virtual (element-center self element)
    (get-position element))
  
  
  ;;;
  ;;;; Selection
  ;;;
  
  
  (method package (set-selection self sel)
    (let ((world (current-world)))
      (set! selection sel)
      (update-target-panel world)
      (refresh-interface world condition: '(target))))
  
  
  (method package virtual (selection-addable? self)
    #f)
  
  
  (method package virtual (selection-update self selection)
    )
  
  
  (method (draw-selection-quad self quad)
    (draw-polygon quad red-dye))
  
  
  ;;;
  ;;;; Camera
  ;;;
  
  
  (method package virtual (size-change self size)
    (size-change (cache-camera self) size)
    (update-camera self))
  
  
  (method (cache-camera self)
    (or camera
        (let ((world (current-world))
              (cam (make-camera)))
          (set-screen-width cam (get-width world))
          (set-screen-height cam (get-height world))
          (set-frustum-radius cam (showcase-spacing self))
          (set-position cam (vertex& -10. 3.15 0.))
          (init-lookat cam (vertex 1. 0. 0.) (vertex 0. 1. 0.))
          (derive-target cam)
          (camera-update cam)
          (prepare-projection cam)
          (prepare-view cam)
          (set! camera cam)
          camera)))
  
  
  (method (update-camera self)
    (let ((camera (cache-camera self)))
      (define (bounds)
        (let ((count (vector-length (cache-elements self))))
          (if (= count 0)
              (values #f #f)
            (let ((spacing (showcase-spacing self)))
              (values 0.
                      (* (- count 1) spacing))))))
      
      (define (position y z)
        (let ((position (get-position camera)))
          (vertex! position -10. y z)
          (derive-target camera)
          (camera-update camera)))
      
      (position 0. 0.)
      (bind-values (leftmost rightmost) (bounds)
        (if (or (not leftmost) (not rightmost))
            (begin
              (set! head #f)
              (set! tail #f)
              (set! top #f)
              (set! bottom #f))
          (let ((width (fxround (get-screen-width camera)))
                (height (fxround (get-screen-height camera)))
                (vertical (showcase-vertical self))
                (front (showcase-front self))
                (back (showcase-back self))
                (top (showcase-top self))
                (bottom (showcase-bottom self))
                (left (showcase-left self))
                (right (showcase-right self)))
            (let ((plane (new Plane (vertex front 0. 0.) (vertex -1. 0. 0.))))
              (define (screen-project h v)
                (bind-values (origin dir) (screen->world camera h v)
                  (let ((mvt (new Movement origin dir)))
                    (bind-values (collision-distance intersect?) (intersect-plane plane mvt)
                      (movement-end-to mvt collision-distance)))))
              
              (let ((v (- height vertical)))
                (let ((l (screen-project 12 v))
                      (r (screen-project (- width 10) v)))
                  (let ((y (+ (- (vertex-y l)) bottom)))
                    (let ((elements-width (- rightmost leftmost)))
                      (if (<= elements-width (- (vertex-z r) (vertex-z l)))
                          (begin
                            (set! head #f)
                            (set! tail #f)
                            (position y (+ leftmost (/ elements-width 2.))))
                        (set! head (+ (- leftmost (vertex-z l)) left))
                        (set! tail (max head (+ (- rightmost (vertex-z r)) right)))
                        (position y head)))))
                (update-top self (get-v (world->screen camera (vertex back top 0.))))
                (update-bottom self v))))))))
  
  
  (method protected virtual (update-top self new-top)
    (set! top new-top))
  
  
  (method protected virtual (update-bottom self new-bottom)
    (set! bottom new-bottom))
  
  
  ;;;
  ;;;; Lighting
  ;;;
  
  
  (method (bake-element self element)
    (unless (table-ref baked element #f)
      (let ((sun-position (showcase-sun self)))
        (bake-entity-uniform-lighting element 60. 0. sun-position))
      (table-set! baked element #t)))
  
  
  (method protected virtual (showcase-sun self)
    baked-sun)
  
  
  ;;;
  ;;;; Draw
  ;;;


  (method package virtual (draw-showcase self)
    (let ((preserved-camera (current-camera)))
      (prepare-view (cache-camera self))
      (set-current-camera (cache-camera self))
      (glClear GL_DEPTH_BUFFER_BIT)
      (draw-elements self)
      (draw-selection self)
      (set-current-camera preserved-camera)))


  (method protected virtual (draw-elements self)
    (reset-renderers)
    (let ((camera (cache-camera self))
          (elements (cache-elements self)))
      (loop (for n from 0 below (vector-length elements))
            (let ((element (vector-ref elements n)))
              (when (in-frustum? camera (element-center self element))
                (draw-element self element)))))
    (render-opaque)
    (render-transparent)
    (render-orb))
  
  
  (method protected virtual (draw-element self element)
    (register-entity element)
    (bake-element self element))
  
  
  (method protected virtual (draw-selection self)
    )
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method package virtual (in-showcase? self v)
    (let ((margin 15))
      (and top bottom (between? v (- top margin) (+ bottom margin)))))
  
  
  (method package virtual (showcase-mouse-down self h v)
    (set! point h)
    (set! moved? #f)
    (set! last-z (vertex-z (get-position (cache-camera self))))
    (set! last-seconds (current-seconds))
    (set! last-speed #f)
    (kill-autoscroll self))
  
  
  (method package virtual (showcase-mouse-drag self h v)
    (when (and point (not (near? h point 2)) head tail)
      (let ((camera (cache-camera self)))
        (let ((delta (/ (cast <fl> (- h point)) -100.)))
          (let ((position (get-position camera)))
            (let ((new-z (+ (vertex-z position) delta)))
              (vertex-z-set! position (min (max new-z head) tail))
              (derive-target camera)
              (camera-update camera))))
        (set! point h)
        (set! moved? #t)
        (let ((new-z (vertex-z (get-position camera)))
              (new-seconds (current-seconds))
              (old-z last-z)
              (old-seconds last-seconds))
          (set! last-z new-z)
          (set! last-seconds new-seconds)
          (unless (= new-seconds old-seconds)
            (let ((new-speed (/ (- new-z old-z) (- new-seconds old-seconds)))
                  (p .9))
              (set! last-speed (+ (* new-speed (- 1. p)) (* (or last-speed 0.) p)))))))))

  
  (method package virtual (showcase-mouse-up self h v)
    (let ((world (current-world)))
      (when (or (not moved?) (< (- (current-seconds) last-seconds) .1))
        (let ((camera (cache-camera self)))
          (if (and moved? head tail)
              (begin
                (set! scrolled? #f)
                (start-autoscroll self))
            (unless scrolled?
              (bind-values (position direction) (screen->world camera h v)
                (let ((closest (ray-closest-element self position direction)))
                  (when closest
                    (bind (poly dist) closest
                      (let ((element (get-element poly)))
                        (unless (eq? element selection)
                          (set-selection self element)
                          (selection-update self selection)
                          (play-interface-sound "warcraft/user/selectTarget"))))))))
            (set! point h)
            (set! moved? #f)
            (set! scrolled? #f)
            (set! last-speed #f)
            (kill-autoscroll self))))))
  
  
  (method package virtual (showcase-double-click self h v)
    )
  
  
  (method package virtual (showcase-wheel self reversed?)
    (when (and head tail)
      (let ((spacing (showcase-spacing self))
            (camera (cache-camera self)))
        (let ((position (get-position camera)))
          (let ((actual-z (vertex-z position)))
            (let ((scroll-z (+ actual-z (if reversed? (- spacing) spacing))))
              (let ((new-z (min (max scroll-z head) tail)))
                (when (/= new-z actual-z)
                  (set! scrolled? #t)
                  (vertex-z-set! position new-z)
                  (derive-target camera)
                  (camera-update camera)))))))))
  
  
  ;;;
  ;;;; Autoscroll
  ;;;
  
  
  (method (start-autoscroll self)
    (set! pulse (new Pulse 'autoscroll 0.01 (~ autoscroll-action self) delay: 0)))
  
  
  (method (kill-autoscroll self)
    (when pulse
      (close pulse)
      (set! pulse #f)))
  
  
  (method (autoscroll-action self)
    (when (and head tail last-speed)
      (let ((camera (cache-camera self)))
        (let ((position (get-position camera)))
          (let ((actual-z (vertex-z position)))
            (let ((scroll-z (+ actual-z (/ last-speed 100.))))
              (let ((new-z (min (max scroll-z head) tail)))
                (when (/= new-z actual-z)
                  (set! scrolled? #t)
                  (vertex-z-set! position (min (max new-z head) tail))
                  (derive-target camera)
                  (camera-update camera)
                  (when (or (<= new-z head)
                            (>= new-z tail))
                    (set! scrolled? #f)
                    (kill-autoscroll self))))))))))
  
  
  ;;;
  ;;;; Hit
  ;;;
  
  
  (method protected virtual (ray-closest-element self orig <vertex> dir <vertex>)
    (let ((entities (cache-elements self))
          (radius (showcase-spacing self)))
      (and entities
           (let ((closest #f)
                 (distance #f))
             (loop (for n from 0 below (vector-length entities))
                   (let ((entity (vector-ref entities n)))
                     (when (ray-intersects-sphere? orig dir (get-position entity) radius)
                       (for-each-bounding-quad$ entity #f
                         (lambda (p)
                           (let ((poly (copy-polygon p)))
                             (free-polygon$ p)
                             (let ((cls (ray-closest-triangle poly orig dir #f)))
                               (when cls
                                 (bind (poly dist) cls
                                   (when (or (not distance)
                                             (< dist distance))
                                     (set! closest (list poly dist))
                                     (set! distance dist)))))))))))
             closest)))))


;;;
;;;; Showcase Bar
;;;


(definition public (showcase-categories-bar)
  (let ((interface (current-interface)))
    (find-type interface World-Showcase-Categories-Bar)))


(class World-Showcase-Categories-Bar extends World-Actionbar))
