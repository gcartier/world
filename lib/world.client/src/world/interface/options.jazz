;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Options
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.options jazz


(import (jazz.application)
        (jazz.component)
        (jazz.event)
        (jazz.graphic)
        (jazz.markup)
        (jazz.outline)
        (jazz.screen)
        (jazz.text)
        (jazz.tree)
        (jazz.ui)
        (jazz.view)
        (world)
        (world.configure)
        (world.context)
        (world.creature)
        (world.interface.dialogs)
        (world.interface.frame)
        (world.interface.panel)
        (world.render)
        (world.settings)
        (world.sound)
        (world.support)
        (world.task)
        (world.track))


;;;
;;;; Restart
;;;


(definition install-needed
  #f)

(definition restart-needed?
  #f)

(definition protected (reset-install/restart)
  (set! install-needed #f)
  (set! restart-needed? #f))

(definition protected (need-install proc)
  (set! install-needed proc))

(definition protected (need-restart)
  (set! restart-needed? #t))

(definition protected (warn-if-restart-needed (title #f))
  (when restart-needed?
    (delay-event
      (lambda ()
        (with-task-mutex
          (lambda ()
            (when install-needed
              (install-needed))
            (open-message (format "{a} needs to be restarted for some options to take effect" (or title tier-title))
                          caption: "Restart"
                          type: 'message
                          closable?: #f
                          execute: (lambda (evt)
                                     (let ((dialog (find-ascendant (get-sender evt) World-Dialog)))
                                       (close dialog))))))))))


;;;
;;;; Frame
;;;


(class World-Options-Frame extends World-Frame
  
  
  (form
    (<install>                                              size: {Dimension 420 639} layout-type: border
      (<caption~>                                           title: "Options")
      (<content~>                                           layout-type: fill
        (<Scroller-View>                                    style: document hscroll?: #t vscroll?: #t
          (<content~>                                       layout-type: fill
            (<Tree-Header>                                  style: document shown?: #f
              (<content~>
                (<Tree-View>           name: options        portfolio: :images filled-column: option background: #f
                  (<Tree-Node-Column>  name: option         title: "Option" width: 235 toplevel-controls?: #f display-images?: #f display-levels?: #f)
                  (<Tree-Label-Column> name: shortcut       title: "Value" width: 115)))))))))
  
  
  (method override (conclude self initargs)
    (nextmethod self initargs)
    (view-options self))
  
  
  (method override (focus-default self)
    (acquire-focus (get-tree self)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method package (get-tree self)
    (locate self 'options))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method protected virtual (options-name self)
    )
  
  
  (method (view-options self)
    (let ((appl (current-application)))
      (let ((options (child (get-options appl) (options-name self)))
            (tree (get-tree self)))
        (define (view-options)
          (for-each (lambda (options)
                      (let ((father (add-row tree
                                      state: 'expanded
                                      children: (list (new Tree-Node title: (get-title options) font: {Font Label-User} color: {Color red: 220 green: 0 blue: 0})
                                                      (new Tree-Label title: "" font: {Font Label-User} color: {Color White}))
                                      user-data: options)))
                        (for-each (lambda (option)
                                    (let ((setting (get-setting option)))
                                      (add-row tree
                                        father: father
                                        children: (list (new Tree-Node title: (or (get-title option) (format "{a}:{a}" (get-name options) (get-name option))) font: {Font Label-User} color: {Color White})
                                                        (new Tree-Label title: (present-setting setting) font: {Font Label-User} color: {Color White}))
                                        user-data: option)))
                                  (get-children options))))
                    (get-children options)))
        
        (define (present-setting setting)
          (let ((value (world-setting setting #f)))
            (->string value)))
        
        (remove-every-row tree)
        (with-update-locked self
          (lambda ()
            (view-options)))))))


;;;
;;;; Option
;;;


(class Option-Frame extends World-Frame
  
  
  (method protected virtual (read-option self option)
    )
  
  
  (method protected virtual (write-option self option value title)
    ))


(class Option-Label extends Label-View
  
  
  (form
    (<install> size: {Dimension 120 16} color: {Color red: .892 green: .773 blue: .290})))


(class Option-Box extends Combo-Box
  
  
  ;; quick hack
  (method override (new-choices-menu self)
    (let ((menu (nextmethod self)))
      (set-background menu {Color red: .000 green: .000 blue: .000 alpha: .700})
      menu))
  
  
  (method override (content-change self origin)
    (option-change parent (get-value self) (get-content self))))


(class Boolean-Option extends Check-Box
  
  
  (property option initialize #f accessors generate)
  
  
  (method override (conclude self initargs)
    (nextmethod self initargs)
    (read-checked self))


  (method override (process-action self sender (properties (action-properties self)))
    (write-checked self checked?))
  
  
  (method protected virtual (read-checked self)
    (set-checked? self (read-option creator option)))
  
  
  (method protected virtual (write-checked self checked?)
    (write-option creator option checked? #f)))


(class Boolean-Setting extends Boolean-Option
  
  
  (method override (read-checked self)
    (set-checked? self
      (case option
        ((world.backface-culling?) backface-culling?)
        ((world.application-retina?) (world-setting 'world.application-retina? #t))
        ((world.multisampling?) (world-setting 'world.multisampling? #t))
        @wait
        ((world.creature-models?) creature-models?)
        ((world.music?) (get-music? (current-music)))
        ((world.ambience?) (get-ambience? (current-ambience)))
        ((world.sounds?) sounds?)
        ((world.independent-camera?) (get-independent-camera?))
        @needs-a-lot-more-work
        ((world.camera-smoothing?) (get-camera-smoothing?))
        ((world.script-toolbox?) (world-setting 'world.script-toolbox? #t))
        ((world.live-coding?) (world-setting 'world.live-coding? #t)))))


  (method override (write-checked self checked?)
    (case option
      ((world.backface-culling?) (set-backface-culling? checked?))
      @wait
      ((world.creature-models?) (change-creature-models? (current-zone) checked?))
      ((world.music?) (change-music? (current-music) checked?))
      ((world.ambience?) (change-ambience? (current-ambience) checked?))
      ((world.sounds?) (set-sounds? checked?))
      ((world.independent-camera?) (set-independent-camera? checked?))
      @needs-a-lot-more-work
      ((world.camera-smoothing?) (change-camera-smoothing (current-world) checked?)))
    (set-setting options-settings option checked?)
    (save-content options-settings)
    (when (memq? option '(world.application-retina? world.multisampling?))
      (need-restart))))


(class Combo-Option extends Border-View
  
  
  (property option initialize #f accessors generate)
  
  
  (form
    (<install>                  size: {Dimension 100 19} border-type: solid border-color: {Color red: 100 green: 100 blue: 100}
      (<Option-Box> name: combo)))
  
  
  (method override (conclude self initargs)
    (nextmethod self initargs)
    (receive (choices value) (read-choices/value self)
      (let ((combo (child self 'combo)))
        (set-choices combo choices)
        (if (unspecified? value)
            (set-entry combo "<invalid>")
          (set-value combo value)))))
  
  
  (method package (option-change self value title)
    (write-value self value title))
  
  
  (method protected virtual (read-choices/value self)
    (read-option creator option))
  
  
  (method protected virtual (write-value self value title)
    (write-option creator option value title)))


(class Combo-Setting extends Combo-Option
  
  
  (form
    (<install> size: {Dimension 140 19}))
  
  
  (method override (read-choices/value self)
    (case option
      ((world.view-distance)
       (values '((ultra "Ultra (15)")
                 (high "High(11)")
                 (medium "Medium (7)")
                 (low "Low (5)"))
               (case (world-setting 'world.view-distance 7)
                 ((15) 'ultra)
                 ((11) 'high)
                 ((7) 'medium)
                 ((5) 'low)
                 (else (unspecified)))))
      ((world.tile-resolution)
       (values '((high "High (128)")
                 (medium "Medium (32)")
                 (low "Low (16)"))
               (case (world-setting 'world.tile-resolution 128)
                 ((128) 'high)
                 ((32) 'medium)
                 ((16) 'low)
                 (else (unspecified)))))
      ((world.rendered-geometry)
       (values '((unlimited "Unlimited")
                 (high "High")
                 (medium "Medium")
                 (low "Low"))
               (case (world-setting 'world.render-triangles-max #f)
                 ((#f) 'unlimited)
                 ((1000000) 'high)
                 ((500000) 'medium)
                 ((200000) 'low)
                 (else (unspecified)))))
      ((world.render-rate)
       (values '((ultra "Ultra (100)")
                 (high "High (60)")
                 (medium "Medium (40)")
                 (low "Low (30)")
                 (very-low "Very low (20)"))
               (case render-rate
                 ((100.) 'ultra)
                 ((60.) 'high)
                 ((40.) 'medium)
                 ((30.) 'low)
                 ((20.) 'very-low)
                 (else (unspecified)))))))
  
  
  (method override (write-value self value title)
    (let ((option-value
            (case option
              ((world.view-distance)
               (let ((distance
                       (case value
                         ((ultra) 15)
                         ((high) 11)
                         ((medium) 7)
                         ((low) 5))))
                 (change-view-distance (current-world) distance)
                 distance))
              ((world.tile-resolution)
               (let ((resolution
                       (case value
                         ((high) 128)
                         ((medium) 32)
                         ((low) 16))))
                 resolution))
              ((world.rendered-geometry)
               (let ((triangles-max
                       (case value
                         ((unlimited) #f)
                         ((high) 1000000)
                         ((medium) 500000)
                         ((low) 200000))))
                 triangles-max))
              ((world.render-rate)
               (let ((rate
                       (case value
                         ((ultra) 100.)
                         ((high) 60.)
                         ((medium) 40.)
                         ((low) 30.)
                         ((very-low) 20.))))
                 rate)))))
      (case option
        ((world.tile-resolution)
         (case option-value
           ((16)
            (need-install
              (lambda ()
                (require-pack (current-zone) "block/16x16")))
            (set-setting   options-settings 'world.tile-resolution  16)
            (set-setting   options-settings 'world.tile-pack        "minecraft")
            (set-setting   options-settings 'world.generate-mipmap? #t)
            (set-setting   options-settings 'world.min-filter       'linear)
            (set-setting   options-settings 'world.mag-filter       'nearest))
           ((32)
            (need-install
              (lambda ()
                (require-pack (current-zone) "block/32x32")))
            (set-setting   options-settings 'world.tile-resolution  32)
            (clear-setting options-settings 'world.tile-pack)
            (clear-setting options-settings 'world.generate-mipmap?)
            (clear-setting options-settings 'world.min-filter)
            (clear-setting options-settings 'world.mag-filter))
           ((128)
            (set-setting   options-settings 'world.tile-resolution  128)
            (clear-setting options-settings 'world.tile-pack)
            (clear-setting options-settings 'world.generate-mipmap?)
            (clear-setting options-settings 'world.min-filter)
            (clear-setting options-settings 'world.mag-filter))))
        ((world.rendered-geometry)
         (set-render-triangles-max option-value)
         (set-setting options-settings 'world.render-triangles-max option-value))
        ((world.render-rate)
         (let ((active option-value)
               (inactive (/ option-value 2.)))
           (setup-render-rate active inactive)
           (set-setting options-settings 'world.render-rate active)
           (set-setting options-settings 'world.render-rate-inactive inactive)))
        (else
         (set-setting options-settings option option-value)))
      (save-content options-settings)
      (when (memq? option '(world.tile-resolution))
        (need-restart)))))


;;;
;;;; Graphics
;;;


(class World-Graphics-Options extends World-Frame
  
  
  (form
    (<install>
      (<caption~>           title: "Graphics Options")
      (<content~>                                      layout-type: center
        (<View>                                        size: {Dimension 290 244}
          (<Option-Label>   title: "View Distance"     position: {Point   0   1})
          (<Combo-Setting>                             position: {Point 130   0} option: world.view-distance)
          (<Option-Label>   title: "Tile Resolution"   position: {Point   0  36})
          (<Combo-Setting>                             position: {Point 130  35} option: world.tile-resolution)
          (<Option-Label>   title: "Render Rate"       position: {Point   0  71})
          (<Combo-Setting>                             position: {Point 130  70} option: world.render-rate)
          (<Option-Label>   title: "Rendered Geometry" position: {Point   0 106})
          (<Combo-Setting>                             position: {Point 130 105} option: world.rendered-geometry)
          (<Option-Label>   title: "Backface Culling"  position: {Point  20 150})
          (<Boolean-Setting>                           position: {Point   0 150} option: world.backface-culling?)
          (<Option-Label>   title: "Retina"            position: {Point  20 190})
          (<Boolean-Setting>                           position: {Point   0 190} option: world.application-retina?)
          (<Option-Label>   title: "Multisampling"     position: {Point  20 220})
          (<Boolean-Setting>                           position: {Point   0 220} option: world.multisampling?)
          @wait
          (<Option-Label>   title: "Enemy Models (experimental)" position: {Point 20 120} size: {Dimension 180 16})
          @wait
          (<Boolean-Setting>                           position: {Point  0 120} option: world.creature-models?)
          (<Label-View>     name: fps                  size: {Dimension 180 16} position: {Point  220 220})))))
  
  
  (slot fps-thread getter generate)
  
  
  (method override (prepare self rest)
    (nextmethod self rest)
    (reset-install/restart)
    (let ((thread
            (new-thread
              (lambda ()
                (declare (proper-tail-calls))
                (let (loop)
                  (delay-event
                    (lambda ()
                      (with-task-mutex
                        (lambda ()
                          (let ((fps (locate self 'fps)))
                            (set-title fps (format "{r precision: 0} FPS" render-fps)))))))
                  (sleep 1)
                  (loop)))
              'fps)))
      (thread-start! thread)
      (set! fps-thread thread)))
  
  
  (method override (destroy self)
    (thread-terminate! fps-thread)
    (set! fps-thread #f)
    (warn-if-restart-needed)
    (nextmethod self)))


;;;
;;;; Sound
;;;


(class World-Sound-Options extends World-Frame
  
  
  (form
    (<install>
      (<caption~>           title: "Sound Options")
      (<content~>                                  layout-type: center
        (<View>                                    size: {Dimension 80 94}
          (<Option-Label>   title: "Music"         position: {Point 20  0})
          (<Boolean-Setting>                       position: {Point  0  0}  option: world.music?)
          (<Option-Label>   title: "Ambience"      position: {Point 20 40})
          (<Boolean-Setting>                       position: {Point  0 40}  option: world.ambience?)
          (<Option-Label>   title: "Sounds"        position: {Point 20 80})
          (<Boolean-Setting>                       position: {Point  0 80}  option: world.sounds?))))))


;;;
;;;; Interface
;;;


(class World-Interface-Options extends World-Frame
  
  
  (form
    (<install>
      (<caption~>           title: "Interface Options")
      (<content~>                                       layout-type: center
        (<View>                                         size: {Dimension 150 174}
          (<Option-Label>   title: "Independent Camera" position: {Point 20  40} size: {Dimension 140 16})
          (<Boolean-Setting>                            position: {Point  0  40} option: world.independent-camera?)
          @needs-a-lot-more-work
          (<Option-Label>   title: "Camera Smoothing"   position: {Point 20  80} size: {Dimension 140 16})
          @needs-a-lot-more-work
          (<Boolean-Setting>                            position: {Point  0  80} option: world.camera-smoothing?)
          @waitscript (<Option-Label>   title: "Script Toolbox"     position: {Point 20 120} size: {Dimension 140 16})
          @waitscript (<Boolean-Setting>                            position: {Point  0 120} option: world.script-toolbox?)
          @waitscript (<Option-Label>   title: "Live coding"        position: {Point 20 160} size: {Dimension 140 16})
          @waitscript (<Boolean-Setting>                            position: {Point  0 160} option: world.live-coding?))))))


;;;
;;;; Sample
;;;


@sample
(class World-Sample-Options extends World-Frame
  
  
  (form
    (<install>
      (<caption~>                    title: "Video Options")
      (<content~>
        (<Group-Box>                 title: "Advanced" position: {Point 20 20} size: {Dimension 400 200} border-color: {Color White alpha: .5}
          (<content~>
            (<Label-View>            title: "Setting" position: {Point 200 22} color: {Color red: .892 green: .773 blue: .290})
            (<Border-View>           position: {Point 250 21} size: {Dimension 100 19} border-type: solid border-color: {Color red: 100 green: 100 blue: 100}
              (<Scroller-View>       hscroll?: #f vscroll?: #f
                (<content~>          layout-type: fill
                  (<World-Text-View> focus-selects-all?: #t show-unfocused-selection?: #f left-padding: 1 top-padding: 1))))
            (<Push-Button>           title: "Push-Button" position: {Point 50 20})
            (<Check-Box>             title: "Check-Box" position: {Point 50 60})
            (<Radio-Button>          title: "Radio-Button 1" position: {Point 50 90} selected?: #t)
            (<Radio-Button>          title: "Radio-Button 2" position: {Point 50 110})
            (<Label-View>            title: "View distance" position: {Point 50 140} color: {Color red: .892 green: .773 blue: .290})
            (<Border-View>           position: {Point 140 139} size: {Dimension 80 19} border-type: solid border-color: {Color red: 100 green: 100 blue: 100}
              (<Combo-Box>           title: "Combo-Box" choices: ((ultra "Ultra") (high "High") (medium "Medium") (low "Low")) content: "High")))))))))
