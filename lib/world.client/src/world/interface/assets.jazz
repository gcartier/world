;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Assets
;;;


(module world.interface.assets jazz


(import (jazz.io)
        (jazz.jml)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.library)
        (jazz.literals)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (world)
        (world.autoload)
        (world.io)
        (world.interface)
        (world.interface.frame)
        (world.syntax (phase syntax)))


(class Model-Import extends World-Frame
  
  
  (form
    (<install>                                     size: {Dimension 420 639} layout-type: border
      (<caption~>                                  title: "Models")
      (<content~>                                  layout-type: fill
        (<Scroller-View>                           style: document hscroll?: #t vscroll?: #t
          (<content~>                              layout-type: fill
            (<Tree-Header>                         style: document shown?: #f
              (<content~>
                (<Tree-View>          name: models portfolio: :images filled-column: model double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
                  (<Tree-Node-Column> name: model  title: "Model" width: 235 toplevel-controls?: #f)))))))))
  
  
  (method override (conclude initargs)
    (nextmethod initargs)
    (view-models))
  
  
  (method override (focus-default)
    (acquire-focus~ (get-tree)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-tree)
    (locate 'models))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-double-click evt)
    (add-selected))
  
  
  (method override (on-return-press evt)
    (add-selected))
  
  
  (method override (on-escape-press evt)
    (close))
  
  
  (method (add-selected)
    (let ((world (current-world)))
      (let ((model (get-single-selected-data~ (get-tree))))
        (when model
          (close)
          (add-model~ world model)))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (view-models)
    (let ((world (current-world)))
      (let ((models (collect-models~ world))
            (tree (get-tree)))
        (remove-every-row~ tree)
        (with-update-locked
          (lambda ()
            (for-each (lambda (model)
                        (add-row~ tree children: (list (new Tree-Node image: {Image-Resource "Document"} title: model font: {Font Label-User^} color: {Color White}))
                          user-data: model))
                      models))))))))
