;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Assets
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.assets jazz


(import (jazz.io)
        (jazz.graphic)
        (jazz.handler)
        (jazz.markup)
        (jazz.outline)
        (jazz.tree)
        (jazz.ui)
        (world)
        (world.autoload)
        (world.context)
        (world.interface.frame)
        (world.syntax (phase syntax)))


(class Model-Import extends World-Frame
  
  
  (form
    (<install>                                     size: {Dimension 420 639} layout-type: border
      (<caption~>                                  title: "Models")
      (<content~>                                  layout-type: fill
        (<Scroller-View>                           style: document hscroll?: #t vscroll?: #t
          (<content~>                              layout-type: fill
            (<Tree-Header>                         style: document shown?: #f
              (<content~>
                (<Tree-View>          name: models portfolio: :images filled-column: model background: #f double-click-handler: {Event-Handler :form on-double-click} return-press-handler: {Event-Handler :form on-return-press} escape-press-handler: {Event-Handler :form on-escape-press}
                  (<Tree-Node-Column> name: model  title: "Model" width: 235 toplevel-controls?: #f display-images?: #f display-levels?: #f)))))))))
  
  
  (method override (conclude self initargs)
    (nextmethod self initargs)
    (view-models self))
  
  
  (method override (focus-default self)
    (acquire-focus (get-tree self)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method package (get-tree self)
    (locate self 'models))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-double-click self evt)
    (add-selected self))
  
  
  (method override (on-return-press self evt)
    (add-selected self))
  
  
  (method override (on-escape-press self evt)
    (close self))
  
  
  (method (add-selected self)
    (let ((world (current-world)))
      (let ((model (get-single-selected-data (get-tree self))))
        (when model
          (close self)
          (add-model world model)))))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (view-models self)
    (let ((world (current-world)))
      (define (partition-models models)
        (partition models key: (lambda (model)
                                 (if (> (length model) 1)
                                     (car model)
                                   ""))
          test: filename=?))
      
      (let ((models (partition-models (map get-spine (collect-models world))))
            (tree (get-tree self)))
        (remove-every-row tree)
        (with-update-locked self
          (lambda ()
            (define (add-children father children)
              (for-each (lambda (model)
                          (let ((path (join model "/"))
                                (title (join (if (> (length model) 1) (cdr model) model) "/")))
                            (add-row tree
                              father: father
                              children: (list (new Tree-Node title: title font: {Font Label-User} color: {Color White}))
                              user-data: path)))
                        children))
            
            (for-each (lambda (partition)
                        (bind (title . children) partition
                          (if (empty-string? title)
                              (add-children #f children)
                            (let ((father (add-row tree
                                            state: 'expanded
                                            children: (list (new Tree-Node title: title font: {Font Label-User} color: {Color red: 220 green: 0 blue: 0})))))
                              (add-children father children)))))
                      models))))))))
