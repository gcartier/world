;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Textures
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.textures jazz


(import (jazz.geometry)
        (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.block)
        (world.draw)
        (world.dye)
        (world.dyes)
        (world.entity)
        (world.generate)
        (world.geometry)
        (world.interface)
        (world.interface.showcase)
        (world.material)
        (world.object)
        (world.opengl)
        (world.quad)
        (world.syntax (phase syntax))
        (world.texture))


(class Textures-Showcase extends World-Showcase
  
  
  (method override (showcase-margin)
    .3)
  
  
  (method override (showcase-spacing)
    .85)
  
  
  (method override (showcase-z)
    -.35)
  
  
  (method override (showcase-elements showing)
    (let ((world (current-world))
          (spacing (showcase-spacing))
          (z 0.))
      (map (lambda (texture)
             (prog1 (list (find-texture~ world texture) z)
               (increase! z spacing)))
           (sort ci<? (table-keys (collect-textures~ world))))))
  
  
  (method override (element-center info)
    (bind (texture z) info
      (vertex (showcase-x) (showcase-y) z)))


  (method override (draw-elements)
    (let ((camera (cache-camera))
          (elements (cache-elements)))
      (loop (for n from 0 below (vector-length elements))
            (let ((element (vector-ref elements n)))
              (when (in-frustum?~ camera (element-center element))
                (draw-element element))))))


  (method (draw-element info)
    (bind (texture z) info
      (glPushMatrix)
      (glTranslatef (showcase-x) (showcase-y) (+ z .35))
      (glRotatef 180. 0. 1. 0.)
      (let ((width .7)
            (height .7))
        (textured-quad~ texture
          (lambda () (glVertex3f 0.0 0.0 height))
          (lambda () (glVertex3f 0.0 0.0 0.0))
          (lambda () (glVertex3f 0.0 width 0.0))
          (lambda () (glVertex3f 0.0 width height))))
      (glPopMatrix)))
  
  
  (method override (draw-selection)
    (when selection
      (gl-colorize-dye red-dye)
      (glLineWidth 2.)
      (glDisable GL_LIGHTING)
      (glBegin GL_LINE_LOOP)
      (let ((quad (texture-quad selection)))
        (let ((v1 (get-v1~ quad))
              (v2 (get-v2~ quad))
              (v3 (get-v3~ quad))
              (v4 (get-v4~ quad)))
          (glVertex3f (vertex-x v1) (vertex-y v1) (vertex-z v1))
          (glVertex3f (vertex-x v2) (vertex-y v2) (vertex-z v2))
          (glVertex3f (vertex-x v3) (vertex-y v3) (vertex-z v3))
          (glVertex3f (vertex-x v4) (vertex-y v4) (vertex-z v4))))
      (glEnd)
      (glEnable GL_LIGHTING)
      (glLineWidth 1.)))
  
  
  (method override (ray-closest-element orig <f64vector> dir <f64vector>)
    (let ((textures (cache-elements)))
      (let ((closest #f)
            (distance #f))
        (loop (for n from 0 below (vector-length textures))
              (let ((info (vector-ref textures n)))
                (let ((poly (texture-quad info)))
                  (let ((cls (ray-closest-triangle~ poly orig dir #f)))
                    (when cls
                      (bind (poly dist) cls
                        (when (or (not distance)
                                  (< dist distance))
                          (set! closest (list poly dist))
                          (set! distance dist))))))))
        closest)))
  
  
  (method (texture-quad info)
    (bind (texture z) info
      (let ((translation (vertex (showcase-x) (showcase-y) (- z .35)))
            (width .7)
            (height .7))
        (let ((v1 (vertex+ translation (vertex 0.0 0.0 height)))
              (v2 (vertex+ translation (vertex 0.0 0.0 0.0)))
              (v3 (vertex+ translation (vertex 0.0 width 0.0)))
              (v4 (vertex+ translation (vertex 0.0 width height))))
          (make-quad info v1 v2 v3 v4)))))))
