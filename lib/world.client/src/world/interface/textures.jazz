;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Textures
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.textures jazz


(import (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.draw)
        (world.dye)
        (world.dyes)
        (world.entity)
        (world.generate)
        (world.geometry)
        (world.interface)
        (world.interface.showcase)
        (world.material)
        (world.object)
        (world.opengl)
        (world.quad)
        (world.syntax (phase syntax))
        (world.texture)
        (world.tile))


(class Textures-Showcase extends World-Showcase
  
  
  (slot textures initialize #f getter generate)
  
  
  (method override (showcase-spacing)
    .85)
  
  
  (method (showcase-textures)
    (or textures
        (let ((world (current-world))
              (spacing (showcase-spacing))
              (z 5.))
          (let ((all (map (lambda (texture)
                            (prog1 (list (find-texture~ world texture) z)
                              (decrease! z spacing)))
                          (table-keys (collect-textures~ world)))))
            (set! textures (list->vector all))
            textures))))


  (method override (draw-elements)
    (let ((textures (showcase-textures)))
      (loop (for n from 0 below (vector-length textures))
            (bind (texture z) (vector-ref textures n)
              (glPushMatrix)
              (glTranslatef 0. -3.2 z)
              (let ((width .7)
                    (height .7))
                (textured-quad~ texture
                  (lambda () (glVertex3f 0.0 0.0 height))
                  (lambda () (glVertex3f 0.0 0.0 0.0))
                  (lambda () (glVertex3f 0.0 width 0.0))
                  (lambda () (glVertex3f 0.0 width height))))
              (glPopMatrix)))))
  
  
  (method override (draw-selection)
    (when selection
      (gl-colorize-dye red-dye)
      (glLineWidth 2.)
      (glDisable GL_LIGHTING)
      (glBegin GL_LINE_LOOP)
      (let ((quad (texture-quad selection)))
        (let ((v1 (get-v1~ quad))
              (v2 (get-v2~ quad))
              (v3 (get-v3~ quad))
              (v4 (get-v4~ quad)))
          (glVertex3f (vertex-x v1) (vertex-y v1) (vertex-z v1))
          (glVertex3f (vertex-x v2) (vertex-y v2) (vertex-z v2))
          (glVertex3f (vertex-x v3) (vertex-y v3) (vertex-z v3))
          (glVertex3f (vertex-x v4) (vertex-y v4) (vertex-z v4))))
      (glEnd)
      (glEnable GL_LIGHTING)
      (glLineWidth 1.)))
  
  
  (method override (ray-closest-element orig <f64vector> dir <f64vector>)
    (let ((textures (showcase-textures)))
      (let ((closest #f)
            (distance #f))
        (loop (for n from 0 below (vector-length textures))
              (let ((info (vector-ref textures n)))
                (let ((poly (texture-quad info)))
                  (let ((cls (ray-closest-triangle~ poly orig dir #f)))
                    (when cls
                      (bind (poly dist) cls
                        (when (or (not distance)
                                  (< dist distance))
                          (set! closest (list poly dist))
                          (set! distance dist))))))))
        closest)))
  
  
  (method (texture-quad info)
    (bind (texture z) info
      (let ((translation (vertex 0. -3.2 z))
            (width .7)
            (height .7))
        (let ((v1 (vertex+ translation (vertex 0.0 0.0 height)))
              (v2 (vertex+ translation (vertex 0.0 0.0 0.0)))
              (v3 (vertex+ translation (vertex 0.0 width 0.0)))
              (v4 (vertex+ translation (vertex 0.0 width height))))
          (make-quad info v1 v2 v3 v4)))))))
