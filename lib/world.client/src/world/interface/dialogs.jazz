;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Dialogs
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.dialogs jazz


(import (jazz.application)
        (jazz.component)
        (jazz.handler)
        (jazz.platform)
        (jazz.text)
        (jazz.ui)
        (jazz.view)
        (jazz.window)
        (world)
        (world.autoload)
        (world.context)
        (world.interface.caption)
        (world.interface.frame)
        (world.interface.panel)
        (world.interface.string))


(definition public (open-dialog view (title: title #f) (size: size #f) (location: location #f) (closable?: closable? #t) (user-closed: user-closed #f))
  (let ((interface (current-interface))
        (world (current-world)))
    (let ((panel (new World-Dialog parent: interface size: (or size (nu+ (get-size view) {Dimension 100 70})) location: (or location 'center) layout-type: 'border user-closed: user-closed)))
      (let ((caption (locate panel 'caption)))
        (when title
          (set-title caption title))
        (set-closable? caption closable?))
      (let ((content (locate panel 'content)))
        (set-layout-type content 'center)
        (set-parent view content))
      (layout-view interface)
      (set-interface? world #t)
      (if application-active?
          (focus-default view)
        (user-attention (current-toplevel)))
      (push-panel panel)
      panel)))


(definition public (open-message text . rest)
  (bind-keywords ((caption #f) (location #f) (layouter #f) (closable? #t) (user-closed #f) . others) rest
    (let ((box (new World-Box)))
      (let ((size (apply process-box box text icon: 'none process-modal?: #f others)))
        (when layouter
          (layouter box))
        (set-size box size)
        (open-dialog box title: caption location: location closable?: closable? user-closed: user-closed)))))


(definition public (ask-string (class: class World-String-Request) (title: title #f) (size: size #f) (prefix: prefix #f) (initial-value: initial-value #f) (action: action #f))
  (define (ok-action view)
    (let ((str (get-modal-result view)))
      (close-dialog view)
      (when action
        (action str))))
  
  (define (cancel-action view)
    (close-dialog view))
  
  (define (close-dialog view)
    (close (find-ascendant view World-Dialog)))
  
  (let ((view (new class)))
    (let ((title (or title "Enter String"))
          (text (locate view 'text)))
      (set-prefix view (or prefix "String:"))
      (set-string-content text (if initial-value (->string initial-value) ""))
      (select-all text)
      (set-action-handler (locate view 'ok) (new Event-Handler execute: (lambda (evt) (ok-action view))))
      (set-action-handler (locate view 'cancel) (new Event-Handler execute: (lambda (evt) (cancel-action view))))
      (prog1 (open-dialog view title: title size: size)
        (acquire-focus text)))))


;;;
;;;; World-Dialog
;;;


(class World-Dialog extends World-Frame
  
  
  (property user-closed initialize #f accessors generate)
  
  
  (method override (user-close self)
    (if user-closed
        (user-closed self)
      (nextmethod self))))


;;;
;;;; World-Box
;;;


(class World-Box extends Message-Box


  (method override (resize self dh dv)
    )
  
  
  (method override (process-return self)
    (let ((button (find action-buttons default-button key: get-name return: 'item)))
      (when button
        (process-action button self))))
  
  
  (method override (process-key self key)
    (when process-keys?
      (let ((button (find-button self key)))
        (when button
          (process-action button self)))))))
