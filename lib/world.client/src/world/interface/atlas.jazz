;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Atlas
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.atlas jazz


(import (jazz.graphic.opengl.glew)
        (jazz.io)
        (world)
        (world.autoload)
        (world.draw)
        (world.dye)
        (world.dyes)
        (world.entity)
        (world.face)
        (world.generate)
        (world.geometry)
        (world.interface)
        (world.interface.showcase)
        (world.material)
        (world.model)
        (world.object)
        (world.opengl)
        (world.quad)
        (world.syntax (phase syntax))
        (world.texture)
        (world.tile))


(class Atlas-Showcase extends World-Showcase
  
  
  (slot showing  initialize #f getter generate)
  (slot entities initialize #f getter generate)
  
  
  (method override (showcase-spacing)
    .85)
  

  (method override (showcase-elements)
    (let ((world (current-world)))
      (let ((atlas (get-tile-atlas~ world)))
        (if (and entities (eq? atlas showing))
            entities
          (let ((spacing (showcase-spacing))
                (y -3.2)
                (z 5.))
            (let ((all (map (lambda (texture)
                              (let ((height 1.)
                                    (depth 1.))
                                (let ((tile (new Quad-Tile
                                              v1: (vertex 0.0 0.0 0.0)
                                              v2: (vertex 0.0 0.0 depth)
                                              v3: (vertex 0.0 height depth)
                                              v4: (vertex 0.0 height 0.0)
                                              image: texture)))
                                  (let ((model (make-faces-model (element-faces~ tile)))
                                        (scale .7))
                                    (let ((entity (new Entity position: (vertex 0. y z) scale: (vertex scale scale scale) user-data: texture)))
                                      (set-radiuses~ entity (vertex .35 .35 .35))
                                      (set-model~ entity model)
                                      (decrease! z spacing)
                                      entity)))))
                            (image-names~ (get-tile-atlas~ world)))))
              (set! showing atlas)
              (set! entities (list->vector all))
              entities))))))


  (method override (draw-elements)
    (let ((entities (showcase-elements)))
      (loop (for n from 0 below (vector-length entities))
            (let ((rank (modulo n (vector-length entities))))
              (let ((entity (vector-ref entities rank)))
                (draw-entity~ entity))))))
  
  
  (method override (draw-selection)
    (when selection
      (gl-colorize-dye red-dye)
      (glLineWidth 2.)
      (glDisable GL_LIGHTING)
      (glBegin GL_LINE_LOOP)
      (let ((quad (texture-quad selection)))
        (let ((v1 (get-v1~ quad))
              (v2 (get-v2~ quad))
              (v3 (get-v3~ quad))
              (v4 (get-v4~ quad)))
          (glVertex3f (vertex-x v1) (vertex-y v1) (vertex-z v1))
          (glVertex3f (vertex-x v2) (vertex-y v2) (vertex-z v2))
          (glVertex3f (vertex-x v3) (vertex-y v3) (vertex-z v3))
          (glVertex3f (vertex-x v4) (vertex-y v4) (vertex-z v4))))
      (glEnd)
      (glEnable GL_LIGHTING)
      (glLineWidth 1.)))
  
  
  (method override (ray-closest-element orig <f64vector> dir <f64vector>)
    (let ((entities (showcase-elements)))
      (let ((closest #f)
            (distance #f))
        (loop (for n from 0 below (vector-length entities))
              (let ((entity (vector-ref entities n)))
                (let ((poly (texture-quad entity)))
                  (let ((cls (ray-closest-triangle~ poly orig dir #f)))
                    (when cls
                      (bind (poly dist) cls
                        (when (or (not distance)
                                  (< dist distance))
                          (set! closest (list poly dist))
                          (set! distance dist))))))))
        closest)))
  
  
  (method (texture-quad entity)
    (let ((center (get-center~ entity))
          (height .7)
          (depth .7))
      (let ((v1 (vertex+ center (vertex 0. height (- depth))))
            (v2 (vertex+ center (vertex 0. 0. (- depth))))
            (v3 (vertex+ center (vertex 0. 0. 0.)))
            (v4 (vertex+ center (vertex 0. height 0.))))
        (make-quad entity v1 v2 v3 v4))))))
