;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Bindings Frame
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.bindings jazz


(import (jazz.jml)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.library)
        (jazz.literals)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.view)
        (world)
        (world.client)
        (world.client.actions)
        (world.interface)
        (world.interface.frame))


(class World-Bindings-Viewer extends World-Frame
  
  
  (form
    (<install>                                              size: {Dimension 420 639} layout-type: border
      (<caption~>                                           title: "Bindings")
      (<content~>                                           layout-type: fill
        (<Scroller-View>                                    style: document hscroll?: #t vscroll?: #t
          (<content~>                                       layout-type: fill
            (<Tree-Header>                                  style: document shown?: #f
              (<content~>
                (<Tree-View>           name: actions        portfolio: :images filled-column: action
                  (<Tree-Node-Column>  name: action         title: "Action" width: 235 toplevel-controls?: #f display-images?: #f)
                  (<Tree-Label-Column> name: shortcut       title: "Shortcut" width: 185)))))))))
  
  
  (method override (conclude initargs)
    (nextmethod initargs)
    (view-bindings))
  
  
  (method override (focus-default)
    (acquire-focus~ (get-tree)))
  
  
  ;;;
  ;;;; Access
  ;;;
  
  
  (method (get-tree)
    (locate 'actions))
  
  
  ;;;
  ;;;; Update
  ;;;
  
  
  (method (view-bindings)
    (let ((appl (get-application)))
      (let ((designer (get-bindings-designer~ appl))
            (bindings (get-bindings))
            (tree (get-tree)))
        (define (view-actions-flat)
          (for-each (lambda (actions)
                      (let ((children (get-visible-children~ actions)))
                        (unless (null? children)
                          (let ((father (add-row~ tree
                                          state: 'expanded
                                          children: (list (new Tree-Node title: (get-title~ actions) font: {Font Label-User^} color: {Color red: 220 green: 0 blue: 0})
                                                          (new Tree-Label title: "" font: {Font Label-User^} color: {Color White}))
                                          user-data: actions)))
                            (for-each (lambda (action)
                                        (when (get-visible?~ action)
                                          (let ((title (get-title~ action)))
                                            (when title
                                              (let ((shortcut (get-shortcut~ action))
                                                    (alternate-shortcut (get-alternate-shortcut~ action)))
                                                (add-row~ tree
                                                  father: father
                                                  children: (list (new Tree-Node title: title font: {Font Label-User^} color: {Color White})
                                                                  (new Tree-Label title: (present-shortcuts shortcut alternate-shortcut) font: {Font Label-User^} color: {Color White}))
                                                  user-data: action))))))
                                      children)))))
                    (collect-type World-Actions (get-children~ (get-bindings)))))
        
        (define (present-shortcuts shortcut alternate-shortcut)
          (if (not shortcut)
              ""
            (let ((shortcut (present-shortcut shortcut)))
              (if (not alternate-shortcut)
                  shortcut
                (format "{a}, {a}" shortcut (present-shortcut alternate-shortcut))))))
        
        (define (present-shortcut shortcut)
          (let ((modifiers (get-modifiers~ shortcut))
                (key (get-key~ shortcut)))
            (format "{?:{l} ~}{a}" (not-null? modifiers) (map enumerator->symbol modifiers) key)))
        
        (remove-every-row~ tree)
        (with-update-locked
          (lambda ()
            (view-actions-flat))))))))
