;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Category Icons
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.category jazz


(import (jazz.graphic)
        (jazz.jml)
        (jazz.view)
        (world)
        (world.autoload)
        (world.dyes)
        (world.block)
        (world.generation.block)
        (world.generation.building)
        (world.generation.categories)
        (world.geometry)
        (world.interface.bars)
        (world.interface.inventory)
        (world.interface.showcase)
        (world.renderer)
        (world.settings)
        (world.syntax (phase syntax)))


(class Category-Icon extends View
  
  
  (form
    (<install> size: {Dimension 48 52}))
  
  
  (method protected virtual (name->category name)
    )
  
  
  (method (selected-category)
    (let ((world (current-world)))
      (get-category~ (get-showcase~ world))))
  
  
  (method (selected?)
    (eq? name (selected-category)))
  
  
  (method (select)
    (let ((world (current-world)))
      (let ((showcase (get-showcase~ world)))
        (update-category~ showcase name))))
  
  
  (method override (mouse-up evt)
    (unless (selected?)
      (let ((previous (selected-category)))
        (select)
        (when previous
          (invalidate-view~ (child~ parent previous))))))
  
  
  (method override (draw surface context)
    (let ((bounds (get-bounds))
          (texture (get-texture~ (name->category name))))
      (blit-surface~ surface (get-slot-image texture -1))
      (when (selected?)
        (let ((left (get-left~ bounds))
              (right (get-right~ bounds))
              (bottom (get-bottom~ bounds)))
          (set-line-width~ surface 2)
          (set-color~ surface {Color Medium-Gray})
          (move-to~ surface (+ left 4) (- bottom 2))
          (line-to~ surface (- right 4) (- bottom 2))))))))
