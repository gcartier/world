;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Chat Panel
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.chat jazz


(import (jazz.graphic)
        (jazz.markup)
        (jazz.text)
        (jazz.ui)
        (world)
        (world.context)
        (world.interface.panel)
        (world.interface.text))


;;;
;;;; Panel
;;;


(class Chat-Panel extends World-Panel
  
  
  (slot displayed-hook initialize #f accessors generate)
  (slot sender-hook    initialize #f accessors generate)
  
  
  (form
    (<install>                             size: {Dimension 420 639} background: {Color Black alpha: 0.3} layout-type: fill
      (<Border-View>          name: border border-color: {Color Medium-Gray} border-type: edge style: entry layout-type: fill
        (<Scroller-View>                   style: document hscroll?: #f vscroll?: #t
          (<content~>                      layout-type: fill
            (<Chat-Text-View> name: text   background: #f))))))
  
  
  (method override (size-change self size)
    (nextmethod self size)
    (let ((text (locate-component self 'text)))
      (when text
        (if (has-selection? text)
            (ensure-displayed text)
          (ensure-cell-displayed text (text-ending text))))))
  
  
  (method package (show-welcome self welcome)
    (fresh self)
    (insert self welcome 'Welcome)
    (displayed self))
  
  
  (method package (show-say self recipient message sender)
    (fresh self)
    (insert-linked self recipient 'Say-Sender sender #t)
    (insert self message 'Say-Message)
    (displayed self))
  
  
  (method package (show-whisper self recipient message sender)
    (fresh self)
    (insert-linked self recipient 'Whisper-Sender sender #t)
    (insert self message 'Whisper-Message)
    (displayed self))
  
  
  (method package (show-message self sender kind message)
    (fresh self)
    (case kind
      ((server)
       (insert-to self sender 'Server-Sender)
       (insert self message 'Server-Message)
       (display-message (current-world) (format "[Server]: {a}" message) color: {Color World-Red} duration: 2.5))
      ((yell)
       (insert-to self sender 'Yell-Sender)
       (insert self message 'Yell-Message)
       (display-message (current-world) (format "[{a}]: {a}" sender message) color: {Color World-Red}))
      ((whisper)
       (insert-linked self sender 'Whisper-Sender sender)
       (insert self message 'Whisper-Message))
      ((gathering)
       (insert-linked self sender 'Gathering-Sender sender)
       (insert self message 'Gathering-Message))
      ((group)
       (insert-linked self sender 'Group-Sender sender)
       (insert self message 'Group-Message))
      ((activity)
       (insert-to self sender 'Activity-Sender)
       (insert self message 'Activity-Message))
      ((show)
       (insert self message 'Show-Message))
      ((problem)
       (insert self message 'Problem-Message))
      ((command)
       (insert self message 'Command-Message))
      (else
       (insert-linked self sender 'Say-Sender sender)
       (insert self message 'Say-Message)))
    (unless (eq? kind 'activity)
      (displayed self))
    (when (eq? kind 'whisper)
      (when sender-hook
        (sender-hook sender #f))
      (set-last-whisperer (current-world) sender)))
  
  
  (method (insert-to self to style)
    (insert self (format "[{a}]: " to)
      style))
  
  
  (method (insert-linked self to style sender (to? #f))
    (insert self (format "{?:To ~}[{a}]: " to? to)
      (new Link-Style
        base: style
        action: (lambda (text pos style)
                  (when sender-hook
                    (sender-hook sender))))))
  
  
  (method (fresh self)
    (let ((text (locate self 'text)))
      (let ((model (get-model text))
            (end (text-ending text)))
        (when (/= (get-col end) 0)
          (insert-newline model (tail self))))))
  
  
  (method (insert self string style)
    (let ((text (locate self 'text)))
      (let ((model (get-model text)))
        (insert-styled model (tail self) string style))))
  
  
  (method (displayed self)
    (unless (and displayed-hook
                 (displayed-hook self))
      (let ((text (locate self 'text)))
        ;; quick hack
        (scroll text (new Point 0 -10000))
        @wait-this-can-bug
        (ensure-range-displayed text (tail)))
      (set-visible? self #t)))
  
  
  (method (tail self)
    (let ((text (locate self 'text)))
      (let ((end (text-ending text)))
        (new Range$Cell$ end end))))
  
  
  (method package (clear self)
    (let ((text (locate self 'text)))
      (empty-text text))))


;;;
;;;; Chat-Text-View
;;;


(class Chat-Text-View extends World-Text-View
  
  
  (form
    (<install> editable?: #f selectable?: #t))
  
  
  (method override (new-model self)
    (new Text-Model wrap?: #t left-padding: 1 top-padding: 1))

  
  (method override (install self rest)
    (nextmethod self rest)
    (add-style self 'Welcome {Text-Style Text-Base color: {Color red: .118 green: .943 blue: .122}})
    (add-style self 'Activity-Sender {Text-Style Text-Base color: {Color World-Purple}})
    (add-style self 'Activity-Message {Text-Style Text-Base color: {Color White}})
    (add-style self 'Show-Message {Text-Style Text-Base color: {Color red: 220 green: 220 blue: 70}})
    (add-style self 'Problem-Message {Text-Style Text-Base color: {Color red: .959 green: .090 blue: .103}})
    (add-style self 'Command-Message {Text-Style Text-Base color: {Color White}})
    (add-style self 'Say-Sender {Link-Style Text-Base color: {Color World-Dark-Green}})
    (add-style self 'Say-Message {Text-Style Text-Base color: {Color White}})
    (add-style self 'Gathering-Sender {Link-Style Text-Base color: {Color World-Dark-Green}})
    (add-style self 'Gathering-Message {Text-Style Text-Base color: {Color White}})
    (add-style self 'Group-Sender {Link-Style Text-Base color: {Color red: .938 green: .428 blue: .066}})
    (add-style self 'Group-Message {Text-Style Text-Base color: {Color White}})
    (add-style self 'Whisper-Sender {Link-Style Text-Base color: {Color red: .995 green: .400 blue: .983}})
    (add-style self 'Whisper-Message {Text-Style Text-Base color: {Color White}})
    (add-style self 'Yell-Sender {Text-Style Text-Base color: {Color red: .959 green: .090 blue: .103}})
    (add-style self 'Yell-Message {Text-Style Text-Base color: {Color White}})
    (add-style self 'Server-Sender {Text-Style Text-Base color: {Color red: .959 green: .090 blue: .103}})
    (add-style self 'Server-Message {Text-Style Text-Base color: {Color White}}))
  
  
  (method override (mouse-copyable? self)
    #t)
  
  
  (method override (mouse-data self pos)
    (let ((pos (view->text self pos)))
      (and pos
           (let ((explorer (new (explorer-class self) model start: pos end: pos))
                 (style (get-style-at model pos)))
             (select-style explorer (lambda (s) (eq? s style)))
             (trim-whitespace (get-string model (get-range explorer)))))))))
