;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Models
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is WorldScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2014
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See http://github.com/gcartier/world for details.


(module world.interface.models jazz


(import (jazz.geometry)
        (jazz.system)
        (world)
        (world.autoload)
        (world.enemy)
        (world.entity)
        (world.geometry)
        (world.interface.bars)
        (world.interface.showcase)
        (world.model)
        (world.role)
        (world.settings)
        (world.syntax (phase syntax)))


(class Models-Showcase extends World-Showcase
  
  
  (method override (activate)
    (nextmethod)
    (when (programmer-role?)
      (set-visible?~ (find-bar 'models) #t)))
  
  
  (method override (deactivate)
    (nextmethod)
    (when (programmer-role?)
      (set-visible?~ (find-bar 'models) #f)))
  
  
  (method override (showcase-margin)
    .25)
  
  
  (method override (showcase-spacing)
    .75)
  
  
  (method override (showcase-z)
    -.3)
  
  
  (method override (showcase-wait?)
    #t)
  
  
  (method override (showcase-elements showing)
    (define (entity-class path)
      (cond ((and (equal? path "creature/cow") (world-setting 'world.path-creatures? #f)) Cow)
            ((and (equal? path "creature/camel") (world-setting 'world.path-creatures? #f)) Camel)
            ((and (equal? path "creature/direwolf") (world-setting 'world.path-creatures? #f)) DireWolf)
            (else Entity)))
    
    (define (setup-mob mob)
      (set-tick?~ mob #f))
    
    (define (rotate entity)
      (rotate~ entity PI/2)
      entity)
    
    (let ((world (current-world)))
      (let ((camera (cache-camera))
            (spacing (showcase-spacing))
            (models (registered-non-block-models))
            (paths (collect-models~ world)))
        (let ((x (showcase-x))
              (y (showcase-y))
              (z 0.))
          (append (map (lambda (model)
                         (let ((class (or (get-entity-class~ model) Entity))
                               (scaling .25))
                           (let ((scale (vertex scaling scaling scaling)))
                             (let ((entity (rotate (new class parent: self position: (anchor->position~ model (vertex x y z) scale) scale: scale))))
                               (set-camera~ entity camera)
                               (set-model~ entity model)
                               (increase! z spacing)
                               entity))))
                       (sort < models key: get-id~))
                  (map (lambda (path)
                         (let ((class (entity-class path))
                               (scaling 1.))
                           (let ((scale (vertex scaling scaling scaling)))
                             (let ((entity (rotate (new class parent: self model: path position: (vertex x y z) scale: scale animate?: #t))))
                               (set-camera~ entity camera)
                               (set-model-update~ entity
                                 (lambda (entity model)
                                   (let ((bounds (get-bounds~ model)))
                                     (let ((width (cuboid-width bounds))
                                           (height (cuboid-height bounds)))
                                       ;; ignore depth
                                       (let ((max (max width height)))
                                         (let ((scaling (if (> max 1.) (/ 1. max) 1.)))
                                           (set-scaling~ entity scaling)))))))
                               (when (eq? class MOB)
                                 (setup-mob entity))
                               (increase! z spacing)
                               entity))))
                       (sort di<? paths key: (lambda (path)
                                               (let ((rank (cond ((starts-with? path "tree/") 0)
                                                                 ((starts-with? path "test/") 1)
                                                                 (else 2))))
                                                 (cons rank path))))))))))
  
  
  (method override (showcase-selection showing elements)
    (or selection (first elements)))
  
  
  (method override (draw-selection)
    (when selection
      (draw-target-circle~ selection)))
  
  
  (method override (selection-addable?)
    #t)))
