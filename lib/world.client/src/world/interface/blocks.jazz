;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Blocks
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is WorldScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2014
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See http://github.com/gcartier/world for details.


(module world.interface.blocks jazz


(import (world)
        (world.autoload)
        (world.block)
        (world.dyes)
        (world.entity)
        (world.geometry)
        (world.interface.showcase)
        (world.model)
        (world.object)
        (world.syntax (phase syntax)))


(class Blocks-Showcase extends World-Showcase
  
  
  (method override (showcase-showing)
    (current-zone))
  
  
  (method override (showcase-spacing)
    .8)
  
  
  (method override (showcase-y)
    -2.8)
  
  
  (method override (showcase-z)
    -.3)
  
  
  (method override (showcase-wait?)
    (not showing))
  
  
  (method override (showcase-elements showing)
    (let ((world (current-world)))
      (define (showcase-blocks)
        (let ((camera (cache-camera))
              (spacing (showcase-spacing))
              (scaling .6)
              (x (showcase-x))
              (y (showcase-y))
              (z 0.)
              (models (registered-block-models)))
          (map (lambda (model)
                 (let ((path (get-name~ model)))
                   (let ((entity (new Block parent: self model: path position: (vertex x y z) scale: (vertex scaling scaling scaling))))
                     (set-camera~ entity camera)
                     (increase! z spacing)
                     entity)))
               (sort < models key: get-id~))))
      
      (define (showcase-minecraft)
        (define (collect-models)
          (let ((queue (new-queue))
                (blockids (minecraft-blockids~ showing)))
            (loop (for n from 0 below (vector-length blockids))
                  (let ((block (vector-ref blockids n)))
                    (let ((name (get-name~ block)))
                      (unless (or (= (get-id~ block) 0)
                                  (eq? name 'Unknown))
                        (enqueue queue name)))))
            (queue-list queue)))
        
        (let ((camera (cache-camera))
              (spacing (showcase-spacing))
              (class (minecraft-block-class~ showing))
              (models (collect-models))
              (x (showcase-x))
              (y (showcase-y))
              (z 0.))
          (map (lambda (model)
                 (let ((scaling .6))
                   (let ((entity (new class parent: self model: model position: (vertex x y z) scale: (vertex scaling scaling scaling))))
                     (set-camera~ entity camera)
                     (set-model~ entity model)
                     (increase! z spacing)
                     entity)))
               models)))
      
      (if (minecraft?~ showing)
          (showcase-minecraft)
        (showcase-blocks))))
  
  
  (method override (showcase-selection showing elements)
    (or selection (first elements)))
  
  
  (method override (draw-selection)
    (when selection
      (draw-target~ selection red-dye)))
  
  
  (method override (ray-closest-element orig <f64vector> dir <f64vector>)
    (let ((blocks (cache-elements))
          (radius (showcase-spacing)))
      (when blocks
        (let ((closest #f)
              (distance #f))
          (loop (for n from 0 below (vector-length blocks))
                (let ((block (vector-ref blocks n)))
                  (when (ray-intersects-sphere? orig dir (get-position~ block) radius)
                    (for-each (lambda (poly)
                                (let ((cls (ray-closest-triangle~ poly orig dir #f)))
                                  (when cls
                                    (bind (poly dist) cls
                                      (when (or (not distance)
                                                (< dist distance))
                                        (set! closest (list poly dist))
                                        (set! distance dist))))))
                              (element-polygons~ block)))))
          closest))))
  
  
  (method override (selection-addable?)
    #t)))
