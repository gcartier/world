;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Profiler Panel
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.profiler jazz


(import (jazz.graphic)
        (jazz.markup)
        (jazz.outline)
        (jazz.resource)
        (jazz.tree)
        (jazz.ui)
        (world)
        (world.chronology)
        (world.context)
        (world.interface.panel)
        (world.profiling)
        (world.settings))


(class Profiler-Panel extends World-Panel
  
  
  (form
    (<install>                                          size: {Dimension 420 639} background: {Color red: .000 green: .000 blue: .000 alpha: .450} layout-type: fill
      (<Border-View>                                    border-type: edge style: entry layout-type: fill
        (<Scroller-View>                                style: document hscroll?: #t vscroll?: #t
          (<content~>                                   layout-type: fill
            (<Tree-Header>                              style: document selectable?: #f
              (<content~>
                (<Profiler-Tree>       name: sites      portfolio: :images filled-column: site show-unfocused-selection?: #f background: #f
                  (<Tree-Node-Column>  name: site       title: "Site" width: 300 controls?: #f display-images?: #f display-levels?: #f)
                  (<Tree-Label-Column> name: time       title: "Time" width: 65)
                  (<Tree-Label-Column> name: time%      title: "Percent" width: 65)
                  (<Tree-Label-Column> name: time-calls title: "Calls" width: 65)
                  (<Tree-Label-Column> name: time-rate  title: "Rate" width: 100)
                  (<Tree-Label-Column> name: memory     title: "Memory" width: 65)
                  (<Tree-Label-Column> name: memory%    title: "Percent" width: 65)
                  (<Tree-Label-Column> name: rate       title: "Rate" width: 50)
                  (<Tree-Image-Column> name: monitor    title: "" width: 20 justification: center offset: 3)))))))))
  
  
  (method package (view-sites self)
    @convert
    (let ((elapsed (update-sites! (current-chronology))))
      (let ((world (current-world))
            (tree (locate self 'sites))
            (root (sites-tree))
            (n/a "")
            (max-rate (world-setting 'world.profiler-max-rate 5000))
            (time-units (world-setting 'world.profiler-time-units 'ms))
            (header-color {Color red: 220 green: 0 blue: 0})
            (content-color {Color Light-Gray})
            (memory-color {Color red: .004 green: .688 blue: .856})
            (hide (world-setting 'world.profiler-hide #f)))
        (remove-every-row tree)
        (with-update-locked self
          (lambda ()
            (bind-values (last-gc-heap-size last-gc-alloc last-gc-live last-gc-movable last-gc-nonmovable) (process-memory)
              (define (root-profile toplevel-nodes)
                (let ((running 0.)
                      (allocated 0.))
                  (for-each (lambda (node)
                              (let ((site (car node)))
                                (let ((profile (get-profile site)))
                                  (increase! running (site-profile-running profile))
                                  (increase! allocated (site-profile-allocated profile)))))
                            toplevel-nodes)
                  (values running allocated)))
              
              (define (node-name node)
                (get-name (car node)))
              
              (define (add-nodes father parent parent-running parent-allocated parent-called nodes)
                (for-each (lambda (node)
                            (let ((site (car node))
                                  (children (cdr node)))
                              (let ((name (get-name site))
                                    (profile (get-profile site))
                                    (kind (getf (get-properties site) kind:)))
                                (let ((running (site-profile-running profile))
                                      (allocated (site-profile-allocated profile))
                                      (called (site-profile-called profile)))
                                  (define (compute-time running called)
                                    (and (> called 0.) (* (/ running called) (if (eq? time-units 'us) 1000000. 1000.))))
                                  
                                  (define (compute-memory allocated called)
                                    (and (> called 0.) (fxround (/ allocated called 1024.))))
                                  
                                  (define (compute-rate elapsed allocated)
                                    (and (> called 0.) (fxround (/ allocated elapsed 1024. 1024.))))
                                  
                                  (let ((time (compute-time running called))
                                        (memory (compute-memory allocated called))
                                        (rate (compute-rate elapsed allocated)))
                                    (define (compute-time%)
                                      (and parent-running running (> parent-running 0.)
                                        (percentage running parent-running)))
                                    
                                    (define (compute-memory%)
                                      (and parent-allocated allocated (> parent-allocated 0.)
                                        (percentage allocated parent-allocated)))
                                    
                                    (define (present-time-calls)
                                      (cond ((= called 0)
                                             n/a)
                                            ((not parent-called)
                                             (if (> elapsed 0.)
                                                 (let ((fps (/ called elapsed)))
                                                   (if (and max-rate (> fps max-rate))
                                                       (format ">{a}/s" max-rate)
                                                     (format "{r precision: 0}/s" fps)))
                                               ">>>"))
                                            (else
                                             n/a)))
                                    
                                    (define (present-time-rate)
                                      (cond ((= called 0)
                                             n/a)
                                            ((not parent-called)
                                             (if (> running 0.)
                                                 (let ((rate (/ called running)))
                                                   (if (and max-rate (> rate max-rate))
                                                       (format ">{a}/s" max-rate)
                                                     (format "{r precision: 0}/s" rate)))
                                               ">>>"))
                                            ((and parent-called called (> called 0.) (> parent-called 0.))
                                             (format "{r precision: 0}" (/ called parent-called)))
                                            (else
                                             n/a)))
                                    
                                    (define (present-memory-period)
                                      (if (> called 0.)
                                          (format "{r precision: 0}s" (/ elapsed called))
                                        n/a))
                                    
                                    (define (present-memory-count)
                                      (format "{a}" (gc-count)))
                                    
                                    (define (present-meg bytes)
                                      (format "{a}m" (meg bytes)))
                                    
                                    (define (meg bytes)
                                      (fxround (/ bytes 1024. 1024.)))
                                    
                                    (unless (and hide (memq? name hide))
                                      (let ((time% (and (> called 0.) (compute-time%)))
                                            (memory% (and (> called 0.) (compute-memory%)))
                                            (color (if (not father) header-color content-color)))
                                        (let ((row (add-row tree
                                                            father: father
                                                            state: 'expanded
                                                            children: (list (new Tree-Node title: (->string name) font: {Font Label-User} color: color)
                                                                            (new Tree-Label title: (if time (format "{r precision: 0}{a}" time time-units) n/a) font: {Font Label-User} color: color)
                                                                            (new Tree-Label title: (if time% (format "{r precision: 0}%" time%) n/a) font: {Font Label-User} color: color)
                                                                            (new Tree-Label title: (if (eq? kind 'memory) (present-memory-period) (present-time-calls)) font: {Font Label-User} color: color)
                                                                            (new Tree-Label title: (if (eq? kind 'memory) (present-memory-count) (present-time-rate)) font: {Font Label-User} color: color)
                                                                            (new Tree-Label title: (if (eq? kind 'memory) (present-meg last-gc-live) (if memory (format "{a}k" memory) n/a)) font: {Font Label-User} color: (if (eq? kind 'memory) memory-color color))
                                                                            (new Tree-Label title: (if (eq? kind 'memory) (present-meg last-gc-movable) (if memory% (format "{r precision: 0}%" memory%) n/a)) font: {Font Label-User} color: (if (eq? kind 'memory) memory-color color))
                                                                            (new Tree-Label title: (if (eq? kind 'memory) (present-meg last-gc-nonmovable) (if rate (format "{a}m/s" rate) n/a)) font: {Font Label-User} color: (if (eq? kind 'memory) memory-color color))
                                                                            (new Tree-Image image: (case (get-monitor site)
                                                                                                     ((performance) {Image-Resource "Green"})
                                                                                                     ((memory) {Image-Resource "Blue"})
                                                                                                     (else #f))))
                                                            user-data: site)))
                                          (add-nodes row node running allocated called children)))))))))
                          nodes))
              
              (let ((toplevel-nodes (cdr root)))
                (bind-values (root-running root-allocated) (root-profile toplevel-nodes)
                  (let ((show (world-setting 'world.profiler-show #f)))
                    (let ((effective-nodes (if show
                                               (map (lambda (name) (find toplevel-nodes name key: node-name return: 'item)) show)
                                             toplevel-nodes)))
                      (add-nodes #f root root-running root-allocated #f effective-nodes))))))))))))


;;;
;;;; Profiler-Tree
;;;


(class Profiler-Tree extends Tree-View
  
  
  (method override (mouse-down self evt)
    (toggle-monitor self (get-position evt) 'performance))
  
  
  (method override (double-click self evt)
    (toggle-monitor self (get-position evt) 'performance))
  
  
  (method override (right-mouse-down self evt)
    (toggle-monitor self (get-position evt) 'memory))
  
  
  (method (toggle-monitor self pos monitor)
    (let ((cell (view->tree self pos)))
      (when cell
        (let ((site (get-user-data (get-row self (get-line cell)))))
          (set-monitor site (if (eq? (get-monitor site) monitor) #f monitor))
          (invalidate-cell self cell)
          (refresh-interface (current-world))))))))
