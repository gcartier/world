;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Interface Bars
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.interface.bars jazz


(import (jazz.application)
        (jazz.system)
        (world)
        (world.autoload)
        (world.interface.actions)
        (world.settings)
        (world.syntax (phase syntax)))


;;;
;;;; Install
;;;


(definition public (install-bars bars)
  (let ((world (current-world)))
    (let ((interface (get-interface~ world))
          (appl (get-application)))
      (define (install-bar class name orientation location initially-visible?)
        (let ((bar (new class parent: interface name: name visible?: #f))
              (trait (find-role-trait~ appl name)))
          (when initially-visible?
            (set-visible?~ bar #t))
          (when trait
            (replace-active-trait~ bar trait))
          (let ((count (length (get-visible-children~ bar))))
            (set-layout-justification~ bar orientation)
            (set-size~ bar (let ((extent (if (= count 0)
                                             0
                                           (+ (* count 22) (* (- count 1) 2)))))
                             (case orientation
                               ((vertical) (new Dimension 22 extent))
                               ((horizontal) (new Dimension extent 22))
                               (else (error "Invalid orientation: {s}" orientation)))))
            (set-location~ bar
              ;; quick hack
              (if (eq? name 'camera)
                  (case current-role
                    ((programmer) '(-10 -36))
                    ((user designer admin) '(-44 -10)))
                location)))))
      
      (for-each (lambda (child)
                  (when (is? child World-Actionbar)
                    (close~ child)))
                (get-children~ interface))
      (for-each (lambda (bar)
                  (bind (class name orientation location . rest) bar
                    (let ((initially-visible? (if (null? rest) #t (car rest))))
                      (install-bar class name orientation location initially-visible?))))
                bars)
      (layout-view~ interface))))


;;;
;;;; Find
;;;


(definition public (find-bar name)
  (let ((world (current-world)))
    (find-component~ (get-interface~ world) name)))


(definition public (find-button bar-name name)
  (let ((bar (find-bar bar-name)))
    (and bar (find-component~ bar name)))))
