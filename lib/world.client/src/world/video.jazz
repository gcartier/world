;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Video
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.video jazz


(import (jazz.sound)
        (jazz.syntax (phase syntax))
        (world)
        (world.autoload)
        (world.configure)
        (world.settings)
        (world.syntax (phase syntax))
        (world.task))


(definition default-rate
  44100)

(definition default-format
  SoundIoFormatFloat32NE)

(definition default-channels
  2)

(definition default-latency
  .2)


;;;
;;;; Task
;;;


(definition public (cam-task task <Task>)
  (declare (proper-tail-calls))
  (let ((cam (current-cam)))
    (with-task task
      (lambda (exit)
        (let (iter (previous (current-seconds)))
          (let ((time (current-seconds)))
            (let ((elapse (- time previous)))
              (unless (stopping?~ task)
                (site (cam on?: #t)
                  (process-cam~ cam)))
              (let ((duration (- (current-seconds) time)))
                (task-sleep task cam-period duration exit))
              (iter time))))))))


;;;
;;;; Cam
;;;


(class World-Cam extends Object
  
  
  (slot cam?   <bool>     initialize #f accessors generate)
  (slot buffer <u8vector> initialize #f accessors generate)
  
  
  (method override (initialize)
    (set! cam? (world-setting 'world.cam? #f))
    (when (and cam? (client?))
      (start-cam)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-toggle-cam evt)
    (toggle-cam))
  
  
  (method (toggle-cam)
    (when (client?)
      (let ((world (current-world)))
        (if (not cam?)
            (start-cam)
          (stop-cam))
        (update-parameter~ world 'cam)
        (display-on/off~ world "Cam" cam?))))
  
  
  ;;;
  ;;;; Process
  ;;;
  
  
  (method (start-cam)
    (unless cam?
      (startup-sound)
      (let ((in-device (sound-default-input-device))
            (rate default-rate)
            (format default-format)
            (layout (sound-get-default-layout default-channels))
            (latency default-latency))
        (start-sound-input in-device rate format layout latency)
        (set! buffer (make-u8vector (sound-input-ring-capacity)))
        (set! cam? #t))))
  
  
  (method (stop-cam)
    (when cam?
      (stop-sound-input)
      ;(shutdown-sound)
      (set! buffer #f)
      (set! cam? #f)))
  
  
  (method (process-cam)
    (when cam?
      (let ((read (sound-read-input-ring buffer)))
        (when (> read 0)
          (send-client-audio~ (client-tier) (subu8vector buffer 0 read))))))))
