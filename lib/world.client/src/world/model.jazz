;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Models
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.model jazz


(import (jazz.jml)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.draw)
        (world.element)
        (world.generate)
        (world.geometry)
        (world.material)
        (world.mesh)
        (world.polygon)
        (world.serialization)
        (world.shader)
        (world.syntax (phase syntax)))


;;;
;;;; Model
;;;


(class Model extends Object
  
  
  (slot file        <object> initialize #f      accessors generate)
  (slot path        <object> initialize #f      accessors generate)
  (slot path-time   <object> initialize #f      accessors generate)
  (slot name        <object>                    accessors generate)
  (slot faces       <object>                    accessors generate)
  (slot orientation <object> initialize #f      accessors generate)
  (slot matrix      <object> initialize #f      accessors generate)
  (slot polygons    <vector+>                   accessors generate)
  (slot mesh        <object>                    accessors generate)
  (slot bounds      <object> initialize #f      getter generate setter explicit)
  (slot anchor      <object> initialize 'center accessors generate)
  (slot center      <object> initialize #f      getter generate)
  (slot animation   <object> initialize #f      accessors generate)
  (slot animations  <object> initialize #f      accessors generate)
  
  
  (method override (initialize (name: name #f) (faces: faces #f) (orientation: orientation #f) (material: material #f) (mesh: mesh #f))
    (set! name~self name)
    (set! faces~self faces)
    (set! orientation~self orientation)
    (set! matrix~self (make-model-matrix))
    (set! polygons~self (and faces (faces-polygons faces)))
    (set! mesh~self (or mesh (if faces (generate-mesh (faces-generator faces) matrix: matrix material: material) (new Mesh)))))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" name))))
  
  
  (method meta override (marshall-object obj)
    (serialize-object (class-of obj)
                      (vector
                        (serialize (get-path~ obj)))))
  
  
  (method meta override (unmarshall-object content)
    (let ((world (current-world)))
      (bind-vector (path) content
        (find-model~ world (deserialize path)))))
  
  
  (method (make-model-matrix)
    (and orientation (make-y-rotation-matrix& orientation)))
  
  
  (method (faces-polygons faces)
    (let ((count (length faces)))
      (let ((vect (make-f64vector (* count polygon-float-size)))
            (offset 0))
        (for-each (lambda (face)
                    (let ((poly (get-polygon~ face)))
                      (polygon-move!~ poly vect offset matrix)
                      (increase! offset polygon-float-size)))
                  faces)
        vect)))
  
  
  (method protected virtual (opaque-cube?)
    #f)
  
  
  (method public (set-bounds bnd)
    (set! bounds bnd)
    (set! center (cuboid-center bnd)))
  
  
  (method public (add-animation name anim)
    (when (not animations)
      (set! animations (make-table test: equal?)))
    (table-set! animations name anim))
  
  
  (method public (find-animation name)
    (if (not name)
        animation
      (and animations (table-ref animations name #f))))
  
  
  (method public (require-animation name)
    (or (find-animation name)
        (error "Unknown animation: {a}" name)))))
