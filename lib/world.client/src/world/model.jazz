;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Models
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.model jazz


(import (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.jml)
        (jazz.library)
        (world)
        (world.autoload)
        (world.draw)
        (world.element)
        (world.generate)
        (world.geometry)
        (world.material)
        (world.mesh)
        (world.polygon)
        (world.renderer)
        (world.serialization)
        (world.shader)
        (world.syntax (phase syntax)))


;;;
;;;; Metadata
;;;


(definition public (load-model-metadata name file)
  (when (exists?~ file)
    (let ((metadata (call-with-input-file (path-settings file) read)))
      (assert (and (pair? metadata)
                   (pair? (cdr metadata))
                   (let ((symbol (cadr metadata)))
                     (and (symbol? symbol)
                          (equal? (symbol->string symbol) name)))))
      (cddr metadata))))


(definition public (model-metadata-render metadata)
  (and metadata
       (let ((pair (assq 'render metadata)))
         (and pair
              (cadr pair)))))


;;;
;;;; Models
;;;


(definition models <table>
  (make-table test: eq?))

(definition model-id <fx>
  0)


(definition public (registered-models)
  models)

(definition public (registered-model name)
  (let ((generate/model (table-ref models name)))
    (if (is? generate/model Model)
        generate/model
      (bind (id . generate) generate/model
        (let ((model (generate)))
          (set-name~ model name)
          (set-id~ model id)
          (table-set! models name model)
          model)))))

(definition public (register-model name generate showcase?)
  (if (not showcase?)
      (table-set! models name (cons #f generate))
    (table-set! models name (cons model-id generate))
    (increase! model-id)))


;;;
;;;; Model
;;;


(class Model extends Component
  
  
  (property position  <f64vector> initialize (vertex 0. 0. 0.) getter generate setter explicit)
  (property rotation  <f64vector> initialize (vertex 0. 0. 0.) getter generate setter explicit)
  (property scale     <f64vector> initialize (vertex 1. 1. 1.) getter generate setter explicit)
  (property faces     <object>    initialize #f                getter generate setter explicit)
  (property material  <object>    initialize #f                accessors generate)
  (property meshes    <vector+>   initialize #f                accessors generate)
  (property program   <object>    initialize #f                accessors generate)
  (property pumps     <object>    initialize #f                accessors generate)
  
  
  (slot file          <object>    initialize #f                     accessors generate)
  (slot path          <object>    initialize #f                     accessors generate)
  (slot path-time     <object>    initialize #f                     accessors generate)
  (slot metadata      <object>    initialize #f                     accessors generate)
  (slot matrix        <f64vector> initialize (make-identity-matrix) accessors generate)
  (slot global-matrix <f64vector> initialize (make-identity-matrix) accessors generate)
  (slot polygons      <vector+>   initialize #f                     accessors generate)
  (slot bounds        <object>    initialize #f                     getter generate setter explicit)
  (slot anchor        <object>    initialize 'center                accessors generate)
  (slot center        <object>    initialize #f                     getter generate)
  (slot animation     <object>    initialize #f                     accessors generate)
  (slot animations    <object>    initialize #f                     accessors generate)
  (slot entity-class  <object>    initialize #f                     accessors generate)
  (slot id            <fx+>       initialize #f                     accessors generate)
  
  ;; debug
  (slot native        <object>    initialize #f                     accessors generate)
  (slot atlas         <object>    initialize #f                     accessors generate)
  
  
  (method override (install-in-parent)
    (nextmethod)
    (update-matrix))
  
  
  (method meta override (marshall-object obj)
    (serialize-object (class-of obj)
                      (vector
                        (serialize (get-path~ obj)))))
  
  
  (method meta override (unmarshall-object content)
    (let ((world (current-world)))
      (bind-vector (path) content
        (find-model~ world (deserialize path)))))
  
  
  (method public (set-position pos <f64vector>) <void>
    (vertex-copy! position pos)
    (update-matrix))
  
  
  (method public (set-rotation rot <f64vector>) <void>
    (vertex-copy! rotation rot)
    (update-matrix))
  
  
  (method public (set-scale s <f64vector>) <void>
    (vertex-copy! scale s)
    (update-matrix))
  
  (method public (set-scaling s <fl>) <void>
    (vertex-init! scale s s s)
    (update-matrix))
  
  
  (method (update-matrix)
    (matrix-multiply! matrix (make-translation-matrix& (vertex-x position) (vertex-y position) (vertex-z position))
                             (matrix-multiply& (make-rotation-matrix& (vertex-x rotation) (vertex-y rotation) (vertex-z rotation))
                                               (make-scaling-matrix& (vertex-x scale) (vertex-y scale) (vertex-z scale))))
    (if (not parent)
        (matrix-copy! global-matrix matrix)
      (matrix-multiply! global-matrix (get-global-matrix~ parent) matrix))
    (for-each update-matrix~ children))
  
  
  (method public (set-faces lst)
    (let ((world (current-world)))
      (set! faces lst)
      (set! polygons (faces-polygons faces))
      (set! meshes (vector (generate-mesh (faces-generator faces) matrix: matrix neighbors?: #t material: (or material (get-block-material~ world)))))))
  
  
  (method (faces-polygons faces)
    (let ((count (length faces)))
      (let ((vect (make-f64vector (* count polygon-float-size)))
            (offset 0))
        (for-each (lambda (face)
                    (let ((poly (get-polygon~ face)))
                      (polygon-move!~ poly vect offset matrix)
                      (increase! offset polygon-float-size)))
                  faces)
        vect)))
  
  
  (method protected virtual (opaque-cube?)
    #f)
  
  
  (method public (set-bounds bnd)
    (set! bounds bnd)
    (set! center (cuboid-center bnd)))
  
  
  (method public (determine-bounds)
    (let ((left +inf.0)
          (bottom +inf.0)
          (back +inf.0)
          (right -inf.0)
          (top -inf.0)
          (front -inf.0))
      (for-each (lambda (face)
                  (let ((vertices (get-vertices~ (get-polygon~ face))))
                    (loop (for n from 0 below (vector-length vertices))
                          (let ((vert (vector-ref vertices n)))
                            (let ((x (vertex-x vert))
                                  (y (vertex-y vert))
                                  (z (vertex-z vert)))
                              (when (< x left) (set! left x))
                              (when (< y bottom) (set! bottom y))
                              (when (< z back) (set! back z))
                              (when (> x right) (set! right x))
                              (when (> y top) (set! top y))
                              (when (> z front) (set! front z)))))))
                faces)
      (cuboid left bottom back right top front)))
  
  
  (method public (bounds-radius)
    (vertex-norm (cuboid-radiuses bounds)))
  
  
  (method public (iterate-meshes proc)
    (when meshes
      (loop (for mesh in-vector meshes)
            (proc mesh))))
  
  
  (method protected virtual (model-renderer animate?)
    (if animate?
        animation-transparent-renderer
      transparent-renderer))
  
  
  (method protected virtual (model-bind-uniforms entity program mesh matrix)
    (bind-uniforms~ program mesh matrix #f))
  
  
  (method protected virtual (model-unbind-uniforms entity program)
    (unbind-uniforms~ program))
  
  
  (method public (add-animation name anim)
    (when (not animations)
      (set! animations (make-table test: equal?)))
    (table-set! animations name anim))
  
  
  (method public (find-animation name)
    (if (not name)
        animation
      (and animations (table-ref animations name #f))))
  
  
  (method public (require-animation name)
    (or (find-animation name)
        (error "Unknown animation: {a}" name)))))
