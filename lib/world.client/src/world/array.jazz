;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Vertex Arrays
;;;


(module world.array jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.foreign)
        (time))


;;;
;;;; VAO
;;;


(class VAO extends Object
  
  
  (slot vao-buffer  <int> initialize #f getter generate)
  (slot vbo-floats  <int> initialize #f getter generate)
  (slot vbo-indices <int> initialize #f getter generate)
   
  
  (method override (initialize)
    (generate-buffer))
  
  
  (method public (activate)
    (when vao-buffer
      (glBindBuffer GL_ARRAY_BUFFER vao-buffer)))
  
  (method public (deactivate)
    (glBindBuffer GL_ARRAY_BUFFER 0))
  
  
  ;; Upload data to video card
  (method public (set-vbo content)
    (set! vbo-floats (f32vector-length content))
    (set! vbo-indices (/ vbo-floats 3))
    (glBindBuffer GL_ARRAY_BUFFER vao-buffer)
    (glBufferDataFloat* GL_ARRAY_BUFFER (f32vector-length content) content GL_STATIC_DRAW)
    (glBindBuffer GL_ARRAY_BUFFER 0))
  
  
  ;; Enable vertex attrib array before passing values to a shader
  (method public (enable-vertex-attrib location)
    (glEnableVertexAttribArray location))
  
  ;; Disable vertex attrib array
  (method public (disable-vertex-attrib location)
    (glDisableVertexAttribArray location))

  
  (method public (draw)
    (when vao-buffer
      (glDrawArrays GL_TRIANGLES 0 vbo-indices)))
  
  
  ;; Set the vertex attrib pointer to pass to the vertex shader
  (method public (set-attrib-pointer location (size: size 3) (type: type GL_FLOAT) (normalize: normalize GL_FALSE) (stride: stride 12) (offset: offset 0))
    (glVertexAttribPointer* location size type normalize stride offset))
  
  
  (method public (generate-buffer)
    (when vao-buffer
      (free-resources))
    (set! vao-buffer (glGenBuffers*)))
  
  
  (method public (free-resources)
    (when vao-buffer
      (glDeleteBuffers* vao-buffer)
      (set! vbo-indices #f)
      (set! vbo-floats #f)
      (set! vao-buffer #f)))
  
  
  (method override (destroy)
    (free-resources))))
