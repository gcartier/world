;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Parameters
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.parameters jazz


(import (jazz.io)
        (jazz.library))


(definition parameters
  (make-table test: eq?))


(definition public (register-parameter name parameter)
  (table-set! parameters name parameter))

(definition public (registered-parameter name)
  (or (table-ref parameters name #f)
      (error "Unknown parameter: {a}" name)))


(definition public (world-parameter name)
  (let ((parameter (registered-parameter name)))
    (let ((getter (get-getter~ parameter)))
      (getter))))

(definition public (world-parameter-set! name value)
  (let ((parameter (registered-parameter name)))
    (let ((setter (get-setter~ parameter)))
      (setter value))))

(definition public (toggle-parameter name)
  (world-parameter-set! name (not (world-parameter name))))


;;;
;;;; Parameter
;;;


(class World-Parameter extends Object
  
  
  (slot getter getter generate)
  (slot setter getter generate)
  (slot view   accessors generate)
  
  
  (method override (initialize getter (setter #f))
    (set! getter~self getter)
    (set! setter~self setter)
    (set! view~self #f))))
