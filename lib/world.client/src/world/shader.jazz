;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Shader
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.shader jazz


(import (jazz.opengl.glew)
        (world)
        (world.foreign))


;;;
;;;; Shader
;;;


(class Shader extends Object
  
  
  (slot type     <int>               accessors generate)
  (slot code           initialize #f accessors generate)
  (slot shaderid       initialize #f accessors generate)
  
  
  (method override (initialize self type)
    (set! self.type type))
  
  
  (method public (create-shader self)
    (set! shaderid (glCreateShader type))
    (glShaderSource shaderid 1 (list code) #f))
  
  
  (method public (compile-shader self)
    (glCompileShader shaderid)
    (when (/= (glGetShaderiv* shaderid GL_COMPILE_STATUS) GL_TRUE)
      (let ((loglength (glGetShaderiv* shaderid GL_INFO_LOG_LENGTH)))
        (when (> loglength 0)
          (error "{a}" (glGetShaderInfoLog* shaderid loglength)))))))


;;;
;;;; Shader-Program
;;;


(class Shader-Program extends Object
  
  
  (slot name            <object> getter generate)
  (slot vertex-shader   <object> accessors generate)
  (slot fragment-shader <object> accessors generate)
  (slot program         <object> accessors generate)
  (slot vao             <object> accessors generate)
  (slot attributes      <object> getter generate)
  (slot uniforms        <object> getter generate)
  
  
  (method override (initialize self (name: name #f))
    (nextmethod self)
    (set! self.name name)
    (set! vertex-shader (new Shader GL_VERTEX_SHADER))
    (set! fragment-shader (new Shader GL_FRAGMENT_SHADER))
    (set! program (glCreateProgram))
    (set! vao (glGenVertexArrays*))
    (set! attributes (make-table test: equal?))
    (set! uniforms (make-table test: equal?))
    (prepare self)
    (gl-check-error))
  
  
  (method package virtual (prepare self)
    )
  
  
  (method protected virtual (default-vertex-shader self)
    )
  
  (method protected virtual (default-fragment-shader self)
    )
  
  
  (method public (set-fs self code)
    (set-code fragment-shader code))
  
  (method public (set-vs self code)
    (set-code vertex-shader code))
  
  
  (method public (link self)
    (create-shaders self)
    (attach-shaders self))
  
  
  (method public (relink self)
    (detach-shaders self)
    (set-vs self (default-vertex-shader self))
    (set-fs self (default-fragment-shader self))
    (prepare self))
  
  
  (method public (create-shaders self)
    (create-shader vertex-shader)
    (create-shader fragment-shader)
    (compile-shader vertex-shader)
    (compile-shader fragment-shader))
  
  
  (method public (attach-shaders self)
    (when (and (get-shaderid vertex-shader)
               (get-shaderid fragment-shader))
      (glAttachShader program (get-shaderid vertex-shader))
      (glAttachShader program (get-shaderid fragment-shader))
      (glLinkProgram program)
      (when (/= (glGetProgramiv* program GL_LINK_STATUS) GL_TRUE)
        (let ((loglength (glGetProgramiv* program GL_INFO_LOG_LENGTH)))
          (when (> loglength 0)
            (error "{a}" (glGetProgramInfoLog* program loglength)))))))
  
  
  (method public (detach-shaders self)
    (when (and (get-shaderid vertex-shader)
               (get-shaderid fragment-shader))
      (glDetachShader program (get-shaderid vertex-shader))
      (glDetachShader program (get-shaderid fragment-shader))
      ;; need to investigate why the world is destroyed twice when exiting so this gets called twice
      (set-shaderid vertex-shader #f)
      (set-shaderid fragment-shader #f)))
  
  
  (method protected virtual (enable-attributes self mesh)
    )
  
  
  (method public (create-array self mesh)
    (let ((array (glGenVertexArrays*)))
      (glBindVertexArray array)
      (enable-attributes self mesh)
      (glBindVertexArray 0)
      array))
  
  
  (method public (use self)
    (glUseProgram program))
  
  
  (method public (unuse self)
    (glUseProgram 0))
  
  
  ;;;
  ;;;; Uniforms
  ;;;
  
  
  (method public (get-uniform self name)
    (table-ref uniforms name))
  
  
  (method public (add-uniform self name)
    (let ((location (glGetUniformLocation* program name)))
      (when validate-shaders?
        (assert (/= location -1)))
      (table-set! uniforms name location)
      location))
  
  
  ;;;
  ;;;; Attributes
  ;;;
  
  
  (method public (get-attribute self name)
    (table-ref attributes name))
  
  
  (method public (add-attribute self name)
    (let ((location (glGetAttribLocation* program name)))
      (when validate-shaders?
        (assert (/= location -1)))
      (table-set! attributes name location)
      location))))
