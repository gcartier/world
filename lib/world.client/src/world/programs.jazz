;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Shader Programs
;;;


(module world.programs jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.foreign)
        (world.geometry)
        (world.shader)
        (world.shaders)
        (time))


;;;
;;;; Mesh
;;;


(class Mesh-Program extends Shader-Program
  
  
  (method protected virtual (bind-uniforms mesh matrix)
    )
  
  
  (method protected virtual (enable-attributes mesh)
    )
  
  
  (method protected virtual (disable-attributes mesh)
    )
  
  
  (method protected virtual (unbind-uniforms)
    ))


;;;
;;;; Mesh-Color
;;;


(class Mesh-Color-Program extends Mesh-Program
  
  
  (method override (prepare)
    (set-vs mesh-color-vs)
    (set-fs mesh-color-fs)
    (link)
    (add-attribute "vertex_coord"))
  
  
  (method override (bind-uniforms mesh matrix)
    )
  
  
  (method override (enable-attributes mesh)
    (enable-vertex-attrib~ mesh (get-attribute "vertex_coord"))
    (set-attrib-pointer~ mesh (get-attribute "vertex_coord") 3 GL_FLOAT stride: (* 9 float-size)))
  
  
  (method override (disable-attributes mesh)
    (disable-vertex-attrib~ mesh (get-attribute "vertex_coord"))))


;;;
;;;; Mesh-Texture
;;;


(class Mesh-Texture-Program extends Mesh-Program
  
  
  (method override (prepare)
    (set-vs (default-vertex-shader))
    (set-fs (default-fragment-shader))
    (link)
    (when (use-texture?)
      (add-uniform "texture"))
    (add-attribute "vertex_coord")
    (add-attribute "texture_coord"))
  
  
  (method override (default-vertex-shader)
    mesh-texture-vs)
  
  (method override (default-fragment-shader)
    mesh-texture-fs)
  
  
  (method protected virtual (use-texture?)
    #t)
  
  
  (method override (bind-uniforms mesh matrix)
    (when (use-texture?)
      (glActiveTexture GL_TEXTURE0)
      (glBindTexture GL_TEXTURE_2D (get-texture~ (get-texture~ (get-material~ mesh))))
      (glUniform1i (get-uniform "texture") 0)))
  
  
  (method override (enable-attributes mesh)
    (let ((stride (* 9 float-size)))
      (enable-vertex-attrib~ mesh (get-attribute "vertex_coord"))
      (set-attrib-pointer~ mesh (get-attribute "vertex_coord") 3 GL_FLOAT stride: stride offset: 0)
      (enable-vertex-attrib~ mesh (get-attribute "texture_coord"))
      (set-attrib-pointer~ mesh (get-attribute "texture_coord") 3 GL_FLOAT stride: stride offset: 12)))
  
  
  (method override (disable-attributes mesh)
    (disable-vertex-attrib~ mesh (get-attribute "vertex_coord"))
    (disable-vertex-attrib~ mesh (get-attribute "texture_coord"))))


;;;
;;;; Mesh-Phong
;;;


(class Mesh-Phong-Program extends Mesh-Program
  
  
  (method override (prepare)
    (set-vs mesh-phong-vs)
    (set-fs mesh-phong-fs)
    (link)
    (add-uniform "texture")
    (add-uniform "lights_count")
    (add-uniform "model_matrix")
    (add-attribute "vertex_coord")
    (add-attribute "texture_coord")
    (add-attribute "normal"))
  
  
  (method override (bind-uniforms mesh matrix)
    ;; Texture
    (glActiveTexture GL_TEXTURE0)
    (glBindTexture GL_TEXTURE_2D (get-texture~ (get-texture~ (get-material~ mesh))))
    (glUniform1i (get-uniform "texture") 0)
    ;; Lighting
    (glUniform1i (get-uniform "lights_count") (count-lights~ zone))
    ;; Model view matrix
    (glUniformMatrix4fv* (get-uniform "model_matrix") 1 #f matrix))
  
  
  (method override (enable-attributes mesh)
    (let ((stride (* 9 float-size)))
      (enable-vertex-attrib~ mesh (get-attribute "vertex_coord"))
      (set-attrib-pointer~ mesh (get-attribute "vertex_coord") 3 GL_FLOAT stride: stride offset: 0)
      (enable-vertex-attrib~ mesh (get-attribute "texture_coord"))
      (set-attrib-pointer~ mesh (get-attribute "texture_coord") 3 GL_FLOAT stride: stride offset: 12)
      (enable-vertex-attrib~ mesh (get-attribute "normal"))
      (set-attrib-pointer~ mesh (get-attribute "normal") 3 GL_FLOAT stride: stride offset: 24)))
  
  
  (method override (disable-attributes mesh)
    (disable-vertex-attrib~ mesh (get-attribute "vertex_coord"))
    (disable-vertex-attrib~ mesh (get-attribute "texture_coord"))
    (disable-vertex-attrib~ mesh (get-attribute "normal")))
  
  
  (method override (unbind-uniforms)
    (glActiveTexture GL_TEXTURE0)
    (glBindTexture GL_TEXTURE_2D 0)))


;;;
;;;; Mesh-Transformation
;;;


(definition transformation-time
  1.0)


(class Mesh-Transformation-Program extends Mesh-Texture-Program
  
  
  (method override (prepare)
    (nextmethod)
    (add-uniform "time")
    (add-uniform "resolution"))
  
  
  (method override (bind-uniforms mesh matrix)
    (nextmethod mesh matrix)
    (glUniform1f (get-uniform "time") transformation-time)
    (set! transformation-time (+ transformation-time .01))
    (glUniform2f (get-uniform "resolution") 1. 1.)))


;;;
;;;; Mesh-Radial-Blur
;;;


(class Mesh-Radial-Blur-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-radial-blur-fs))


;;;
;;;; Mesh-Motion-Blur
;;;


(class Mesh-Motion-Blur-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-motion-blur-fs))


;;;
;;;; Mesh-Water
;;;


(class Mesh-Water-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-water-fs))


;;;
;;;; Mesh-Julia
;;;


(class Mesh-Julia-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-julia-fs)
  
  
  (method override (use-texture?)
    #f))


;;;
;;;; Mesh-Sierpinski
;;;


(class Mesh-Sierpinski-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-sierpinski-fs))


;;;
;;;; Mesh-Multitexture
;;;


(class Mesh-Multitexture-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-multitexture-fs)
  
  
  (method override (bind-uniforms mesh matrix)
    (nextmethod mesh matrix)
    (glActiveTexture GL_TEXTURE1)
    (glBindTexture GL_TEXTURE_2D (get-texture~ (find-texture~ world "test.png")))))


;;;
;;;; Mesh-Kaleidoscope
;;;


(class Mesh-Kaleidoscope-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-kaleidoscope-fs))


;;;
;;;; Mesh-Tunnel
;;;


(class Mesh-Tunnel-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-tunnel-fs))


;;;
;;;; Mesh-Square-Tunnel
;;;


(class Mesh-Square-Tunnel-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-square-tunnel-fs))


;;;
;;;; Mesh-Fly
;;;


(class Mesh-Fly-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-fly-fs))


;;;
;;;; Mesh-Pulse
;;;


(class Mesh-Pulse-Program extends Mesh-Transformation-Program
  
  
  (method override (default-fragment-shader)
    mesh-pulse-fs))


;;;
;;;; Texture
;;;


(class Texture-Program extends Shader-Program
  
  
  (method override (prepare)
    (set-vs texture-vs)
    (set-fs texture-fs)
    (link)
    (add-uniform "texture")
    (add-uniform "overlay"))))
