;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Entities
;;;


(module world.entity jazz


(import (jazz.jml)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.draw)
        (world.dye)
        (world.dyes)
        (world.element)
        (world.foreign)
        (world.foreign.obj)
        (world.geometry)
        (world.mesh)
        (world.shader)
        (world.syntax (phase syntax)))


;;;
;;;; Entity
;;;


(class Entity extends Element
  
  
  (property model  <object>    initialize #f accessors generate)
  (property matrix <f32vector> initialize (make-identity-matrix) getter generate)
  
  
  (method public (set-matrix mat)
    (matrix-copy! matrix mat))
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-color white-dye))
  
  
  (method override (position-update)
    (nextmethod)
    (update-matrix))
  
  
  (method (update-matrix)
    (matrix-multiply! matrix (make-translation-matrix& (get-x) (get-y) (get-z)) (make-scaling-matrix& radius radius radius)))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (method override (draw-3d-textured)
    (if (not model)
        (nextmethod)
      (draw-entity)))
  
  
  (method override (draw-3d)
    (if (not model)
        (nextmethod)
      (draw-entity)))
  
  
  (method (draw-entity)
    (let ((world (current-world)))
      (let ((mesh (get-mesh~ model))
            (program (find-mesh-program~ world (entity-program))))
        (use~ program)
        (entity-bind-uniforms program mesh matrix)
        ;(glDisable GL_DEPTH_TEST)
        ;(glEnable GL_CULL_FACE)
        ;(glCullFace GL_FRONT)
        ;(glFrontFace GL_CCW)
        (glEnable GL_ALPHA_TEST)
        (glAlphaFunc GL_GEQUAL .7)
        ;(glDisable GL_BLEND)
        (draw-mesh~ program mesh)
        (glDisable GL_ALPHA_TEST)
        ;(glDisable GL_CULL_FACE)
        ;(glEnable GL_DEPTH_TEST)
        ;(glEnable GL_BLEND)
        ;(glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
        (unbind-uniforms~ program)
        (unuse~ program))))
  
  
  (method protected virtual (entity-program)
    'dynamic-phong)
  
  
  (method protected virtual (entity-bind-uniforms program mesh matrix)
    (bind-uniforms~ program mesh matrix))
  
  
  (method protected virtual (animate elapse)
    ))


;;;
;;;; Skeletal Entity
;;;


(class Skeletal-Entity extends Entity
  
  
  (slot frame         initialize 0. getter generate)
  (slot bone-matrices initialize #f getter generate)
  (slot animate?      initialize #f accessors generate)
  
  
  (method override (finish rest)
    (nextmethod rest)
    (let ((world (current-world)))
      (register-tick~ world
        animate)))
  
  
  (method override (animate elapse)
    (declare (optimize-dead-local-variables))
    (when animate?
      (let ((imported (get-imported~ model)))
        (let ((fps (get-animation-fps~ imported))
              (frames (cast <fl> (get-total-frames~ imported))))
          (let ((new-frame (flmodulo (+ frame (* elapse fps)) frames)))
            (set-frame new-frame))))))
  
  
  (method public (set-frame frame)
    (declare (optimize-dead-local-variables))
    (set! frame~self frame)
    (set! bone-matrices (extract-bone-matrices)))
  
  
  (method (extract-bone-matrices)
    (declare (optimize-dead-local-variables))
    (let ((imported (get-imported~ model)))
      (set-frame~ imported frame)
      (let ((joints (get-joints~ imported)))
        (let ((count (vector-length joints)))
          (let ((content <f32vector> (or bone-matrices (make-f32vector (* 200 16)))))
            (loop (for i from 0 below count)
                  (let ((joint (vector-ref joints i)))
                    (let ((bone-transformation <f32vector> (get-bone-transformation~ joint)))
                      (subf32vector-move! bone-transformation 0 16 content (* i 16)))))
            content)))))
  
  
  (method override (entity-program)
    'skeletal-phong)
  
  
  (method override (entity-bind-uniforms program mesh matrix)
    (assert bone-matrices)
    (bind-uniforms~ program mesh matrix bone-matrices))))
