;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Rendering
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2013
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.render jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.foreign)
        (world.shader)
        (world.shaders)
        (time))


;;;
;;;; Render-Target
;;;


(class Render-Target extends Object
  
  
  (slot fbo          <int>  initialize #f getter generate)
  (slot depth-buffer <int>  initialize #f getter generate)
  (slot color-buffer <int>  initialize #f getter generate)
  (slot texture      <int>  initialize #f getter generate)
  (slot multisample? <bool> initialize #f getter generate)
  (slot width        <int>  initialize #f)
  (slot height       <int>  initialize #f)
  
  
  ;; Assign width, height and generate the necessary buffers if none are passed
  (method override (initialize width height (fbo: fbo #f) (depth-buffer: depth-buffer #f) (texture: texture #f) (multisample?: multisample? #f))
    (nextmethod)
    (set! fbo~self fbo)
    (set! depth-buffer~self depth-buffer)
    (set! texture~self texture)
    (set! width~self width)
    (set! height~self height)
    (set! multisample?~self multisample?)

    (when (and (not texture~self)
               (not multisample?))
      (initialize-texture))
    
    (when multisample?
      (initialize-multisampled-fbo))

    (when (and (not depth-buffer~self)
               (not multisample?))
      (initialize-depth-buffer))

    (when (not fbo~self)
      (initialize-fbo)))
  
  
  ;; Initialize a texture where the scene will be rendered
  (method protected (initialize-texture)
    (set! texture (gl-generate-texture))
    (glBindTexture GL_TEXTURE_2D texture)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER GL_NEAREST)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MIN_FILTER GL_NEAREST)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_S GL_CLAMP_TO_EDGE)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_T GL_CLAMP_TO_EDGE)
    (glTexImage2D GL_TEXTURE_2D 0 GL_RGBA width height 0 GL_RGBA GL_UNSIGNED_BYTE #f)
    (glBindTexture GL_TEXTURE_2D 0))
  
  
  (method protected (initialize-multisampled-fbo)
    (set! color-buffer (glGenRenderbuffersEXT*))
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT color-buffer)
    (glRenderbufferStorageMultisampleEXT GL_RENDERBUFFER_EXT (find-setting 'world.multisampling 4) GL_RGBA8 width height)
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT 0)
    
    (set! depth-buffer (glGenRenderbuffersEXT*))
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT depth-buffer)
    (glRenderbufferStorageMultisampleEXT GL_RENDERBUFFER_EXT (find-setting 'world.multisampling 4) GL_DEPTH_COMPONENT32 width height)
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT 0))
  
  
  ;; Initialize a depth buffer for rendering
  ;; Two or more render targets can share the same depth buffer
  (method protected (initialize-depth-buffer)
    (set! depth-buffer (glGenRenderbuffersEXT*))
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT depth-buffer)
    (glRenderbufferStorageEXT GL_RENDERBUFFER_EXT GL_DEPTH_COMPONENT32 width height)
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT 0))
  
  
  ;; Bind the texture and the depth buffer in a single framebuffer object
  (method protected (initialize-fbo)
    (when (and (or color-buffer texture)
               depth-buffer)
      (set! fbo (glGenFramebuffersEXT*))
      (glBindFramebufferEXT GL_FRAMEBUFFER_EXT fbo)
      
      (if multisample?
          (glFramebufferRenderbufferEXT GL_FRAMEBUFFER_EXT GL_COLOR_ATTACHMENT0_EXT GL_RENDERBUFFER_EXT color-buffer)
        (glFramebufferTexture2DEXT GL_FRAMEBUFFER_EXT GL_COLOR_ATTACHMENT0_EXT GL_TEXTURE_2D texture 0))
      
      (glFramebufferRenderbufferEXT GL_FRAMEBUFFER_EXT GL_DEPTH_ATTACHMENT_EXT GL_RENDERBUFFER_EXT depth-buffer)
      (glBindFramebufferEXT GL_FRAMEBUFFER_EXT 0)))
  
  
  ;;; Sets the render target for rendering
  (method public (activate)
    (when fbo
      (glBindFramebufferEXT GL_FRAMEBUFFER_EXT fbo)))
  
  
  ;; Unsets the current render-target for rendering, and sets the default back (opengl's native buffers)
  (method public (deactivate)
    (glBindFramebufferEXT GL_FRAMEBUFFER_EXT 0))
  
  
  (method public (blit destination)
    (glBindFramebufferEXT GL_READ_FRAMEBUFFER_EXT fbo)
    (glBindFramebufferEXT GL_DRAW_FRAMEBUFFER_EXT destination)
    (glBlitFramebufferEXT 0 0 width height 0 0 width height GL_COLOR_BUFFER_BIT GL_NEAREST)
    (glBindFramebufferEXT GL_READ_FRAMEBUFFER_EXT 0))
  
  
  ;; Resize buffers (useful when the window is resized)
  (method public (resize width height)
    (free-resources)
    (initialize width height multisample?: multisample?~self))
  
  
  ;; Unsets everything
  (method public (free-resources)
    (glDeleteRenderbuffersEXT* depth-buffer)
    (when texture
      (glDeleteTextures* texture))
    (when multisample?
      (glDeleteRenderbuffersEXT* color-buffer))
    (glDeleteFramebuffersEXT* fbo)
    (set! fbo #f)
    (set! texture #f)
    (set! color-buffer #f)
    (set! depth-buffer #f)))


;;;
;;;; PP-Program
;;;


@wait
(class PP-Program extends Shader-Program
  
  
  (slot vbo/fbo-vertices initialize #f accessors generate)
  
  
  (method override (prepare)
    (set-vs pp-vs)
    (set-fs pp-fs)
    (link)
    (set-vbo/fbo-vertices (glGenVertices*))
    (add-uniform "fbo_texture")
    (add-attribute "vertex_coord"))))
