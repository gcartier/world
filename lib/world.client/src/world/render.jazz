;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Rendering
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is WorldScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2014
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See http://github.com/gcartier/world for details.


(module world.render jazz


(import (jazz.graphic.opengl.glew)
        (jazz.system)
        (world.foreign))


;;;
;;;; Render-Target
;;;


(class Render-Target extends Object
  
  
  (slot frame-buffer <int>  initialize #f getter generate)
  (slot color-buffer <int>  initialize #f getter generate)
  (slot depth-buffer <int>  initialize #f getter generate)
  (slot texture      <int>  initialize #f getter generate)
  (slot multisample? <bool> initialize #f getter generate)
  (slot width        <int>  initialize #f)
  (slot height       <int>  initialize #f)
  
  
  (method override (initialize width height (multisample?: multisample? #f))
    (nextmethod)
    (set! width~self width)
    (set! height~self height)
    (set! multisample?~self multisample?)

    (if multisample?
        (initialize-multisampled)
      (initialize-texture)
      (initialize-depth-buffer))

    (initialize-frame-buffer))
  
  
  ;; Initialize using multisample extension
  (method protected (initialize-multisampled)
    (let ((multisampling (find-setting 'world.multisampling 4)))
      ;; color
      (set! color-buffer (glGenRenderbuffersEXT*))
      (glBindRenderbufferEXT GL_RENDERBUFFER_EXT color-buffer)
      (glRenderbufferStorageMultisampleEXT GL_RENDERBUFFER_EXT multisampling GL_RGBA width height)
      (glBindRenderbufferEXT GL_RENDERBUFFER_EXT 0)
      ;; depth
      (set! depth-buffer (glGenRenderbuffersEXT*))
      (glBindRenderbufferEXT GL_RENDERBUFFER_EXT depth-buffer)
      (glRenderbufferStorageMultisampleEXT GL_RENDERBUFFER_EXT multisampling GL_DEPTH_COMPONENT32 width height)
      (glBindRenderbufferEXT GL_RENDERBUFFER_EXT 0)))
  
  
  ;; Initialize texture where the scene will be rendered
  (method protected (initialize-texture)
    (set! texture (gl-generate-texture))
    (glBindTexture GL_TEXTURE_2D texture)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER GL_NEAREST)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MIN_FILTER GL_NEAREST)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_S GL_CLAMP_TO_EDGE)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_T GL_CLAMP_TO_EDGE)
    (glTexImage2D GL_TEXTURE_2D 0 GL_RGBA width height 0 GL_BGRA GL_UNSIGNED_BYTE #f)
    (glBindTexture GL_TEXTURE_2D 0))
  
  
  ;; Initialize depth buffer for rendering
  ;; Note that two or more render targets can share the same depth buffer
  (method protected (initialize-depth-buffer)
    (set! depth-buffer (glGenRenderbuffersEXT*))
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT depth-buffer)
    (glRenderbufferStorageEXT GL_RENDERBUFFER_EXT GL_DEPTH_COMPONENT32 width height)
    (glBindRenderbufferEXT GL_RENDERBUFFER_EXT 0))
  
  
  ;; Bind the texture / color buffer and the depth buffer in a single framebuffer object
  (method protected (initialize-frame-buffer)
    (set! frame-buffer (glGenFramebuffersEXT*))
    (glBindFramebufferEXT GL_FRAMEBUFFER_EXT frame-buffer)
    (if multisample?
        (glFramebufferRenderbufferEXT GL_FRAMEBUFFER_EXT GL_COLOR_ATTACHMENT0_EXT GL_RENDERBUFFER_EXT color-buffer)
      (glFramebufferTexture2DEXT GL_FRAMEBUFFER_EXT GL_COLOR_ATTACHMENT0_EXT GL_TEXTURE_2D texture 0))
    (glFramebufferRenderbufferEXT GL_FRAMEBUFFER_EXT GL_DEPTH_ATTACHMENT_EXT GL_RENDERBUFFER_EXT depth-buffer)
    (glBindFramebufferEXT GL_FRAMEBUFFER_EXT 0))
  
  
  ;;; Set the render target for rendering
  (method public (activate)
    (when frame-buffer
      (glBindFramebufferEXT GL_FRAMEBUFFER_EXT frame-buffer)))
  
  
  ;; Set the render target back to opengl's native buffer
  (method public (deactivate)
    (glBindFramebufferEXT GL_FRAMEBUFFER_EXT 0))
  
  
  (method public (blit destination)
    (glBindFramebufferEXT GL_READ_FRAMEBUFFER_EXT frame-buffer)
    (glBindFramebufferEXT GL_DRAW_FRAMEBUFFER_EXT destination)
    (glBlitFramebufferEXT 0 0 width height 0 0 width height GL_COLOR_BUFFER_BIT GL_NEAREST)
    (glBindFramebufferEXT GL_READ_FRAMEBUFFER_EXT 0))
  
  
  (method public (resize width height)
    (free-resources)
    (initialize width height multisample?: multisample?))
  
  
  (method public (free-resources)
    (when depth-buffer
      (glDeleteRenderbuffersEXT* depth-buffer))
    (when texture
      (glDeleteTextures* texture))
    (when color-buffer
      (glDeleteRenderbuffersEXT* color-buffer))
    (when frame-buffer
      (glDeleteFramebuffersEXT* frame-buffer))
    (set! texture #f)
    (set! frame-buffer #f)
    (set! color-buffer #f)
    (set! depth-buffer #f)))


;;;
;;;; PP-Program
;;;


@wait
(class PP-Program extends Shader-Program
  
  
  (slot fbo_texture-uniform getter generate)
  (slot vertex_coord-attribute getter generate)
  (slot vbo/fbo-vertices initialize #f accessors generate)
  
  
  (method override (prepare)
    (set-vs pp-vs)
    (set-fs pp-fs)
    (link)
    (set-vbo/fbo-vertices (glGenVertices*))
    (set! fbo_texture-uniform (add-uniform "fbo_texture"))
    (set! vertex_coord-attribute (add-attribute "vertex_coord")))))
