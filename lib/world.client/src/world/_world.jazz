;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World
;;;


(module world jazz


(import (jazz.graphic)
        (jazz.graphic.opengl)
        (jazz.graphic.opengl.platform)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.window)
        (time)
        (world.geometry)
        (world.object)
        (world.texture))


;; TODO
;; - we are redoing all the camera stuff all the time in draw-world


(class World extends Object
  
  
  (slot window    initialize #f)
  (slot width     initialize #f       getter generate)
  (slot height    initialize #f       getter generate)
  (slot objects   initialize '()      getter generate)
  (slot eye       initialize #f       getter generate)
  (slot eye-sight initialize #f       getter generate)
  (slot center    initialize #f       getter generate)
  (slot right     initialize #f       getter generate)
  (slot up        initialize #f       getter generate)
  (slot animate?  initialize #t       getter generate)
  (slot light?    initialize #t       getter generate)
  (slot mode      initialize 'explore getter generate)
  (slot what      initialize #f       getter generate)
  (slot target    initialize #f       getter generate)
  
  
  (slot last-point      initialize #f)
  (slot last-time       initialize #f)
  (slot camera-distance initialize 0.6)


  (slot me              initialize #f)
  (slot polygon-mode    initialize GL_FILL)
  (slot fps             initialize #f)
  (slot fps?            initialize #f)
  (slot interface?      initialize #t)
  (slot music?          initialize #f)
  (slot test            initialize #f)
  
  
  (method override (initialize)
    (prepare-camera)
    (reset-camera)
    (when music?
      (change-music)))
  
  
  (method override (destroy)
    (nextmethod)
    (close-music "ambiance"))
  
  
  ;;;
  ;;;; Window
  ;;;
  
  
  (method public (get-window)
    window)
  
  
  (method public (set-window value)
    (set! window value))
  
  
  (method protected virtual (initialize-opengl)
    (let ((size (get-size~ window)))
      (set! width (get-width~ size))
      (set! height (get-height~ size)))
    (gl-polygon-mode GL_FRONT GL_FILL)
    (gl-matrix-mode GL_MODELVIEW)
    (gl-enable GL_NORMALIZE))
  
  
  (method protected virtual (initialize-world)
    (set! fps (new FPS self 0.0 0.0 0.0))
    (set! test (make-cairo-texture 200 200)))
  
  
  (method (add-object object)
    (set! objects (cons object objects))
    object)
  
  
  (method (reset-world)
    (set! objects '())
    (initialize-world))
  
  
  (method protected virtual (draw-world)
    (process-keys)
    (prepare-world)
    (setup-camera)
    (setup-lighting)
    (update-fps~ fps)
    (draw-objects)
    (render-interface))
  
  
  (method protected virtual (prepare-world)
    (gl-clear (bitwise-ior GL_COLOR_BUFFER_BIT GL_DEPTH_BUFFER_BIT))
    (prepare-projection)
    (gl-matrix-mode GL_MODELVIEW)
    (gl-load-identity)
    (gl-disable GL_LIGHTING)
    (gl-disable GL_LIGHT0)
    (gl-disable GL_LIGHT1)
    (glEnable GL_BLEND)
    (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA))
  
  
  (method protected virtual (prepare-projection)
    (gl-matrix-mode GL_PROJECTION)
    (gl-load-identity)
    (let ((ratio (/ (cast <fl> width) (cast <fl> height))))
      (glu-perspective 45.0 ratio 0.1 100.0)))
  
  
  (method (change-music)
    (set! music? #t)
    (let ((music-dir {Directory Home ".world" "music"}))
      (when (exists?~ music-dir)
        (let ((music-list (get-content~ music-dir nodes?: #f)))
          (let ((music (random-element music-list)))
            (close-music "ambiance")
            (open-music music "ambiance")
            (play-music "ambiance"))))))
  
  
  (method (setup-camera)
    (glu-look-at
      (get-x~ eye) (get-y~ eye) (get-z~ eye)
      (get-x~ center) (get-y~ center) (get-z~ center)
      (get-x~ up) (get-y~ up) (get-z~ up)))
  
  
  (method (setup-lighting)
    (when light?
      (gl-enable GL_LIGHTING)
      (gl-enable GL_LIGHT0)
      (gl-enable GL_LIGHT1)
      (gl-material-specular GL_FRONT 0.1 0.1 0.1 1.0)
      (gl-material-shininess GL_FRONT 50.0)
      (gl-light-model GL_LIGHT_MODEL_AMBIENT 0.2 0.2 0.2 1.0)
      ;; positioned light
      (gl-light GL_LIGHT0 GL_DIFFUSE 0.5 0.5 0.5 1.0)
      (gl-light GL_LIGHT0 GL_POSITION 4.0 0.0 8.0 1.0)
      ;; directed light
      (gl-light GL_LIGHT1 GL_DIFFUSE 0.5 0.5 0.4 1.0)
      (gl-light GL_LIGHT1 GL_POSITION -1.0 0.5 0.5 0.0)
      @debug-lighting
      (draw-sphere 4.0 0.0 8.0 0.5 26 52 {Color Orange})
      @debug-lighting
      (draw-sphere -1.0 0.5 0.5 0.5 26 52 {Color Yellow})))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method protected virtual (process-keys)
    )
  
  
  (method protected virtual (key-down c)
    )
  
  
  (method protected virtual (key-press c)
    )
  
  
  (method (process-movement)
    (when (eq? window (get-window-focus))
      (receive (shift? control? alt?) (modifiers-down)
        (when (key-down? (char->integer #\W))
          (move-forward))
        (when (key-down? (char->integer #\S))
          (move-backward))
        (when (key-down? (char->integer #\Q))
          (strafe-left))
        (when (key-down? (char->integer #\E))
          (strafe-right))
        (when (or (key-down? (char->integer #\Z))
                  (key-down? (char->integer #\space)))
          (strafe-up))
        (when (key-down? (char->integer #\X))
          (strafe-down))
        (when (key-down? (char->integer #\A))
          (rotate-left))
        (when (key-down? (char->integer #\D))
          (rotate-right)))))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (method protected virtual (draw-objects)
    (for-each (lambda (object)
                (draw~ object))
              objects))
  
  
  (method (render-interface)
    (when (or interface? fps?)
      (gl-disable GL_LIGHTING)
      (gl-disable GL_LIGHT0)
      (gl-disable GL_LIGHT1)
      (gl-matrix-mode GL_MODELVIEW)
      (gl-load-identity)
      (gl-matrix-mode GL_PROJECTION)
      (glEnable GL_BLEND)
      (glBlendFunc GL_SRC_ALPHA GL_ONE_MINUS_SRC_ALPHA)
      (with-gl-matrix
        (lambda ()
          (gl-load-identity)
          (gl-ortho 0.0 (exact->inexact width) 0.0 (exact->inexact height) -10.0 10.0)
          (when interface?
            (draw-interface)
            @test (
            (gl-color 1.0 0.0 0.0)
            (let ((surface (get-surface~ test)))
              (set-operator~ surface CAIRO_OPERATOR_CLEAR)
              (paint~ surface)
              (set-operator~ surface CAIRO_OPERATOR_SOURCE)
              (let ((color {Color Red alpha: 0.5}))
                (ellipse~ surface {Rect 0 0 200 200} color color)))
            (map-texture~ test)
            (with-gl-matrix
              (lambda ()
                (gl-translate (exact->inexact (- width 200)) 0.0 0.0)
                (textured-quad~ test
                  (lambda () (gl-vertex 0.0   0.0 0.0))
                  (lambda () (gl-vertex 200.0   0.0 0.0))
                  (lambda () (gl-vertex 200.0 200.0 0.0))
                  (lambda () (gl-vertex 0.0 200.0 0.0)))))))))))
  
  
  (method protected virtual (draw-interface)
    (draw-fps))
  
  
  (method (draw-fps)
    (when fps?
      (draw~ fps)))
  
  
  ;;;
  ;;;; Camera
  ;;;
  
  
  (method protected virtual (prepare-camera)
    (set! eye (new Vertex 0.0 3.0 10.0))
    (set! eye-sight (new Vertex 0.0 0.0 -6.0))
    (set! right (new Vertex 1.0 0.0 0.0))
    (set! up (new Vertex 0.0 1.0 0.0)))
  
  
  (method (get-me)
    (vertex+ eye (vertex-scalar* eye-sight camera-distance)))

  
  (method (zoom-in)
    (let ((me (get-me)))
      (increase! camera-distance (- (zoom-speed)))
      (set! eye (vertex+ me (vertex-scalar* eye-sight (- camera-distance))))))
  
  
  (method (zoom-out)
    (let ((me (get-me)))
      (increase! camera-distance (zoom-speed))
      (set! eye (vertex+ me (vertex-scalar* eye-sight (- camera-distance))))))
  
  
  (method protected virtual (zoom-speed)
    0.1)

  
  (method (reset-camera)
    (update-camera))
  
  
  (method (update-camera)
    (set! center (vertex+ eye eye-sight)))
  
  
  (method (report-camera)
    (terminal eye: eye)
    (terminal eye-sight: eye-sight (vertex-norm eye-sight))
    (terminal center: center)
    (terminal right: right (vertex-norm right))
    (terminal up: up (vertex-norm up)))
  
  
  (method (mouse-wheel h v delta)
    (if (> delta 0)
        (zoom-in)
      (zoom-out)))
  
  
  ;;;
  ;;;; Movement
  ;;;
  
  
  (method protected virtual (movement-speed)
    0.05)
  
  
  (method (move-forward)
    (set! eye (vertex+ eye (vertex-scalar* eye-sight (movement-speed))))
    (reset-camera)
    (movement))
  
  
  (method (move-backward)
    (set! eye (vertex+ eye (vertex-scalar* eye-sight (- (movement-speed)))))
    (reset-camera)
    (movement))
  
  
  (method (strafe-left)
    (set! eye (vertex+ eye (vertex-scalar* right (- (* (movement-speed) 2)))))
    (reset-camera)
    (movement))
  
  
  (method (strafe-right)
    (set! eye (vertex+ eye (vertex-scalar* right (* (movement-speed) 2))))
    (reset-camera)
    (movement))
  
  
  (method (strafe-up)
    (set! eye (vertex+ eye (vertex-scalar* up (* (movement-speed) 2))))
    (reset-camera)
    (movement))
  
  
  (method (strafe-down)
    (set! eye (vertex+ eye (vertex-scalar* up (- (* (movement-speed) 2)))))
    (reset-camera)
    (movement))
  
  
  (method protected virtual (movement)
    )
  
  
  (define rotation-speed
    0.02)
  
  
  (method (rotate-left)
    (rotate-horizontal rotation-speed))
  
  
  (method (rotate-right)
    (rotate-horizontal (- rotation-speed)))
  
  
  (method (rotate-horizontal angle)
    (set! eye-sight (rotate-upon angle up eye-sight))
    (set! right (vertex-normalize (cross-product eye-sight up)))
    (reset-camera))
  
  
  (method (rotate-up)
    @wrong-number-of-parameters???
    (rotate-vertical rotation-speed right eye-sight))
  
  
  (method (rotate-down)
    (rotate-vertical (- rotation-speed)))
  
  
  (method (rotate-vertical angle)
    (set! eye-sight (rotate-upon angle right eye-sight))
    (set! up (vertex-normalize (rotate-upon angle right up)))
    (reset-camera))
  
  
  (method (cursor-update)
    (set-cursor :arrow))
  
  
  (method (mouse-down h v)
    (set! animate? (not animate?)))
  
  
  (method (right-mouse-down h v)
    (set! last-point (new Point h v))
    (set! last-time  (time->seconds (current-time)))
    @w
    (case mode
      ((explore)
       (set! animate? (not animate?)))
      ((insert)
       (case what
         ((cube)
          (add-object (new Cube self (random-in 3.0) (random-in 3.0) -6.0)))))))
  
  
  (method (mouse-move h v)
    (when last-point
      (let ((new-point (new Point h v))
            (new-time (time->seconds (current-time))))
        (let ((distance (norm (point- new-point last-point)))
              (time (- new-time last-time)))
          (when (> time 0)
            (let ((speed (/ distance time)))
              (let ((delta (nu- new-point last-point)))
                (let ((dh (get-h~ delta))
                      (dv (get-v~ delta)))
                  (rotate-horizontal (exact->inexact (* speed (/ (- dh) 200000))))
                  (rotate-vertical (exact->inexact (* speed (/ (- dv) 200000)))))))))
        (set! last-point new-point)
        (set! last-time new-time))))
  
  
  (method (mouse-up h v)
    (set! last-point #f))
  
  
  (method (right-mouse-up h v)
    (set! last-point #f))
  
  
  ;;;
  ;;;; Objects
  ;;;
    
  
  (method (add-axes)
    (add-object (new Axes self)))
  
  
  ;;;
  ;;;; Toggle
  ;;;
  
  
  (method (toggle-interface)
    (set! interface? (not interface?)))
  
  
  (method (toggle-fps)
    (set! fps? (not fps?)))
  
  
  (method (toggle-polygon-mode)
    (set! polygon-mode (ecase polygon-mode
                         ((GL_POINT) GL_LINE)
                         ((GL_LINE) GL_FILL)
                         ((GL_FILL) GL_POINT)))
    (glPolygonMode GL_FRONT_AND_BACK polygon-mode))
  
  
  (method (toggle-lighting)
    (set! light? (not light?)))
  
  
  (method (toggle-fullscreen)
    (set-fullscreen?~ window (not (get-fullscreen?~ window))))))
