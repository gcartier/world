;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Meshes
;;;


(module world.mesh jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.foreign)
        (time))


;;;
;;;; Mesh
;;;


(class Mesh extends Object
  
  
  (slot vbo-buffer  <int+>   initialize #f getter generate)
  (slot vbo-floats  <int+>   initialize #f getter generate)
  (slot vbo-indices <int+>   initialize #f getter generate)
  (slot material    <object> initialize #f accessors generate)
  
  
  (method override (initialize (material: material #f))
    (set! material~self material))
  
  
  (method public (bind)
    (when vbo-buffer
      (glBindBuffer GL_ARRAY_BUFFER vbo-buffer)))
  
  (method public (unbind)
    (glBindBuffer GL_ARRAY_BUFFER 0))
  
  
  (method public (empty?)
    (not vbo-buffer))
  
  
  ;; Upload data to video card
  (method public (set-vbo content)
    (set! vbo-floats (f32vector-length content))
    (set! vbo-indices (/ vbo-floats 9))
    (glBindBuffer GL_ARRAY_BUFFER vbo-buffer)
    (glBufferDataFloat* GL_ARRAY_BUFFER (f32vector-length content) content GL_STATIC_DRAW)
    (glBindBuffer GL_ARRAY_BUFFER 0))
  
  
  ;; Enable vertex attrib array before passing values to a shader
  (method public (enable-vertex-attrib location)
    (when (or (/= location -1) validate-shaders?)
      (glEnableVertexAttribArray location)))
  
  ;; Disable vertex attrib array
  (method public (disable-vertex-attrib location)
    (when (or (/= location -1) validate-shaders?)
      (glDisableVertexAttribArray location)))

  
  (method public (draw)
    (when vbo-buffer
      (glDrawArrays GL_TRIANGLES 0 vbo-indices)))
  
  
  ;; Set the vertex attrib pointer to pass to the vertex shader
  (method public (set-attrib-pointer location size type (normalize: normalize GL_FALSE) (stride: stride 12) (offset: offset 0))
    (when (or (/= location -1) validate-shaders?)
      (glVertexAttribPointer* location size type normalize stride offset)))
  
  
  (method public (generate-buffer)
    (when vbo-buffer
      (free-resources))
    (set! vbo-buffer (glGenBuffers*)))
  
  
  (method public (free-resources)
    (when vbo-buffer
      (glDeleteBuffers* vbo-buffer)
      (set! vbo-indices #f)
      (set! vbo-floats #f)
      (set! vbo-buffer #f)))
  
  
  (method override (destroy)
    (free-resources))))
