;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Texture
;;;


(module world.texture jazz


(import (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.library)
        (jazz.literals)
        (jazz.platform)
        (jazz.platform.cairo))


;;;
;;;; Cairo
;;;


(class Cairo-Texture extends Object
  
  
  (slot width   <fx>      getter generate)
  (slot height  <fx>      getter generate)
  (slot surface <Surface> getter generate)
  (slot data    <object>  getter generate)
  (slot texture <object>  getter generate)
  (slot program <object>  getter generate)
  (slot overlay <fl+>     getter generate)
  (slot file    <object>  getter generate)
  
  
  (method override (initialize width height (surface: surface #f) (program: program #f) (overlay: overlay #f) (file: file #f))
    (set! width~self width)
    (set! height~self height)
    (set! program~self program)
    (set! overlay~self overlay)
    (set! file~self file)
    (set! surface~self (or surface (new Surface (cairo_image_surface_create CAIRO_FORMAT_ARGB32 width height))))
    (set! data~self (cairo_image_surface_get_data (get-handle~ surface~self)))
    (set! texture~self (gl-generate-texture)))
  
  
  (method protected virtual (map-texture)
    (glEnable GL_TEXTURE_2D)
    (glBindTexture GL_TEXTURE_2D texture)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MIN_FILTER GL_LINEAR)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER GL_LINEAR)
    (glTexImage2D GL_TEXTURE_2D
                  0
                  GL_RGBA
                  width
                  height
                  0
                  GL_BGRA
                  GL_UNSIGNED_BYTE
                  data)
    (glBindTexture GL_TEXTURE_2D 0))
  
  
  (method (textured-quad bl tl tr br)
    (if (not program)
        (begin
          (glColor3f 1. 1. 1.)
          (glBindTexture GL_TEXTURE_2D texture))
      (activate~ program)
      (glActiveTexture GL_TEXTURE0)
      (glBindTexture GL_TEXTURE_2D texture)
      (glUniform1i (get-uniform~ program "texture") 0)
      (glUniform1f (get-uniform~ program "overlay") (or overlay 0.6)))
    (glBegin GL_QUADS)
    (glTexCoord2d 0. 1.)
    (bl)
    (glTexCoord2d 1. 1.)
    (tl)
    (glTexCoord2d 1. 0.)
    (tr)
    (glTexCoord2d 0. 0.)
    (br)
    (glEnd)
    (if (not program)
        (glBindTexture GL_TEXTURE_2D 0)
      (deactivate~ program)
      (glBindTexture GL_TEXTURE_2D 0))))


;;;
;;;; Image
;;;


(class Image-Texture extends Cairo-Texture
  
  
  (method override (map-texture)
    (glBindTexture GL_TEXTURE_2D texture)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MIN_FILTER GL_LINEAR_MIPMAP_LINEAR)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER GL_LINEAR)
    (glTexImage2D GL_TEXTURE_2D
                  0
                  GL_RGBA
                  width
                  height
                  0
                  GL_BGRA
                  GL_UNSIGNED_BYTE
                  data)
    (glGenerateMipmapEXT GL_TEXTURE_2D)
    (glBindTexture GL_TEXTURE_2D 0)))


;;;
;;;; Creation
;;;


(definition public (make-cairo-texture width height (program: program #f) (overlay: overlay #f))
  (new Cairo-Texture width height program: program overlay: overlay))


(definition public (make-png-texture file program)
  (assert (exists?~ file))
  (let ((png (cairo_image_surface_create_from_png (parse~ file))))
    (let ((surface (new Surface png))
          (width (cairo_image_surface_get_width png))
          (height (cairo_image_surface_get_height png)))
      (let ((ct (new Image-Texture width height surface: surface program: program file: file)))
        (map-texture~ ct)
        ct)))))
