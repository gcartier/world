;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Texture
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.texture jazz


(require (gaea.types (phase syntax)))

(import (jazz.cairo)
        (jazz.graphic)
        (jazz.graphic.image)
        (jazz.io)
        (jazz.jpeg)
        (jazz.opengl.glew)
        (jazz.opengl.texture)
        (jazz.screen)
        (world)
        (world.context)
        (world.external.targa)
        (world.foreign))


;;;
;;;; File
;;;


(definition public (make-file-texture file (min-filter: min-filter #f) (mag-filter: mag-filter #f) (wrap: wrap #f) (mipmap?: mipmap? #t) (metadata: metadata #f))
  (let ((ext (get-extension file)))
    (cond ((extension=? ext "png")
           (make-png-texture file min-filter: min-filter mag-filter: mag-filter wrap: wrap mipmap?: mipmap? metadata: metadata))
          ((extension=? ext "tga")
           (make-tga-texture file min-filter: min-filter mag-filter: mag-filter wrap: wrap mipmap?: mipmap? metadata: metadata))
          ((jpeg-extension? ext)
           (make-jpeg-texture file min-filter: min-filter mag-filter: mag-filter wrap: wrap mipmap?: mipmap? metadata: metadata))
          (else
           (error "Unsupported texture format: {a}" ext)))))


(definition public (cairo-surface-from-file file)
  (let ((ext (get-extension file)))
    (cond ((extension=? ext "png")
           (cairo-surface-from-png file))
          ((extension=? ext "tga")
           (cairo-surface-from-tga file))
          ((jpeg-extension? ext)
           (cairo-surface-from-jpeg file))
          (else
           (error "Unsupported texture format: {a}" ext)))))


;;;
;;;; PNG
;;;


(definition protected (make-png-texture file (min-filter: min-filter #f) (mag-filter: mag-filter #f) (wrap: wrap #f) (mipmap?: mipmap? #t) (metadata: metadata #f))
  (assert (exists? file))
  (bind-values (width height surf) (cairo-surface-from-png file)
    (prog1 (let ((data (cairo_image_surface_get_data surf)))
             (let ((ct (new Image-Texture width height data: data min-filter: min-filter mag-filter: mag-filter wrap: wrap mipmap?: mipmap? metadata: metadata file: file)))
               (map-texture ct)
               ct))
      (cairo_surface_destroy surf))))


(definition protected (cairo-surface-from-png file)
  (let ((surf (create-cairo-surface-from-png file)))
    (let ((width (cairo_image_surface_get_width surf))
          (height (cairo_image_surface_get_height surf)))
      (values width height surf))))


;;;
;;;; TGA
;;;


(definition protected (make-tga-texture file (min-filter: min-filter #f) (mag-filter: mag-filter #f) (wrap: wrap #f) (mipmap?: mipmap? #t) (metadata: metadata #f))
  (assert (exists? file))
  (bind-values (width height bytes/pixel data free-data) (load-tga file)
    (let ((ct (new Image-Texture width height data: data min-filter: min-filter mag-filter: mag-filter wrap: wrap mipmap?: mipmap? metadata: metadata file: file)))
      ;; work around cairo_image_surface_create_for_data
      ;; not taking ownership of the data
      (make-will ct
        (lambda (ct)
          (free-data)))
      (map-texture ct)
      ct)))


(definition protected (cairo-surface-from-tga file)
  (bind-values (width height bytes/pixel data free-data) (load-tga file)
    (let ((format CAIRO_FORMAT_ARGB32))
      (let ((stride (cairo_format_stride_for_width format width)))
        (assert (= stride (* width bytes/pixel)))
        (let ((surf (cairo_image_surface_create_for_data data format width height stride)))
          ;; work around cairo_image_surface_create_for_data
          ;; not taking ownership of the data
          (make-will surf
            (lambda (surf)
              (free-data)))
          (values width height surf))))))


;;;
;;;; JPEG
;;;


(definition protected (make-jpeg-texture file (min-filter: min-filter #f) (mag-filter: mag-filter #f) (wrap: wrap #f) (mipmap?: mipmap? #t) (metadata: metadata #f))
  (assert (exists? file))
  (bind-values (width height surf) (cairo-surface-from-jpeg file)
    (prog1 (let ((data (cairo_image_surface_get_data surf)))
             (let ((ct (new Image-Texture width height data: data min-filter: min-filter mag-filter: mag-filter wrap: wrap mipmap?: mipmap? metadata: metadata file: file)))
               (map-texture ct)
               ct))
      (cairo_surface_destroy surf))))


;;;
;;;; UV
;;;


(constant inline uv/left   0)
(constant inline uv/top    1)
(constant inline uv/right  2)
(constant inline uv/bottom 3)


(definition public inline (uv left <fl> top <fl> right <fl> bottom <fl> depth <fl>) <uv>
  (f32vector left top right bottom depth))


(definition public inline (allocate-uv left <fl> top <fl> right <fl> bottom <fl> depth <fl>) <uv>
  (f32vector left top right bottom depth))


(definition public inline (make-uv) <uv>
  (f32vector 0. 0. 0. 0. 0.))


(definition public (copy-uv uv <uv>) <uv>
  (let ((copy (make-uv)))
    (uv-copy! copy uv)
    copy))


(definition public (uv-rect uv <uv> l <fl> t <fl> r <fl> b <fl>) <uv>
  (let ((left (uv-left uv))
        (top (uv-top uv)))
    (let ((width (- (uv-right uv) left))
          (height (- (uv-bottom uv) top)))
      (allocate-uv (+ left (* l width)) (+ top (* t height)) (+ left (* r width)) (+ top (* b height)) (uv-depth uv)))))
        

(definition public inline (uv-left uv <uv>) <fl>
  (f32vector-ref uv 0))

(definition public inline (uv-top uv <uv>) <fl>
  (f32vector-ref uv 1))

(definition public inline (uv-right uv <uv>) <fl>
  (f32vector-ref uv 2))

(definition public inline (uv-bottom uv <uv>) <fl>
  (f32vector-ref uv 3))

(definition public inline (uv-depth uv <uv>) <fl>
  (f32vector-ref uv 4))


(definition public inline (uv-left-set! uv <uv> left <fl>) <void>
  (f32vector-set! uv 0 left))

(definition public inline (uv-top-set! uv <uv> top <fl>) <void>
  (f32vector-set! uv 1 top))

(definition public inline (uv-right-set! uv <uv> right <fl>) <void>
  (f32vector-set! uv 2 right))

(definition public inline (uv-bottom-set! uv <uv> bottom <fl>) <void>
  (f32vector-set! uv 3 bottom))

(definition public inline (uv-depth-set! uv <uv> depth <fl>) <void>
  (f32vector-set! uv 4 depth))


(definition public (uv=? c1 <uv> c2 <uv>) <bool>
  (and (= (uv-left c1) (uv-left c2))
       (= (uv-top c1) (uv-top c2))
       (= (uv-right c1) (uv-right c2))
       (= (uv-bottom c1) (uv-bottom c2))
       (= (uv-depth c1) (uv-depth c2))))


(definition public (uv! uv <uv> left <fl> top <fl> right <fl> bottom <fl> depth <fl>) <void>
  (uv-left-set! uv left)
  (uv-top-set! uv top)
  (uv-right-set! uv right)
  (uv-bottom-set! uv bottom)
  (uv-depth-set! uv depth))


(definition public (uv-copy! dst <uv> src <uv>) <void>
  (uv-left-set! dst (uv-left src))
  (uv-top-set! dst (uv-top src))
  (uv-right-set! dst (uv-right src))
  (uv-bottom-set! dst (uv-bottom src))
  (uv-depth-set! dst (uv-depth src)))


;;;
;;;; Coordinates
;;;


(definition public (image-coordinates image)
  (let ((world (current-world)))
    (image-rect (get-block-atlas world) image)))


;;;
;;;; Dummy
;;;


(definition public dummy-uv <f32vector>
  (uv 0. 0. 1. 1. 0.)))
