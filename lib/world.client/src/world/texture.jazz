;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Texture
;;;


(module world.texture jazz


(import (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.library)
        (jazz.literals)
        (jazz.platform)
        (jazz.platform.cairo))


;;;
;;;; Texture
;;;


(class Cairo-Texture extends Object
  
  
  (slot width   <fx>      getter generate)
  (slot height  <fx>      getter generate)
  (slot surface <Surface> getter generate)
  (slot data    <object>  getter generate)
  (slot texture <object>  getter generate)
  
  
  (method override (initialize width height (surface: surface #f))
    (set! width~self width)
    (set! height~self height)
    (set! surface~self (or surface (new Surface (cairo_image_surface_create CAIRO_FORMAT_ARGB32 width height))))
    (set! data~self (cairo_image_surface_get_data (get-handle~ surface~self)))
    (set! texture~self (gl-generate-texture)))
  
  
  (method (map-texture)
    (glEnable GL_TEXTURE_2D)
    (glBindTexture GL_TEXTURE_2D texture)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MIN_FILTER GL_LINEAR)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER GL_LINEAR)
    (glTexImage2D GL_TEXTURE_2D
                  0
                  GL_RGBA
                  width
                  height
                  0
                  GL_BGRA
                  GL_UNSIGNED_BYTE
                  data)
    (glDisable GL_TEXTURE_2D))
  
  
  (method (textured-quad bl tl tr br)
    (glEnable GL_TEXTURE_2D)
    (glColor3f 1.0 1.0 1.0)
    (glBindTexture GL_TEXTURE_2D texture)
    (glBegin GL_QUADS)
    (glTexCoord2f 0.0 1.0)
    (bl)
    (glTexCoord2f 1.0 1.0)
    (tl)
    (glTexCoord2f 1.0 0.0)
    (tr)
    (glTexCoord2f 0.0 0.0)
    (br)
    (glEnd)
    (glDisable GL_TEXTURE_2D)))


;;;
;;;; Creation
;;;


(definition public (make-cairo-texture width height)
  (new Cairo-Texture width height))


(definition public (make-png-texture file)
  (let ((png (cairo_image_surface_create_from_png (parse~ file))))
    (let ((width (cairo_image_surface_get_width png))
          (height (cairo_image_surface_get_height png)))
      (let ((ct (new Cairo-Texture width height surface: (new Surface png))))
        (map-texture~ ct)
        ct)))))
