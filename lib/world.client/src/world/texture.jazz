;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Texture
;;;


(module world.texture jazz


(import (jazz.graphic)
        (jazz.graphic.opengl)
        (jazz.graphic.opengl.platform)
        (jazz.library)
        (jazz.literals)
        (jazz.platform)
        (jazz.platform.cairo))


;;;
;;;; Texture
;;;


(class Cairo-Texture extends Object
  
  
  (slot width   getter generate)
  (slot height  getter generate)
  (slot surface getter generate)
  (slot data    getter generate)
  (slot texture getter generate)
  
  
  (method override (initialize width height (surface: surface #f))
    (set! width~self width)
    (set! height~self height)
    (set! surface~self (or surface (new Surface (cairo_image_surface_create CAIRO_FORMAT_ARGB32 width height))))
    (set! data~self (cairo_image_surface_get_data (get-handle~ surface~self)))
    (set! texture~self (gl-generate-texture)))
  
  
  (method (map-texture)
    (with-gl-enable/disable GL_TEXTURE_2D
      (lambda ()
        (gl-bind-texture GL_TEXTURE_2D texture)
        (gl-texture-parameterize GL_TEXTURE_2D GL_TEXTURE_MIN_FILTER GL_NEAREST @w GL_LINEAR)
        (gl-texture-parameterize GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER GL_NEAREST @w GL_LINEAR)
        (gl-texture-image GL_TEXTURE_2D
                          0
                          GL_RGBA
                          width
                          height
                          0
                          GL_BGRA
                          GL_UNSIGNED_BYTE
                          data))))
  
  
  (method (textured-quad bl tl tr br)
    (with-gl-enable/disable GL_TEXTURE_2D
      (lambda ()
        (gl-color 1.0 1.0 1.0)
        (gl-bind-texture GL_TEXTURE_2D texture)
        (with-gl-begin/end GL_QUADS
          (lambda ()
            (gl-texture-coord 0.0 1.0)
            (bl)
            (gl-texture-coord 1.0 1.0)
            (tl)
            (gl-texture-coord 1.0 0.0)
            (tr)
            (gl-texture-coord 0.0 0.0)
            (br)))))))


;;;
;;;; Creation
;;;


(definition public (make-cairo-texture width height)
  (new Cairo-Texture width height))


(definition public (make-png-texture file)
  (let ((png (cairo_image_surface_create_from_png (parse~ file))))
    (let ((width (cairo_image_surface_get_width png))
          (height (cairo_image_surface_get_height png)))
      (let ((ct (new Cairo-Texture width height surface: (new Surface png))))
        (map-texture~ ct)
        ct)))))
