;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World History
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.history jazz


(import (jazz.event)
        (world)
        (world.autoload)
        (world.change)
        (world.syntax (phase syntax))
        (world.task))


;;;
;;;; Id
;;;


(definition next-unique-id
  0)

(definition protected (next-unique-id!)
  (prog1 next-unique-id
    (increase! next-unique-id)))


;;;
;;;; Logic
;;;


(definition stepping-history?
  (make-parameter #f))


(definition protected previous-logic
  #f)

(definition protected at-logic
  #f)

(definition protected logic-continuation
  #f)


(definition any-logic
  '(any))

(definition skip-logic
  '(skip))

(definition complete-logic
  '(complete))

(definition protected logic-request
  #f)


(definition protected logic-again?
  #f)


(definition protected logic-exit
  #f)

(definition (with-logic-exit thunk)
  (continuation-capture
    (lambda (exit)
      (set! logic-exit exit)
      (thunk))))

(definition (exit-logic)
  (let ((exit logic-exit))
    (if exit
        (begin
          (set! logic-exit #f)
          (continuation-return exit #f))
      (error "No available logic exit"))))

(definition (clear-exit)
  (set! logic-exit #f))


(definition (clear-where)
  (set! previous-logic #f)
  (set! at-logic #f)
  (update-at-logic))


(definition (clear-logic (reset-request? #f))
  (set! previous-logic #f)
  (set! at-logic #f)
  (when reset-request?
    (set! logic-request #f))
  (set! logic-continuation #f)
  (update-at-logic))


(definition protected (break-logic-at name)
  (let ((request logic-request))
    (when request
      (set! previous-logic at-logic)
      (set! at-logic name)
      (when (or (eq? request any-logic)
                (eq? request name))
        (update-at-logic)
        (continuation-capture
          (lambda (cont)
            (set! logic-request #f)
            (set! logic-continuation cont)
            (exit-logic)))))))


(definition protected (break-logic-action action)
  (when logic-request
    (action)))


(definition protected (in-logic?)
  (let ((request logic-request))
    (or (and (stepping-history?)
             (eq? request #f))
        (eq? request any-logic)
        (eq? request complete-logic))))


(definition protected (debug-logic . rest)
  (when (in-logic?)
    (execute-event
      (lambda ()
        (apply debug rest)))))


;;;
;;;; Update
;;;


(definition update-at-logic
  (lambda ()
    ))

(definition protected (set-update-at-logic proc)
  (set! update-at-logic proc))


;;;
;;;; History
;;;


(definition index-size <fx>
  4)

(definition change-size <fx>
  6)

(definition metadata-size <fx>
  1)


(class History extends Object
  
  
  (slot size            <fx>     getter generate)
  (slot indexes-size    <fx>     getter generate)
  (slot indexes         <object> getter generate)
  (slot changes-size    <fx>     getter generate)
  (slot changes-head    <fx>     getter generate)
  (slot changes         <object> getter generate)
  (slot metadatas-size  <fx>     getter generate)
  (slot metadatas-head  <fx>     getter generate)
  (slot metadatas       <object> getter generate)
  (slot now             <object> accessors generate)
  (slot first           <fx>     accessors generate)
  (slot count           <fx>     accessors generate)
  (slot paused?         <bool>   accessors generate)
  
  
  (method override (initialize size)
    (nextmethod)
    (set! size~self size)
    (set! indexes-size (* size index-size))
    (set! indexes (make-u32vector indexes-size))
    (set! changes-size (* size change-size))
    (set! changes-head 0)
    (set! changes (make-f64vector changes-size))
    (set! metadatas-size (* size metadata-size))
    (set! metadatas-head 0)
    (set! metadatas (make-vector metadatas-size))
    (set! now 0)
    (set! first 0)
    (set! count 0)
    (set! paused? #f))
  
  
  (method (pause)
    (set! paused? #t))
  
  
  (method (unpause)
    (clear-logic #t)
    (clear-exit)
    (set! paused? #f))

  
  (method (history-start)
    0)
  
  
  (method (history-end)
    count)
  
  
  (method (history-first)
    0)
  
  
  (method (history-last)
    (- (history-end) 1))
  
  
  (method (historical-index n)
    (modulo (+ first (* n index-size)) indexes-size))
  
  
  (method (historical-change-id n)
    (u32vector-ref indexes (historical-index n)))
  
  (method (historical-forward-index n)
    (u32vector-ref indexes (+ (historical-index n) 1)))
  
  (method (historical-backward-index n)
    (u32vector-ref indexes (+ (historical-index n) 2)))
  
  (method (historical-metadata-index n)
    (u32vector-ref indexes (+ (historical-index n) 3)))
  
  
  (method (historical-metadata n)
    (vector-ref metadatas (historical-metadata-index n)))
  
  
  (method (in-history n)
    (max (history-first) (min n (history-end))))
  
  
  (method (changes-push val <fl>)
    (f64vector-set! changes changes-head val)
    (set! changes-head (modulo (+ changes-head 1) changes-size)))
  
  
  (method (metadatas-push val)
    (vector-set! metadatas metadatas-head val)
    (set! metadatas-head (modulo (+ metadatas-head 1) metadatas-size)))
  
  
  (method (add-change change-id forward backward metadata)
    (let ((index (historical-index count)))
      (u32vector-set! indexes index change-id)
      (u32vector-set! indexes (+ index 1) changes-head)
      (forward changes-push)
      (u32vector-set! indexes (+ index 2) changes-head)
      (backward changes-push)
      (u32vector-set! indexes (+ index 3) metadatas-head)
      (metadatas-push metadata)
      (if (< count size)
          (begin
            (increase! count)
            (increase! now))
        (set! first (modulo (+ first index-size) indexes-size)))))
  
  
  (method (backward-history)
    (goto-history (- now 1)))
  
  
  (method (forward-history)
    (goto-history (+ now 1)))
  
  
  (method (goto-now)
    (goto-history now))

  
  (method (goto-history n)
    (let ((world (current-world)))
      (let ((n (in-history n)))
        (when (/= n now)
          (cond ((> n now)
                 (loop (for s from now below n)
                       (let ((id (historical-change-id s))
                             (forward-index (historical-forward-index s)))
                         (forward-change id (lambda (n)
                                              (f64vector-ref changes (modulo (+ forward-index n) changes-size)))))))
                (else
                 (loop (for s from (- now 1) downto n)
                       (let ((id (historical-change-id s))
                             (backward-index (historical-backward-index s)))
                         (backward-change id (lambda (n)
                                               (f64vector-ref changes (modulo (+ backward-index n) changes-size))))))))
          (set! now n)
          (invalidate-slider)
          (set-?n now)))))
  
  
  (method (stepped-history)
    (when (< now (history-end))
      (increase! now)
      (invalidate-slider)
      (set-?n now)))
  
  
  (method (invalidate-slider)
    (let ((interface (current-interface)))
      (invalidate-view~ (child~ interface 'slider))))
  
  
  (method (slide-history where)
    (goto-history (fxround (* where (cast <fl> (history-end))))))
  
  
  (method (truncate-history)
    (set! count now))
  
  
  ;;;
  ;;;; Step
  ;;;

  
  (method (step-backward)
    (if previous-logic
        (let ((request previous-logic))
          (goto-now)
          (step-to request))
      ;; find last logic
      (backward-history)
      (step-request skip-logic)
      ;; backward again to it
      (let ((request at-logic))
        (backward-history)
        (step-to request))))
  
  
  (method (step-forward)
    (if logic-continuation
        (continue-logic any-logic #t)
      (step-to any-logic)))
  
  
  (method (step-over)
    (if logic-continuation
        (begin
          (clear-where)
          ;; let logic run to completion
          (continue-logic complete-logic #f))
      (step-history)))
  
  
  (method (continue-logic request again?)
    (with-logic-exit
      (lambda ()
        (let ((cont logic-continuation))
          (set! logic-request request)
          (set! logic-again? again?)
          (set! logic-continuation #f)
          (continuation-return cont #f)))))
  
  
  (method (step-to request)
    (with-logic-exit
      (lambda ()
        (step-again request))))
  
  
  (method (step-again request)
    (step-request request)
    ;; exit was not consumed so step again
    (if logic-again?
        (when (< now (history-end))
          (step-again any-logic))
      (clear-logic #t))
    (exit-logic))
  
  
  (method (step-request request)
    (set! logic-request request)
    (step-history))
  
  
  (method (step-history)
    @convert
    (let ((world (current-world))
          (me (current-me)))
      (clear-logic)
      (when (< now (history-end))
        (let ((moment (historical-moment now)))
          (when moment
            (parameterize ((stepping-history? #t))
              (let ((commands (get-commands~ moment))
                    (time (get-time~ moment))
                    (elapse (get-elapse~ moment)))
                (complete~ commands)
                (tick-sliced~ world me commands time elapse)
                (tick-actors~ world commands time elapse)))))
        (stepped-history))))
  
  
  ;;;
  ;;;; Future
  ;;;
  
  
  ;; convert
  (method (recalculate-future)
    )))
