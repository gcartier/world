;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World History
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2016
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.history jazz


(import (jazz.event)
        (world)
        (world.autoload)
        (world.syntax (phase syntax))
        (world.task))


;;;
;;;; Logic
;;;


(definition stepping-history?
  (make-parameter #f))


(definition protected previous-logic
  #f)

(definition protected at-logic
  #f)

(definition protected logic-continuation
  #f)


(definition any-logic
  '(any))

(definition skip-logic
  '(skip))

(definition complete-logic
  '(complete))

(definition protected logic-request
  #f)


(definition protected logic-again?
  #f)


(definition protected logic-exit
  #f)

(definition (with-logic-exit thunk)
  (continuation-capture
    (lambda (exit)
      (set! logic-exit exit)
      (thunk))))

(definition (exit-logic)
  (let ((exit logic-exit))
    (if exit
        (begin
          (set! logic-exit #f)
          (continuation-return exit #f))
      (error "No available logic exit"))))

(definition (clear-exit)
  (set! logic-exit #f))


(definition (clear-where)
  (set! previous-logic #f)
  (set! at-logic #f)
  (update-at-logic))


(definition (clear-logic (reset-request? #f))
  (set! previous-logic #f)
  (set! at-logic #f)
  (when reset-request?
    (set! logic-request #f))
  (set! logic-continuation #f)
  (update-at-logic))


(definition protected (break-logic-at name)
  (let ((request logic-request))
    (when request
      (set! previous-logic at-logic)
      (set! at-logic name)
      (when (or (eq? request any-logic)
                (eq? request name))
        (update-at-logic)
        (continuation-capture
          (lambda (cont)
            (set! logic-request #f)
            (set! logic-continuation cont)
            (exit-logic)))))))


(definition protected (break-logic-action action)
  (when logic-request
    (action)))


(definition protected (in-logic?)
  (let ((request logic-request))
    (or (and (stepping-history?)
             (eq? request #f))
        (eq? request any-logic)
        (eq? request complete-logic))))


(definition protected (debug-logic . rest)
  (when (in-logic?)
    (execute-event
      (lambda ()
        (apply debug rest)))))


;;;
;;;; Update
;;;


(definition update-at-logic
  (lambda ()
    ))

(definition protected (set-update-at-logic proc)
  (set! update-at-logic proc))


;;;
;;;; Change
;;;


(class Change extends Object
  
  
  (slot metadata getter generate)
  (slot forward  getter generate)
  (slot backward getter generate)
  
  
  (method override (initialize metadata forward backward)
    (set! metadata~self metadata)
    (set! forward~self forward)
    (set! backward~self backward)))


;;;
;;;; History
;;;


(class History extends Object
  
  
  (slot changes  <object> initialize #f accessors generate)
  (slot now      <object> initialize #f accessors generate)
  (slot maximum  <fx+>    initialize #f accessors generate)
  (slot first    <fx+>    initialize #f accessors generate)
  (slot count    <fx+>    initialize #f accessors generate)
  (slot paused?  <bool>   initialize #f accessors generate)
  
  
  (method override (initialize maximum)
    (nextmethod)
    (set! maximum~self maximum)
    (setup))
  
  
  (method (setup)
    (set! changes (make-vector maximum))
    (set! now 0)
    (set! first 0)
    (set! count 0))
  
  
  (method (pause)
    (set! paused? #t))
  
  
  (method (unpause)
    (clear-logic #t)
    (clear-exit)
    (set! paused? #f))

  
  (method (history-start)
    0)
  
  
  (method (history-end)
    count)
  
  
  (method (history-first)
    0)
  
  
  (method (history-last)
    (- (history-end) 1))
  
  
  (method (historical-index n)
    (modulo (+ first n) maximum))
  
  
  (method (historical-change n)
    (element changes (historical-index n)))
  
  
  (method (in-history n)
    (max (history-first) (min n (history-end))))
  
  
  (method (add-history forward backward (metadata: metadata #f))
    (let ((change (new Change metadata forward backward))
          (index (historical-index count)))
      (set-element! changes index change)
      (if (< count maximum)
          (begin
            (increase! count)
            (increase! now))
        (set! first (modulo (+ first 1) maximum)))))
  
  
  (method (backward-history)
    (goto-history (- now 1)))
  
  
  (method (forward-history)
    (goto-history (+ now 1)))
  
  
  (method (goto-now)
    (goto-history now))
  
  
  (method (goto-history n)
    (let ((world (current-world)))
      (let ((n (in-history n)))
        (when (/= n now)
          (cond ((> n now)
                 (loop (for s from now below n)
                       (let ((forward (get-forward~ (historical-change s))))
                         (forward))))
                (else
                 (loop (for s from (- now 1) downto n)
                       (let ((backward (get-backward~ (historical-change s))))
                         (backward)))))
          (set! now n)
          (invalidate-slider)
          (set-?n now)))))
  
  
  (method (stepped-history)
    (when (< now (history-end))
      (increase! now)
      (invalidate-slider)
      (set-?n now)))
  
  
  (method (invalidate-slider)
    (let ((interface (current-interface)))
      (invalidate-view~ (child~ interface 'slider))))
  
  
  (method (slide-history where)
    (goto-history (fxround (* where (cast <fl> (history-end))))))
  
  
  (method (truncate-history)
    (set! count now))
  
  
  ;;;
  ;;;; Step
  ;;;

  
  (method (step-backward)
    (if previous-logic
        (let ((request previous-logic))
          (goto-now)
          (step-to request))
      ;; find last logic
      (backward-history)
      (step-request skip-logic)
      ;; backward again to it
      (let ((request at-logic))
        (backward-history)
        (step-to request))))
  
  
  (method (step-forward)
    (if logic-continuation
        (continue-logic any-logic #t)
      (step-to any-logic)))
  
  
  (method (step-over)
    (if logic-continuation
        (begin
          (clear-where)
          ;; let logic run to completion
          (continue-logic complete-logic #f))
      (step-history)))
  
  
  (method (continue-logic request again?)
    (with-logic-exit
      (lambda ()
        (let ((cont logic-continuation))
          (set! logic-request request)
          (set! logic-again? again?)
          (set! logic-continuation #f)
          (continuation-return cont #f)))))
  
  
  (method (step-to request)
    (with-logic-exit
      (lambda ()
        (step-again request))))
  
  
  (method (step-again request)
    (step-request request)
    ;; exit was not consumed so step again
    (if logic-again?
        (when (< now (history-end))
          (step-again any-logic))
      (clear-logic #t))
    (exit-logic))
  
  
  (method (step-request request)
    (set! logic-request request)
    (step-history))
  
  
  (method (step-history)
    @convert
    (let ((world (current-world))
          (me (current-me)))
      (clear-logic)
      (when (< now (history-end))
        (let ((moment (historical-moment now)))
          (when moment
            (parameterize ((stepping-history? #t))
              (let ((commands (get-commands~ moment))
                    (time (get-time~ moment))
                    (elapse (get-elapse~ moment)))
                (complete~ commands)
                (tick-sliced~ world me commands time elapse)
                (tick-actors~ world commands time elapse)))))
        (stepped-history))))
  
  
  ;;;
  ;;;; Future
  ;;;
  
  
  ;; convert
  (method (recalculate-future)
    )))
