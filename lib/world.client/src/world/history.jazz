;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World History
;;;


(module world.history jazz


(import (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (world.draw)
        (world.shaders)
        (world.foreign))


;;;
;;;; Parameters
;;;


(definition public in-history?
  (make-parameter #f))

(definition history-fs
  #<<SHADER
#version 120
 
void main()
{
    gl_FragColor = vec4(0.0,1.0,0.0,1.0);
}
SHADER
)


;;;
;;;; Moment
;;;


(class Moment extends Object
  
  
  (slot snapshot <object> initialize #f accessors generate)
  (slot commands <object> initialize #f accessors generate)
  (slot elapse   <object> initialize #f accessors generate)
  
  
  (method override (initialize snapshot commands elapse)
    (nextmethod)
    (set! snapshot~self snapshot)
    (set! commands~self commands)
    (set! elapse~self elapse)))


;;;
;;;; History
;;;


(class History extends Object
  
  
  (slot world   <object> initialize #f getter generate)
  (slot shader  <object> initialize #f getter generate)
  (slot moments <object> initialize #f getter generate)
  (slot now     <object> initialize #f getter generate)
  (slot current <object> initialize #f accessors generate)
  (slot maximum <fx+>    initialize #f getter generate)
  (slot first   <fx+>    initialize #f getter generate)
  (slot count   <fx+>    initialize #f getter generate)
  (slot hist-fbo                         initialize #f            accessors generate)
  (slot hist-texture                 initialize #f            accessors generate)
  (slot width initialize #f accessors generate)
  (slot height initialize #f accessors generate)
  
  (method override (initialize world maximum)
    (nextmethod)
    (set! world~self world)
    (set! maximum~self maximum)
    (set! shader~self (new Shader-Program))
    (set! width (get-width~ world))
    (set! height (get-height~ world))
    (initialize-fbo)
    (setup))
  
  
  (method (setup)
    (set-fs~ shader history-fs)
    (set-vs~ shader default-vs)
    (create-attach~ shader)
    (cond ((not maximum)
           (set! moments (new Axis 0))
           (set! now 0))
          (else
           (set! moments (make-vector maximum))
           (set! now 0)
           (set! first 0)
           (set! count 0))))
  
  (method (initialize-fbo)
    (glActiveTexture GL_TEXTURE1)
    (set! hist-texture (gl-generate-texture))
    (glBindTexture GL_TEXTURE_2D hist-texture)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MAG_FILTER GL_NEAREST)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_MIN_FILTER GL_NEAREST)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_S GL_CLAMP_TO_EDGE)
    (glTexParameteri GL_TEXTURE_2D GL_TEXTURE_WRAP_T GL_CLAMP_TO_EDGE)
    (glTexImage2D GL_TEXTURE_2D 0 GL_RGBA (fxround width) (fxround height) 0 GL_RGBA GL_UNSIGNED_BYTE #f)
    (glBindTexture GL_TEXTURE_2D 0)    
    
    (set! hist-fbo (glGenFramebuffersEXT*))
    (glBindFramebufferEXT GL_FRAMEBUFFER_EXT hist-fbo)
    (glFramebufferTexture2DEXT GL_FRAMEBUFFER_EXT GL_COLOR_ATTACHMENT0_EXT GL_TEXTURE_2D hist-texture 0)
    (glFramebufferRenderbufferEXT GL_FRAMEBUFFER_EXT GL_DEPTH_ATTACHMENT_EXT GL_RENDERBUFFER_EXT (get-rbo_depth~ world))
    (glBindFramebufferEXT GL_FRAMEBUFFER_EXT 0))

  
  (method (history-start)
    0)
  
  
  (method (history-end)
    (if (not maximum)
        (cardinality moments)
      count))
  
  
  (method (history-first)
    0)
  
  
  (method (history-last)
    (- (history-end) 1))
  
  
  (method (historical-index n)
    (modulo (+ first n) maximum))
  
  
  (method (historical-moment n)
    (if (not maximum)
        (element moments n)
      (element moments (historical-index n))))
  
  
  (method (in-history n)
    (max (history-first) (min n (history-end))))
  
  
  (method (add-history moment)
    (cond ((not maximum)
           (axis-add moments moment)
           (increase! now))
          (else
           (let ((index (historical-index count)))
             (set-element! moments index moment)
             (if (< count maximum)
                 (begin
                   (increase! count)
                   (increase! now))
               (set! first (historical-index (+ first 1))))))))
  
  
  (method (backward-history)
    (goto-history (- now 1)))
  
  
  (method (forward-history)
    (goto-history (+ now 1)))
  
  
  (method (goto-history n)
    (let ((n (in-history n)))
      (unless (= n now)
        (set! now n)
        (if (= n (history-end))
            (load-world~ world current)
          (load-world~ world (get-snapshot~ (historical-moment now)))))))
  
  
  (method (slide-history where)
    (goto-history (fxround (* where (history-end)))))
  
  
  (method (truncate-future)
    (if (not maximum)
        (axis-adjust moments now)
      (set! count now)))
  
  
  (method (step-world)
    (when (< now (history-end))
      (let ((moment (historical-moment now)))
        (increase! now)
        (when moment
          (let ((commands (get-commands~ moment))
                (elapse (get-elapse~ moment)))
            (tick-actors~ world commands elapse))))))
  
  
  (method (recalculate-future)
    (let ((end (history-end)))
      (loop (for n from now below end)
            (let ((moment (historical-moment n)))
              (let ((commands (get-commands~ moment))
                    (elapse (get-elapse~ moment)))
                (tick-actors~ world commands elapse)
                (let ((next (+ n 1)))
                  (let ((moment (historical-moment next))
                        (snapshot (snapshot-world~ world)))
                    (if (= next end)
                        (set! current snapshot)
                      (set-snapshot~ moment snapshot)))))))
      ;; for now
      (if (= now end)
          (load-world~ world current)
        (load-world~ world (get-snapshot~ (historical-moment now))))))
  
  
  (method (draw-history-slider)
    (let ((width (fxround (get-width~ world)))
          (height (fxround (get-height~ world))))
      (let ((cx (fxround/ width 2)))
        (gl-frame-rect (new Rect (- cx 200) (- height 30) (+ cx 200) (- height 31)) {Color Gray})
        (gl-frame-rect (new Rect (- cx 200) (- height 32) (+ cx 200) (- height 33)) {Color Light-Gray})
        (let ((end (history-end)))
          (when (> end 0)
            (let ((h (+ (fxround (- (* (/ (cast <fl> now) end) 400.) 200.)) cx)))
              (gl-segment-circle (new Point h (- height 32)) 7 {Color Light-Blue} 100)))))))
  
  
  (method (draw-history draw)
    (glBindFramebufferEXT GL_FRAMEBUFFER_EXT hist-fbo)
    (glClear GL_COLOR_BUFFER_BIT)
    (activate~ shader)
    (let ((step 1)
          (max 1000))
      ;; past
      (loop (for n from (- now 1) downto (history-first) by step)
            (repeat max)
            (draw n))
      ;; future
      (loop (for n from now to (history-last) by step)
            (repeat max)
            (draw n)))
    (deactivate~ shader)
    (glBindFramebufferEXT GL_FRAMEBUFFER_EXT (get-fbo~ world)))
  
  
  (method (present-history)
    (define (present-coordinates obj)
      (format "{r precision: 1} {r precision: 1}" (get-x~ obj) (get-y~ obj)))
    
    (if (< (history-end) 5)
        `(,(format "c -> {a}" (present-coordinates (get-player~ world)))
          ,@(loop (for n from 0 to (history-last))
                  (collect
                    (let ((moment (historical-moment n)))
                      (let ((snapshot (get-snapshot~ moment))
                            (commands (get-commands~ moment)))
                        (let ((player (find-player~ world (get-actors~ snapshot))))
                          (format "{a} -> {a}" n (present-coordinates player))))))))
      '())))


;;;
;;;; Snapshot
;;;


(class Snapshot extends Object
  
  
  (slot actors getter generate)
  (slot marks  getter generate)
  
  
  (method override (initialize actors marks)
    (nextmethod)
    (set! actors~self actors)
    (set! marks~self marks))))
