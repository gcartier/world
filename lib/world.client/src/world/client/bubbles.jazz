;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Bubbles
;;;


(module world.client.bubbles jazz


(import (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.window)
        (time)
        (world)
        (world.autoload)
        (world.element)
        (world.geometry)
        (world.object)
        (world.texture)
        (world.client.window))


(class Bubbles-World extends World
  
  
  (slot me? <bool> initialize #f)
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (method override (render-world)
    (nextmethod)
    (draw-me))
  
  
  (method (draw-me)
    (when me?
      (let ((me (get-camera-me)))
        (draw-sphere (vertex-x me) (vertex-y me) (vertex-z me) 0.5 26 52 {Color Green alpha: 0.5}))))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (process-keys)
    (let ((evt (make-event Keyboard-Event :char self)))
      (process-movement evt)))
  
  
  (method override (key-press c)
    (case c
      ((#\1) (add-bubble))
      ((#\2) (add-pyramid))
      ((#\3) (add-cube))
      ((#\4) (add-cube-center))
      ((#\7) (loop (repeat 100) (add-bubble)))
      ((#\8) (loop (repeat 100) (add-bubble2 8 16)))
      ((#\m) (change-music))
      ((#\c) (mode-insert 'cube))
      ((#\p) (toggle-me))
      ((#\z) (toggle-interface))
      ((#\y) (toggle-polygon-mode))
      ((#\l) (toggle-lighting))
      ((#\f) (toggle-fullscreen))))
  
  
  (method (add-bubble)
    (let ((color (new Color red: (random 256) green: (random 256) blue: (random 256))))
      (add-object (new Sphere self (random-in 10.0) (random-in 10.0) (- (* (random-real) 25.0)) color: color))))
  
  
  (method (add-bubble2 slices stacks)
    (let ((color (new Color red: (random 256) green: (random 256) blue: (random 256))))
      (add-object (new Sphere self (random-in 10.0) (random-in 10.0) (- (* (random-real) 25.0)) slices: slices stacks: stacks color: color))))
  
  
  (method (add-pyramid)
    (add-object (new Pyramid self (random-in 10.0) (random-in 10.0) (- (* (random-real) 25.0)))))
  
  
  (method (add-cube)
    (add-object (new Cube self (random-in 10.0) (random-in 10.0) (- (* (random-real) 25.0)))))
  
  
  (method (add-cube-center)
    (add-object (new Cube self 0.0 0.0 0.0)))
  
  
  (method (explode)
    (let ((me (get-camera-me)))
      (let ((inside
              (collect-if (lambda (object)
                            (inside?~ object 0.5 me))
                          objects)))
        (when (not (null? inside))
          (play-sound {File Settings "resources" "sounds" "explosion.wav"}))
        (set! objects (difference objects inside)))))
  
  
  ;;;
  ;;;; Movement
  ;;;
  
  
  (method (toggle-me)
    (set! me? (not me?)))
  
  
  (method (mode-explore)
    (set! mode 'explore))
  
  
  (method (mode-insert w)
    (set! mode 'insert)
    (set! what w))))
