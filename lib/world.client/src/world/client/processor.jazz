;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; UDP Processor Client
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.client.processor jazz


(import (jazz.syntax (phase syntax))
        (jazz.zlib)
        (world)
        (world.change)
        (world.configure)
        (world.context)
        (world.event)
        (world.format)
        (world.record (phase syntax))
        (world.settings)
        (world.task)
        (world.udp))


(class UDP-Processor-Client extends Object
  
  
  (slot client-no       initialize #f  getter generate)
  (slot udp-port        initialize #f  getter generate)
  (slot udp-stopping?   initialize #f  getter generate)
  (slot processor-host  initialize #f  getter generate)
  (slot processor-port  initialize #f  getter generate)
  (slot write-mutex     initialize #f  getter generate)
  (slot live-task       initialize #f  getter generate)
  (slot droppable-task  initialize #f  getter generate)
  (slot started?        initialize #f  getter generate)
  (slot monitor         initialize #f  getter generate)
  (slot invalid-packets initialize 0   getter generate)
  
  
  (method override (initialize self client-no)
    (nextmethod self)
    (set! self.client-no client-no)
    (set! write-mutex (make-mutex 'write)))
  
  
  (method package (connect self local-address host service)
    (set! udp-port (open-udp (list
                               local-address: local-address
                               address: host
                               port-number: service)))
    (setup-udp-port-buffers udp-port)
    (set! udp-stopping? #f)
    (let ()
      (define (send-connect)
        (with-write-mutex self
          (lambda ()
            (with-record processor-connect
              (let ((data (make-u8vector (+ (calculate-connect-size) udp-code-size))))
                (write-header-kind data udp-connect)
                (write-header-sender data client-no)
                (write-processor-connect-sent data (current-seconds))
                (write-udp-code data)
                (write-data data udp-port))))))
      
      (set! processor-host #f)
      (set! processor-port #f)
      (set! monitor (new UDP-Monitor))
      (let ()
        (declare (proper-tail-calls))
        (let (loop (n 0))
          (define (read-udp)
            (input-port-timeout-set! udp-port .01)
            (prog1 (read udp-port)
              (input-port-timeout-set! udp-port +inf.0)))
          
          (send-connect)
          (let ((reply (read-udp)))
            (with-record processor-connected
              (if (or (eof-object? reply) (/= (read-header-kind reply) udp-connected))
                  (if (>= n 500)
                      (begin
                        (terminal 'BAILOUT 'did 'not 'receive 'udp-connected 'from 'processor)
                        (exit 1))
                    (loop (+ n 1)))
                (let ((source-info (udp-source-socket-info udp-port)))
                  (set! processor-host (socket-info-address source-info))
                  (set! processor-port (socket-info-port-number source-info)))
                (set! live-task (new Task '(live processor) (~ run-live self) priority: live-priority))
                (set! droppable-task (new Task 'droppable (~ run-droppable self) exit: (~ exit-droppable self) priority: droppable-priority))
                (start-task live-task)
                (start-task droppable-task))))))))
  
  
  (method (exit-droppable self task)
    (set! udp-stopping? #t)
    (close-port udp-port))

  
  (method (deconnect self)
    (stop-task live-task)
    (set! live-task #f)
    (stop-task droppable-task)
    (set! droppable-task #f)
    (set! udp-port #f))
  
  
  (method (start self)
    (unless started?
      (set! started? #t)))
  
  
  (method (stop self)
    (when started?
      (set! started? #f)))
  
  
  (method (with-write-mutex self thunk)
    (unwind-protect
        (begin
          (mutex-lock! write-mutex)
          (thunk))
      (mutex-unlock! write-mutex)))
  
  
  (method protected (increase-sent self data)
    (increase-sent monitor data))
  
  
  (method protected (increase-received self data)
    (increase-received monitor data))
  
  
  ;;;
  ;;;; Send
  ;;;
  
  
  (method package (send-droppable self changes)
    (let ((content (object->u8vector changes)))
      (let ((payload (zlib-deflate content)))
        (with-write-mutex self
          (lambda ()
            (with-record client-droppable
              (let ((size (calculate-client-droppable-size payload)))
                (let ((data (make-u8vector (+ size udp-code-size))))
                  (write-header-kind data udp-client-droppable)
                  (write-header-sender data client-no)
                  (write-client-droppable-sent data (current-seconds))
                  (write-client-droppable-payload data payload)
                  (write-udp-code data)
                  (send-data self data)
                  data))))))))
  
  
  (method (send-data self data)
    (send-processor self data)
    (increase-sent self data))
  
  
  (method (send-processor self data)
    ;; quick temporary hack to get back the safe behavior
    ;; of retrying-write until port being false is fixed
    (when udp-port
      (udp-destination-set! processor-host processor-port udp-port)
      (send-udp data udp-port)))
  
  
  ;;;
  ;;;; Live
  ;;;
  
  
  (method (run-live self task <Task>)
    (declare (proper-tail-calls))
    (with-task task
      (lambda (exit)
        (let (iter (previous (current-monotonic)))
          (let ((time (current-monotonic)))
            (let ((elapse (- time previous)))
              (unless (stopping? task)
                (site live
                  (send-live self)))
              (let ((duration (- (current-monotonic) time)))
                (task-wait task live-period duration exit))
              (iter time)))))))
  
  
  ;;;
  ;;;; Receive
  ;;;
  
  
  (method (run-droppable self task)
    (declare (proper-tail-calls))
    (with-task task
      (lambda (exit)
        (let (loop)
          (let ((data (catch-exception-filter
                        (lambda (exc)
                          udp-stopping?)
                        (lambda (exc)
                          (continuation-return exit #f))
                        (lambda ()
                          (read-data udp-port)))))
            (when data
              (site udp-droppable
                (receive-data self data))
              (loop)))))))
  
  
  (method protected (receive-data self data)
    (if (or (< (u8vector-length data) 4)
            (/= (read-udp-code data) udp-code))
        (increase! invalid-packets)
      (increase-received self data)
      (let ((kind (with-record header
                    (read-header-kind data))))
        (if (not started?)
            (ecase kind
              ((udp-connected))
              ((udp-live-ack)
               (receive-live-ack self data))
              ((udp-processor-droppable))
              (else
               (error "Unknown UDP kind: {s}" kind)))
          (ecase kind
            ((udp-connected))
            ((udp-live-ack)
             (receive-live-ack self data))
            ((udp-processor-droppable)
             (receive-droppable self data))
            (else
             (error "Unknown UDP kind: {s}" kind)))))))
  
  
  (method (send-live self)
    (with-write-mutex self
      (lambda ()
        (with-record header
          (let ((size (calculate-live-size)))
            (let ((data (make-u8vector (+ size udp-code-size))))
              (write-header-kind data udp-live)
              (write-header-sender data client-no)
              (write-udp-code data)
              (send-data self data)))))))
  
  
  (method (receive-live-ack self data)
    )
  
  
  (method (receive-droppable self data)
    (with-record processor-droppable
      (let ((sent (read-processor-droppable-sent data))
            (payload (read-processor-droppable-payload data)))
        (let ((changes (u8vector->object (zlib-inflate payload))))
          (with-task-mutex
            (lambda ()
              (unless (stopping? droppable-task)
                (forward-changes sent (current-seconds) changes))))))))))
