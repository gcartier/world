;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Lights
;;;


(module world.light jazz


(import (jazz.jml)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.draw)
        (world.dye)
        (world.dyes)
        (world.element)
        (world.foreign)
        (world.gadget)
        (world.generate)
        (world.geometry)
        (world.material)
        (world.model)
        (world.object)
        (world.shader)
        (world.syntax (phase syntax)))


;;;
;;;; Light
;;;


(class Light extends Gadget
  
  
  (property w                     <fl>     initialize 1.     accessors generate)
  (property constant-attenuation  <fl>     initialize 1.     accessors generate)
  (property linear-attenuation    <fl>     initialize 0.     accessors generate)
  (property quadratic-attenuation <fl>     initialize 0.0001 accessors generate)
  (property ambient-color         <object> initialize #f     accessors generate)
  (property dynamic?              <bool>   initialize #f     accessors generate)
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-size (vertex 5. 5. 5.))
    (set-radius 2.5)
    (set-color white-dye)
    (set! ambient-color black-dye)
    (set-visible? (get-draw-gadgets?~ (current-world))))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (let ((world (current-world)))
      (let ((material (new Material texture: (find-texture~ world "dirty.png"))))
        (set! model (new Model mesh: (generate-sphere 48 24 material: material)))
        (update-matrix))))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" (present-vertex position)))))
  
  
  (method override (update-radius)
    (set-radiuses radius radius radius))
  
  
  (method public (get-diffuse-color)
    color)
  
  
  (method override (entity-program)
    'color)
  
  
  (method override (entity-bind-uniforms program mesh matrix)
    (bind-uniforms~ program mesh matrix color)))


;;;
;;;; Light-Point
;;;


(class Light-Point extends Light)


;;;
;;;; Light-Ambient
;;;


(class Light-Ambient extends Light
  
  
  (form
    (<install> quadratic-attenuation: 0. ambient-color: #f32(1. 1. 1. 1.) dynamic?: #t)))


;;;
;;;; Light-Directional
;;;


(class Light-Directional extends Light
  
  
  (form
    (<install> quadratic-attenuation: 0. color: #f32(1. 1. 1. 1.) dynamic?: #t))))
