;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Lights
;;;


(module world.light jazz


(import (jazz.jml)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (world)
        (world.autoload)
        (world.draw)
        (world.dye)
        (world.dyes)
        (world.element)
        (world.foreign)
        (world.gadget)
        (world.generate)
        (world.geometry)
        (world.material)
        (world.model)
        (world.object)
        (world.serialization)
        (world.shader)
        (world.syntax (phase syntax)))


;;;
;;;; Light
;;;


(class Light extends Gadget
  
  
  (property w                     <fl>     initialize 1.     accessors generate)
  (property constant-attenuation  <fl>     initialize 1.     accessors generate)
  (property linear-attenuation    <fl>     initialize 0.     accessors generate)
  (property quadratic-attenuation <fl>     initialize 0.0001 accessors generate)
  (property ambient-color         <object> initialize #f     accessors generate)
  (property dynamic?              <bool>   initialize #f     accessors generate)
  
  
  (method override (prepare rest)
    (nextmethod rest)
    (set-radiuses (vertex .5 .5 .5))
    (set-color white-dye)
    (set! ambient-color black-dye)
    (set-visible? (get-draw-gadgets?~ (current-world))))
  
  
  (method override (finish rest)
    (nextmethod rest)
    (set! model (cache-sphere-model))
    (update-matrix))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{a}" (present-vertex position)))))
  
  
  (method meta override (marshall-object obj)
    (serialize-object (class-of obj)
                      (vector
                        ;; component
                        (serialize (get-name~ obj))
                        (serialize (get-visible?~ obj))
                        ;; element
                        (serialize (get-position~ obj))
                        (serialize (get-radiuses~ obj))
                        (serialize (get-radius~ obj))
                        (serialize (get-scale~ obj))
                        (serialize (get-color~ obj))
                        (serialize (get-texture~ obj))
                        (serialize (get-hit-behavior~ obj))
                        (serialize (get-user-data~ obj))
                        ;; entity
                        ;; gadget
                        ;; light
                        (serialize (get-w~ obj))
                        (serialize (get-constant-attenuation~ obj))
                        (serialize (get-linear-attenuation~ obj))
                        (serialize (get-quadratic-attenuation~ obj))
                        (serialize (get-ambient-color~ obj))
                        (serialize (get-dynamic?~ obj)))))
  
  
  (method meta override (unmarshall-object content)
    (bind-vector (;; component
                  name
                  visible?
                  ;; element
                  position
                  radiuses
                  radius
                  scale
                  color
                  texture
                  hit-behavior
                  user-data
                  ;; entity
                  ;; gadget
                  ;; light
                  w
                  constant-attenuation
                  linear-attenuation
                  quadratic-attenuation
                  ambient-color
                  dynamic?)
        content
      (let ((obj (new self name: name visible?: visible?)))
        ;; element
        (set-position~ obj (deserialize position))
        (set-radiuses~ obj (deserialize radiuses))
        (set-radius~ obj (deserialize radius))
        (set-scale~ obj (deserialize scale))
        (set-color~ obj (deserialize color))
        (set-texture~ obj (deserialize texture))
        (set-hit-behavior~ obj (deserialize hit-behavior))
        (set-user-data~ obj (deserialize user-data))
        ;; entity
        ;; gadget
        ;; light
        (set-w~ obj (deserialize w))
        (set-constant-attenuation~ obj (deserialize constant-attenuation))
        (set-linear-attenuation~ obj (deserialize linear-attenuation))
        (set-quadratic-attenuation~ obj (deserialize quadratic-attenuation))
        (set-ambient-color~ obj (deserialize ambient-color))
        (set-dynamic?~ obj (deserialize dynamic?)))))
  
  
  (method override (element-radius)
    .5)
  
  
  (method public (get-diffuse-color)
    color)
  
  
  (method override (entity-program)
    'color)
  
  
  (method override (entity-bind-uniforms program mesh matrix)
    (bind-uniforms~ program mesh matrix color)))


;;;
;;;; Light-Point
;;;


(class Light-Point extends Light)


;;;
;;;; Light-Ambient
;;;


(class Light-Ambient extends Light
  
  
  (form
    (<install> quadratic-attenuation: 0. ambient-color: #f32(1. 1. 1. 1.) dynamic?: #t)))


;;;
;;;; Light-Directional
;;;


(class Light-Directional extends Light
  
  
  (form
    (<install> quadratic-attenuation: 0. color: #f32(1. 1. 1. 1.) dynamic?: #t))))
