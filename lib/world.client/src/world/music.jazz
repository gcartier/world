;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Music
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.music jazz


(import (jazz.audio)
        (jazz.event)
        (jazz.io)
        (jazz.media)
        (world)
        (world.audio)
        (world.autoload)
        (world.geometry)
        (world.settings)
        (world.syntax (phase syntax)))


(class World-Music extends Object
  
  
  (slot music?          <bool>   initialize #f   accessors generate)
  (slot music-style     <object> initialize #f   accessors generate)
  (slot music-volume    <fl>     initialize #f   accessors generate)
  (slot music-playing   <object> initialize #f   getter generate)
  (slot music-path      <object> initialize #f   getter generate)
  (slot silence-thread  <object> initialize #f   getter generate)
  (slot silence-mutex   <object> initialize #f   getter generate)
  (slot silence-minimum <fl>     initialize 60.  accessors generate)
  (slot silence-maximum <fl>     initialize 180. accessors generate)
  (slot played-actions  <object> initialize '()  getter generate)
  
  
  (method override (initialize)
    (set! music? (world-setting 'world.music? #t))
    (set! music-style (world-setting 'world.music-style #f))
    (set! music-volume (world-setting 'world.music-volume default-music-volume))
    (set! silence-mutex (make-mutex 'silence)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method (on-toggle-music evt)
    (toggle-music))
  
  
  (method (on-change-music evt)
    (change-music)
    (display-music))
  
  
  (method (on-display-music evt)
    (display-music))
  
  
  ;;;
  ;;;; Music
  ;;;
  
  
  (method (initial-music)
    (when music?
      (enter-silence minimum: 0. maximum: 15.)))
  
  
  (method (toggle-music)
    (let ((world (current-world)))
      (if music-playing
          (close-music)
        (change-music))
      (display-on/off~ world "Music" (music-playing?))))
  
  
  (method (change-music)
    (let ((world (current-world)))
      (let ((queue (new-queue)))
        (iterate-assets~ world
          (lambda (assets)
            (let ((music-dir (new-directory~ (get-directory~ assets) "music")))
              (when (exists?~ music-dir)
                (let ((music-context (make-search-context music-style)))
                  (iterate-directory~ music-dir
                    (lambda (path kind)
                      (let ((path (join path "/")))
                        (when (or (extension=? (extract-extension path) "mp3")
                                  (extension=? (extract-extension path) "ogg"))
                          (when (or (not music-style)
                                    (search (join (subpath~ (get-parent~ (get-directory~ assets)) (new-file~ music-dir (tokenise-filename path))) "/")
                                            music-context))
                            (enqueue queue (extract-spine path))))))
                    full?: #f
                    files?: #t
                    directories?: #f))))))
        (let ((music-list (queue-list queue)))
          (if (null? music-list)
              (display-error~ world "No music found")
            (let ((music (random-element music-list)))
              (play-music (concatenate "music/" music))))))))
  
  
  (method (enter-silence (minimum: minimum #f) (maximum: maximum #f))
    (let ((minimum (or minimum silence-minimum))
          (maximum (or maximum silence-maximum)))
      (mutex-lock! silence-mutex)
      (set! silence-thread
            (thread-start!
              (new-thread
                (lambda ()
                  (sleep (random-between minimum maximum))
                  (mutex-lock! silence-mutex)
                  (set! silence-thread #f)
                  (mutex-unlock! silence-mutex)
                  (post-event
                    (lambda ()
                      (change-music))))
                'silence)))
      (mutex-unlock! silence-mutex)))
  
  
  (method (exit-silence)
    (mutex-lock! silence-mutex)
    (if (not silence-thread)
        (mutex-unlock! silence-mutex)
      (let ((thread silence-thread))
        (exit-thread silence-thread)
        (set! silence-thread #f)
        (mutex-unlock! silence-mutex)
        (thread-join! thread))))
  
  
  (method (play-intro-music)
    (maybe-play-music "music/Intro"))
  
  
  (method (music-playing?)
    (boolean music-playing))
  
  
  (method (maybe-play-music path)
    (when music?
      (play-music path)))
  
  
  (method (play-music path)
    (let ((world (current-world)))
      (let ((file (effective-music path)))
        (when file
          (let ((sound (new-stream-sound file)))
            (close-music)
            (exit-silence)
            (set! music-playing sound)
            (set! music-path path)
            (set! played-actions '())
            (set-volume~ sound music-volume)
            (play~ sound)
            (update-parameter~ world 'music)
            sound)))))
  
  
  (method (effective-music path)
    (let ((world (current-world)))
      (when path
        (continuation-capture
          (lambda (return)
            (iterate-assets~ world
              (lambda (assets)
                (let ((mp3 (new-file~ (get-directory~ assets) (make-filename path "mp3"))))
                  (if (exists?~ mp3)
                      (continuation-return return mp3)
                    (let ((ogg (new-file~ (get-directory~ assets) (make-filename path "ogg"))))
                      (when (exists?~ ogg)
                        (continuation-return return ogg)))))))
            #f)))))
  
  
  (method (close-music)
    (let ((world (current-world)))
      (when music-playing
        (close~ music-playing)
        (set! music-playing #f)
        (set! music-path #f)
        (exit-silence)
        (update-parameter~ world 'music))))
  
  
  (method (register-played-action action)
    (set! played-actions (cons action played-actions)))
  
  
  (method (display-music)
    (let ((world (current-world)))
      (if music-playing
          (display-status~ world (format "Playing {a}" music-path))
        (display-status~ world "No music playing"))))))
