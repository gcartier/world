;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Quads
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.quad jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.foreign)
        (world.geometry)
        (world.plane)
        (world.polygon)
        (world.texture)
        (time))


(definition public (make-quad element v1 v2 v3 v4)
  (new Quad element (vector v1 v2 v3 v4)))


(definition public (make-smooth-quad element v1 v2 v3 v4 n1 n2 n3 n4)
  (new Smooth-Quad element (vector v1 v2 v3 v4) n1 n2 n3 n4))


;;;
;;;; Quad
;;;


(class Quad extends Polygon
  
  
  (method override (triangle-count) <fx>
    2)
  
  
  (method override (add-triangles uv depth add)
    (let ((v1 (get-v1))
          (v2 (get-v2))
          (v3 (get-v3))
          (v4 (get-v4))
          (n1 (get-normal1))
          (n2 (get-normal2))
          (n3 (get-normal3))
          (n4 (get-normal4)))
      (add v1 v2 v3 n1 n2 n3 (f32vector-ref uv 0) (f32vector-ref uv 1) (f32vector-ref uv 2) (f32vector-ref uv 3) (f32vector-ref uv 4) (f32vector-ref uv 5) depth)
      (add v3 v4 v1 n3 n4 n1 (f32vector-ref uv 4) (f32vector-ref uv 5) (f32vector-ref uv 6) (f32vector-ref uv 7) (f32vector-ref uv 0) (f32vector-ref uv 1) depth)))
  
  
  (method override (iterate-triangles proc)
    (let ((v1 (get-v1))
          (v2 (get-v2))
          (v3 (get-v3))
          (v4 (get-v4)))
      (proc v1 v2 v3)
      (proc v3 v4 v1)))
  
  
  (method public (get-v1)
    (vector-ref vertices 0))
  
  (method public (get-v2)
    (vector-ref vertices 1))
  
  (method public (get-v3)
    (vector-ref vertices 2))
  
  (method public (get-v4)
    (vector-ref vertices 3))
  
  
  (method public virtual (get-normal1)
    normal)
  
  (method public virtual (get-normal2)
    normal)
  
  (method public virtual (get-normal3)
    normal)
  
  (method public virtual (get-normal4)
    normal)
  
  
  (method override (copy-polygon) <Polygon>
    (allocate Quad
              (copy-vertex origin)
              (copy-vertex normal)
              element
              (vector (copy-vertex (get-v1))
                      (copy-vertex (get-v2))
                      (copy-vertex (get-v3))
                      (copy-vertex (get-v4)))
              radius))
  
  
  (method override (polygon-move! vect <f64vector> offset <fx>) <void>
    (let ((v1 (get-v1))
          (v2 (get-v2))
          (v3 (get-v3))
          (v4 (get-v4)))
      (f64vector-set! vect (+ offset  0) (vertex-x origin))
      (f64vector-set! vect (+ offset  1) (vertex-y origin))
      (f64vector-set! vect (+ offset  2) (vertex-z origin))
      (f64vector-set! vect (+ offset  3) (vertex-x v1))
      (f64vector-set! vect (+ offset  4) (vertex-y v1))
      (f64vector-set! vect (+ offset  5) (vertex-z v1))
      (f64vector-set! vect (+ offset  6) (vertex-x v2))
      (f64vector-set! vect (+ offset  7) (vertex-y v2))
      (f64vector-set! vect (+ offset  8) (vertex-z v2))
      (f64vector-set! vect (+ offset  9) (vertex-x v3))
      (f64vector-set! vect (+ offset 10) (vertex-y v3))
      (f64vector-set! vect (+ offset 11) (vertex-z v3))
      (f64vector-set! vect (+ offset 12) (vertex-x v4))
      (f64vector-set! vect (+ offset 13) (vertex-y v4))
      (f64vector-set! vect (+ offset 14) (vertex-z v4))
      (f64vector-set! vect (+ offset 15) radius)))
  
  
  (method override (polygon-extract! elem vect <f64vector> offset <fx> sphere-matrix <f64vector+>) <void>
    (matrix+-transform-3x4-offset! origin~self sphere-matrix vect offset)
    (matrix+-transform-3x4-offset! (get-v1) sphere-matrix vect (+ offset 3))
    (matrix+-transform-3x4-offset! (get-v2) sphere-matrix vect (+ offset 6))
    (matrix+-transform-3x4-offset! (get-v3) sphere-matrix vect (+ offset 9))
    (matrix+-transform-3x4-offset! (get-v4) sphere-matrix vect (+ offset 12))
    (polygon-normal! normal~self vertices)
    (set! element~self elem)
    (set! radius~self (polygon-radius origin vertices))))


;;;
;;;; Smooth-Quad
;;;


(class Smooth-Quad extends Quad
  
  
  (slot n1 <f64vector> getter generate)
  (slot n2 <f64vector> getter generate)
  (slot n3 <f64vector> getter generate)
  (slot n4 <f64vector> getter generate)
  
  
  (method override (initialize element vertices n1 n2 n3 n4)
    (nextmethod element vertices)
    (set! n1~self n1)
    (set! n2~self n2)
    (set! n3~self n3)
    (set! n4~self n4))
  
  
  (method override (get-normal1)
    n1)
  
  (method override (get-normal2)
    n2)
  
  (method override (get-normal3)
    n3)
  
  (method override (get-normal4)
    n4)))
