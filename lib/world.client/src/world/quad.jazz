;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; Quads
;;;


(module world.quad jazz


(import (jazz.geometry)
        (jazz.graphic)
        (jazz.graphic.opengl.glew)
        (jazz.io)
        (jazz.library)
        (jazz.literals)
        (jazz.media)
        (jazz.network)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.runtime)
        (jazz.system)
        (jazz.ui)
        (jazz.ui.dialog)
        (jazz.ui.window)
        (world)
        (world.foreign)
        (world.geometry)
        (world.plane)
        (world.polygon)
        (world.texture)
        (time))


(definition public (make-quad element normal v1 v2 v3 v4 radius)
  (new Quad element normal v1 v2 v3 v4 radius))


(definition public (make-smooth-quad element normal v1 v2 v3 v4 radius n1 n2 n3 n4)
  (new Smooth-Quad element normal v1 v2 v3 v4 radius n1 n2 n3 n4))


;;;
;;;; Quad
;;;


(class Quad extends Polygon
  
  
  (method override (initialize element normal v1 v2 v3 v4 radius)
    (nextmethod element (vector v1 v2 v3 v4) radius normal))
  
  
  (method override (triangle-count)
    2)
  
  
  (method override (add-triangles uv add)
    (let ((v1 (get-v1))
          (v2 (get-v2))
          (v3 (get-v3))
          (v4 (get-v4))
          (n1 (get-normal1))
          (n2 (get-normal2))
          (n3 (get-normal3))
          (n4 (get-normal4)))
      (add v1 v2 v3 n1 n2 n3 (f32vector-ref uv 0) (f32vector-ref uv 1) (f32vector-ref uv 2) (f32vector-ref uv 3) (f32vector-ref uv 4) (f32vector-ref uv 5))
      (add v3 v4 v1 n3 n4 n1 (f32vector-ref uv 4) (f32vector-ref uv 5) (f32vector-ref uv 6) (f32vector-ref uv 7) (f32vector-ref uv 0) (f32vector-ref uv 1))))
  
  
  (method override (iterate-triangles proc)
    (let ((v1 (get-v1))
          (v2 (get-v2))
          (v3 (get-v3))
          (v4 (get-v4)))
      (proc v1 v2 v3)
      (proc v3 v4 v1)))
  
  
  (method public (get-v1)
    (vector-ref vertices 0))
  
  (method public (get-v2)
    (vector-ref vertices 1))
  
  (method public (get-v3)
    (vector-ref vertices 2))
  
  (method public (get-v4)
    (vector-ref vertices 3))
  
  
  (method public virtual (get-normal1)
    normal)
  
  (method public virtual (get-normal2)
    normal)
  
  (method public virtual (get-normal3)
    normal)
  
  (method public virtual (get-normal4)
    normal))


;;;
;;;; Smooth-Quad
;;;


(class Smooth-Quad extends Quad
  
  
  (slot n1 <f32vector> getter generate)
  (slot n2 <f32vector> getter generate)
  (slot n3 <f32vector> getter generate)
  (slot n4 <f32vector> getter generate)
  
  
  (method override (initialize element normal v1 v2 v3 v4 radius n1 n2 n3 n4)
    (nextmethod element normal v1 v2 v3 v4 radius)
    (set! n1~self n1)
    (set! n2~self n2)
    (set! n3~self n3)
    (set! n4~self n4))
  
  
  (method override (get-normal1)
    n1)
  
  (method override (get-normal2)
    n2)
  
  (method override (get-normal3)
    n3)
  
  (method override (get-normal4)
    n4)))
