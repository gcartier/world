;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Stream Preprocess
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.preprocess jazz


(import (jazz.io)
        (jazz.stream)
        (world)
        (world.audio)
        (world.context)
        (world.settings)
        (world.stream)
        (world.streaming)
        (world.video))


(definition protected preprocess-level
  (world-setting 'world.preprocess-level 2))


(definition public (preprocess-directory dir (what: what #f) (profile: profile #f) (dry?: dry? #f))
  (iterate-directory dir
    (lambda (file)
      (preprocess-file file what: what profile: profile dry?: dry?))
    files?: #t;
    directories?: #f
    recursive?: #t))


(definition public (preprocess-file file (what: what #f) (profile: profile #f) (dry?: dry? #f) (force?: force? #f))
  (let ((profile (and profile (name->video-profile profile))))
    (define (maybe-audio file)
      (let ((media-file (media-brother file 'audio profile)))
        (when (or (not (exists? media-file)) force?)
          (if dry?
              (terminal 'audio file)
            (preprocess-audio file media-file profile)))))
    
    (define (maybe-video file)
      (define (maybe-profile profile)
        (let ((media-file (media-brother file 'video profile)))
          (when (or (not (exists? media-file)) force?)
            (if dry?
                (terminal 'video file (get-name profile))
              (preprocess-video file media-file profile)))))
      
      (if profile
          (maybe-profile profile)
        (loop (for level from 0 to preprocess-level)
              (maybe-profile (level->video-profile level)))))
    
    (define (maybe-image file)
      (let ((media-file (media-brother file 'video profile)))
        (when (or (not (exists? media-file)) force?)
          (if dry?
              (terminal 'video file)
            (preprocess-image file media-file profile)))))
    
    (let ((ext (get-extension file)))
      (cond ((member? ext '("jpg" "jpeg" "png") test: extension=?)
             (maybe-image file))
            ((member? (get-extension file) '("mp4" "mov" "webm") test: extension=?)
             (case what
               ((audio)
                (maybe-audio file))
               ((video)
                (maybe-video file))
               (else
                (maybe-audio file)
                (maybe-video file))))))))


(definition public (preprocess-audio file media-file profile)
  (let ((media-file (or media-file (media-brother file 'audio profile))))
    (let ((src (new File-Preprocess-Audio-Src
                 (parse file))))
      (preprocess-media file media-file src "audio" 'audio #f #f #f #f 'vorbis)
      (thread-join! (get-thread (get-task src))))))


(definition public (preprocess-video file media-file profile)
  (let ((media-file (or media-file (media-brother file 'video profile)))
        (level (get-level profile))
        (resolution (get-resolution profile))
        (framerate (get-framerate profile))
        (bitrate (get-bitrate profile))
        (codec (get-codec profile)))
    (let ((src (new File-Preprocess-Video-Src
                 (parse file)
                 resolution: resolution
                 framerate: framerate
                 bitrate: bitrate
                 codec: codec)))
      (preprocess-media file media-file src (format "video {a}" (get-name profile)) 'video level resolution framerate bitrate codec)
      (thread-join! (get-thread (get-task src))))))


(definition public (preprocess-image file media-file profile)
  (let ((media-file (or media-file (media-brother file 'video profile))))
    (let ((src (new Image-Stream-Video-Src
                 (get-name file))))
      (preprocess-media file media-file src "image" 'image #f #f #f #f #f)
      (thread-join! (get-thread (get-task src))))))


(definition (preprocess-media file media-file src suffix kind level resolution framerate bitrate codec)
  (let ((preprocess-pipeline #f)
        (preprocess-writer #f)
        (start-time #f)
        (eos? #f)
        (count 0))
    (define (play-pipeline)
      (set! start-time (current-seconds))
      (play preprocess-pipeline)
      (format :terminal "{a} ({a})" (get-name file) suffix))
    
    (define (pipeline-output buffer timestamp duration)
      (write-frame preprocess-writer buffer timestamp duration)
      (when (= (modulo count 100) 0)
        (format :terminal "."))
      (increase! count))
    
    (define (pipeline-eos)
      (unless eos?
        (set! eos? #t)
        (close preprocess-writer)
        (release preprocess-pipeline)
        (format :terminal "({a}){%}" (present-seconds (- (current-seconds) start-time)))))
    
    (define (maybe-add-metadata)
      (and (is? src File-Preprocess-Video-Src)
           (let ((appsink (gst_bin_get_by_name (get-pipeline src) "appsink")))
             (let ((pad (gst_element_get_static_pad appsink "sink")))
               (let ((caps (gst_pad_get_current_caps pad)))
                 (let ((caps-string (gst_caps_to_string caps)))
                   (add-metadata preprocess-writer caps: caps-string)
                   (and (not level)
                        (let ((struc (gst_caps_get_structure caps 0)))
                          (let ((width (gst_structure_get_int struc "width"))
                                (height (gst_structure_get_int struc "height")))
                            (set-resolution preprocess-writer (new Dimension width height)))))))))))
    
    (let ((sink (new Stream-Consumer
                  consumer: (lambda (buffer timestamp duration)
                              (if buffer
                                  (pipeline-output buffer timestamp duration)
                                (pipeline-eos))))))
      (set! preprocess-pipeline (pipeline (list src sink)))
      (set! preprocess-writer (new Stream-Writer media-file kind level resolution framerate bitrate codec))
      (play-pipeline))))


(definition (media-brother file kind profile)
  (let ((extension (case kind
                     ((audio) "streamaudio")
                     ((video) (get-extension profile)))))
    (get-brother file (add-extension (get-base file) extension)))))
