;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Stream Preprocess
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.preprocess jazz


(import (jazz.io)
        (jazz.stream)
        (jazz.stream.syntax (phase syntax))
        (world)
        (world.audio)
        (world.context)
        (world.profile)
        (world.settings)
        (world.stream)
        (world.streaming)
        (world.video))


(definition protected preprocess-extensions
  '("jpg" "jpeg" "m4a" "m4v" "mp3" "mp4" "mov" "ogg" "png" "webm" "wav"))


(class Preprocess-Unsupported-Error extends Error)


(definition public (preprocess-directory dir (what: what #f) (profile: profile #f) (denoise?: denoise? #f) (voice?: voice? #f) (prime?: prime? #f) (dry?: dry? #f) (feedback?: feedback? #t) (feedback: feedback #f))
  (iterate-directory dir
    (lambda (file)
      (preprocess-file file what: what profile: profile denoise?: denoise? voice?: voice? prime?: prime? dry?: dry? feedback?: feedback? feedback: feedback))
    files?: #t;
    directories?: #f
    recursive?: #t))


(definition public (preprocess-file file (what: what #f) (profile: profile #f) (denoise?: denoise? #f) (voice?: voice? #f) (prime?: prime? #f) (skip: skip #f) (dry?: dry? #f) (feedback?: feedback? #t) (feedback: feedback #f) (force?: force? #f))
  (let ((profile (and profile (name->graphic-profile profile))))
    (define (maybe-audio file discoverer-info)
      (define (maybe-profile info)
        (let ((media-file (media-brother file 'audio profile)))
          (when (or (not (exists? media-file)) force?)
            (if dry?
                (terminal 'audio file)
              (preprocess-audio file media-file profile denoise? voice? feedback? feedback)))))
      
      (define (maybe-prime info)
        (bind (container-type media-type media-version bitrate sample_rate channels caps) info
          (define (determine-container container-type)
            (cond ((or (equal? container-type "video/quicktime")
                       (equal? container-type "audio/x-m4a"))
                   'qt)
                  ((or (equal? container-type "application/x-id3")
                       (equal? container-type "audio/mpeg"))
                   'mp3)
                  ((equal? container-type "video/mpegts")
                   'mpegts)
                  ((equal? container-type "audio/ogg")
                   'ogg)
                  (else
                   (signal (new Preprocess-Unsupported-Error message: (format "Unsupported container type: {a}" container-type))))))
          
          (define (determine-codec media-type)
            (cond ((equal? media-type "audio/mpeg")
                   (if (eqv? media-version 1)
                       'mp3
                     'aac))
                  ((equal? media-type "audio/x-vorbis")
                   'vorbis)
                  (else
                   (signal (new Preprocess-Unsupported-Error message: (format "Unsupported audio type: {a}" media-type))))))
          
          (let ((container (determine-container container-type))
                (codec (determine-codec media-type))
                (media-file (media-brother file 'audio profile)))
            (when (or (not (exists? media-file)) force?)
              (if dry?
                  (terminal 'audio file)
                (preprocess-prime-audio file media-file profile container codec denoise? voice? feedback? feedback))))))
      
      (let ((info (discoverer-info-audio discoverer-info)))
        (when info
          (if (and prime? (not denoise?) (not voice?))
              (maybe-prime info)
            (maybe-profile info)))))
    
    (define (maybe-video file discoverer-info)
      (define (maybe-profile profile)
        (let ((media-file (media-brother file 'video profile)))
          (when (or (not (exists? media-file)) force?)
            (if dry?
                (terminal 'video file (get-name profile))
              (preprocess-video file media-file profile feedback? feedback)))))
      
      (define (maybe-prime)
        (let ((info (discoverer-info-video discoverer-info)))
          (when info
            (bind (container-type media-type resolution framerate bitrate caps) info
              (define (determine-container container-type)
                (cond ((equal? container-type "video/quicktime")
                       'qt)
                      ((equal? container-type "video/mpegts")
                       'mpegts)
                      (else
                       (signal (new Preprocess-Unsupported-Error message: (format "Unsupported container type: {a}" container-type))))))
              
              (define (determine-codec media-type)
                (cond ((equal? media-type "video/x-h264")
                       'h264)
                      ((equal? media-type "video/mpeg")
                       'mpeg)
                      (else
                       (signal (new Preprocess-Unsupported-Error message: (format "Unsupported video type: {a}" media-type))))))
              
              (let ((container (determine-container container-type))
                    (codec (determine-codec media-type)))
                (let ((profile (estimate-resolution-profile resolution)))
                  (let ((media-file (media-brother file 'video profile)))
                    (when (or (not (exists? media-file)) force?)
                      (if dry?
                          (terminal 'video file (get-name profile))
                        (preprocess-prime-video file media-file profile container resolution framerate bitrate codec feedback? feedback skip))))))))))
      
      (if prime?
          (maybe-prime)
        (if profile
            (maybe-profile profile)
          (loop (for level from 0 to (active-graphic-level))
                (maybe-profile (level->graphic-profile level))))))
    
    (define (maybe-image file)
      (let ((media-file (media-brother file 'image profile)))
        (when (or (not (exists? media-file)) force?)
          (if dry?
              (terminal 'image file)
            (preprocess-image file media-file draw-resolution draw-framerate draw-bitrate draw-codec feedback? feedback)))))
    
    (let ((ext (get-extension file)))
      (when (member? ext preprocess-extensions test: extension=?)
        (if (member? ext image-extensions test: extension=?)
            (maybe-image file)
          ;; make sure costly discoverer-info is only called once
          (with-discoverer-info file
            (lambda (discoverer-info)
              (case what
                ((audio)
                 (maybe-audio file discoverer-info))
                ((video)
                 (maybe-video file discoverer-info))
                (else
                 (maybe-audio file discoverer-info)
                 (maybe-video file discoverer-info))))))))))


(definition public (preprocess-audio file media-file profile denoise? voice? feedback? feedback)
  (let ((media-file (or media-file (media-brother file 'audio profile))))
    (let ((src (new File-Preprocess-Audio-Src
                 (parse file)
                 denoise?: denoise?
                 voice?: voice?)))
      (preprocess-media file media-file src "audio" 'audio #f #f #f #f audio-codec feedback? feedback)
      (thread-join! (get-thread (get-task src))))))


(definition public (preprocess-prime-audio file media-file profile container codec denoise? voice? feedback? feedback)
  (let ((media-file (or media-file (media-brother file 'audio profile))))
    (let ((src (new File-Preprocess-Audio-Src
                 (parse file)
                 container: container
                 prime?: #t
                 denoise?: denoise?
                 voice?: voice?)))
      (preprocess-media file media-file src "audio" 'audio #f #f #f #f codec feedback? feedback)
      (thread-join! (get-thread (get-task src))))))


(definition public (preprocess-video file media-file profile feedback? feedback)
  (let ((media-file (or media-file (media-brother file 'video profile)))
        (level (get-level profile))
        (resolution (get-resolution profile))
        (framerate (get-framerate profile))
        (bitrate (get-bitrate profile))
        (codec (get-codec profile)))
    (let ((src (new File-Preprocess-Video-Src
                 (parse file)
                 level: level
                 resolution: resolution
                 framerate: framerate
                 bitrate: bitrate
                 codec: codec)))
      (preprocess-media file media-file src (format "video {a}" (get-name profile)) 'video level resolution framerate bitrate codec feedback? feedback)
      (thread-join! (get-thread (get-task src))))))


(definition public (preprocess-prime-video file media-file profile container resolution framerate bitrate codec feedback? feedback skip)
  (let ((media-file (or media-file (media-brother file 'video profile)))
        (level (get-level profile)))
    (let ((src (new File-Preprocess-Video-Src
                 (parse file)
                 container: container
                 prime?: #t
                 level: level
                 resolution: resolution
                 framerate: framerate
                 bitrate: bitrate
                 codec: codec)))
      (preprocess-media file media-file src (format "video {a}" (get-name profile)) 'video level resolution framerate bitrate codec feedback? feedback skip)
      (thread-join! (get-thread (get-task src))))))


(definition (preprocess-image file media-file resolution framerate bitrate codec feedback? feedback)
  (let ((src (new File-Preprocess-Image-Src
               file)))
    (preprocess-media file media-file src "image" 'image #f resolution framerate bitrate codec feedback? feedback)
    (thread-join! (get-thread (get-task src)))))


(definition (preprocess-media file media-file src suffix kind level resolution framerate bitrate codec feedback? feedback (skip #f))
  (let ((preprocess-pipeline #f)
        (preprocess-writer #f)
        (start-time #f)
        (file-size (file-size (parse file)))
        (eos? #f)
        (count 0))
    (define (play-pipeline)
      (set! start-time (current-seconds))
      (play preprocess-pipeline)
      (when feedback?
        (if feedback
            (feedback 'start file suffix #f)
          (format :terminal "{a} ({a})" (get-name file) suffix))))
    
    (define (pipeline-output buffer dts pts duration flags)
      (when (and skip (frame-keyframe? flags))
        (if (= skip 0)
            (set! skip #f)
          (decrease! skip)))
      (when (not preprocess-writer)
        (set! preprocess-writer (new Stream-Writer media-file kind level resolution framerate bitrate codec #f)))
      ;; ignore vorbis headers
      (unless (and (eq? codec 'vorbis)
                   (= pts GST_CLOCK_TIME_NONE))
        (unless skip
          (when (write-frame preprocess-writer buffer dts pts duration flags)
            (when (and feedback? (= (modulo count 100) 0))
              (if feedback
                  (let ((percentage (percentage (gst_stat_probe_get_size (get-stat-probe src)) file-size)))
                    ;; quick hack until investigation
                    (let ((effective-percentage (min percentage 100.)))
                      (feedback 'progress file suffix effective-percentage)))
                (format :terminal ".")))
            (increase! count)))))
    
    (define (pipeline-eos)
      (unless eos?
        (set! eos? #t)
        (when preprocess-writer
          (maybe-add-metadata)
          (close preprocess-writer))
        (release preprocess-pipeline)
        (when feedback?
          (if feedback
              (feedback 'end file suffix 100.)
            (format :terminal "({a}){%}" (present-seconds (- (current-seconds) start-time)))))))
    
    (define (maybe-add-metadata)
      (typecase src
        ((File-Preprocess-Audio-Src)
         (with-unref ((appsink (gst_bin_get_by_name (get-pipeline src) "appsink")))
           (with-unref ((pad (gst_element_get_static_pad appsink "sink")))
             (let ((caps (gst_pad_get_current_caps pad)))
               (let ((caps-string (gst_caps_to_string caps)))
                 (set-caps preprocess-writer caps-string))
               (gst_caps_unref caps)))))
        ((File-Preprocess-Video-Src)
         (with-unref ((appsink (gst_bin_get_by_name (get-pipeline src) "appsink")))
           (with-unref ((pad (gst_element_get_static_pad appsink "sink")))
             (let ((caps (gst_pad_get_current_caps pad)))
               (let ((caps-string (gst_caps_to_string caps)))
                 (set-caps preprocess-writer caps-string)
                 (let ((struc (gst_caps_get_structure caps 0)))
                   (let ((width (gst_structure_get_int struc "width"))
                         (height (gst_structure_get_int struc "height")))
                     (set-resolution preprocess-writer (new Dimension width height)))))
               (gst_caps_unref caps)))))
        ((File-Preprocess-Image-Src)
         (let ((width (get-width resolution))
               (height (get-height resolution)))
           (let ((caps (tie "video/x-h264, stream-format=byte-stream, alignment=au, width={width}, height={height}, framerate={framerate}")))
             (set-caps preprocess-writer caps))))))
    
    (let ((sink (new Stream-Frame-Consumer
                  consumer: (lambda (buffer dts pts duration flags)
                              (pipeline-output buffer dts pts duration flags))
                  eos: (lambda ()
                         (pipeline-eos)))))
      (set! preprocess-pipeline (pipeline (list src sink)))
      (play-pipeline))))


(definition (media-brother file kind profile)
  (let ((extension (case kind
                     ((audio) "streamaudio")
                     ((video) (get-extension profile))
                     ((image) "streamimage"))))
    (get-brother file (add-extension (get-base file) extension)))))
