;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Skybox
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2015
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.skybox jazz


(import (jazz.graphic.opengl.glew)
        (jazz.platform.cairo)
        (world)
        (world.autoload)
        (world.camera)
        (world.generate)
        (world.geometry)
        (world.material)
        (world.mesh)
        (world.object)
        (world.opengl)
        (world.programs)
        (world.settings)
        (world.shape)
        (world.syntax (phase syntax))
        (world.texture))


(definition public (skybox-kind directory)
  (cond ((exists?~ (new-file~ directory "left.png")) 'left)
        ((exists?~ (new-file~ directory "lf.png")) 'lf)
        (else #f)))


(definition skyboxes
  '())


(definition public (get-skyboxes)
  skyboxes)


(definition public (prepare-skyboxes)
  (let ((world (current-world)))
    (define (collect-skyboxes)
      (define (add-skyboxes dir queue)
        (when (exists?~ dir)
          (iterate-directory~ dir
            (lambda (dir)
              (when (skybox-kind dir)
                (enqueue queue (new Skybox dir))))
            files?: #f
            directories?: #t)))
      
      (let ((queue (new-queue)))
        (iterate-assets~ world
          (lambda (assets)
            (add-skyboxes (new-directory~ assets "skybox") queue)))
        (sort di<? (queue-list queue) key: get-name~)))
    
    (set! skyboxes (collect-skyboxes))))


(definition public (find-skybox name)
  (find-if (lambda (skybox)
             (di=? (get-name~ skybox) name))
           skyboxes))


(definition skybox-mesh
  #f)

(definition skybox-program
  #f)


(definition public (prepare-skybox-program)
  (let ((zone (current-zone)))
    (when (not skybox-program)
      (let ((program (new Skybox-Program)))
        (let ((mesh (generate-cube-mesh)))
          (set-array~ mesh (create-array~ program mesh))
          (set! skybox-mesh mesh)
          (set! skybox-program program))))))


;;;
;;;; Skybox
;;;


(class Skybox extends Object
  
  
  (slot directory               getter generate)
  (slot kind      initialize #f getter generate)
  (slot sample    initialize #f getter generate)
  (slot material  initialize #f getter generate)
  
  
  (method override (initialize directory)
    (set! directory~self directory))
  
  
  (method override (print output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{s}" (get-name)))))
  
  
  (method public (get-name)
    (get-base~ directory))
  
  
  ;;;
  ;;;; Prepare
  ;;;
  
  
  (method public (prepare)
    (let ((size #f))
      (define (make-surface name (sample? #f))
        (let ((file (new-file~ directory name)))
          (receive (image-width image-height surface) (cairo-surface-from-file file)
            (let ((image-size (new Dimension image-width image-height)))
              (if (not size)
                  (set! size image-size)
                (when (nu/=? image-size size)
                  (error "Inconsistant skybox sizes"))))
            (when sample?
              (let ((data (cairo_image_surface_get_data surface)))
                (let ((texture (new Image-Texture image-width image-height data: data file: file)))
                  (map-texture~ texture)
                  (set! sample texture))))
            surface)))
      
      (unless kind
        (let ((kind (or (skybox-kind directory)
                        (error "Unknown skybox format: {a}" (parse~ directory)))))
          (define (with-surfaces proc)
            (case kind
              ((left)
               (proc (make-surface "right.png")
                     (make-surface "left.png")
                     (make-surface "back.png")
                     (make-surface "front.png" #t)
                     (make-surface "up.png")
                     (make-surface "down.png")))
              ((lf)
               (proc (make-surface "ft.png" #t)
                     (make-surface "bk.png")
                     (make-surface "lf.png")
                     (make-surface "rt.png")
                     (make-surface "up.png")
                     (make-surface "dn.png")))))
          
          (with-surfaces
            (lambda (front back left right up down)
              (let ((texture (new Image-Texture (get-width~ size) (get-height~ size))))
                (map-texture-cube~ texture front back up down right left)
                (close~ front)
                (close~ back)
                (close~ left)
                (close~ right)
                (close~ up)
                (close~ down)
                (set! material~self (new Material name: 'skybox texture: texture))
                (set! kind~self kind))))))))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (method public (draw-skybox)
    (let ((camera (current-camera)))
      (let ((projection-matrix (get-projection-matrix~ camera))
            (view-matrix (make-view-matrix& (vertex& 0. 0. 0.) (get-lookat~ camera))))
        (use~ skybox-program)
        (glDepthFunc GL_LEQUAL)
        (bind-uniforms~ skybox-program projection-matrix view-matrix material)
        (draw-array~ skybox-mesh)
        (unbind-uniforms~ skybox-program)
        (glDepthFunc GL_LESS)
        (unuse~ skybox-program)
        (gl-check-error))))))
