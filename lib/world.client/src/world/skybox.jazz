;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Skybox
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.skybox jazz


(import (jazz.cairo)
        (jazz.graphic)
        (jazz.io)
        (jazz.opengl.glew)
        (jazz.opengl.texture)
        (gaea.dye)
        (gaea.geometry)
        (world)
        (world.asset)
        (world.autoload)
        (world.camera)
        (world.context)
        (world.fog)
        (world.image)
        (world.mesh)
        (world.programs)
        (world.settings)
        (world.shader)
        (world.shape)
        (world.texture))


(definition public (skybox-kind path)
  (let ((name (filename-name path)))
    (cond ((filename=? name "left.png") 'left)
          ((filename=? name "lf.png") 'lf)
          (else #f))))


(definition skyboxes
  '())

(definition public (get-skyboxes)
  skyboxes)


(definition protected (collect-skyboxes)
  (let ((world (current-world)))
    (let ((queue (new-queue)))
      (iterate-assets world
        (lambda (assets)
          (iterate-entries assets
            (lambda (path)
              (when (starts-with? path "skybox/")
                (let ((kind (skybox-kind path)))
                  (when kind
                    (let ((dirpath (filename-dir path)))
                      (let ((skybox-path (substring dirpath (string-length "skybox/") (- (string-length dirpath) 1))))
                        (enqueue queue (new Skybox assets skybox-path kind)))))))))))
      (sort di<? (queue-list queue) key: get-path))))


(definition public (prepare-skyboxes)
  (set! skyboxes (collect-skyboxes)))


(definition public (find-skybox path)
  (find-if (lambda (skybox)
             (di=? (get-path skybox) path))
           skyboxes))


(definition protected nebula-skybox
  #f)

(definition protected (cache-nebula-skybox)
  (or nebula-skybox
      (let ((skybox (find-skybox "world/nebula")))
        (prepare skybox)
        (activate skybox)
        (set! nebula-skybox skybox)
        skybox)))


(definition skybox-mesh
  #f)

(definition skybox-program
  #f)


(definition public (prepare-skybox-program)
  (let ((zone (current-zone)))
    (when (not skybox-program)
      (let ((program (new Skybox-Program)))
        (let ((mesh (generate-cube-mesh)))
          (set-array mesh (create-array program mesh))
          (set! skybox-mesh mesh)
          (set! skybox-program program))))))


;;;
;;;; Skybox
;;;


(class Skybox extends Object
  
  
  (slot assets                getter generate)
  (slot path                  getter generate)
  (slot kind    initialize #f getter generate)
  (slot texture initialize #f getter generate)
  (slot plane   initialize #f getter generate)
  (slot fog     initialize #f getter generate)
  (slot sample  initialize #f getter generate)
  
  
  (method override (initialize self assets path kind)
    (set! self.assets assets)
    (set! self.path path)
    (set! self.kind kind)
    (set! self.fog (cond ((equal? path "world/nebula") (dye .052 .002 .050 1.))
                         ((equal? path "world/calm_sea") (dye .196 .377 .448 1.))
                         ((equal? path "world/dark_sea") (dye .134 .050 .009 1.))
                         ((equal? path "world/earth") (dye .003 .033 .077 1.))
                         (else (dye .003 .033 .077 1.)))))
  
  
  (method override (print self output readably)
    (print-unreadable self output
      (lambda (output)
        (format output "{s}" path))))
  
  
  ;;;
  ;;;; Prepare
  ;;;
  
  
  (method public (prepare self)
    (let ((size #f))
      (define (make-image base)
        (let ((name (add-extension base "png")))
          (let ((asset (new Asset assets (append (list "skybox") (tokenise-filename path) (list name)))))
            (bind-values (width height data) (read-image asset)
              (let ((image-size (new Dimension width height)))
                (if (not size)
                    (set! size image-size)
                  (when (nu/=? image-size size)
                    (error "Inconsistant skybox sizes"))))
              data))))
      
      (define (close-image obj)
        (unless (u8vector? obj)
          (cairo_surface_destroy obj)))
      
      (define (with-images proc)
        (case kind
          ((left)
           (proc (make-image "right")
                 (make-image "left")
                 (make-image "back")
                 (make-image "front")
                 (make-image "up")
                 (make-image "down")))
          ((lf)
           (proc (make-image "ft")
                 (make-image "bk")
                 (make-image "lf")
                 (make-image "rt")
                 (make-image "up")
                 (make-image "dn")))))
      
      (define (with-plane front back left right down proc)
        (define (apply-color image (full? #f))
          (let ((width (get-width size))
                (height (get-height size))
                (start {Color red: .002 green: .067 blue: .037})
                (end {Color red: .004 green: .069 blue: .113}))
            (let ((surface (if (u8vector? image)
                               (let ((cairo-format CAIRO_FORMAT_ARGB32))
                                 (let ((stride (cairo_format_stride_for_width cairo-format width)))
                                   (new Surface (cairo_image_surface_create_for_data (still-u8vector-body image) CAIRO_FORMAT_ARGB32 width height stride))))
                             (new Surface image handle-shared?: #t))))
              (if full?
                  (fill-rect surface (new Rect 0 0 width height) start)
                (let ((mid (fxround/ height 2)))
                  (let ((low (+ mid (fxround/ height 12))))
                    (gradient-fill-rect surface (new Rect 0 mid width low) (list (list 0 end) (list 1 start)) direction: 'vertical)
                    (fill-rect surface (new Rect 0 low width height) start))))
              (flush surface)
              (close surface)
              image)))
        
        (proc (apply-color front)
              (apply-color back)
              (apply-color left)
              (apply-color right)
              (apply-color down #t)))
      
      (unless texture
        (with-images
          (lambda (front back left right up down)
            (let ((tex (new Image-Texture (get-width size) (get-height size))))
              (map-texture-cube tex front back up down right left)
              (set! texture tex))
            (when (equal? path "world/earth")
              (with-plane front back left right down
                (lambda (front back left right down)
                  (let ((tex (new Image-Texture (get-width size) (get-height size))))
                    (map-texture-cube tex front back up down right left)
                    (set! plane tex)))))
            (close-image front)
            (close-image back)
            (close-image left)
            (close-image right)
            (close-image up)
            (close-image down))))))
  
  
  (method public (sample-texture self)
    (or sample
        (let ((base (case kind
                      ((left) "front")
                      ((lf) "ft"))))
          (let ((name (add-extension base "png")))
            (let ((asset (new Asset assets (append (list "skybox") (tokenise-filename path) (list name)))))
              (let ((file (retrieve-file asset)))
                (bind-values (image-width image-height surface) (cairo-surface-from-file file)
                  (let ((data (cairo_image_surface_get_data surface)))
                    (let ((tex (new Image-Texture image-width image-height data: data file: file)))
                      (map-texture tex)
                      (cairo_surface_destroy surface)
                      (set! sample tex)
                      sample)))))))))
  
  
  ;;;
  ;;;; Activate
  ;;;
  
  
  (method public (activate self)
    (set-fog-color fog))
  
  
  ;;;
  ;;;; Draw
  ;;;
  
  
  (method public (draw-skybox self)
    (let ((camera (current-camera)))
      (let ((projection-matrix (get-projection-matrix camera))
            (view-matrix (make-view-matrix^ (vertex^ 0. 0. 0.) (get-lookat camera))))
        (use skybox-program)
        (glDepthFunc GL_LEQUAL)
        (bind-uniforms skybox-program projection-matrix view-matrix (if (and plane (2d?)) plane texture))
        (draw-array skybox-mesh)
        (unbind-uniforms skybox-program)
        (glDepthFunc GL_LESS)
        (unuse skybox-program)
        (gl-check-error))))))
