;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; World Window
;;;
;;;  The contents of this file are subject to the Mozilla Public License Version
;;;  1.1 (the "License"); you may not use this file except in compliance with
;;;  the License. You may obtain a copy of the License at
;;;  http://www.mozilla.org/MPL/
;;;
;;;  Software distributed under the License is distributed on an "AS IS" basis,
;;;  WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License
;;;  for the specific language governing rights and limitations under the
;;;  License.
;;;
;;;  The Original Code is WorldScheme.
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2014
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):
;;;
;;;  Alternatively, the contents of this file may be used under the terms of
;;;  the GNU General Public License Version 2 or later (the "GPL"), in which
;;;  case the provisions of the GPL are applicable instead of those above. If
;;;  you wish to allow use of your version of this file only under the terms of
;;;  the GPL, and not to allow others to use your version of this file under the
;;;  terms of the MPL, indicate your decision by deleting the provisions above
;;;  and replace them with the notice and other provisions required by the GPL.
;;;  If you do not delete the provisions above, a recipient may use your version
;;;  of this file under the terms of any one of the MPL or the GPL.
;;;
;;;  See http://github.com/gcartier/world for details.


(module world.window jazz


(import (jazz.graphic.opengl.autoload)
        (jazz.ui.view)
        (jazz.ui.window)
        (world)
        (world.autoload)
        (world.interface)
        (world.syntax (phase syntax)))


(definition public (with-window-cursor cursor thunk)
  (let ((window (current-world-window)))
    (unwind-protect
        (begin
          (set-cursor cursor)
          (prog1 (thunk)
            (render-scene~ window)))
      (cursor-update~ window #t))))


(class World-Window extends OpenGL-Window
  
  
  (slot window-cursor initialize #f getter generate)
  
  
  (method override (destroy)
    (destroy~ (current-world))
    (nextmethod))
  
  
  ;;;
  ;;;; Render
  ;;;
  
  
  (method override (draw-scene)
    @temporary-patch-to-multi-threading-problems
    (let ((world (current-world+)))
      (when world
        (render-world~ world))))
  
  
  ;;;
  ;;;; Cursor
  ;;;
  
  
  (method public (set-window-cursor cursor)
    (set! window-cursor cursor)
    (cursor-update #t))

  
  (method override (cursor-update hit)
    (if hit
        (when window-cursor
          (set-cursor window-cursor))
      (nextmethod hit)))
  
  
  ;;;
  ;;;; Size
  ;;;
  
  
  (method override (size-change state size)
    (let ((world (current-world+)))
      (when world
        (size-change~ world size)))
    (nextmethod state size))
  
  
  (method override (resize-scene width height)
    (nextmethod width height)
    (let ((world (current-world+)))
      (when world
        (resize-scene~ world))))
  
  
  ;;;
  ;;;; Mouse
  ;;;
  
  
  (method override (mouse-down h v)
    (let ((world (current-world))
          (view (find-view (new Point h v))))
      (cond ((get-explore-interface?~ world)
             (set-explore-interface?~ world #f)
             (set-window-cursor~ (current-world-window) :arrow)
             (set-% view))
            ((and view (is-not? view World-Interface))
             (nextmethod h v))
            (else
             (bring-to-front)
             (acquire-capture)
             (acquire-focus)
             (mouse-down~ world h v)))))
  
  
  (method override (mouse-move h v)
    (let ((world (current-world))
          (view (find-view (new Point h v))))
      (cond ((get-explore-interface?~ world)
             (display-status~ world (format "{a}" view)))
            ((and (not (get-down?~ world)) (not (get-moving?~ world)) (or (get-mouse-down-view) (and view (is-not? view World-Interface))))
             (nextmethod h v))
            (else
             (when (get-mouse-in)
               (dispatch-mouse-leave~ (get-mouse-in)))
             (reset-mouse-in)
             (mouse-move~ world h v)))))
  
  
  (method override (mouse-up h v)
    (let ((world (current-world))
          (view (find-view (new Point h v))))
      (if (and (not (get-down?~ world)) (not (get-moving?~ world)) (or (get-mouse-down-view) (and view (is-not? view World-Interface))))
          (nextmethod h v)
        (release-capture)
        (mouse-up~ world h v))))
  
  
  (method override (double-click h v)
    (let ((world (current-world))
          (view (find-view (new Point h v))))
      (if (and (not (get-down?~ world)) (not (get-moving?~ world)) view (is-not? view World-Interface))
          (nextmethod h v)
        (release-capture)
        (double-click~ world h v))))
  
  
  (method override (right-mouse-down h v)
    (let ((world (current-world)))
      (acquire-capture)
      (right-mouse-down~ world h v)))
  
  
  (method override (right-mouse-up h v)
    (let ((world (current-world)))
      (release-capture)
      (right-mouse-up~ world h v)))
  
  
  (method override (mouse-wheel h v delta)
    (let ((world (current-world)))
      (if (get-mouse-in)
          (nextmethod h v delta)
        (mouse-wheel~ world h v delta))))
  
  
  ;;;
  ;;;; Keyboard
  ;;;
  
  
  (method override (key-down c)
    (let ((world (current-world)))
      (if (get-focus)
          (nextmethod c)
        (key-down~ world c))))
  
  
  (method override (key-press c)
    (let ((world (current-world)))
      (if (get-focus)
          (nextmethod c)
        (key-press~ world c))))))
