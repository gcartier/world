;;;==============
;;;  JazzScheme
;;;==============
;;;
;;;; World Window
;;;


(module world.window jazz


(import (jazz.graphic)
        (jazz.graphic.opengl.autoload)
        (jazz.graphic.opengl.glew)
        (jazz.library)
        (jazz.literals)
        (jazz.platform)
        (jazz.platform.cairo)
        (jazz.ui)
        (jazz.ui.window)
        (world)
        (world.autoload)
        (world.interface))


(class World-Window extends OpenGL-Window
  
  
  (slot down-interface? initialize #f getter generate)
  
  
  (method override (destroy)
    (destroy~ world)
    (nextmethod))
  
  
  ;;;
  ;;;; Scene
  ;;;
  
  
  (method override (draw-scene)
    (when world
      (render-world~ world)))
  
  
  ;;;
  ;;;; Events
  ;;;
  
  
  (method override (size-change state size)
    (when world
      (set-width~ world (cast <fl> (get-width~ size)))
      (set-height~ world (cast <fl> (get-height~ size))))
    (nextmethod state size))
  
  
  (method override (resize-scene width height)
    (nextmethod width height)
    (when world
      (resize-scene~ world)))

  
  (method override (cursor-update hit)
    (if hit
        (cursor-update~ world)
      (nextmethod hit)))
  
  
  (method override (mouse-down h v)
    (let ((view (find-view (new Point h v))))
      (set! down-interface? (and view (is-not? view World-Interface)))
      (if down-interface?
          (nextmethod h v)
        (bring-to-front)
        (acquire-capture)
        (acquire-focus)
        (mouse-down~ world h v))))
  
  
  (method override (mouse-move h v)
    (if down-interface?
        (nextmethod h v)
      (mouse-move~ world h v)))
  
  
  (method override (mouse-up h v)
    (if down-interface?
        (nextmethod h v)
      (release-capture)
      (mouse-up~ world h v)))
  
  
  (method override (right-mouse-down h v)
    (acquire-capture)
    (right-mouse-down~ world h v))
  
  
  (method override (right-mouse-up h v)
    (release-capture)
    (right-mouse-up~ world h v))
  
  
  (method override (mouse-wheel h v delta)
    (mouse-wheel~ world h v delta))
  
  
  (method override (key-down code)
    (key-down~ world code))
  
  
  (method override (key-press c)
    (key-press~ world c))
  
  
  (method (toggle-fullscreen)
    (set! fullscreen? (not fullscreen?))
    (if fullscreen?
        (set-fullscreen-mode)
      (reset-fullscreen-mode)))))
