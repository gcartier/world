;;;===============
;;;  WorldScheme
;;;===============
;;;
;;;; Billboard Consoles
;;;
;;;  The Initial Developer of the Original Code is Guillaume Cartier.
;;;  Portions created by the Initial Developer are Copyright (C) 2012-2018
;;;  the Initial Developer. All Rights Reserved.
;;;
;;;  Contributor(s):


(module world.console jazz


(import (jazz.component)
        (jazz.exemplar)
        (jazz.geometry)
        (jazz.graphic)
        (jazz.markup)
        (jazz.outline)
        (jazz.text)
        (jazz.ui)
        (jazz.view)
        (jazz.window)
        (world)
        (world.context)
        (world.pane))


;;;
;;;; Entity
;;;


(class Entity-Pane extends World-Pane
  
  
  (property entity initialize #f accessors generate)
  
  
  (method override (destroy self)
    (reset-layout entity)
    (nextmethod self))
  
  
  ;; bantoo #f for now
  (method override (scale? self)
    #f)
  
  
  ;; makes the code a bit darker but is a lot nicer when
  ;; viewed from afar and it is exactly the same when viewed
  ;; from pane-distance which is where it really counts
  (method override (mipmap? self)
    #t))


;;;
;;;; Script
;;;


(definition public (make-script-pane entity data)
  (let ((expanse @phone {Dimension$fl$ .5625 1.} @tablet {Dimension$fl$ 2. 1.125} {Dimension$fl$ 6. 3.375})
        (resolution @phone {Dimension 720 1280} {Dimension 1280 720}))
    (let ((pane (new Script-Pane
                  title: "Script"
                  expanse: expanse
                  resolution: resolution
                  entity: entity
                  data: data))
          (manager (new world.interface.script:Script-Manager size: resolution)))
      (set-player manager pane)
      (set-root pane manager)
      (set-background manager #f)
      (set-edited manager entity)
      (let ((view (locate manager 'script)))
        (set-visible? (locate view 'run) #f)
        (let ((text (locate view 'text))
              (script (get-script entity)))
          (when script
            (set-string-content text (effective-text script)))))
      (let ((pos (center (get-size manager) resolution)))
        (set-position manager pos))
      (invalidate-view manager)
      pane)))


(class Script-Pane extends Entity-Pane
  
  
  (method override (draw self surface)
    (let ((width (get-width resolution))
          (height (get-height resolution)))
      (let ((rect (new Rect 0 0 width height))
            (color {Color red: .000 green: .000 blue: .000 alpha: .5}))
        (clear surface)
        (fill-rect surface rect color)
        (frame-rect surface rect color)
        (paint-drawing root surface '())))))


@wait
(definition public (active-script-manager interface? force?)
  (let ((world (current-world)))
    (let ((target (first-target world)))
      (if (is? target entity)
      (interface-script #t)
      (let ((interface (current-interface)))
        (let ((panel (child interface 'script)))
          (let ((manager (get-manager panel)))
            (let ((splitter (locate manager 'console-splitter)))
              (update-console (get-edited manager))
              (toggle-zoom splitter 'second)))))))))


(definition public (find-script-manager entity)
  (or (let ((interface (current-interface+)))
        (and interface
             (let ((panel (child interface 'script)))
               (and (get-visible? panel)
                    (let ((manager (get-manager panel)))
                      (and (eq? (get-edited manager) entity)
                           manager))))))
      (let ((pane (get-script-pane entity)))
        (and pane
             (get-root pane)))))


(definition protected (clear-indicators entity)
  ;; quicky should not be necessary
  (when (window?)
    (let ((manager (find-script-manager entity)))
      (when manager
        (let ((text (locate manager '(script text))))
          (let ((model (get-model text)))
            (for-each-paragraph model
              (lambda (line <fx> paragraph <Paragraph>)
                (for-each (lambda (run)
                            (let ((style (get-style run)))
                              (when (or (inherits? style 'Walk)
                                        (inherits? style 'Error))
                                (set-style run (get-default-style model))
                                (invalidate-paragraph model paragraph))))
                          (get-runs paragraph))))))))))


;; some code copied from edit-stop/detail for now
(definition public (update-script-walk entity locations)
  (let ((world (current-world)))
    (let ((manager (find-script-manager entity)))
      (when manager
        (let ((text (locate manager '(script text)))
              (script (get-script entity)))
          (define (find-location location)
            (bind (container line col) location
              ;; adjust for inserted module line
              (let ((line (max 0 (- line 1))))
                ;; the valid-cell is a quick hack around issue #277
                ;; collaborative scripting needs to be tought through...
                (let ((start (valid-cell text (new Cell line col)))
                      (end (valid-cell text (new Cell line (+ col 1)))))
                  (let ((expl <Text-Explorer> (new (explorer-class text) (get-model text) start: start end: start)))
                    (catch (Explore-Error exc
                             (valid-range text (new Range$Cell$ start end)))
                      (if (outer-expr expl uniform-style?: #t)
                          (get-range expl)
                        (valid-range text (new Range$Cell$ start end)))))))))
            
            (let ((model (get-model text)))
              (let ((ranges (map find-location locations)))
                (for-each (lambda (range)
                            (apply-style model 'Walk (get-start range) (get-end range)))
                          ranges))))))))


;; some code copied from Indicator:edit for now
(definition public (update-script-error entity locat status)
  (let ((world (current-world)))
    (when locat
      (bind (container line col) (locat->container/line/col locat)
        (when (eq? container 'instance)
          (let ((manager (find-script-manager entity)))
            (when manager
              ;; adjust for inserted module line
              (let ((line (max 0 (- line 1))))
                (let ((text (locate manager '(script text))))
                  (define (determine-range)
                    (let ((line (valid-line text line)))
                      ;; the valid-cell is a quick hack around issue #277
                      ;; collaborative scripting needs to be tought through...
                      (let ((start (valid-cell text (if col (new Cell line col) (new Cell line (get-column text line)))))
                            (end (valid-cell text (if col (new Cell line (+ col 1)) (new Cell line (paragraph-length text line))))))
                        (if (eq? status 'read)
                            (valid-range text (new Range$Cell$ start end))
                          (let ((expl <Text-Explorer> (new (explorer-class text) (get-model text) start: start end: start)))
                            (catch (Explore-Error exc
                                     (valid-range text (new Range$Cell$ start end)))
                              (if (outer-expr expl uniform-style?: #t)
                                  (get-range expl)
                                (valid-range text (new Range$Cell$ start end)))))))))
                  
                  (let ((range (determine-range)))
                    (apply-style (get-model text) (if (eq? status 'read) 'Walk 'Error) (get-start range) (get-end range))))))))))))


;;;
;;;; Console
;;;


(definition public (make-console-pane entity width height data)
  (new Console-Pane
    title: "Console"
    expanse: (if (and width height) (new Dimension$fl$ width height) {Dimension$fl$ 6. 3.375})
    resolution: {Dimension 640 360}
    entity: entity
    data: data))


(class Console-Pane extends Entity-Pane
  
  
  (method override (draw self surface)
    (clear-glass surface resolution)
    (bind (message point-size) data
      (let ((point-size (or point-size 28))
            (text (if (and (pair? message)
                           (eq? (car message) '<span>))
                      `(<span>
                         ,@(map (lambda (par)
                                  (if (string? par)
                                      `(<paragraph> (<text> color: White ,par))
                                    par))
                                (cdr message)))
                    `(<span>
                       (<paragraph>
                         (<text> color: White ,(->string message)))))))
        (let ((view (console-view text point-size)))
          (let ((pos (center (get-size view) resolution)))
            (set-position view pos)
            (paint-drawing view surface '())))))))


;;;
;;;; View
;;;


(definition protected (console-view text point-size)
  (let* ((view (new World-Console-View size: {Dimension 4000 10}))
         (text-view (get-text view))
         (text-model (get-model text-view)))
    (setup-style text-view (new Text-Style font: (new Font font-name: 'tahoma point-size: point-size hint-style: 'slight hint-metrics: 'on) color: {Color White}))
    (set-text text-model text)
    (let ((size (scrollee-content-size text-view))
          (max-width 600)
          (padding 4))
      (if (< (get-width size) max-width)
          (set-size view (inflate-size (scrollee-content-size text-view) padding 0))
        (set-size view (new Dimension max-width 10))
        (set-wrap? text-model #t)
        (wrap-content text-model '())
        (set-wrap? text-model #f)
        (set-size view (new Dimension (+ (calculate-contour-scrollee-width text-model) padding) (get-height text-view)))))
    view))


;;;
;;;; World-Console-View
;;;


(class World-Console-View extends Layout-View
  
  
  (form
    (<install>                                     layout-type: fill
      (<Scroller-View>                             hscroll?: #f vscroll?: #f style: tool
        (<!>                         name: content layout-type: fill
          (<World-Console-Text-View> name: text    background: #f editable?: #f selectable?: #f)))))
  
  
  (method public (get-text self)
    (locate self 'text)))


;;;
;;;; World-Console-Text-View
;;;


(class World-Console-Text-View extends Plain-Formatted-Text-View
  
  
  (method override (new-model self)
    (new Formatted-Text-Model base-style: {Text-Style font: {Font Label-User} color: {Color White}} left-padding: 0 top-padding: 0))))
